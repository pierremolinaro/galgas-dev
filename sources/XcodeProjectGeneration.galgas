//
//  MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software
// and associated documentation files (the "Software"), to deal in the Software without
// restriction, including without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the
// Software is furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all copies or
// substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING
// BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
// NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
// DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//--------------------------------------------------------------------------------------------------

map @projectQualifiedFeatureMap {
  public let @lstring mFeatureValue
  insert insertKey error message "the '%K' attribute is already declared"
  search searchKey error message "the '%K' attribute is not declared"
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addICNS_file
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef
{
  [!?self getReferenceKey ?ref:outFileRef]
  self.mICNS_fileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addTIFF_file
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef {
  [!?self getReferenceKey ?ref:outFileRef]
  self.mTIFF_fileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addInfoPlistFile
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef {
  [!?self getReferenceKey ?ref:outFileRef]
  self.mInfoPlistFileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addXIBFile
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef {
  [!?self getReferenceKey ?ref:outFileRef]
  self.mXIB_fileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addFrameworkFile
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef {
  [!?self getReferenceKey ?ref:outFileRef]
  self.mFrameworkFileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addSwiftFile
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef {
  [!?self getReferenceKey ?ref:outFileRef]
  self.m_Swift_FileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addMFile
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef {
  [!?self getReferenceKey ?ref:outFileRef]
  self.m_M_FileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addMMFile
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef {
  [!?self getReferenceKey ?ref:outFileRef]
  self.m_MM_FileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addCppFile
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef {
  [!?self getReferenceKey ?ref:outFileRef]
  self.mCppFileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addCFile
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef {
  [!?self getReferenceKey ?ref:outFileRef]
  self.mCFileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addHeaderFile
  ?fileName:let @string inFileName
  !fileRef:@string outFileRef {
  [!?self getReferenceKey ?ref:outFileRef]
  self.mHeaderFileList += !outFileRef !inFileName
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addBuildFile
  ?fileRef:let @string inFileReference
  ?fileName:let @string inFileName
  !buildRef:@string outBuildRef {
  [!?self getReferenceKey ?ref:outBuildRef]
  self.mBuildFileList += !inFileReference !inFileName !outBuildRef
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addToolTarget
  ?targetName:let @string inTargetName
  ?productFileName:let @string inProductFileName
  ?sources:let @stringlist inSourceList
  ?frameworkRefs:let @stringlist inToolFrameworksFileRefList
  ?buildConfigurationSettingList:let @stringlist inBuildConfigurationSettingList
  !targetRef:@string outTargetRef
  !productFileRef:@string outProductFileRef {
  [!?self getReferenceKey ?ref:outProductFileRef]
  [!?self getReferenceKey ?ref:let buildPhaseRef]
  [!?self getReferenceKey ?ref:outTargetRef]
  [!?self getReferenceKey ?ref:let buildConfigurationListRef]
  [!?self getReferenceKey ?ref:let buildConfigurationRef]
  [!?self getReferenceKey ?ref:let frameworkBuildRef]
  self.mToolTargetList +=
    !outTargetRef
    !inTargetName
    !outProductFileRef
    !inProductFileName
    !inSourceList
    !buildPhaseRef
    !buildConfigurationListRef
    !inBuildConfigurationSettingList
    !buildConfigurationRef
    !inToolFrameworksFileRefList
    !frameworkBuildRef
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addAppTarget
  ?targetName:let @string inTargetName
  ?productFileName:let @string inProductFileName
  ?sources:let @stringlist inSourceList
  ?frameworkFileRefs:let @stringlist inFrameworksFileRefList
  ?resourceFileRefs:let @stringlist inResourceFileBuildRefs
  ?buildConfigurationSettingList:let @stringlist inBuildConfigurationSettingList
  ?dependantTargetList:let @stringlist inDependentTargetRefList
  ?productCopyList:let @2stringlist inProductCopyList
  !productFileRef:@string outProductFileRef {
  [!?self getReferenceKey ?ref:outProductFileRef]
  [!?self getReferenceKey ?ref:let buildPhaseRef]
  [!?self getReferenceKey ?ref:let targetRef]
  [!?self getReferenceKey ?ref:let buildConfigurationListRef]
  [!?self getReferenceKey ?ref:let buildConfigurationRef]
  [!?self getReferenceKey ?ref:let frameworkBuildRef]
  [!?self getReferenceKey ?ref:let resourceBuildRef]
  var @2stringlist dependentTargets = @()
  for (ref) in inDependentTargetRefList do
    [!?self getReferenceKey ?ref:let dependencyBuildRef]
    dependentTargets += !dependencyBuildRef !ref
  end
  var @stringlist resourceFileBuildRefs = inResourceFileBuildRefs
  for (ref name) in inProductCopyList do
    [!?self addBuildFile !fileRef:ref !fileName:name ?buildRef:let @string buildRef]
    resourceFileBuildRefs += !buildRef
  end
  self.mAppTargetList +=
    !targetRef
    !inTargetName
    !outProductFileRef
    !inProductFileName
    !inSourceList
    !buildPhaseRef
    !buildConfigurationListRef
    !inBuildConfigurationSettingList
    !buildConfigurationRef
    !inFrameworksFileRefList
    !frameworkBuildRef
    !dependentTargets
    !resourceBuildRef
    !resourceFileBuildRefs
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addGroup
  ?groupName:let @string inGroupName
  ?groupPath:let @string inGroupPath
  ?childrenRefs:let @stringlist inChildrenRefs
  !groupRef:@string outGroupRef
{
  if self.mDefaultConfigurationRef == "" then
    [!?self getReferenceKey ?ref: self.mDefaultConfigurationRef]
  end
  if self.mProjectBuildConfigurationRef == "" then
    [!?self getReferenceKey ?ref:self.mProjectBuildConfigurationRef]
  end
  if self.mMainGroupReference == "" then
    [!?self getReferenceKey ?ref:self.mMainGroupReference]
  end
  [!?self getReferenceKey ?ref:outGroupRef]
  self.mGroupList += !outGroupRef !inGroupName !inGroupPath !inChildrenRefs
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addGroupWithFiles
  ?groupName:let @string inGroupName
  ?groupPath:let @string inGroupPath
  ?fileNames:let @stringset inFileNames
  ?!cFileRefs:@stringlist ioCFileBuildRefs
  ?!cppFileRefs:@stringlist ioCppFileBuildRefs
//  ?!mFileRefs:@stringlist ioMFileBuildRefs
//  ?!mmFileRefs:@stringlist ioMMFileBuildRefs
  ?!swiftFileRefs:@stringlist ioSwiftFileBuildRefs
  ?!frameworkFileRefs:@stringlist ioFrameWorkFileBuildRefs
  ?!resourceFileRefs:@stringlist ioResourceFileBuildRefs
  !groupRef:@string outGroupRef {
  if self.mDefaultConfigurationRef == "" then
    [!?self getReferenceKey ?ref:self.mDefaultConfigurationRef]
  end
  if self.mProjectBuildConfigurationRef == "" then
    [!?self getReferenceKey ?ref:self.mProjectBuildConfigurationRef]
  end
  if self.mMainGroupReference == "" then
    [!?self getReferenceKey ?ref:self.mMainGroupReference]
  end
  [!?self getReferenceKey ?ref:outGroupRef]
//---
  var @stringlist childrenRefs = @()
  for (f) in inFileNames do
    if f.pathExtension == "c" then
      [!?self addCFile !fileName:f ?fileRef:let @string cFileRef]
      childrenRefs += !cFileRef
      [!?self addBuildFile !fileRef:cFileRef !fileName:f ?buildRef:let @string buildRef]
      ioCFileBuildRefs += !buildRef
    elsif f.pathExtension == "cpp" then
      [!?self addCppFile !fileName:f ?fileRef:let @string cppFileRef]
      childrenRefs += !cppFileRef
      [!?self addBuildFile !fileRef:cppFileRef !fileName:f ?buildRef:let @string buildRef]
      ioCppFileBuildRefs += !buildRef
    elsif f.pathExtension == "h" then
      [!?self addHeaderFile !fileName:f ?fileRef:let @string headerFileRef]
      childrenRefs += !headerFileRef
    elsif f.pathExtension == "framework" then
      [!?self addFrameworkFile !fileName:f ?fileRef:let @string framework_FileRef]
      [!?self addBuildFile !fileRef:framework_FileRef !fileName:f ?buildRef:let @string buildRef]
      childrenRefs += !framework_FileRef
      ioFrameWorkFileBuildRefs += !buildRef
    elsif f.pathExtension == "plist" then
      [!?self addInfoPlistFile !fileName:f ?fileRef:let @string resource_FileRef]
      childrenRefs += !resource_FileRef
    elsif f.pathExtension == "xib" then
      [!?self addXIBFile !fileName:f ?fileRef:let @string resource_FileRef]
      childrenRefs += !resource_FileRef
      [!?self addBuildFile !fileRef:resource_FileRef !fileName:f ?buildRef:let @string buildRef]
      ioResourceFileBuildRefs += !buildRef
    elsif f.pathExtension == "tiff" then
      [!?self addTIFF_file !fileName:f ?fileRef:let @string resource_FileRef]
      childrenRefs += !resource_FileRef
      [!?self addBuildFile !fileRef:resource_FileRef !fileName:f ?buildRef:let @string buildRef]
      ioResourceFileBuildRefs += !buildRef
    elsif f.pathExtension == "icns" then
      [!?self addICNS_file !fileName:f ?fileRef:let @string resource_FileRef]
      childrenRefs += !resource_FileRef
      [!?self addBuildFile !fileRef:resource_FileRef !fileName:f ?buildRef:let @string buildRef]
      ioResourceFileBuildRefs += !buildRef
    elsif f.pathExtension == "swift" then
      [!?self addSwiftFile !fileName:f ?fileRef:let @string swiftFileRef]
      [!?self addBuildFile !fileRef:swiftFileRef !fileName:f ?buildRef:let @string buildRef]
      childrenRefs += !swiftFileRef
      ioSwiftFileBuildRefs += !buildRef
    else
      error @location. nowhere : "unhandled extension for file '" + f + "'"
    end
  end
  self.mGroupList += !outGroupRef !inGroupName !inGroupPath !childrenRefs
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor placeGroupAsMainGroup
  ?groupRef:let @string inGroupRef {
  self.mMainGroupChildrenRefs += !inGroupRef
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor addSettingsToDefaultConfiguration
  ?settingList:let @stringlist inSettingList {
  self.mDefaultConfigurationSettingList += inSettingList
}

//--------------------------------------------------------------------------------------------------

list @XCodeGroupList {
  public let @string mGroupReference
  public let @string mGroupName
  public let @string mGroupPath
  public let @stringlist mChildrenRefs
}

//--------------------------------------------------------------------------------------------------

list @XCodeToolTargetList {
  public let @string mTargetRef
  public let @string mTargetName
  public let @string mProductFileReference
  public let @string mProductFileName
  public let @stringlist mBuildPhaseRefList
  public let @string mBuildPhaseRef
  public let @string mBuildConfigurationListRef
  public let @stringlist mBuildConfigurationSettingList
  public let @string mBuildConfigurationRef
  public let @stringlist mFrameworksFileRefList
  public let @string mFrameworkBuildPhaseRef
}

//--------------------------------------------------------------------------------------------------

list @XCodeAppTargetList {
  public let @string mTargetRef
  public let @string mTargetName
  public let @string mProductFileReference
  public let @string mProductFileName
  public let @stringlist mBuildPhaseRefList
  public let @string mBuildPhaseRef
  public let @string mBuildConfigurationListRef
  public let @stringlist mBuildConfigurationSettingList
  public let @string mBuildConfigurationRef
  public let @stringlist mFrameworksFileRefList
  public let @string mFrameworkBuildPhaseRef
  public let @2stringlist mDependentTargets
  public let @string mResourceBuildRef
  public let @stringlist mResourceFileBuildRefs
}

//--------------------------------------------------------------------------------------------------

list @BuildFileList {
  public let @string mFileReference
  public let @string mFileName
  public let @string mBuildReference
}

//--------------------------------------------------------------------------------------------------

setter @XcodeProjectDescriptor getReferenceKey
  !ref:@string outRef {
  if self.mProjectObjectReference == "" then
    self.mProjectObjectReference = [self.mSequenceNumber.string.md5 rightSubString !24]
    self.mSequenceNumber += 1
  end
  outRef = [self.mSequenceNumber.string.md5 rightSubString!24]
  self.mSequenceNumber += 1
}

//--------------------------------------------------------------------------------------------------

struct @XcodeProjectDescriptor {
  public var @uint mSequenceNumber = 0
  public let @string mMainGroupReference = ""
  public var @stringlist mMainGroupChildrenRefs = @()
  public var @string mProjectObjectReference = ""
  public var @XCodeGroupList mGroupList = @()
  public var @XCodeToolTargetList mToolTargetList = @()
  public var @XCodeAppTargetList mAppTargetList = @()
  public var @2stringlist mCFileList = @()
  public var @2stringlist mCppFileList = @()
  public var @2stringlist m_Swift_FileList = @()
  public var @2stringlist m_M_FileList = @()
  public var @2stringlist m_MM_FileList = @()
  public var @2stringlist mFrameworkFileList = @()
  public var @2stringlist mHeaderFileList = @()
  public var @BuildFileList mBuildFileList = @()
//--- Default configuration
  public let @string mDefaultConfigurationRef = ""
  public var @stringlist mDefaultConfigurationSettingList = @()
  public let @string mProjectBuildConfigurationRef = ""
//--- Resource files
  public var @2stringlist mInfoPlistFileList = @()
  public var @2stringlist mXIB_fileList = @()
  public var @2stringlist mTIFF_fileList = @()
  public var @2stringlist mICNS_fileList = @()
//--- Project
  public var @string mApplicationBundleName = ""
  public var @string mProjectName = ""
}

//--------------------------------------------------------------------------------------------------

private filewrapper xcodeProjectGenerationFilewrapper in "+generation-templates/target_generation/project-xcode-swiftui" {
}{
}{
  template xcodeproj "project.pbxproj.galgasTemplate"
    ?@string PROJECT_REF
    ?@string MAIN_GROUP_REF
    ?@XCodeGroupList GROUPS
    ?@stringlist MAIN_GROUP_CHILDREN_REFS
    ?@XCodeToolTargetList TOOL_TARGET_LIST
    ?@XCodeAppTargetList APP_TARGET_LIST
    ?@2stringlist C_FILE_LIST
    ?@2stringlist CPP_FILE_LIST
    ?@2stringlist SWIFT_FILE_LIST
    ?@2stringlist M_FILE_LIST
    ?@2stringlist MM_FILE_LIST
    ?@2stringlist FRAMEWORK_FILE_LIST
    ?@2stringlist HEADER_FILE_LIST
    ?@2stringlist PLIST_FILE_LIST
    ?@2stringlist TIFF_FILE_LIST
    ?@2stringlist ICNS_FILE_LIST
    ?@2stringlist XIB_FILE_LIST
    ?@BuildFileList BUILD_FILE_LIST
    ?@string DEFAULT_CONFIGURATION_REF
    ?@stringlist DEFAULT_CONFIGURATION_SETTING_LIST
    ?@string PROJECT_BUILD_DEFAULT_CONFIGURATION_REF
    ?@string APPLICATION_BUNDLE_BASE
    ?@string PROJECT_NAME
}

//--------------------------------------------------------------------------------------------------

method @XcodeProjectDescriptor generateAtPath
  ?xcodeprojPath:let @string inPath
  ?cacheFilePath:let @string inCacheFilePath {
  let contents = [filewrapper xcodeProjectGenerationFilewrapper.xcodeproj
    !self.mProjectObjectReference
    !self.mMainGroupReference
    !self.mGroupList
    !self.mMainGroupChildrenRefs
    !self.mToolTargetList
    !self.mAppTargetList
    !self.mCFileList
    !self.mCppFileList
    !self.m_Swift_FileList
    !self.m_M_FileList
    !self.m_MM_FileList
    !self.mFrameworkFileList
    !self.mHeaderFileList
    !self.mInfoPlistFileList
    !self.mTIFF_fileList
    !self.mICNS_fileList
    !self.mXIB_fileList
    !self.mBuildFileList
    !self.mDefaultConfigurationRef
    !self.mDefaultConfigurationSettingList
    !self.mProjectBuildConfigurationRef
    !self.mApplicationBundleName
    !self.mProjectName
  ]
  let projectCoreFile = inPath + "/project.pbxproj"
  if (not projectCoreFile.fileExists) || (not inCacheFilePath.fileExists) || (@string. stringWithContentsOfFile {!inCacheFilePath} != contents) then
    [contents writeToFileWhenDifferentContents !inCacheFilePath ?*]
    [contents makeDirectoryAndWriteToFile !projectCoreFile]
  end
}

//--------------------------------------------------------------------------------------------------

