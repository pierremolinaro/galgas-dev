//
//  MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software
// and associated documentation files (the "Software"), to deal in the Software without
// restriction, including without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the
// Software is furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all copies or
// substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING
// BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
// NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
// DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//--------------------------------------------------------------------------------------------------
//   EMBEDDED TYPE NAME UTILITIES
//--------------------------------------------------------------------------------------------------

func makeEmbeddedTypeLName ?outer: let @lstring inOuterTypeName
                          ?inner: let @lstring inInnerTypeName -> @lstring {
  result = @(makeEmbeddedTypeName (!outer: inOuterTypeName !inner: inInnerTypeName),
                  [inOuterTypeName.location union !inInnerTypeName])
}

//--------------------------------------------------------------------------------------------------

func makeEmbeddedTypeName ?outer: let @string inOuterTypeName
                          ?inner: let @string inInnerTypeName -> @string {
  result = inOuterTypeName + "." + inInnerTypeName
}

//--------------------------------------------------------------------------------------------------
//   OPTIONAL TYPE NAME UTILITIES
//--------------------------------------------------------------------------------------------------

func makeOptionalTypeName ?forTypeNamed: let @string inTypeName -> @string {
  result = inTypeName + "?"
}

//--------------------------------------------------------------------------------------------------

func makeOptionalTypeLName ?forTypeNamed: let @lstring inTypeName -> @lstring {
  result = inTypeName
  result.string += "?"
}

//--------------------------------------------------------------------------------------------------
//   EMBEDDED ELEMENT TYPE NAME UTILITIES
//--------------------------------------------------------------------------------------------------

func makeEmbeddedElementTypeName ?let @string inTypeName -> @string {
  result = makeEmbeddedTypeName (!outer: inTypeName !inner: "element")
}

//--------------------------------------------------------------------------------------------------

func makeEmbeddedElementTypeLName ?let @lstring inTypeName -> @lstring {
  result = @(makeEmbeddedElementTypeName (!inTypeName), inTypeName)
}

//--------------------------------------------------------------------------------------------------
//   ARRAY TYPE NAME UTILITIES
//--------------------------------------------------------------------------------------------------

func makeArrayTypeName ?let @string inTypeName -> @string {
  result = "[" + inTypeName + "]"
}

//--------------------------------------------------------------------------------------------------

func makeArrayTypeLName ?let @lstring inTypeName -> @lstring {
  result = @(makeArrayTypeName (!inTypeName), inTypeName)
}

//--------------------------------------------------------------------------------------------------
//   WEAK TYPE NAME UTILITIES
//--------------------------------------------------------------------------------------------------

func makeWeakTypeName ?let @string inTypeName -> @string {
  result = makeEmbeddedTypeName (!outer: inTypeName !inner: "weak")
}

//--------------------------------------------------------------------------------------------------

func makeWeakTypeLName ?let @lstring inTypeName -> @lstring {
  result = @(makeWeakTypeName (!inTypeName), inTypeName)
}

//--------------------------------------------------------------------------------------------------
//   SYNTAX
//--------------------------------------------------------------------------------------------------

syntax extension galgas3DeclarationsSyntax {

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

//  rule <declaration_ggs3> ?!@galgasDeclarationAST ioDeclarations {
//    $typealias$
//    $@type$ ?let aliasTypeName indexing dictionaryDefinition
//    $=$
//    <type_definition_ggs3> !?ioDeclarations ?let @lstring definedTypeName
//    ioDeclarations.mDeclarationList += !@typealiasDeclarationAST {
//      !isPredefined: false
//      !aliasTypeName
//      !definedTypeName
//    }
//  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  // SIMPLE ALIAS
  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <type_definition_ggs3> ?!@galgasDeclarationAST unused ioDeclarations
                              !@lstring outTypeName {
    $@type$ ?outTypeName
    repeat
    while
      $.$
      $@type$ ?let typeName
      outTypeName = makeEmbeddedTypeLName (!outer: outTypeName !inner: typeName)
    end
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <type_definition_ggs3> ?!@galgasDeclarationAST ioDeclarations
                              !@lstring outTypeName {
    $[$
    <type_definition_ggs3> !?ioDeclarations ?outTypeName
    $:$
    <type_definition_ggs3> !?ioDeclarations ?*
    error .here : "Not handled yet"
    $]$
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <type_definition_ggs3> ?!@galgasDeclarationAST ioDeclarations
                              !@lstring outTypeName {
    <type_definition_ggs3> !?ioDeclarations ?outTypeName
    $<$
    repeat
      <type_definition_ggs3> !?ioDeclarations ?let t
      error t : "not handled yet"
    while
      $,$
    end
    $>$
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <type_definition_ggs3> ?!@galgasDeclarationAST ioDeclarations
                              !@lstring outTypeName {
    <type_definition_ggs3> !?ioDeclarations ?outTypeName
    $?$
    let unwrappedTypeName = outTypeName 
    [!?ioDeclarations getOptionalTypeName ?outTypeName !forTypeNamed: unwrappedTypeName !isPredefined: false]
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <type_definition_ggs3> ?!@galgasDeclarationAST ioDeclarations
                              !@lstring outTypeName {
    $($
    repeat
      <type_definition_ggs3> !?ioDeclarations ?outTypeName
    while
      $,$
    end
    error .here : "Not handled yet"
    $)$
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

}

//--------------------------------------------------------------------------------------------------

syntax extension galgas4DeclarationsSyntax {

   // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

//  rule <declaration> ?!@galgasDeclarationAST ioDeclarations {
//    $typealias$
//    $@type$ ?let aliasTypeName indexing dictionaryDefinition
//    $=$
//    <type_definition> !?ioDeclarations ?let @lstring definedTypeName
//    ioDeclarations.mDeclarationList += !@typealiasDeclarationAST {
//      !isPredefined: false // Is not predefined
//      !aliasTypeName
//      !definedTypeName
//    }
//  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <type_definition> ?!@galgasDeclarationAST ioDeclarations
                         !@lstring outTypeName {
    <simple_type_definition> !?ioDeclarations ?outTypeName
    repeat
    while
      $.$
      <simple_type_definition> !?ioDeclarations ?let typeName
      outTypeName = makeEmbeddedTypeLName (!outer: outTypeName !inner: typeName)
    while
      $<$
      repeat
        <type_definition> !?ioDeclarations ?let t
        error t : "not handled yet"
      while
        $,$
      end
      $>$
    end
  //--- Optional ?
    select
    or
      $?$
      let unwrappedTypeName = outTypeName 
      [!?ioDeclarations getOptionalTypeName ?outTypeName !forTypeNamed: unwrappedTypeName !isPredefined: false]
    end
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <simple_type_definition> ?!@galgasDeclarationAST unused ioDeclarations
                                !@lstring outTypeName {
    $@type$ ?outTypeName
  }
  
  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <simple_type_definition> ?!@galgasDeclarationAST ioDeclarations
                                !@lstring outTypeName {
    $[$
    <type_definition> !?ioDeclarations ?outTypeName
    select
      let elementTypeName = outTypeName 
      outTypeName = makeArrayTypeLName (!elementTypeName)
      if not [ioDeclarations.implicitTypeDeclarationSet hasKey !outTypeName.string] then
        ioDeclarations.implicitTypeDeclarationSet ++= outTypeName.string
        ioDeclarations.mDeclarationList += !@arrayTypeDeclarationAST (
          !isPredefined: false,
          outTypeName,
          elementTypeName,
          !isUsefull: true,
          !equatable: false
        )
      end
    or
      $:$
      <type_definition> !?ioDeclarations ?*
      error .here : "Not handled yet"
      end
    $]$
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <simple_type_definition> ?!@galgasDeclarationAST ioDeclarations
                                !@lstring outTypeName {
    $($
    repeat
      <type_definition> !?ioDeclarations ?outTypeName
    while
      $,$
    end
    error .here : "Not handled yet"
    $)$
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

}

//--------------------------------------------------------------------------------------------------
