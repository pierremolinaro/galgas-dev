

before {
}

case . "classSampleLanguage"
message "an '.class_sample_language' source file"
%useGrammar class_sample_language_grammar
?sourceFilePath:@lstring inSourceFile {
#--- Build Abstract Syntax Tree
  grammar class_sample_language_grammar (?let declarationListAST) in inSourceFile
#--- Build dependence graph
  var inheritanceGraph = @inheritanceGraph ()
  for (mDeclaration) in  declarationListAST do
    [mDeclaration buildGraph !?inheritanceGraph]
  end
#--- Undefined classes ?
  if [inheritanceGraph undefinedNodeCount] > 0 then
    for (mValue) in  [inheritanceGraph undefinedNodeReferenceList] do
      error mValue : "the '" + mValue + "' class is not declared"
    end
  else
  #--- Topologic sort
    [inheritanceGraph topologicalSort
      ?let @declarationListAST sortedDeclarationListAST
      ?*
      ?*
      ?let @lstringlist circularDefinitionClassNameList
    ]
    if [circularDefinitionClassNameList count] > 0 then
      for (mValue) in  circularDefinitionClassNameList do
        error mValue : "the '" + mValue + "' class is involved in a circular definition"
      end
    else
    #--- Generate code
      var @string codeToGenerate = ""
      for (mDeclaration) in  sortedDeclarationListAST do
        [mDeclaration generateCode !?codeToGenerate]
      end
      let @string targetFile = [inSourceFile stringByDeletingPathExtension] + ".cpp"
      [codeToGenerate writeToFile !targetFile]
    end
  end
}

after {
}