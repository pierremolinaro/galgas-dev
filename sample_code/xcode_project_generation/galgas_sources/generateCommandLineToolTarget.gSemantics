#---------------------------------------------------------------------------*

semantics generateCommandLineToolTarget :

import semantics baseForXcodeProjectGeneration in "baseForXcodeProjectGeneration.gSemantics" ;

#---------------------------------------------------------------------------*

routine generateProjectWithCommandLineTarget ??@string inGenerationDirectory :
#-------------------------------------------------- Create XCode project descriptor object
  @XcodeProjectDescription XcodeProjectDescription ;
  initXcodeProjectDescription ?XcodeProjectDescription ;
#-------------------------------------------------- Adding a group that contains main.cpp
  @XcodeObjectReferenceList groupContents [emptyList] ;
  enter_file_in_group !?XcodeProjectDescription !"main.cpp" !?groupContents ;
  enter_group
    !?XcodeProjectDescription
    !"Source" # The group name, as it appears in Project Window
    !"." # Group Path (relative to project file)
    !groupContents
  ;
#-------------------------------------------------- Define a target
  @Xcode_PBXBuildFile_list buildPhaseList [emptyList] ;
#---
  enter_file_for_sources_build_phase !?XcodeProjectDescription !"main.cpp" !?buildPhaseList ;
#--- Target settings
  @stringlist toolTargetSettings [emptyList] ;
  toolTargetSettings  += !"GCC_OPTIMIZATION_LEVEL = 2;" ;
#--- Add Tool target
  @string toolTargetKey ;
  add_tool_target
    !?XcodeProjectDescription
    !"tool" # Name for Xcode project Window
    !"sampleTool" # Executable name
    !toolTargetSettings
    !"prefix.pch" # (Absolute or relative) path to precompiled prefix, or empty string if none
    !buildPhaseList
    ![@Xcode_PBXBuildFile_list emptyList] # No framework
    ![@stringlist emptyList] # No Dependency
    ?toolTargetKey # Unused here
  ;
#-------------------------------------------------- Define project default configuration
  @stringlist pathes [emptyList] ;
  @stringlist projectDefaultSettings ;
  enterProjectDefaultSettings
    !pathes
    ?projectDefaultSettings
  ;
#-------------------------------------------------- Build Project file
  generateXCodeFile
    !?XcodeProjectDescription
    !"./projectWithCommandLineTarget.xcodeproj"
    !projectDefaultSettings
    !inGenerationDirectory
  ;
end routine ;

#---------------------------------------------------------------------------*

end semantics ;

