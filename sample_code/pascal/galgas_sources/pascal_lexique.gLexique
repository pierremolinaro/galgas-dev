lexique pascal_lexique :

@string identifierString ;
@string tokenString ;

style keywordsStyle -> "Keywords" ;
style delimitersStyle -> "Delimiters" ;

# -------------------------- identifier or key word --------------------
# keywords can be written in lower or upper case
# for identifiers, case is significant

$identifier$ ! tokenString error message "an identifier" ;

list galgasKeyWordList error message "the '%K' keyword" style keywordsStyle :
  "program",
  "end",
  "type",
  "record",
  "begin",
  "procedure"
;

rule 'a'->'z' |  'A'->'Z' :
  repeat
    enterCharacterIntoString !?identifierString !toLower (!*) ;
    enterCharacterIntoString !?tokenString !* ;
  while 'a'->'z' | 'A'->'Z' | '_' | '0'->'9' :
  end repeat ;
  send search identifierString in galgasKeyWordList default $identifier$ ;
end rule ;

# ----------------------------- Delimitors ---------------------------------------
list galgasDelimitorsList error message "the '%K' delimitor" style delimitersStyle :
  "*",     "|",    ",",      "+",
  ">",    ";",      ":",
  "-",     "(",    ")",
  "=",    ":=",
  "[",     "]",     ".",
  "/",    "<>",   ">=",      "&",
  "^" ;

rule list galgasDelimitorsList ;

end lexique ;
