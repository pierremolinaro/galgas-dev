%!TEX encoding = UTF-8 Unicode
%!TEX root = ../galgas-book.tex

%--------------------------------------------------------------
\chapter{Le type \texttt{list}}
%-------------------------------------------------------------

\section{List Type Declaration}

A \galgas{@list} type declaration names all attributes of the list elements:

\begin{lstlisting}[language=galgas]
list @MyList {
  @string mFirstAttribute ;
  @bool mSecondAttribute ;
}
\end{lstlisting}

\section{Constructors}

\subsection{The \texttt{emptyList} constructor}

For every list, an \galgas{emptyList} constructor is implicitly declared. It returns an empty list:

\begin{lstlisting}[language=galgas]
@MyList aList := [@MyList emptyList] ;
\end{lstlisting}


\subsection{The \texttt{listWithValue} constructor}

A list can be constructed directly with one value:

\begin{lstlisting}[language=galgas]
@MyList aList := [@myList listWithValue !"c" !3] ;
\end{lstlisting}


Using this constructor is equivalent to:

\begin{lstlisting}[language=galgas]
@MyList aList := [@MyList emptyList] ;
aList += !"c" !3 ;
\end{lstlisting}

\section{Adding elements}

\subsectionLabel{The \texttt{+=} operator}{operatorPlusEgalSurListe}

The  \galgas{+=} operator adds a new element at the end of the list. The right side expressions should correspond to the attributes declared in the \galgas{list} declaration:\\

\begin{lstlisting}[language=galgas]
@MyList aList := ... ;
@string aString := ... ;
@bool aBool := ... ;
aList += !aString !aBool ;''
\end{lstlisting}


\subsection{L'instruction \texttt{.=}}

L'instruction \galgas{cible .= expression ;} concatène la liste définie par la valeur de \galgas{expression} à la la liste \galgas{cible} :

\begin{lstlisting}[language=galgas]
@MyList aList := ... ;
@MyList secondList := ... ;
aList .= secondList ;''
\end{lstlisting}



\subsection{The \texttt{prependValue} modifier}

{\bf \textcolor{red}{Ce modifier a été supprimé ; utiliser le modifier \emph{insertAtIndex}.}}

The \galgas{prependValue} modifier adds a new element at the begining of the list. The right side expressions should correspond to the attributes declared in the  \galgas{list} declaration:

\begin{lstlisting}[language=galgas]
@MyList aList := ... ;
@string aString := ... ;
@bool aBool := ... ;
[!?aList prependValue !aString !aBool];
\end{lstlisting}




\subsection{Modifier \texttt{insertAtIndex}}

Le modifier \galgas{insertAtIndex} permet d'insérer un nouvel élément à une position quelconque de la liste. Si le type \galgas{list} correspondant déclare $n$ champs, l'appel du modifier comprend $n+1$ arguments :
\begin{itemize}
  \item les $n$ premiers correspondent aux valeurs des champs du nouvel élément inséré ;
  \item le dernier est l'indice d'insertion, une valeur de type \galgas{@uint}.
\end{itemize}

L'indice d'insertion peut varier entre $0$ (insertion au début, comme le faisait le modifier \emph{prependValue}), et la longueur courante de la liste (insertion à la fin, comme le fait l'opérateur \galgas{+=}, \refSubsectionPage{operatorPlusEgalSurListe}). Si la liste est vide, insérer à l'indice $0$ est donc la seule possibilité.

Par exemple :

\begin{lstlisting}[language=galgas]
@MyList aList := ... ;
@string aString := ... ;
@bool aBool := ... ;
[!?aList insertAtIndex !aString !aBool !0];
\end{lstlisting}

\subsection{The concatenation operator}

The «~\galgas{.}~» operator can be used for concatenating two lists of the same type:


\begin{lstlisting}[language=galgas]
@MyList firstList := ... ;
@MyList secondList := ... ;
@MyList thirdList := firstList . secondList ;
\end{lstlisting}

\section{Removing elements}

\subsection{The \texttt{popFirst} modifier}


The \galgas{popFirst} modifier removes and returns the first element of the list. The right side expressions should correspond to the attributes declared in the \galgas{list} declaration:\\

\begin{lstlisting}[language=galgas]
@MyList aList := ... ;
@string aString ;
@bool aBool ;
[!?aList popFirst ?aString ?aBool];
\end{lstlisting}

If the list is empty when \galgas{popFirst} modifier is invoked, a run-time error is raised and the input arguments are not valuated.

\subsection{The \texttt{popLast} modifier}


The \galgas{popLast} modifier removes and returns the last element of the list. The right side expressions should correspond to the attributes declared in the \galgas{list} declaration:

\begin{lstlisting}[language=galgas]
@MyList aList := ... ;
@string aString ;
@bool aBool ;
[!?aList popLast ?aString ?aBool];
\end{lstlisting}

If the list is empty when \galgas{popLast} is invoked, a run-time error is raised and the input arguments are not valuated.

\section{Methods}

\subsection{The \texttt{first} method}

The \galgas{first} method returns the first element of the list. The element is not removed. The right side expressions should correspond to the attributes declared in the \galgas{list} declaration:

\begin{lstlisting}[language=galgas]
@MyList aList := ... ;
@string aString ;
@bool aBool ;
[aList first ?aString ?aBool];
\end{lstlisting}

If the list is empty when \galgas{first} is invoked, a run-time error is raised and the input arguments are not valuated.

\subsection{The \texttt{last} method}

The \galgas{last} method returns the last element of the list. The element is not removed. The right side expressions should correspond to the attributes declared in the \galgas{list} declaration:\\

\begin{lstlisting}[language=galgas]
@MyList aList := ... ;
@string aString ;
@bool aBool ;
[aList last ?aString ?aBool];
\end{lstlisting}


If the list is empty when \galgas{last} is invoked, a run-time error is raised and the input arguments are not valuated.








\section{Readers}

\subsection{Le reader \texttt{length}r}

\begin{lstlisting}[language=galgas]
reader length -> @uint ;
\end{lstlisting}

Le reader \galgas{length} reader retourne le nombre d'éléments du receveur.


\subsection{The \texttt{range} reader}

\begin{lstlisting}[language=galgas]
reader range -> @range ;
\end{lstlisting}

The \galgas{range} reader returns a range starting at $0$ of length equal to the number of elements of the receiver.




\subsection{The \texttt{subListFromIndex} reader}

\begin{lstlisting}[language=galgas]
reader subListFromIndex ?@uint inIndex -> @self
\end{lstlisting}

This reader returns a new list containing the elements of the receiver from the one at a given index to the end. The  \galgas{inIndex} value should be lower or equal to the length of the receiver's value. If \galgas{inIndex} is equal to the length of the receiver, the reader returns an empty list.


\subsection{The \texttt{subListWithRange} reader}

\begin{lstlisting}[language=galgas]
reader subListWithRange
  ?@range inRange
  -> @self
\end{lstlisting}

This reader returns a list containing the elements of the receiver that lie within a given range. The range must not exceed the length of the receiver's value, that is $range\_start + range\_length \leqslant list\_length$. If the range's length is equal to zero, this reader returns an empty list.





\section{Enumerating a list with a foreach instruction}

The \galgas{foreach} instruction can be used for enumerating list objects. By default, lists are enumerated in the insertion order; enumeration in the reverse order is performed using the \galgas{>} qualifier.

There are two ways for accessing element values:
\begin{itemize}
\item using the implicitly declared constants that receive the current attribute values;
\item declare explicitly constants that receive the current attribute values.
\end{itemize}

Given the list declaration:

\begin{lstlisting}[language=galgas]
list @MyList {
  @string mFirstAttribute ;
  @bool mSecondAttribute ;
}
\end{lstlisting}

\subsection{Enumeration using the implicitly declared constants}

For every attribute, a constant of the same name is available in the \galgas{do} instruction list. Theses constants receive the value of the corresponding attribute of the current element.

\begin{lstlisting}[language=galgas]
foreach aList do
  # the mFirstAttribute constant receives the value
  # of the mFirstAttribute attribute of the current element,
  # and the mSecondAttribute constant receives the value
  # of the mSecondAttribute attribute of the current element.
end foreach ;
\end{lstlisting}

\subsection{Enumeration using the explicitly declared constants}

The \galgas{foreach} header declares a sequence of constants, corresponding to the attribute list of the \galgas{do} declaration. Theses constants receive the value of the corresponding attribute of the current element.


\begin{lstlisting}[language=galgas]
foreach aList (@string kString @bool kBool) do
  # the kString constant receives the value
  # of the mFirstAttribute attribute of the current element,
  # and the kBool constant receives the value
  # of the mSecondAttribute attribute of the current element.
end foreach ;
\end{lstlisting}

\subsection{Enumeration in the reverse order}

In GALGAS 1.7.3 and later, you can enumerate a list in the reverse order using the \galgas{>} qualifier:

\begin{lstlisting}[language=galgas]
foreach > aList (@string kString @bool kBool) do
  ...
end foreach ;
\end{lstlisting}




\section{Direct Access of an element attribute}

In GALGAS 1.7.5 and later, lists can be used as an array. Each element of a list is associated with an \galgas{@uint} index, spanning from 0 to element count (value returned by \galgas{length} reader) minus one.

The element retrieved with \galgas{first} method is at index 0.

The element retrieved with \galgas{last} method is at index equal to element count minus one.

\subsection{Read Access}

By default and for every attribute, a reader is provided to retrieve the value of this attribute for an element at a given index. For example, for an attribute named \emph{name}, the \emph{nameAtIndex} reader is provided. It accepts one \galgas{@uint} argument, the value of the index.

You can disable the default reader generation, by using \galgas{feature nogetter}.

For example:
\begin{lstlisting}[language=galgas]
list @MyList {
  @string mFirstAttribute ;
  @bool mSecondAttribute feature nogetter ;
}
...
@MyList aList := ... ;
@string s := [aList mFirstAttributeAtIndex !1] ;
\end{lstlisting}

One reader is available: \galgas{mFirstAttributeAtIndex}; the \galgas{mSecondAttributeAtIndex} reader is not available.


\subsection{Write Access}

By default, no modifier is provided for performing a direct write access to an attribute at a given index. You should use \galgas{feature setter} for enabling setter generation for a given attribute.

The modifier name is the name of the attribute with the first letter capitalized, prefixed by \emph{set} and suffixed by \emph{AtIndex}: for an attribute named \emph{name}, the modifier is named \emph{setNameAtIndex}. It accepts two arguments, the first one is the new attribute's value, the second one an \galgas{@uint} argument, the value of the index.

For example:

\begin{lstlisting}[language=galgas]
list @MyList {
  @string mFirstAttribute feature setter ;
  @bool mSecondAttribute ;
}
...
@string s := ... ;
[!?aList setMFirstAttributeAtIndex !s !1] ;
\end{lstlisting}

One modifier is available: \galgas{@setMFirstAttributeAtIndex}; the \galgas{@setMSecondAttributeAtIndex} modifier is not available.

\subsection{Example of read and write accesses}

\begin{lstlisting}[language=galgas]
list @myList {
  @string name ;
}
...
@myList strList [emptyList] ;
strList += !"a" ;
strList += !"b" ;
strList += !"c" ;
strList += !"d" ;
@string s := [strList nameAtIndex !0] ;
log s ; # displays LOGGING s: <@string:"a">
s := [strList nameAtIndex !1] ;
log s ; # displays LOGGING s: <@string:"b">
s := [strList nameAtIndex !2] ;
log s ; # displays LOGGING s: <@string:"c">
s := [strList nameAtIndex !3] ;
log s ; # displays LOGGING s: <@string:"d">
[!?strList setNameAtIndex !"x" !0] ;
[!?strList setNameAtIndex !"y" !1] ;
[!?strList setNameAtIndex !"z" !2] ;
[!?strList setNameAtIndex !"t" !3] ;
s := [strList nameAtIndex !0] ;
log s ; # displays LOGGING s: <@string:"x">
s := [strList nameAtIndex !1] ;
log s ; # displays LOGGING s: <@string:"y">
s := [strList nameAtIndex !2] ;
log s ; # displays LOGGING s: <@string:"z">
s := [strList nameAtIndex !3] ;
log s ; # displays LOGGING s: <@string:"t">
\end{lstlisting}
