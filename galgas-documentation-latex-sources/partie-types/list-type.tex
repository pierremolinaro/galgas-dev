%!TEX encoding = UTF-8 Unicode
%!TEX root = ../galgas-book.tex

%--------------------------------------------------------------
\chapterLabel{Le type \texttt{list}}{typeList}
%-------------------------------------------------------------

\section{List Type Declaration}

A \ggs+list+ type declaration names all attributes of the list elements:

\begin{galgas}
list @MyList {
  @string mFirstAttribute ;
  @bool mSecondAttribute ;
}
\end{galgas}

\section{Constructors}

\subsection{The \texttt{emptyList} constructor}

For every list, an \ggs+emptyList+ constructor is implicitly declared. It returns an empty list:

\begin{galgas}
@MyList aList = [@MyList emptyList] ;
\end{galgas}


\subsection{The \texttt{listWithValue} constructor}

A list can be constructed directly with one value:

\begin{galgas}
@MyList aList = [@myList listWithValue !"c" !3] ;
\end{galgas}


Using this constructor is equivalent to:

\begin{galgas}
@MyList aList = [@MyList emptyList] ;
aList += !"c" !3 ;
\end{galgas}

\section{Adding elements}

\subsectionLabel{The \texttt{+=} operator}{operatorPlusEgalSurListe}

The  \ggs*+=* operator adds a new element at the end of the list. The right side expressions should correspond to the attributes declared in the \ggs+list+ declaration:\\

\begin{galgas}
@MyList aList = ... ;
@string aString = ... ;
@bool aBool = ... ;
aList += !aString !aBool ;
\end{galgas}


\subsection{L'instruction \texttt{+=}}

L'instruction \ggs*cible += expression* concatène la liste définie par la valeur de \ggs+expression+ à la la liste \ggs+cible+ :

\begin{galgas}
@MyList aList = ...
@MyList secondList = ...
aList += secondList
\end{galgas}



\subsection{The \texttt{prependValue} setter}

{\bf \textcolor{red}{Ce setter a été supprimé ; utiliser le setter \emph{insertAtIndex}.}}

The \ggs+prependValue+ setter adds a new element at the begining of the list. The right side expressions should correspond to the attributes declared in the  \ggs+list+ declaration:

\begin{galgas}
@MyList aList = ... ;
@string aString = ... ;
@bool aBool = ... ;
[!?aList prependValue !aString !aBool];
\end{galgas}




\subsection{Setter \texttt{insertAtIndex}}

Le setter \ggs+insertAtIndex+ permet d'insérer un nouvel élément à une position quelconque de la liste. Si le type \ggs+list+ correspondant déclare $n$ champs, l'appel du setter comprend $n+1$ arguments :
\begin{itemize}
  \item les $n$ premiers correspondent aux valeurs des champs du nouvel élément inséré ;
  \item le dernier est l'indice d'insertion, une valeur de type \ggs+@uint+.
\end{itemize}

L'indice d'insertion peut varier entre $0$ (insertion au début, comme le faisait le setter \emph{prependValue}), et la longueur courante de la liste (insertion à la fin, comme le fait l'opérateur \ggs*+=*, \refSubsectionPage{operatorPlusEgalSurListe}). Si la liste est vide, insérer à l'indice $0$ est donc la seule possibilité.

Par exemple :

\begin{galgas}
@MyList aList = ... ;
@string aString = ... ;
@bool aBool = ... ;
[!?aList insertAtIndex !aString !aBool !0];
\end{galgas}

\subsection{The concatenation operator}

The «~\ggs*+*~» operator can be used for concatenating two lists of the same type:


\begin{galgas}
@MyList firstList = ... ;
@MyList secondList = ... ;
@MyList thirdList = firstList . secondList ;
\end{galgas}

\section{Removing elements}

\subsection{Setter \texttt{popFirst}}


The \ggs+popFirst+ setter removes and returns the first element of the list. The right side expressions should correspond to the attributes declared in the \ggs+list+ declaration:

\begin{galgas}
@MyList aList = ... ;
@string aString ;
@bool aBool ;
[!?aList popFirst ?aString ?aBool];
\end{galgas}

If the list is empty when \ggs+popFirst+ setter is invoked, a run-time error is raised and the input arguments are not valuated.

\subsection{Setter \texttt{popLast}}


The \ggs+popLast+ setter removes and returns the last element of the list. The right side expressions should correspond to the attributes declared in the \ggs+list+ declaration:

\begin{galgas}
@MyList aList = ... ;
@string aString ;
@bool aBool ;
[!?aList popLast ?aString ?aBool];
\end{galgas}

If the list is empty when \ggs+popLast+ is invoked, a run-time error is raised and the input arguments are not valuated.

\section{Methods}

\subsection{The \texttt{first} method}

The \ggs+first+ method returns the first element of the list. The element is not removed. The right side expressions should correspond to the attributes declared in the \ggs+list+ declaration:

\begin{galgas}
@MyList aList = ... ;
@string aString ;
@bool aBool ;
[aList first ?aString ?aBool];
\end{galgas}

If the list is empty when \ggs+first+ is invoked, a run-time error is raised and the input arguments are not valuated.

\subsection{The \texttt{last} method}

The \ggs+last+ method returns the last element of the list. The element is not removed. The right side expressions should correspond to the attributes declared in the \ggs+list+ declaration:\\

\begin{galgas}
@MyList aList = ... ;
@string aString ;
@bool aBool ;
[aList last ?aString ?aBool];
\end{galgas}


If the list is empty when \ggs+last+ is invoked, a run-time error is raised and the input arguments are not valuated.








\section{Getters}

\subsection{Le getter \texttt{length}}

\begin{galgas}
getter length -> @uint ;
\end{galgas}

Le \emph{getter} \ggs+length+ retourne le nombre d'éléments du récepteur.


\subsection{Le getter \texttt{range}}

\begin{galgas}
getter range -> @range ;
\end{galgas}

The \ggs+range+ getter returns a range starting at $0$ of length equal to the number of elements of the receiver.




\subsection{Le getter \texttt{subListFromIndex}}

\begin{galgas}
getter subListFromIndex ?@uint inIndex -> @self
\end{galgas}

This getter returns a new list containing the elements of the receiver from the one at a given index to the end. The  \ggs+inIndex+ value should be lower or equal to the length of the receiver's value. If \ggs+inIndex+ is equal to the length of the receiver, the getter returns an empty list.






\subsection{Le getter \texttt{subListToIndex}}

\begin{galgas}
getter subListToIndex ?@uint inIndex -> @self
\end{galgas}

Ce \emph{getter} retourne une liste comprenant les éléments du récepteur à partir de l'indice $0$ jusqu'à l'indice \ggs+inIndex+ compris. Si \ggs+inIndex+ est supérieur ou égal au nombre d'éléments de la liste, une erreur d'exécution est déclenchée.




\subsection{Le getter \texttt{subListWithRange}}

\begin{galgas}
getter subListWithRange
  ?@range inRange
  -> @self
\end{galgas}

This getter returns a list containing the elements of the receiver that lie within a given range. The range must not exceed the length of the receiver's value, that is $range\_start + range\_length \leqslant list\_length$. If the range's length is equal to zero, this getter returns an empty list.





\section{Enumerating a list with a \texttt{for} instruction}

The \ggs+for+ instruction can be used for enumerating list objects. By default, lists are enumerated in the insertion order; enumeration in the reverse order is performed using the \ggs+>+ qualifier.

There are two ways for accessing element values:
\begin{itemize}
\item using the implicitly declared constants that receive the current attribute values;
\item declare explicitly constants that receive the current attribute values.
\end{itemize}

Given the list declaration:

\begin{galgas}
list @MyList {
  @string mFirstAttribute ;
  @bool mSecondAttribute ;
}
\end{galgas}

\subsection{Enumeration using the implicitly declared constants}

For every attribute, a constant of the same name is available in the \ggs+do+ instruction list. Theses constants receive the value of the corresponding attribute of the current element.

\begin{galgas}
for () in aList do
  # the mFirstAttribute constant receives the value
  # of the mFirstAttribute attribute of the current element,
  # and the mSecondAttribute constant receives the value
  # of the mSecondAttribute attribute of the current element.
end
\end{galgas}

\subsection{Enumeration using the explicitly declared constants}

The \ggs+for+ header declares a sequence of constants, corresponding to the attribute list of the \ggs+do+ declaration. Theses constants receive the value of the corresponding attribute of the current element.


\begin{galgas}
for (kString kBool) in aList do
  # the kString constant receives the value
  # of the mFirstAttribute attribute of the current element,
  # and the kBool constant receives the value
  # of the mSecondAttribute attribute of the current element.
end
\end{galgas}

\subsection{Enumeration in the reverse order}

In GALGAS 1.7.3 and later, you can enumerate a list in the reverse order using the \ggs+>+ qualifier:

\begin{galgas}
for > (kString kBool) in aList do
  ...
end
\end{galgas}




\section{Direct Access of an element attribute}

In GALGAS 1.7.5 and later, lists can be used as an array. Each element of a list is associated with an \ggs+@uint+ index, spanning from 0 to element count (value returned by \ggs+length+ getter) minus one.

The element retrieved with \ggs+first+ method is at index 0.

The element retrieved with \ggs+last+ method is at index equal to element count minus one.

\subsection{Read Access}

By default and for every attribute, a getter is provided to retrieve the value of this attribute for an element at a given index. For example, for an attribute named \emph{name}, the \emph{nameAtIndex} getter is provided. It accepts one \ggs+@uint+ argument, the value of the index.

You can disable the default getter generation, by using \ggs+%nogetter+.

For example:
\begin{galgas}
list @MyList {
  @string mFirstAttribute ;
  @bool mSecondAttribute %nogetter ;
}
...
@MyList aList = ... ;
@string s = [aList mFirstAttributeAtIndex !1] ;
\end{galgas}

One getter is available: \ggs+mFirstAttributeAtIndex+; the \ggs+mSecondAttributeAtIndex+ getter is not available.


\subsection{Write Access}

By default, no setter is provided for performing a direct write access to an attribute at a given index. You should use \ggs+%setter+ for enabling setter generation for a given attribute.

The setter name is the name of the attribute with the first letter capitalized, prefixed by \emph{set} and suffixed by \emph{AtIndex}: for an attribute named \emph{name}, the setter is named \emph{setNameAtIndex}. It accepts two arguments, the first one is the new attribute's value, the second one an \ggs+@uint+ argument, the value of the index.

For example:

\begin{galgas}
list @MyList {
  @string mFirstAttribute %setter ;
  @bool mSecondAttribute ;
}
...
@string s = ... ;
[!?aList setMFirstAttributeAtIndex !s !1] ;
\end{galgas}

One setter is available: \ggs+setMFirstAttributeAtIndex+; the \ggs+setMSecondAttributeAtIndex+ setter is not available.

\subsection{Example of read and write accesses}

\begin{galgas}
list @myList {
  @string name ;
}
...
@myList strList [emptyList] ;
strList += !"a" ;
strList += !"b" ;
strList += !"c" ;
strList += !"d" ;
@string s = [strList nameAtIndex !0] ;
log s ; # displays LOGGING s: <@string:"a">
s = [strList nameAtIndex !1] ;
log s ; # displays LOGGING s: <@string:"b">
s = [strList nameAtIndex !2] ;
log s ; # displays LOGGING s: <@string:"c">
s = [strList nameAtIndex !3] ;
log s ; # displays LOGGING s: <@string:"d">
[!?strList setNameAtIndex !"x" !0] ;
[!?strList setNameAtIndex !"y" !1] ;
[!?strList setNameAtIndex !"z" !2] ;
[!?strList setNameAtIndex !"t" !3] ;
s = [strList nameAtIndex !0] ;
log s ; # displays LOGGING s: <@string:"x">
s = [strList nameAtIndex !1] ;
log s ; # displays LOGGING s: <@string:"y">
s = [strList nameAtIndex !2] ;
log s ; # displays LOGGING s: <@string:"z">
s = [strList nameAtIndex !3] ;
log s ; # displays LOGGING s: <@string:"t">
\end{galgas}


\section{Types liste prédéfinis}

\subsectionTypePredefiniLabelIndex{2stringlist}

Le type \ggs!@2stringlist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @2stringlist {
  @string mValue0
  @string mValue1
}
\end{galgasbox}








\subsectionTypePredefiniLabelIndex{2lstringlist}

Le type \ggs!@2lstringlist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @2lstringlist {
  @lstring mValue0
  @lstring mValue1
}
\end{galgasbox}








\subsectionTypePredefiniLabelIndex{bigintlist}

Le type \ggs!@bigintlist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @bigintlist {
  @bigint mValue
}
\end{galgasbox}








\subsectionTypePredefiniLabelIndex{functionlist}

Le type \ggs!@functionlist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @functionlist {
  @function mValue
}
\end{galgasbox}








\subsectionTypePredefiniLabelIndex{lbigintlist}

Le type \ggs!@lbigintlist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @lbigintlist {
  @lbigint mValue
}
\end{galgasbox}










\subsectionTypePredefiniLabelIndex{luintlist}

Le type \ggs!@luintlist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @luintlist {
  @luint mValue
}
\end{galgasbox}










\subsectionTypePredefiniLabelIndex{lstringlist}

Le type \ggs!@lstringlist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @lstringlist {
  @lstring mValue
}
\end{galgasbox}







\subsectionTypePredefiniLabelIndex{objectlist}

Le type \ggs!@objectlist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @objectlist {
  @object mValue
}
\end{galgasbox}





\subsectionTypePredefiniLabelIndex{stringlist}

Le type \ggs!@stringlist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @stringlist {
  @string mValue
}
\end{galgasbox}







\subsectionTypePredefiniLabelIndex{typelist}

Le type \ggs!@typelist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @typelist {
  @type mValue
}
\end{galgasbox}





\subsectionTypePredefiniLabelIndex{uintlist}

Le type \ggs!@uintlist! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @uintlist {
  @uint mValue
}
\end{galgasbox}






\subsectionTypePredefiniLabelIndex{uint64list}

Le type \ggs!@uint64list! est implicitement déclaré de la façon suivante :

\begin{galgasbox}
list @uint64list {
  @uint64 mValue
}
\end{galgasbox}

