%!TEX encoding = UTF-8 Unicode
%!TEX root = ../galgas-book.tex

%--------------------------------------------------------------
\chapterLabel{Extensions}{extensions}\index{Extensions}
%-------------------------------------------------------------

\emph{Categories} are the way for adding \emph{getters}, \emph{methods} and \emph{setters} to any type. They are defined outside type declarations.

You can declare for any type:
\begin{itemize}
\item \emph{category getters};
\item \emph{category methods};
\item \emph{category setters}.
\end{itemize}

Additional features are available for classes and are described in \refSectionPage{categoriesAndClasses}.


A \emph{category getter} is called in an expression. As expressions have no side-effect, a category getter cannot change current object's value.

A \emph{category method} is called by the \emph{method call instruction} (\refSectionPage{methodCallInstruction}). A category method cannot modify current object's value.

A \emph{category setter} is called by the \emph{setter call instruction} (\refSectionPage{setterCallInstruction}). A category setter can modify current object's value.

Within the category getter, method and setter instruction list, the \lstinline[language=galgas]!self! key word is allowed in any expression. It represents a copy of the current object. Of course, the current is lazily copied only when required.

The \lstinline[language=galgas]!self! key word is just a syntactic tag for representing a write or a read/write access to the current object. Using \lstinline[language=galgas]!self! is not allowed in category methods and category getters since they cannot modify the current object. Using \lstinline[language=galgas]!self! in category setters is explained in \refSectionPage{categorySetter}. 


A category getter, method and setter can be declared in:
\begin{itemize}
\item a \emph{semantics} component;
\item a \emph{syntax} component;
\item a \emph{program} component.
\end{itemize}

A declared category getter, method and setter has a global scope, meaning it is available in the current component, and in any component that includes it directly or indirectly.

A type does not accept several category getters with the same name. During compilation of the project file, the project global checking mechanism detects such declarations and issues an error. Consequently, it is forbidden to declare a category getter with the same name than a predefined getter: the compiler issues an error on on a such declaration. The same rules apply on category methods and category setters.

However, it is safe to declare for a given type a category getter, a category method and a category setter with the same name. GALGAS compiler uses different naming spaces for them, and call syntax are different, so there is no ambiguity.










\sectionLabel{Category getter}{categoryGetter}

A category getter is declared like a function, but its header names a type and a getter name. As a function, it accepts zero, one or more input and constant input formal parameters.

For example, the following code add a getter to the \ggs+uint64+ that computes the square of its value:
\begin{lstlisting}[language=galgas]
getter @uint64 square -> @uint64 outResult {
  outResult = self * self
}
\end{lstlisting}

This getter is called like a predefined getter:
\begin{lstlisting}[language=galgas]
@uint64 v = 7L
log "Square of 7": [v square] # LOGGING Square of 7 : <@uint64:49>
\end{lstlisting}

You can add a category getter to a list :
\begin{lstlisting}[language=galgas]
getter @uintlist sum -> @uint outResult {
  outResult = 0
  for self do
    outResult = outResult + mValue
  }
}
\end{lstlisting}

For counting the number of element values greater than the value given in argument:
\begin{lstlisting}[language=galgas]
getter @uintlist countValuesGreaterThan
  ?let @uint inTestValue -> @uint outResult
{
  outResult = 0
  for self do
    if mValue > inTestValue then
      outResult ++
    end if
  }
}
\end{lstlisting}

When used with a struct or class type, current object attributes values can be read by naming the attribute in an expression. For example, the \refTypePredefini{lstring} has an attribute 
\lstinline[language=galgas]!string! whose type is \ggs+@string+. The following getter returns the value of this attribute, appended with the \lstinline[language=galgas]?" !"? string:
\begin{lstlisting}[language=galgas]
getter @lstring op -> @string outResult {
  outResult = string . " !"
}
\end{lstlisting}







\sectionLabel{Category method}{categoryMethod}

A category method is declared like a routine, but its header names a type and a method name. As a routine, it accepts zero, one or more input, output, input/output constant input formal parameters.

For example, the following code add a method to the \ggs+uint64+ that computes the square of its value:
\begin{lstlisting}[language=galgas]
method @uint64 square !@uint64 outResult {
  outResult = self * self
}
\end{lstlisting}

This getter is called like a predefined method:
\begin{lstlisting}[language=galgas]
@uint64 v
[7L square ?v]
log "Square of 7": v # LOGGING Square of 7 : <@uint64:49>
\end{lstlisting}

You can add a category method to a list :
\begin{lstlisting}[language=galgas]
method @uintlist sum !@uint outResult {
  outResult = 0
  for self do
    outResult = outResult + mValue
  }
}
\end{lstlisting}

For counting the number of element values greater than the value given in argument:
\begin{lstlisting}[language=galgas]
method @uintlist countValuesGreaterThan
  ?let @uint inTestValue
  !@uint outResult
{
  outResult = 0
  for self do
    if mValue > inTestValue then
      outResult ++
    end if
  }
}
\end{lstlisting}

When used with a struct or class type, current object attributes values can be read by naming the attribute in an expression. For example, the \refTypePredefini{lstring} has an attribute 
\lstinline[language=galgas]!string! whose type is \ggs+@string+. The following method returns the value of this attribute, appended with the \lstinline[language=galgas]?" !"? string:
\begin{lstlisting}[language=galgas]
method @lstring op !@string outResult {
  outResult = string . " !"
}
\end{lstlisting}











\sectionLabel{Category setter}{categorySetter}

A category method is declared like a routine, but its header names a type and a setter name. As a routine, it accepts zero, one or more output, input/output, input and constant input formal parameters. Unlike a category method, a category setter can change the value of the current object.

For structure and classes types, attributes can be read, written, read / written. For example :
\begin{lstlisting}[language=galgas]
setter @lstring appendInt ?let @uint inValue {
  string .= [inValue string]
}
\end{lstlisting}


The \lstinline[language=galgas]!self! key word is used as a syntactic tag for denoting a read/write or a write access on the current object. This key word is syntactically accepted in the following constructs:
\begin{enumerate}
\item the \emph{setter call instruction} (\refSectionPage{setterCallInstruction});
\item the \emph{concat instruction} (\refSectionPage{concatInstruction});
\item the \emph{increment instruction} (\refSectionPage{incrementInstruction});
\item the \emph{decrement instruction} (\refSectionPage{decrementInstruction});
\item the \emph{assignment instruction} (\refSectionPage{assignmentInstruction}).
\end{enumerate}

Example of using \lstinline[language=galgas]!self! in setter call instruction; this setter prepends the square of argument value to the \galgas{@uint64list} value:
\begin{lstlisting}[language=galgas]
setter @uint64list prependSquare ?let @uint64 inValue {
  [!?self prependValue !inValue * inValue]
}
\end{lstlisting}


Example of using \lstinline[language=galgas]!self! in the append instruction; this setter appends the square of argument value to the \galgas{@uintlist} value:
\begin{lstlisting}[language=galgas]
setter @uintlist appendSquare ?let @uint inValue {
  self += !inValue * inValue
}
\end{lstlisting}
This construct is valid only for types that handle the \lstinline[language=galgas]!+=! operator.


Example of using \lstinline[language=galgas]!self! in the concat instruction; this setter appends to the string all items of the \galgas{@stringlist} argument value:
\begin{lstlisting}[language=galgas]
setter @string concatList ?let @stringlist inList {
  for inList do
    self .= mValue
  }
}
\end{lstlisting}
This construct is valid only for types that handle the \lstinline[language=galgas]!.=! operator.




Example of using \lstinline[language=galgas]!self! in the increment instruction; this setter increments the receiver's value:
\begin{lstlisting}[language=galgas]
setter @uint increment {
  self ++
}
\end{lstlisting}
This construct is valid only for types that handle the \lstinline[language=galgas]!++! operator, such as \refTypePredefini{uint}, \ggs+uint64+, \refTypePredefini{sint}, \refTypePredefini{sint64}.





Example of using \lstinline[language=galgas]!self! in the assignment instruction; this setter removes all odd values of the receiver:
\begin{lstlisting}[language=galgas]
setter @uintlist removeOddValues {
  @uintlist listWithEvenValues [emptyList]
  for self do
    if (mValue & 1) == 0 then
      listWithEvenValues += !mValue
    end if
  }
  self = listWithEvenValues
}
\end{lstlisting}
This construct is valid only for types, but class types.










\sectionLabel{Categories and classes}{categoriesAndClasses}


Additional features are available only for classes; in addition to category getters, methods and setters described in the above sections, you can declare:
\begin{itemize}
\item \emph{abstract} category getters, methods, setters;
\item \emph{overriding} category getters, methods, setters;
\item \emph{overriding abstract} category getters, methods, setters.
\end{itemize}

\emph{Abstract} category getters, methods, setters and \emph{overriding abstract} category getters, methods, setters do not contain any instruction list: they act as \emph{prototypes}.

Examples of \emph{abstract} category getters, methods, setters declarations :
{\lstset{emph={getterName, methodName, setterName}, emphstyle=\galgasEmphStyle}
\begin{lstlisting}[language=galgas]
abstract getter @aType getterName
  ?anOtherType aParameter
  -> @resultType outResult
;

abstract method @aType methodName
  ?anOtherType aParameter
;

abstract setter @aType setterName
  ?anOtherType aParameter
;
\end{lstlisting}
}

Examples of \emph{overriding} category getters, methods, setters declarations :
{\lstset{emph={instructions, getterName, methodName, setterName}, emphstyle=\galgasEmphStyle}
\begin{lstlisting}[language=galgas]
override getter @aType getterName
  ?anOtherType aParameter
  -> @resultType outResult
{
  instructions
}

override method @aType methodName
  ?anOtherType aParameter
{
  instructions
}

override setter @aType setterName
  ?anOtherType aParameter
{
  instructions
}
\end{lstlisting}
}

Examples of \emph{overriding abstract} category getters, methods, setters declarations :
{\lstset{emph={getterName, methodName, setterName}, emphstyle=\galgasEmphStyle}
\begin{lstlisting}[language=galgas]
override abstract getter @aType getterName
  ?@anOtherType aParameter
  -> @resultType outResult
;

override abstract method @aType methodName
  ?anOtherType aParameter
;

override abstract setter @aType setterName
  ?anOtherType aParameter
;
\end{lstlisting}
}


Neither \emph{abstract} category getters, methods, setters, neither \emph{overriding abstract} category getters, methods, setters cannot be declared for concrete classes. Any kind of category getter, method, setter can be declared for abstract classes.

If an \emph{abstract} category getter, method, setter, or an \emph{overriding abstract} category getter, method, setter is declared for an abstract class, it should be also declared as \emph{overriding} with the same name for every first concrete successor class.

A category getter, method, setter that has the same name as a category getter, method, setter declared for one of its super classes should be declared as \emph{overriding}.

An abstract category getter, method, setter that has the same name as a category getter, method, setter declared for one of its super classes should be declared as \emph{overriding abstract}.

The following example illustrates how theses rules should be applied. In the \refFigure{}{figureCategoryExample}, four classes are shown. An arrow links a class to its super class. The \lstinline[language=galgas]!@A! and \lstinline[language=galgas]!@C! classes are abstract. \lstinline[language=galgas]!m1! is a name for any getter, method or setter.

\begin{figure}[t]
  \centering
   \begin{tikzpicture}[every state/.style={draw, circle}, node distance=1.5cm, <-, thick]
     \node at (0, 4.5) [state, accepting] (A) {\lstinline[language=galgas]!@A!};
     \node at (0, 3) [state]            (B) {\lstinline[language=galgas]!@B!};
     \node at (0, 1.5) [state, accepting] (C) {\lstinline[language=galgas]!@C!};
     \node at (0, 0) [state]            (D) {\lstinline[language=galgas]!@D!};
     \path (A) edge (B) ;
     \path (B) edge (C) ;
     \path (C) edge (D) ;
     \node at (1.5, 4.5) [right] {\lstinline[language=galgas]!abstract @A m1!} ;
     \node at (1.5, 3) [right] {\lstinline[language=galgas]!override @B m1!} ;
     \node at (1.5, 1.5) [right] {\lstinline[language=galgas]!override abstract @C m1!} ;
     \node at (1.5, 0) [right] {\lstinline[language=galgas]!override @D m1!} ;
   \end{tikzpicture}
  \caption{inheritance graph and categories}
  \labelFigure{figureCategoryExample}
  \ligne
\end{figure}

\lstinline[language=galgas]!m1! is declared as \lstinline[language=galgas]!abstract! for the \lstinline[language=galgas]!@A! class. It is allowed since \lstinline[language=galgas]!@A! is abstract. Consequently, the concrete \lstinline[language=galgas]!@B! class should override \lstinline[language=galgas]!m1!. The \lstinline[language=galgas]!@C! class is also abstract, and \lstinline[language=galgas]!m1! can be declared as \lstinline[language=galgas]!abstract! for this class. But as it has been also declared for one of theses super class, it should also declared as \lstinline[language=galgas]!override!. As \lstinline[language=galgas]!@D! is concrete, \lstinline[language=galgas]!m1! should be declared for this class with \lstinline[language=galgas]!override! tag.










