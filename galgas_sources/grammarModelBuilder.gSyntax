#---------------------------------------------------------------------------*
#                                                                           *
#  'grammar' component parser, as model builder                             *
#                                                                           *
#  Copyright (C) 2007, ..., 2007 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax grammarModelBuilder :
with "galgas_scanner.gLexique" ;

import lexique galgas_scanner in "galgas_scanner.gLexique" ;
import semantics grammarMetamodel in "grammarMetamodel.gSemantics" ;

#---------------------------------------------------------------------------*

nonterminal <grammar_component_start_symbol>
  !@grammarComponentRoot outGrammarComponentRoot
;

nonterminal <grammar_start_symbol_label>
  ??@lstring inLabelName
  ?!@nonTerminalLabelList ioLabelList
;

nonterminal <label_formal_parameter>
  !@lstring outArgumentName
  ?!@formalParameterList ioStartSymbolLabelFormalParameterList
;

#---------------------------------------------------------------------------*
#                                                                           *
#   G R A M M A R    C O M P O N E N T                                      *
#                                                                           *
#---------------------------------------------------------------------------*

rule <grammar_component_start_symbol>
  !@grammarComponentRoot outGrammarComponentRoot
:
  $grammar$ ;
  $identifier$ ? @lstring mGrammarName ;
  $literal_string$ ? @lstring mGrammarClass ;
  $:$ ;
#--- New style imports
  @lstringlist syntaxComponents [emptyList] ;
  repeat
    $with$ ;
    $literal_string$ ? @lstring syntaxComponent ;
    $;$ ;
    syntaxComponents += !syntaxComponent ;
  while
  end repeat ;
#--- Old style imports
  repeat
  while
    $import$ ;
    select
      $semantics$ ;
    or
      $lexique$ ;
    or
      $grammar$ ;
    or
      $syntax$ ;
    or
      $option$ ;
    end select ;
    $identifier$ ?* ;
    $in$ ;
    $literal_string$ ? * ;
    $;$ ;
  end repeat ;
#--- Root symbol
  $root$ ;
  $non_terminal_symbol$ ?@lstring  mStartSymbol ;
  @lstring labelName [new !"" !here] ;
  @nonTerminalLabelList mLabelList [emptyList] ;
  repeat
    <grammar_start_symbol_label> !labelName !? mLabelList ;
  while
    $label$ ;
    $identifier$ ? labelName ;
  end repeat ;
  $;$ ;
#--- Unused non terminal symbols
  @lstringlist mUnusedNonterminalList [emptyList] ;
  repeat
  while
   $unused$ ;
   repeat
     $non_terminal_symbol$ ? @lstring nonterminalSymbolName ;
     mUnusedNonterminalList += ! nonterminalSymbolName ;
   while
     $,$ ;
   end repeat ;
   $;$ ;
  end repeat ;
#---
  $end$ ;
  outGrammarComponentRoot := [@grammarComponentRoot new
    !mGrammarName
    !mGrammarClass
    !syntaxComponents
    !mStartSymbol
    !mLabelList
    !mUnusedNonterminalList
    !here
  ] ;
  $grammar$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <grammar_start_symbol_label>
  ??@lstring inLabelName
  ?!@nonTerminalLabelList ioLabelList
:
  @stringset argumentNameSet [emptySet] ;
  @formalParameterList mFormalParameterList [emptyList] ;
  repeat
  while
    @lstring argumentName ;
    <label_formal_parameter> ?argumentName !?mFormalParameterList ;
    if [argumentNameSet hasKey ![argumentName string]] then
      error argumentName: "there is already an argument named '" . argumentName . "'" ;
    end if ;
    argumentNameSet += ![argumentName string] ;
  end repeat ;
  ioLabelList += !inLabelName !mFormalParameterList ;
end rule ;

#---------------------------------------------------------------------------*

rule <label_formal_parameter>
  !@lstring outArgumentName
  ?!@formalParameterList ioStartSymbolLabelFormalParameterList
:
  $?$ ;
  $type_name$ ?@lstring mTypeName ;
  select
  or
   $unused$ ;
  end select ;
  $identifier$ ?outArgumentName ;
  ioStartSymbolLabelFormalParameterList +=
    ![@formalArgumentPassingMode argumentIn]
    !mTypeName
    !outArgumentName
    !false
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <label_formal_parameter>
  !@lstring outArgumentName
  ?!@formalParameterList ioStartSymbolLabelFormalParameterList
:
  $??$ ;
  $type_name$ ?@lstring mTypeName ;
  select
  or
   $unused$ ;
  end select ;
  $identifier$ ?outArgumentName ;
  ioStartSymbolLabelFormalParameterList +=
    ![@formalArgumentPassingMode argumentConstantIn]
    !mTypeName
    !outArgumentName
    !false
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <label_formal_parameter>
  !@lstring outArgumentName
  ?!@formalParameterList ioStartSymbolLabelFormalParameterList
:
  $?!$ ;
  $type_name$ ?@lstring mTypeName ;
  select
  or
   $unused$ ;
  end select ;
  $identifier$ ?outArgumentName ;
  ioStartSymbolLabelFormalParameterList +=
    ![@formalArgumentPassingMode argumentInOut]
    !mTypeName
    !outArgumentName
    !false
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <label_formal_parameter>
  !@lstring outArgumentName
  ?!@formalParameterList ioStartSymbolLabelFormalParameterList
:
  $!$ ;
  $type_name$ ?@lstring mTypeName ;
  $identifier$ ?outArgumentName ;
  ioStartSymbolLabelFormalParameterList +=
    ![@formalArgumentPassingMode argumentOut]
    !mTypeName
    !outArgumentName
    !false
  ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;