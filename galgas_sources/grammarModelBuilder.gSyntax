#---------------------------------------------------------------------------*
#                                                                           *
#  'grammar' component parser, as model builder                             *
#                                                                           *
#  Copyright (C) 2007, ..., 2007 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax grammarModelBuilder ("galgas_scanner.gLexique") :

import "grammarMetamodel.gSemantics" ;

#---------------------------------------------------------------------------*

nonterminal <grammar_component_start_symbol>
  !@grammarComponentAST outGrammarComponentRoot
;

nonterminal <grammar_start_symbol_label>
  ??@lstring inLabelName
  ?!@nonTerminalLabelListAST ioLabelList
;

nonterminal <label_formal_parameter>
  !@lstring outArgumentName
  ?!@formalParameterListAST ioStartSymbolLabelFormalParameterList
;

#---------------------------------------------------------------------------*
#                                                                           *
#   G R A M M A R    C O M P O N E N T                                      *
#                                                                           *
#---------------------------------------------------------------------------*

rule <grammar_component_start_symbol>
  !@grammarComponentAST outGrammarComponentRoot
:
  $grammar$ ;
  $identifier$ ? @lstring mGrammarName ;
  $literal_string$ ? @lstring mGrammarClass ;
  $:$ ;
#--- New style imports
  @lstringlist syntaxComponents [emptyList] ;
  repeat
    $with$ ;
    $literal_string$ ? @lstring syntaxComponent ;
    $;$ ;
    syntaxComponents += !syntaxComponent ;
  while
  end repeat ;
#--- Old style imports
  repeat
  while
    $import$ ;
    select
      $semantics$ ;
    or
      $lexique$ ;
    or
      $grammar$ ;
    or
      $syntax$ ;
    or
      $option$ ;
    end select ;
    $identifier$ ?* ;
    warning here:"obsolete GALGAS 1 'import' declaration: useless in GALGAS 2" ;
    $in$ ;
    $literal_string$ ? * ;
    $;$ ;
  end repeat ;
#--- Root symbol
  $root$ ;
  $non_terminal_symbol$ ?@lstring  mStartSymbol ;
  @bool startSymbolHasParseLabel := false ;
  @nonTerminalLabelListAST mLabelList [emptyList] ;
  <grammar_start_symbol_label> ![@lstring new !"" !here] !?mLabelList ;
  repeat
  while
    $label$ ;
    select
      $identifier$ ?@lstring labelName ;
      <grammar_start_symbol_label> !labelName !?mLabelList ;
    or
      $parse$ ;
      if startSymbolHasParseLabel then
        error here : "'parse' label has been already declared" ;
      end if ;
      startSymbolHasParseLabel := true ;
    end select ;
  end repeat ;
  $;$ ;
#--- Unused non terminal symbols
  @lstringlist mUnusedNonterminalList [emptyList] ;
  repeat
  while
   $unused$ ;
   repeat
     $non_terminal_symbol$ ? @lstring nonterminalSymbolName ;
     mUnusedNonterminalList += ! nonterminalSymbolName ;
   while
     $,$ ;
   end repeat ;
   $;$ ;
  end repeat ;
#---
  $end$ ;
  outGrammarComponentRoot := [@grammarComponentAST new
    !mGrammarName
    !mGrammarClass
    !syntaxComponents
    !mStartSymbol
    !mLabelList
    !startSymbolHasParseLabel
    !mUnusedNonterminalList
    !here
  ] ;
  $grammar$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <grammar_start_symbol_label>
  ??@lstring inLabelName
  ?!@nonTerminalLabelListAST ioLabelList
:
  @stringset argumentNameSet [emptySet] ;
  @formalParameterListAST mFormalParameterList [emptyList] ;
  repeat
  while
    @lstring argumentName ;
    <label_formal_parameter> ?argumentName !?mFormalParameterList ;
    if [argumentNameSet hasKey ![argumentName string]] then
      error argumentName: "there is already an argument named '" . argumentName . "'" ;
    end if ;
    argumentNameSet += ![argumentName string] ;
  end repeat ;
  ioLabelList += !inLabelName !mFormalParameterList !here ;
end rule ;

#---------------------------------------------------------------------------*

rule <label_formal_parameter>
  !@lstring outArgumentName
  ?!@formalParameterListAST ioStartSymbolLabelFormalParameterList
:
  $?$ ;
  $type_name$ ?@lstring mTypeName ;
  select
  or
   $unused$ ;
  end select ;
  $identifier$ ?outArgumentName ;
  ioStartSymbolLabelFormalParameterList +=
    ![@formalArgumentPassingModeAST argumentIn]
    !mTypeName
    !outArgumentName
    !false
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <label_formal_parameter>
  !@lstring outArgumentName
  ?!@formalParameterListAST ioStartSymbolLabelFormalParameterList
:
  $??$ ;
  $type_name$ ?@lstring mTypeName ;
  select
  or
   $unused$ ;
  end select ;
  $identifier$ ?outArgumentName ;
  ioStartSymbolLabelFormalParameterList +=
    ![@formalArgumentPassingModeAST argumentConstantIn]
    !mTypeName
    !outArgumentName
    !false
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <label_formal_parameter>
  !@lstring outArgumentName
  ?!@formalParameterListAST ioStartSymbolLabelFormalParameterList
:
  $?!$ ;
  $type_name$ ?@lstring mTypeName ;
  select
  or
   $unused$ ;
  end select ;
  $identifier$ ?outArgumentName ;
  ioStartSymbolLabelFormalParameterList +=
    ![@formalArgumentPassingModeAST argumentInOut]
    !mTypeName
    !outArgumentName
    !false
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <label_formal_parameter>
  !@lstring outArgumentName
  ?!@formalParameterListAST ioStartSymbolLabelFormalParameterList
:
  $!$ ;
  $type_name$ ?@lstring mTypeName ;
  $identifier$ ?outArgumentName ;
  ioStartSymbolLabelFormalParameterList +=
    ![@formalArgumentPassingModeAST argumentOut]
    !mTypeName
    !outArgumentName
    !false
  ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;