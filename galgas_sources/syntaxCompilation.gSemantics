#---------------------------------------------------------------------------*
#                                                                           *
#  Syntax component compilation                                             *
#                                                                           *
#  Copyright (C) 2009, ..., 2014 Pierre Molinaro.                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

semantics syntaxCompilation :
import "semanticsCompilation.gSemantics" ;
import "semanticGeneration.gSemantics" ;

#---------------------------------------------------------------------------*
#                                                                           *
#           C O M P I L E    S Y N T A X    C O M P O N E N T               *
#                                                                           *
#---------------------------------------------------------------------------*

routine compileSyntaxComponentFromAST
  ??@lstring inSourceFile
  ??@string inOutputDirectory
  ??@galgas3SyntaxComponentListAST-element inSyntaxComponentRoot
  ?!@parsedComponentStruct ioParsedComponentStruct
:
  const @string basename := [[inSourceFile lastPathComponent] stringByDeletingPathExtension] ;
#--- Check component name
  const @lstring componentName := [inSyntaxComponentRoot mSyntaxComponentName] ;
  if [componentName string] != basename then
    warning componentName: "GALGAS checks "
      "the component name ('" . [componentName string]
    . "') against the source file base name ('" . basename . "'): they should be identical"
    ;
  end if ; 
#--- Recursively parse all imported files
  @semanticDeclarationListAST semanticDeclarationList := [inSyntaxComponentRoot mSemanticDeclarationList] ;
  @stringset parsedFileSet [setWithString ![inSourceFile string]] ;
  @lstringlist optionComponentFileNameList [emptyList] ;
  @grammarComponentASTList grammarComponentASTList [emptyList] ;
  @stringlist headerIncludeList [emptyList] ;
  @stringlist implementationIncludeList [emptyList] ;
  recursivelyImportSemanticComponentAndCheckNoCircularityInImportSemantics
    !inSourceFile
    !?ioParsedComponentStruct
    !?optionComponentFileNameList
    !?grammarComponentASTList
    !?semanticDeclarationList
    !?parsedFileSet
    !?headerIncludeList
    !?implementationIncludeList
    ![inSyntaxComponentRoot mImportedComponentFileNameList]
    ![inSourceFile stringByDeletingLastPathComponent]
  ;
#------ Add predefined types to semantics declarations
  @semanticDeclarationListAST semanticDeclarationListWithPredefinedTypes := semanticDeclarationList ;
  appendPredefinedTypesASTs !?semanticDeclarationListWithPredefinedTypes ;
#------ Build semantic context
  buildSemanticContext
    !semanticDeclarationListWithPredefinedTypes
    ?@semanticContext semanticContext
  ;
#--- Perform semantic analysis of grammar components
  buildGrammarComponentMapForSemanticAnalysis
    !grammarComponentASTList
    !?semanticContext
  ;
#--- Build option component map for analysis
  buildOptionComponentMapForSemanticAnalysis
    !optionComponentFileNameList
    !?ioParsedComponentStruct
    !?semanticContext->mOptionComponentMapForSemanticAnalysis
  ;
#--- Build lexique component map for semantic analysis
  buildLexiqueComponentMapForSemanticAnalysis
    !ioParsedComponentStruct
    ?@lexiqueComponentMapForSemanticAnalysis lexiqueMap
  ;
  semanticContext->mLexiqueComponentMapForSemanticAnalysis := lexiqueMap ;
#--- Parse lexique component
  const lexiqueFilePath := [@lstring new
    ![[inSyntaxComponentRoot mImportedLexiqueFilePath] absolutePathFromPath ![inSourceFile stringByDeletingLastPathComponent]]
    ![[inSyntaxComponentRoot mImportedLexiqueFilePath] location]
  ] ;
  @galgas3LexiqueComponentListAST-element lexiqueComponentRoot ;
  parseLexiqueComponent
    !lexiqueFilePath
    !?ioParsedComponentStruct
    ?lexiqueComponentRoot
  ;
#--- Build token map
  @terminalMap terminalMap ;
  buildTerminalMap !lexiqueComponentRoot ?terminalMap ;
#--------------------------------------- Build "indexing" set
  @stringset indexNameSet [emptySet] ;
  foreach [lexiqueComponentRoot mIndexingListAST] do
    indexNameSet += ![mIndexName string] ;
  end foreach ;
  const @bool hasIndexing := not [[lexiqueComponentRoot mIndexingDirectory] isNowhere] ;
#--- Build semantic context from rule headers and non terminal declaration
  if [@uint errorCount] == 0 then
    const @string lexiqueName := [[[inSyntaxComponentRoot mImportedLexiqueFilePath] lastPathComponent] stringByDeletingPathExtension] ;
    @semanticDeclarationSortedListForGeneration decoratedDeclarationListForGeneration ;
    const @predefinedTypes predefinedTypes := buildPredefinedTypes [!semanticContext] ;
    @syntaxDeclarationForGeneration syntaxDeclarationForGeneration ;
    semanticAnalysisOfSyntaxComponent
      ![inSyntaxComponentRoot mNonterminalDeclarationList]
      ![inSyntaxComponentRoot mRuleList]
      ![componentName string]
      !lexiqueName
      !semanticContext
      !predefinedTypes
      !terminalMap
      !indexNameSet
      !hasIndexing
      !inSyntaxComponentRoot->mHasTranslateFeature
      ?syntaxDeclarationForGeneration
    ;
  #------ Semantic Analysis
    performSemanticAnalysis
      !inOutputDirectory
      ![inSyntaxComponentRoot mSemanticDeclarationList]
      !semanticContext
      ?decoratedDeclarationListForGeneration
    ;
    decoratedDeclarationListForGeneration +=
      !"syntax"
      !syntaxDeclarationForGeneration
      !""
    ;
  #------ Generate Header File
    if [@uint errorCount] == 0 then
      var headerString := [filewrapper syntaxFileGenerationTemplate.syntaxFileHeader
        ![componentName string]
        !lexiqueName
        !headerIncludeList
      ] ;
      foreach decoratedDeclarationListForGeneration do
        var unusedInclusionSet := [@stringset emptySet] ;
        [mDeclaration appendDeclaration1 !?unusedInclusionSet ??@string code] ;
        headerString .= code ;
      end foreach ;
      foreach decoratedDeclarationListForGeneration do
      var inclusionSet2 := [@stringset emptySet] ;
      [mDeclaration appendDeclaration2 !false !inOutputDirectory !?inclusionSet2 ??@string headerString2] ;
      headerString .= headerString2 ;
      end foreach ;
      [@string generateFileWithPattern
        !inOutputDirectory
        ![componentName string] . ".h"
        !"//"
        !"\n\n" # Defaut user zone1
        !headerString
        !"\n\n" # Defaut user zone2
        !"#endif\n"
      ] ;
    #------ Generate Implementation File
      if [@uint errorCount] == 0 then
        @string implementationString := [filewrapper syntaxFileGenerationTemplate.syntaxFileImplementation
          ![componentName string]
          !implementationIncludeList
        ] ;
        foreach decoratedDeclarationListForGeneration do
          implementationString .= [mDeclaration appendTypeGenericImplementation] ;
          var unusedInclusionSet := [@stringset emptySet] ;
          [mDeclaration appendSpecificImplementation ![@unifiedTypeMap emptyMap] !?unusedInclusionSet ??@string code] ;
          implementationString += code ;
        end foreach ;
        [@string generateFileWithPattern
          !inOutputDirectory
          ![componentName string] . ".cpp"
          !"//"
          !"\n\n" # Defaut user zone1
          !implementationString
          !"\n\n" # Defaut user zone2
          !"\n"
        ] ;
      end if ;
    end if ;
  end if ;
end routine ;

#---------------------------------------------------------------------------*

routine compileSyntaxComponentFromSourceFile
  ??@lstring inSourceFile
  ??@string inOutputDirectory
  ?!@parsedComponentStruct ioParsedComponentStruct
:
#--- Parse source file
  const basename := [[inSourceFile lastPathComponent] stringByDeletingPathExtension] ;
  const basenameKey := [@lstring new !basename !here] ;
  @galgas3SyntaxComponentListAST-element syntaxComponentRoot ;
  if [[ioParsedComponentStruct mParsedSyntaxComponentMap] hasKey ! basename] then
    [[ioParsedComponentStruct mParsedSyntaxComponentMap] searchKey !basenameKey ?* ?syntaxComponentRoot] ;
  else
    grammar syntaxSLRgrammar in inSourceFile ?syntaxComponentRoot ;
    [!?ioParsedComponentStruct->mParsedSyntaxComponentMap insertKey !basenameKey !inSourceFile !syntaxComponentRoot] ;
  end if ;
#---
  compileSyntaxComponentFromAST
    !inSourceFile
    !inOutputDirectory
    !syntaxComponentRoot
    !?ioParsedComponentStruct
  ;
end routine ;

#---------------------------------------------------------------------------*
#                                                                           *
#           G R O U P    S Y N T A X    C O M P O N E N T S                 *
#                                                                           *
#---------------------------------------------------------------------------*

routine groupSyntaxComponentsGalgas3
  ??@galgas3SyntaxComponentListAST inAllSyntaxComponents
  ??@syntaxExtensions inSyntaxExtensions
  ?!@semanticDeclarationListAST ioDeclarationList
:
#--- Add extensions to syntax list
  @stringset componentNameSet := {} ;
  for component in inAllSyntaxComponents do
    componentNameSet += !component->mSyntaxComponentName->string ;
    var nonterminalDeclarationList := component->mNonterminalDeclarationList ;
    var ruleList := component->mRuleList ;
    for () ext_ in [inSyntaxExtensions listForKey !component->mSyntaxComponentName->string] do
      nonterminalDeclarationList += ext_mNonterminalDeclarationList ;
      ruleList += ext_mRuleList ;
    end for ;
    ioDeclarationList += ![@galgas3SyntaxComponentAST new
      !false # Is not predefined
      !component->mSyntaxComponentName
      !component->mImportedLexiqueFilePath
      !nonterminalDeclarationList
      !ruleList
      !component->mHasTranslateFeature
    ] ;
  end for ;
#--- Check all extensions are handled
  for (key theLists) in inSyntaxExtensions do
    if not [componentNameSet hasKey !key] then
      for () in theLists do
        error mSyntaxComponentName : "there is no '" . mSyntaxComponentName ."' syntax component" ;
      end for ;    
    end if ;
  end for ;
end routine ;

#---------------------------------------------------------------------------*

end semantics ;
