#---------------------------------------------------------------------------*
#                                                                           *
#  Copyright (C) 2014, ..., 2014 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax galgas3DeclarationsSyntax ("galgas3Scanner.gLexique") :
import "galgas_cli_options.gOption" ;
import "semanticsTypesForAST.gSemantics" ;

#------------------------------------------------------------------------------*

nonterminal <declaration_with_private>
  ??@bool inIsInternal
  ?!@galgas3DeclarationAST ioDeclarations
;

#------------------------------------------------------------------------------*
#                                                                              *
#          Start symbol                                                        *
#                                                                              *
#------------------------------------------------------------------------------*

rule <start_symbol>
 ?!@galgas3DeclarationAST ioDeclarations
:
  repeat
  while
    $;$ ;
  while
    <declaration> !?ioDeclarations ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#          Rules prototypes                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <formal_parameter_list> !@formalParameterListAST outFormalParameterList ;

nonterminal <semantic_instruction_list> !@semanticInstructionListAST outInstructionsList ;

nonterminal <formal_input_parameter_list> !@formalInputParameterListAST outFormalInputParameterList ;

#---------------------------------------------------------------------------*
#                                                                           *
#    A T T R I B U T E    I N    C O L L E C T I O N                        *
#                                                                           *
#---------------------------------------------------------------------------*

rule <attribute_declaration> ?!@attributeInCollectionListAST ioAttributeInCollectionList :
  $type_name$ ? @lstring mAttributeTypeName ;
  $identifier$ ? @lstring mAttributeName ;
  @lstringlist featureList [emptyList] ;
  repeat
  while
    $attribute$ ? @lstring featureName ;
    featureList += !featureName ;
  end repeat ;
  ioAttributeInCollectionList += !mAttributeTypeName !mAttributeName !featureList ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    L I S T M A P    D E C L A R A T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

#! Listmap Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $listmap$ ;
#--- Map type name
  $type_name$ ? @lstring mListmapTypeName indexing listmapDefinition ;
  $($ ;
  $type_name$ ? @lstring mAssociatedListTypeName ;
  $)$ ;
  ioDeclarations->mSemanticDeclarationList += ![@listmapDeclarationAST new
    !false # Is not predefined
    !mListmapTypeName
    !mAssociatedListTypeName
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    A R R A Y    D E C L A R A T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

#! Array Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $array$ ;
#--- Map type name
  $type_name$ ? @lstring arrayTypeName indexing arrayTypeDefinition ;
  $:$ ;
  $type_name$ ? @lstring elementListTypeName indexing arrayElementTypeReference ;
  $[$ ;
  $unsigned_literal_integer$ ? @luint dimension ;
  $]$ ;
  ioDeclarations->mSemanticDeclarationList += ![@arrayDeclarationAST new
    !false # Is not predefined
    !arrayTypeName
    !elementListTypeName
    !dimension
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M A P    D E C L A R A T I O N                                         *
#                                                                           *
#---------------------------------------------------------------------------*

#! Map Declaration

#---------------------------------------------------------------------------*

rule <insert_method_declaration> ?!@insertMethodListAST ioMapMethodList :
  $identifier$ ? @lstring mMethodName ;
  @lstring stateName ;
  select
    stateName := [@lstring new !"" !here] ;
  or
    $state$ ;
    $identifier$ ? stateName ;
  end select ;
  $error$ ;
  $message$ ;
  $literal_string$ ? @lstring mErrorMessage ;
  @lstring mShadowErrorMessage ;
  select
    mShadowErrorMessage := [@lstring new !"" !here] ;
  or
    $,$ ;
    $literal_string$ ? mShadowErrorMessage ;
  end select ;
  ioMapMethodList +=
    !mMethodName
    !mErrorMessage
    !mShadowErrorMessage
    !stateName
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $map$ ;
#--- Map type name
  $type_name$ ? @lstring mMapTypeName indexing mapDefinition ;
#--- Map properties
  ${$ ;
  @attributeInCollectionListAST mAttributeList [emptyList] ;
  @insertMethodListAST mInsertMethodList [emptyList] ;
  @mapSearchMethodListAST mSearchMethodList [emptyList] ;
  @mapRemoveMethodListAST mRemoveMethodList [emptyList] ;
  @insertOrReplaceDeclarationListAST insertOrReplaceDeclarationListAST [emptyList] ;
  repeat
  while
    <attribute_declaration> !? mAttributeList ;
  while
    $insert$ ;
    <insert_method_declaration> !? mInsertMethodList ;
  while
    $search$ ;
    $identifier$ ? @lstring mMethodName ;
    $error$ ;
    $message$ ;
    $literal_string$ ? @lstring mErrorMessage ;
    mSearchMethodList +=
      !mMethodName
      !mErrorMessage
      ![@lstring new !"" !here]
    ;
  while
    $remove$ ;
    $identifier$ ? @lstring mMethodName ;
    $error$ ;
    $message$ ;
    $literal_string$ ? @lstring mErrorMessage ;
    mRemoveMethodList +=
      !mMethodName
      !mErrorMessage
    ;
  while
    $insert$ ;
    $or$ ;
    $replace$ ;
    insertOrReplaceDeclarationListAST += !here ;
  end repeat ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@mapDeclarationAST new
    !false # Is not predefined
    !mMapTypeName
    !mAttributeList
    !mInsertMethodList
    !mSearchMethodList
    !mRemoveMethodList
    !insertOrReplaceDeclarationListAST
  ] ;
end rule ;

#---------------------------------------------------------------------------*

#! Unique Map Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $uniquemap$ ;
#--- Map type name
  $type_name$ ? @lstring mMapTypeName indexing uniquemapDefinition ;
  $type_name$ ??@lstring mapProxyTypeName indexing mapProxyDefinition ;
#--- Map properties
  ${$ ;
  @attributeInCollectionListAST mAttributeList [emptyList] ;
  @insertMethodListAST mInsertMethodList [emptyList] ;
  @mapSearchMethodListAST mSearchMethodList [emptyList] ;
  @mapStateList mapStateList [emptyList] ;
  @mapOverrideBlockListAST mapOverrideBlockListAST [emptyList] ;
  repeat
  while
    $state$ ;
    $identifier$ ? @lstring stateName ;
    @mapAutomatonMessageKind stateMessageKind ;
    @lstring stateMessage ;
    select
      stateMessageKind := [@mapAutomatonMessageKind noMessage] ;
      stateMessage := [@lstring new !"" !here] ;
    or
      $warning$ ;
      $literal_string$ ? stateMessage ;
      stateMessageKind := [@mapAutomatonMessageKind warningMessage] ;
    or
      $error$ ;
      $literal_string$ ? stateMessage ;
      stateMessageKind := [@mapAutomatonMessageKind errorMessage] ;
    end select ;
    ${$ ;
    @mapStateTransitionList transitionList [emptyList] ;
    repeat
    while
      $identifier$ ? @lstring actionName ;
      $->$ ;
      $identifier$ ? @lstring targetStateName ;
      @mapAutomatonMessageKind transitionMessageKind ;
      @lstring transitionMessage ;
      select
        transitionMessageKind := [@mapAutomatonMessageKind noMessage] ;
        transitionMessage := [@lstring new !"" !here] ;
      or
        $warning$ ;
        $literal_string$ ? transitionMessage ;
        transitionMessageKind := [@mapAutomatonMessageKind warningMessage] ;
      or
        $error$ ;
        $literal_string$ ? transitionMessage ;
        transitionMessageKind := [@mapAutomatonMessageKind errorMessage] ;
      end select ;
      transitionList +=
        !actionName
        !targetStateName
        !transitionMessageKind
        !transitionMessage
      ;
    end repeat ;
    mapStateList +=
      !stateName
      !stateMessageKind
      !stateMessage
      !transitionList
    ;
    $}$ ;
  while
    <attribute_declaration> !? mAttributeList ;
  while
    $insert$ ;
    <insert_method_declaration> !? mInsertMethodList ;
  while
    $search$ ;
    $identifier$ ? @lstring mMethodName ;
    @lstring actionName ;
    select
      actionName := [@lstring new !"" !here] ;
    or
      $do$ ;
      $identifier$ ? actionName ;
    end select ;
    $error$ ;
    $message$ ;
    $literal_string$ ? @lstring mErrorMessage ;
    mSearchMethodList +=
      !mMethodName
      !mErrorMessage
      !actionName
    ;
  while
    $override$ ;
    $identifier$ ? @lstring overrideBlockName ;
    @mapOverrideBlockDescriptorAST mapOverrideBlockDescriptor1AST [emptyList] ;
    ${$ ;
    repeat
    while
      $identifier$ ? @lstring sourceState1 ;
      $:$ ;
      $identifier$ ? @lstring sourceState2 ;
      $->$ ;
      $identifier$ ? @lstring targetState ;
      @mapAutomatonMessageKind messageKind ;
      @lstring messageText ;
      select
        messageKind := [@mapAutomatonMessageKind noMessage] ;
        messageText := [@lstring new !"" !here] ;
      or
        $warning$ ;
        messageKind := [@mapAutomatonMessageKind warningMessage] ;
        $literal_string$ ? messageText ;
      or
        $error$ ;
        messageKind := [@mapAutomatonMessageKind errorMessage] ;
        $literal_string$ ? messageText ;
      end select ;
      mapOverrideBlockDescriptor1AST += !sourceState1 !sourceState2 !targetState !messageKind !messageText ;
    end repeat ;
    $}$ ;
    @mapOverrideBlockDescriptorAST mapOverrideBlockDescriptor2AST [emptyList] ;
    ${$ ;
    repeat
    while
      $identifier$ ? @lstring sourceState1 ;
      $:$ ;
      $identifier$ ? @lstring sourceState2 ;
      $->$ ;
      $identifier$ ? @lstring targetState ;
      @mapAutomatonMessageKind messageKind ;
      @lstring messageText ;
      select
        messageKind := [@mapAutomatonMessageKind noMessage] ;
        messageText := [@lstring new !"" !here] ;
      or
        $warning$ ;
        messageKind := [@mapAutomatonMessageKind warningMessage] ;
        $literal_string$ ? messageText ;
      or
        $error$ ;
        messageKind := [@mapAutomatonMessageKind errorMessage] ;
        $literal_string$ ? messageText ;
      end select ;
      mapOverrideBlockDescriptor2AST += !sourceState1 !sourceState2 !targetState !messageKind !messageText ;
    end repeat ;
    $}$ ;
    mapOverrideBlockListAST += !overrideBlockName !mapOverrideBlockDescriptor1AST !mapOverrideBlockDescriptor2AST ;
  end repeat ;
  $}$ ;
#--- Declare map
  ioDeclarations->mSemanticDeclarationList += ![@uniqueMapDeclarationAST new
    !false # Is not predefined
    !mMapTypeName
    !mAttributeList
    !mInsertMethodList
    !mSearchMethodList
    !mapStateList
    !mapOverrideBlockListAST
  ] ;
#--- Declare map proxy
  ioDeclarations->mSemanticDeclarationList += ![@mapProxyDeclarationAST new
    !false # Is not predefined
    !mapProxyTypeName
    !mMapTypeName
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    C L A S S    D E C L A R A T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

#! Class Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  @bool mIsAbstract ;
  select
    mIsAbstract := false ;
  or
    $abstract$ ;
    mIsAbstract := true ;
  end select ;
  $class$ ;
  @lstringlist classNameList [emptyList] ;
  repeat
    $type_name$ ?@lstring className indexing classDefinition ;
    classNameList += !className ;
  while
    $,$ ;
  end repeat ;
  @lstring mSuperClassName ; # Empty if no super class
  select
    mSuperClassName := ["" nowhere] ;
  or
    $:$ ;
    $type_name$ ? mSuperClassName indexing classReferencedAsSuperClass ;
  end select ;
  @lstringlist classFeatureList [emptyList] ;
  repeat
  while
    $attribute$ ? @lstring classFeatureName ;
    classFeatureList += !classFeatureName ;
  end repeat ;
  ${$ ;
  @string mClassMessage := "" ;
#--- Class properties
  @attributeInCollectionListAST mAttributeList [emptyList] ;
  repeat
  while
    <attribute_declaration> !? mAttributeList ;
  end repeat ;
  $}$ ;
  foreach classNameList do
    ioDeclarations->mSemanticDeclarationList += ![@classDeclarationAST new
      !false # Is not predefined
      !mIsAbstract
      !mValue
      !mSuperClassName # Empty if no super class
      !classFeatureList
      !mAttributeList
    ] ;
  end foreach ;
end rule ;


#---------------------------------------------------------------------------*
#                                                                           *
#    S T R U C T    D E C L A R A T I O N                                   *
#                                                                           *
#---------------------------------------------------------------------------*

#! Struct Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $struct$ ;
  $type_name$ ?@lstring mListTypeName indexing structDefinition ;
  ${$ ;
  @attributeInCollectionListAST mAttributeList [emptyList] ;
  repeat
  while
    <attribute_declaration> !? mAttributeList ;
  end repeat ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@structDeclarationAST new
    !false # Is not predefined
    !mListTypeName
    !mAttributeList
    !""
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    L I S T    D E C L A R A T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

#! List Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $list$ ;
  $type_name$ ?@lstring mListTypeName indexing listDefinition ;
  ${$ ;
  @attributeInCollectionListAST mAttributeList [emptyList] ;
  repeat
  while
    <attribute_declaration> !? mAttributeList ;
  end repeat ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@listDeclarationAST new
    !false # Is not predefined
    !mListTypeName
    !mAttributeList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    S O R T E D    L I S T    D E C L A R A T I O N                        *
#                                                                           *
#---------------------------------------------------------------------------*

#! Sorted Declaration

#---------------------------------------------------------------------------*

rule <sortedlist_sort_descriptor> ?!@sortedListSortDescriptorListAST ioSortedListSortDescriptorList :
  $identifier$ ? @lstring mSortedAttributeName ;
  @bool mAscending ;
  select
    $<$ ;
    mAscending := true ;
  or
    $>$ ;
    mAscending := false ;
  end select ;
  ioSortedListSortDescriptorList += !mSortedAttributeName !mAscending ;
end rule ;

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $sortedlist$ ;
  $type_name$ ?@lstring mSortedListTypeName indexing sortedListDefinition ;
  ${$ ;
  @attributeInCollectionListAST mAttributeList [emptyList] ;
  repeat
  while
    <attribute_declaration> !? mAttributeList ;
  end repeat ;
  $}$ ;
  ${$ ;
  @sortedListSortDescriptorListAST mSortDescriptorList[emptyList] ;
  repeat
    <sortedlist_sort_descriptor> !? mSortDescriptorList ;
  while
    $,$ ;
  end repeat ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@sortedListDeclarationAST new
    !false # Is not predefined
    !mSortedListTypeName
    !mAttributeList
    !mSortDescriptorList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E N U M    D E C L A R A T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

#! Enum Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $enum$ ;
  $type_name$ ??@lstring mEnumTypeName indexing enumDefinition ;
  ${$ ;
  @enumConstantList mConstantList [emptyList] ;
  repeat
    $identifier$ ??@lstring constantName ;
    @2lstringlist associatedValueTypeList := {} ;
    select
    or
      $($ ;
      repeat
        $type_name$ ??@lstring associatedValueType ;
        @lstring associatedValueName ;
        select
          associatedValueName := ["" nowhere] ;
        or
          $identifier$ ?associatedValueName ;
        end select ;
        associatedValueTypeList += !associatedValueType !associatedValueName ;
      while
        $,$ ;
      end repeat ;
      $)$ ;
    end select ;
    mConstantList += !constantName !associatedValueTypeList ;
  while
   $,$ ;
  end repeat ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@enumDeclarationAST new
    !false # Is not predefined
    !mEnumTypeName
    !mConstantList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    F I L E W R A P P E R    D E C L A R A T I O N                         *
#                                                                           *
#---------------------------------------------------------------------------*

#! Filewrapper Declaration

#---------------------------------------------------------------------------*

rule <declaration_with_private>
  ??@bool inIsInternal
  ?!@galgas3DeclarationAST ioDeclarations
:
  $filewrapper$ ;
  $identifier$ ? @lstring mFilewrapperName indexing filewrapperDefinition ;
  $in$ ;
  $literal_string$ ? @lstring mFilewrapperPath ;
  ${$ ;
  @lstringlist filewrapperTextFileExtensionList [emptyList] ;
  select
  or
    repeat
      $literal_string$ ?@lstring extension ;
      filewrapperTextFileExtensionList += !extension ;
    while
      $,$ ;
    end repeat ;
  end select ;
  $}$ ;
  ${$ ;
  @lstringlist filewrapperBinaryFileExtensionList [emptyList] ;
  select
  or
    repeat
      $literal_string$ ?@lstring extension ;
      filewrapperBinaryFileExtensionList += !extension ;
    while
      $,$ ;
    end repeat ;
  end select ;
  $}$ ;
  ${$ ;
  @filewrapperTemplateListAST mFilewrapperTemplateList [emptyList] ;
  repeat
  while
    $template$ ;
    $identifier$ ? @lstring mFilewrapperTemplateName ;
    $literal_string$ ? @lstring mFilewrapperTemplatePath ;
    @formalTemplateInputParameterListAST filewrapperTemplateFormalInputParameters [emptyList] ;
    repeat
    while
      $?$ ?const selector ;
      $type_name$ ? @lstring typeName ;
      @bool isUnused ;
      select
        isUnused := false ;
      or
        $unused$ ;
        isUnused := true ;
      end select ;
      $identifier$ ? @lstring argumentName ;
      filewrapperTemplateFormalInputParameters += !selector !typeName !argumentName !isUnused ;
    end repeat ;
    mFilewrapperTemplateList +=
      !mFilewrapperTemplateName
      !mFilewrapperTemplatePath
      !filewrapperTemplateFormalInputParameters
    ;
  end repeat ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@filewrapperDeclarationAST new
    !false # Is not predefined
    !inIsInternal
    ![@string stringWithSourceFilePath]
    !mFilewrapperName
    !mFilewrapperPath
    !filewrapperTextFileExtensionList
    !filewrapperBinaryFileExtensionList
    !mFilewrapperTemplateList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#A B S T R A C T    C A T E G O R Y    M E T H O D    D E C L A R A T I O N *
#                                                                           *
#---------------------------------------------------------------------------*

#! Abstract method Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $abstract$ ;
  $method$ ;
  $type_name$ ? @lstring className indexing typeReferenceAbstractCategoryMethod ;
  $identifier$ ? @lstring mMethodName indexing abstractCategoryMethodDefinition ;
  <formal_parameter_list> ??@formalParameterListAST formalParameterList ;
  ioDeclarations->mSemanticDeclarationList += ![@abstractCategoryMethodAST new
    !false # Is not predefined
    !className
    !mMethodName
    !formalParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
# O V E R R I D E    A B S T R A C T    C A T E G O R Y    M E T H O D      *
#                       D E C L A R A T I O N                               *
#                                                                           *
#---------------------------------------------------------------------------*

#! Override abstract method Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $override$ ;
  $abstract$ ;
  $method$ ;
  $type_name$ ? @lstring className indexing typeReferenceOverrideAbstractCategoryMethod ;
  $identifier$ ? @lstring mMethodName indexing overrideAbstractCategoryMethodDefinition ;
  <formal_parameter_list> ??@formalParameterListAST formalParameterList ;
  ioDeclarations->mSemanticDeclarationList += ![@overridingAbstractCategoryMethodAST new
    !false # Is not predefined
    !className
    !mMethodName
    !formalParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E X T E N S I O N     M E T H O D    D E C L A R A T I O N             *
#                                                                           *
#---------------------------------------------------------------------------*

#! Method declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $method$ ;
  $type_name$ ? @lstring className indexing typeReferenceCategoryMethod ;
  $identifier$ ? @lstring mMethodName indexing categoryMethodDefinition ;
  <formal_parameter_list> ??@formalParameterListAST formalParameterList ;
  ${$ ;
  <semantic_instruction_list> ??@semanticInstructionListAST routineInstructionList ;
  const endOfMethodLocation := here ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@categoryMethodAST new
    !false # Is not predefined
    !className
    !mMethodName
    !formalParameterList
    !routineInstructionList
    !endOfMethodLocation
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#O V E R R I D I N G   C A T E G O R Y   M E T H O D   D E C L A R A T I O N*
#                                                                           *
#---------------------------------------------------------------------------*

#! Overriding method

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $override$ ;
  $method$ ;
  $type_name$ ? @lstring className indexing typeReferenceOverrideCategoryMethodDefinition ;
  $identifier$ ? @lstring mMethodName indexing overrideCategoryMethodDefinition ;
  <formal_parameter_list> ??@formalParameterListAST formalParameterList ;
  ${$ ;
  <semantic_instruction_list> ??@semanticInstructionListAST routineInstructionList ;
  const @location endOfMethodLocation := here ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@overridingCategoryMethodAST new
    !false # Is not predefined
    !className
    !mMethodName
    !formalParameterList
    !routineInstructionList
    !endOfMethodLocation
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#A B S T R A C T    C A T E G O R Y    R E A D E R    D E C L A R A T I O N *
#                                                                           *
#---------------------------------------------------------------------------*

#! Abstract getter

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $abstract$ ;
  $getter$ ;
  $type_name$ ? @lstring className ;
  $identifier$ ? @lstring mReaderName indexing abstractCategoryReaderDefinition ;
  @formalInputParameterListAST mFormalInputParameterList ;
  <formal_input_parameter_list> ? mFormalInputParameterList ;
  $->$ ;
  $type_name$ ? @lstring mReturnedTypeName ;
  $identifier$ ? * ;
  ioDeclarations->mSemanticDeclarationList += ![@abstractCategoryReaderAST new
    !false # Is not predefined
    !className
    !mReaderName
    !mFormalInputParameterList
    !mReturnedTypeName
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    C A T E G O R Y    R E A D E R    D E C L A R A T I O N                *
#                                                                           *
#---------------------------------------------------------------------------*

#! Getter declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $getter$ ;
  $type_name$ ? @lstring className ;
  $identifier$ ? @lstring mMethodName indexing categoryReaderDefinition ;
  @formalInputParameterListAST mFormalInputParameterList ;
  <formal_input_parameter_list> ? mFormalInputParameterList ;
  $->$ ;
  $type_name$ ? @lstring mReturnedTypeName ;
  $identifier$ ? @lstring mReturnedVariableName ;
  ${$ ;
  <semantic_instruction_list> ??@semanticInstructionListAST routineInstructionList ;
  const @location endOfInstructionList := here ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@categoryReaderAST new
    !false # Is not predefined
    !className
    !mMethodName
    !mFormalInputParameterList
    !mReturnedTypeName
    !mReturnedVariableName
    !routineInstructionList
    !endOfInstructionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#O V E R R I D I N G   C A T E G O R Y   R E A D E R   D E C L A R A T I O N*
#                                                                           *
#---------------------------------------------------------------------------*

#! Overriding getter declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $override$ ;
  $getter$ ;
  $type_name$ ? @lstring className ;
  $identifier$ ? @lstring mMethodName indexing overrideCategoryReaderDefinition ;
  @formalInputParameterListAST mFormalInputParameterList ;
  <formal_input_parameter_list> ? mFormalInputParameterList ;
  $->$ ;
  $type_name$ ? @lstring mReturnedTypeName ;
  $identifier$ ? @lstring mReturnedVariableName ;
  ${$ ;
  <semantic_instruction_list> ??@semanticInstructionListAST routineInstructionList ;
  const @location endOfInstructionList := here ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@overridingCategoryReaderAST new
    !false # Is not predefined
    !className
    !mMethodName
    !mFormalInputParameterList
    !mReturnedTypeName
    !mReturnedVariableName
    !routineInstructionList
    !endOfInstructionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
# O V E R R I D I N G    A B S T R A C T   C A T E G O R Y    R E A D E R   *
#                     D E C L A R A T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

#! Overriding abstract getter

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $override$ ;
  $abstract$ ;
  $getter$ ;
  $type_name$ ? @lstring className ;
  $identifier$ ? @lstring mReaderName indexing overrideAbstractCategoryReaderDefinition ;
  @formalInputParameterListAST mFormalInputParameterList ;
  <formal_input_parameter_list> ? mFormalInputParameterList ;
  $->$ ;
  $type_name$ ? @lstring mReturnedTypeName ;
  $identifier$ ? * ;
  ioDeclarations->mSemanticDeclarationList += ![@overridingAbstractCategoryReaderAST new
    !false # Is not predefined
    !className
    !mReaderName
    !mFormalInputParameterList
    !mReturnedTypeName
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#A B S T R A C T    C A T E G O R Y    M O D I F I E R    D E C L A R A T I O N *
#                                                                           *
#---------------------------------------------------------------------------*

#! Abstract setter

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $abstract$ ;
  $setter$ ;
  $type_name$ ? @lstring className indexing typeReferenceAbstractCategoryModifier ;
  $identifier$ ? @lstring modifierName indexing abstractCategoryModifierDefinition ;
  <formal_parameter_list> ??@formalParameterListAST formalParameterList ;
  ioDeclarations->mSemanticDeclarationList += ![@abstractCategoryModifierAST new
    !false # Is not predefined
    !className
    !modifierName
    !formalParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
# O V E R R I D E    A B S T R A C T    C A T E G O R Y    M O D I F I E R  *
#                       D E C L A R A T I O N                               *
#                                                                           *
#---------------------------------------------------------------------------*

#! Overriding abstract setter

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $override$ ;
  $abstract$ ;
  $setter$ ;
  $type_name$ ? @lstring className indexing typeReferenceOverrideAbstractCategoryModifier ;
  $identifier$ ? @lstring modifierName indexing overrideAbstractCategoryModifierDefinition ;
  <formal_parameter_list> ??@formalParameterListAST formalParameterList ;
  ioDeclarations->mSemanticDeclarationList += ![@overridingAbstractCategoryModifierAST new
    !false # Is not predefined
    !className
    !modifierName
    !formalParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    C A T E G O R Y    M O D I F I E R    D E C L A R A T I O N            *
#                                                                           *
#---------------------------------------------------------------------------*

#! Setter declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $setter$ ;
  $type_name$ ? @lstring className indexing typeReferenceCategoryModifier ;
  $identifier$ ? @lstring modifierName indexing categoryModifierDefinition ;
  <formal_parameter_list> ??@formalParameterListAST formalParameterList ;
  ${$ ;
  <semantic_instruction_list> ??@semanticInstructionListAST routineInstructionList ;
  const @location endOfModifierLocation := here ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@categoryModifierAST new
    !false # Is not predefined
    !className
    !modifierName
    !formalParameterList
    !routineInstructionList
    !endOfModifierLocation
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#O V E R R I D I N G   C A T E G O R Y   M O D I F I E R   D E C L A R A T I O N*
#                                                                           *
#---------------------------------------------------------------------------*

#! Overriding setter declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $override$ ;
  $setter$ ;
  $type_name$ ? @lstring className indexing typeReferenceOverrideCategoryModifierDefinition ;
  $identifier$ ? @lstring modifierName indexing overrideCategoryModifierDefinition ;
  <formal_parameter_list> ??@formalParameterListAST formalParameterList ;
  ${$ ;
  <semantic_instruction_list> ??@semanticInstructionListAST routineInstructionList ;
  const @location endOfModifierLocation := here ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@overridingCategoryModifierAST new
    !false # Is not predefined
    !className
    !modifierName
    !formalParameterList
    !routineInstructionList
    !endOfModifierLocation
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#     G R A P H                                                             *
#                                                                           *
#---------------------------------------------------------------------------*

#! Graph Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $graph$ ;
  $type_name$ ? @lstring graphTypeName indexing graphDefinition ;
  $($ ;
  $type_name$ ? @lstring associatedListTypeName ;
  $)$ ;
  ${$ ;
  @graphInsertModifierList graphInsertModifierList [emptyList] ;
  repeat
  while
    $insert$ ;
    $identifier$ ? @lstring insertName ;
    $error$ ;
    $message$ ;
    $literal_string$ ? @lstring errorMessage ;
    graphInsertModifierList += !insertName !errorMessage ;
  end repeat ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@graphDeclarationAST new
    !false # Is not predefined
    !graphTypeName
    !associatedListTypeName
    !graphInsertModifierList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#     E X T E R N    T Y P E    D E C L A R A T I O N                       *
#                                                                           *
#---------------------------------------------------------------------------*

#! Extern type Declaration

#---------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations
:
  $extern$ ;
  $type_name$ ? @lstring externTypeName indexing externTypeDefinition ;
  ${$ ;
    @string cppPredeclarationCode := "" ;
    repeat
    while
      $literal_string$ ? @lstring cppPredeclarationCodeElement ;
      cppPredeclarationCode .= cppPredeclarationCodeElement->string ;
    end repeat ;
  $}$ ;
  ${$ ;
    @string cppClassCode := "" ;
    repeat
    while
      $literal_string$ ? @lstring cppClassCodeElement ;
      cppClassCode .= cppClassCodeElement->string ;
    end repeat ;
  $}$ ;
  ${$ ;
  @externTypeConstructorList externTypeConstructorList [emptyList] ;
  @externTypeReaderList externTypeReaderList [emptyList] ;
  @externTypeModifierList externTypeModifierList [emptyList] ;
  @externTypeMethodList externTypeMethodList [emptyList] ;
  repeat
  while
    $constructor$ ;
    $identifier$ ? @lstring constructorName ;
    @typeNameFormalParameterNameList argumentTypeList [emptyList] ;
    repeat
    while
      $?$ ?const selector ;
      $type_name$ ? @lstring argumentTypeName ;
      $identifier$ ? @lstring argumentName ;
      argumentTypeList += !selector !argumentTypeName !argumentName ;
    end repeat ;
    $->$ ;
    $type_name$ ? @lstring resultTypeName ;
    externTypeConstructorList +=
      !constructorName
      !resultTypeName
      !argumentTypeList
    ;
  while
    $getter$ ;
    $identifier$ ? @lstring readerName ;
    @typeNameFormalParameterNameList argumentTypeList [emptyList] ;
    repeat
    while
      $?$ ?const selector ;
      $type_name$ ? @lstring argumentTypeName ;
      $identifier$ ? @lstring argumentName ;
      argumentTypeList += !selector !argumentTypeName !argumentName ;
    end repeat ;
    $->$ ;
    $type_name$ ? @lstring resultTypeName ;
    externTypeReaderList +=
      !readerName
      !resultTypeName
      !argumentTypeList
    ;
  while
    $setter$ ;
    $identifier$ ? @lstring modifierName ;
    <formal_parameter_list> ?@formalParameterListAST formalParameterList ;
    externTypeModifierList +=
      !modifierName
      !formalParameterList
    ;
  while
    $method$ ;
    $identifier$ ? @lstring methodName ;
    <formal_parameter_list> ?@formalParameterListAST formalParameterList ;
    externTypeMethodList +=
      !methodName
      !formalParameterList
      !here
    ;
  end repeat ;
  $}$ ;
  ioDeclarations->mSemanticDeclarationList += ![@externTypeDeclarationAST new
    !false # Is not predefined
    !externTypeName
    !cppPredeclarationCode
    !cppClassCode
    !externTypeConstructorList
    !externTypeReaderList
    !externTypeModifierList
    !externTypeMethodList
  ] ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;