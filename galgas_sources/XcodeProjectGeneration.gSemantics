#---------------------------------------------------------------------------*

semantics XcodeProjectGeneration :

#---------------------------------------------------------------------------*
#                                                                           *
#    PBXFileReference                                                       *
#                                                                           *
#---------------------------------------------------------------------------*

abstract class @Xcode_PBXFileReference_abstract {
  lazy @string key :
    key := [[[[@uint sequenceNumber] string] md5] rightSubString!24] ;
  end lazy ;

  abstract method buildXcodeProject ?!@string ioString ;
}

#---------------------------------------------------------------------------*

class @Xcode_PBXFileReference_CompiledMachOExecutable extends @Xcode_PBXFileReference_abstract {
  @string mFileName ;

  override method buildXcodeProject ?!@string ioString :
    ioString .= "\t\t" . [self key]
    . " = {isa = PBXFileReference; explicitFileType = \"compiled.mach-o.executable\"; includeInIndex = 0; path = "
    . mFileName . "; sourceTree = BUILT_PRODUCTS_DIR; };\n" ;
  end method ;
}

#---------------------------------------------------------------------------*

list @Xcode_PBXFileReference_list {
  @Xcode_PBXFileReference_abstract mFileReference ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#    PBXSourcesBuildPhase                                                   *
#                                                                           *
#---------------------------------------------------------------------------*

class @Xcode_PBXSourcesBuildPhase {
  @string mNameForComment ;
  @stringlist mFileReferenceList ;

  lazy @string key :
    key := [[[[@uint sequenceNumber] string] md5] rightSubString!24] ;
  end lazy ;
}

#---------------------------------------------------------------------------*

list @Xcode_PBXSourcesBuildPhase_list {
  @Xcode_PBXSourcesBuildPhase mBuildPhase ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#    PBXGroup                                                               *
#                                                                           *
#---------------------------------------------------------------------------*

class @Xcode_PBXGroup {
  @string mGroupName ;
  @stringlist mChildrenGroupList ;

  lazy @string key :
    key := [[[[@uint sequenceNumber] string] md5] rightSubString!24] ;
  end lazy ;
}

#---------------------------------------------------------------------------*

list @Xcode_PBXGroup_list {
  @Xcode_PBXGroup mGroup ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#    XCBuildConfiguration                                                   *
#                                                                           *
#---------------------------------------------------------------------------*

class @Xcode_XCBuildConfiguration {
  @stringlist mSettings ;

  lazy @string configurationKey :
    configurationKey := [[[[@uint sequenceNumber] string] md5] rightSubString!24] ;
  end lazy ;

  lazy @string key :
    key := [[[[@uint sequenceNumber] string] md5] rightSubString!24] ;
  end lazy ;
}

#---------------------------------------------------------------------------*

list @Xcode_XCBuildConfiguration_list {
  @Xcode_XCBuildConfiguration mBuildConfig ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#    PBXNativeTarget                                                        *
#                                                                           *
#---------------------------------------------------------------------------*

list @Xcode_PBXNativeTarget_list {
  @string mTargetKey ;
  @string mTargetName ;
  @string mTargetConfigurationKey ;
  @string mProductInstallPath ;
  @string mProductName ;
  @string mProductReferenceKey ;
  @string mProductTypeExtension ;
  @Xcode_PBXSourcesBuildPhase_list mBuildPhaseList ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#    Root Object                                                            *
#                                                                           *
#---------------------------------------------------------------------------*

class @Xcode_rootObject {
  @string mProjectName ;
  @string mRootObjectKey ;
  @Xcode_PBXGroup_list mGroupList ;
  @string mMainGroupKey ;
  @Xcode_XCBuildConfiguration_list mBuildConfigurationList ;
  @Xcode_PBXNativeTarget_list mTargetList ;
  @string mProjectDefaultConfigurationKey ;
  @Xcode_PBXFileReference_list mPBXFileReference_list ;
  @Xcode_PBXSourcesBuildPhase_list mPBXSourcesBuildPhase_list ;

  method build !@string ioString :
#--- Header
    ioString := "// !$*UTF8*$!\n"
    . "{\n"
    . "\tarchiveVersion = 1;\n"
    . "\tclasses = {\n"
    . "\t};\n"
    . "\tobjectVersion = 42;\n"
    . "\tobjects = {\n\n" ;
#--- PBXFileReference section
    ioString .= "/* Begin PBXFileReference section */\n" ;
    foreach mPBXFileReference_list do
      [mFileReference buildXcodeProject !?ioString] ;
    end foreach ;
    ioString .= "/* End PBXFileReference section */\n" ;
#--- PBXGroup section
    ioString .= "/* Begin PBXGroup section */\n" ;
    foreach mGroupList do
      ioString .= "\t\t" . [mGroup key] . " = {\n"
     . "\t\t\tisa = PBXGroup;\n"
     . "\t\t\tchildren = (\n" ;
      foreach [mGroup mChildrenGroupList] do
        ioString .= "\t\t\t\t" . mValue . ",\n" ;
      end foreach ;
      ioString .= "\t\t\t);\n" ;
      if [mGroup mGroupName] != "" then
        ioString .= "\t\t\tname = \"" . [mGroup mGroupName] . "\";\n" ;
      end if ;
    ioString .= "\t\t\tsourceTree = \"<group>\";\n"
    . "\t\t};\n" ;
    end foreach ;
    ioString .= "/* End PBXGroup section */\n\n" ;
#--- PBXNativeTarget section
    ioString .= "/* Begin PBXNativeTarget section */\n" ;
    foreach mTargetList do
      ioString .= "\t\t" . mTargetKey . " /* " . mTargetName . " */ = {\n"
      . "\t\t\tisa = PBXNativeTarget;\n"
      . "\t\t\tbuildConfigurationList = " . mTargetConfigurationKey . " /* Build configuration list for PBXNativeTarget \"" . mTargetName . "\" */;\n"
      . "\t\t\tbuildPhases = (\n" ;
      foreach mBuildPhaseList do
        ioString .= "\t\t\t\t" . [mBuildPhase key] . " ,\n" ;
      end foreach ;
      ioString .= "\t\t\t);\n"
      . "\t\t\tbuildRules = (\n"
      . "\t\t\t);\n"
      . "\t\t\tdependencies = (\n"
      . "\t\t\t);\n"
      . "\t\t\tname = \"" . mTargetName . "\";\n"
      . "\t\t\tproductInstallPath = \"" . mProductInstallPath . "\";\n"
      . "\t\t\tproductName = \"" . mProductName . "\";\n"
      . "\t\t\tproductReference = " . mProductReferenceKey . " ;\n"
      . "\t\t\tproductType = \"com.apple.product-type." . mProductTypeExtension . "\";\n"
      . "\t\t};\n"
      ;
    end foreach ;
    ioString .= "/* End PBXNativeTarget section */\n" ;
#--- PBXProject section
    ioString .= "/* Begin PBXProject section */\n"
   . "\t\t" . mRootObjectKey . " /* Project object */ = {\n"
   . "\t\t\tisa = PBXProject;\n"
   . "\t\t\tbuildConfigurationList = " . mProjectDefaultConfigurationKey . " /* Build configuration list for PBXProject \"" . mProjectName . "\" */;\n"
   . "\t\t\tcompatibilityVersion = \"Xcode 2.4\";\n"
   . "\t\t\thasScannedForEncodings = 1;\n"
   . "\t\t\tmainGroup = " . mMainGroupKey . ";\n"
   . "\t\t\tprojectDirPath = \"\";\n"
   . "\t\t\tprojectRoot = \"\";\n"
   . "\t\t\ttargets = (\n" ;
    foreach mTargetList do
      ioString .= "\t\t\t\t" . mTargetKey . " /* " . mTargetName . " */,\n" ;
    end foreach ;
   ioString .= "\t\t\t);\n"
   . "\t\t};\n"
   . "/* End PBXProject section */\n\n" ;
#--- PBXSourcesBuildPhase section
    ioString .= "/* Begin PBXSourcesBuildPhase section */\n" ;
    foreach mPBXSourcesBuildPhase_list do
      ioString .= "\t\t" . [mBuildPhase key]
      . " /* " . [mBuildPhase mNameForComment] . " */ = {\n"
      . "\t\t\tisa = PBXSourcesBuildPhase;\n"
      . "\t\t\tbuildActionMask = 2147483647;\n"
      . "\t\t\tfiles = (\n" ;
      foreach [mBuildPhase mFileReferenceList] do
        ioString .= "\t\t\t\t" . mValue . "\n" ;
      end foreach ;
      ioString .=  "\t\t\t);\n"
      . "\t\t\trunOnlyForDeploymentPostprocessing = 0;\n"
      . "\t\t};\n" ;
    end foreach ;
    ioString .= "/* End PBXSourcesBuildPhase section */\n" ;
#--- XCBuildConfiguration section
    ioString .= "/* Begin XCBuildConfiguration section */\n" ;
    foreach mBuildConfigurationList do
      ioString .= "\t\t" . [mBuildConfig configurationKey] . " /* Default */ = {\n"
      . "\t\t\tisa = XCBuildConfiguration;\n"
      . "\t\t\tbuildSettings = {\n" ;
      foreach [mBuildConfig mSettings] do
        ioString .= "\t\t\t\t" . mValue . "\n" ;
      end foreach ;
      ioString .=  "\t\t\t};\n"
      . "\t\t\tname = Default;\n"
      . "\t\t};\n" ;
    end foreach ;
    ioString .= "/* End XCBuildConfiguration section */\n\n" ;
#--- XCConfigurationList section
    ioString .= "/* Begin XCConfigurationList section */\n" ;
    foreach mBuildConfigurationList do
      ioString .= "\t\t" . [mBuildConfig key] . " /* Build configuration list */ = {\n"
      . "\t\t\tisa = XCConfigurationList;\n"
      . "\t\t\tbuildConfigurations = (\n"
      . "\t\t\t\t" . [mBuildConfig configurationKey] . " /* Default */,\n"
      . "\t\t\t);\n"
      . "\t\t\tdefaultConfigurationIsVisible = 0;\n"
      . "\t\t\tdefaultConfigurationName = Default;\n"
      . "\t\t};\n" ;
    end foreach ;
    ioString .= "/* End XCConfigurationList section */\n" ;
#--- 
    ioString .= "\t};\n"
    . "\trootObject = " . mRootObjectKey . " /* Project object */ ;\n"
    . "}\n" ;
  end method ;
}

#---------------------------------------------------------------------------*

routine generateXcodeProject
  ??@string inProjectName
  ??@string in_pbxproj_filePath
:
  @Xcode_PBXGroup_list groupList [emptyList] ;
  @Xcode_XCBuildConfiguration_list buildConfigurationList [emptyList] ;
  @Xcode_PBXNativeTarget_list targetList [emptyList] ;
  @stringlist mainGroupChildrenList [emptyList] ;
  @stringlist productGroupChildrenList [emptyList] ;
  @Xcode_PBXFileReference_list PBXFileReferenceList [emptyList] ;
  @Xcode_PBXSourcesBuildPhase_list PBXSourcesBuildPhase_list [emptyList] ;
#-------------------------------------------------- Build "tool" target
  @Xcode_PBXFileReference_CompiledMachOExecutable toolFileReference [new !inProjectName] ;
  PBXFileReferenceList += !toolFileReference ;
  productGroupChildrenList += ![toolFileReference key] ;
  @Xcode_PBXSourcesBuildPhase compileSourcesForToolTargetBuildPhase [new
    !"Sources"
    ![@stringlist emptyList]
  ] ;
  PBXSourcesBuildPhase_list += !compileSourcesForToolTargetBuildPhase ;
  @Xcode_XCBuildConfiguration toolTargetBuildConfiguration [new
#    ![@stringlist listWithValue !"PRODUCT_NAME = \"" . inProjectName . "\";"]
    ![@stringlist emptyList]
  ] ;
  buildConfigurationList += !toolTargetBuildConfiguration ;
  targetList +=
    ![[[[@uint sequenceNumber] string] md5] rightSubString!24]
    !inProjectName
    ![toolTargetBuildConfiguration key]
    !"$(HOME)/bin"
    !inProjectName
    ![toolFileReference key]
    !"tool"
    ![@Xcode_PBXSourcesBuildPhase_list listWithValue !compileSourcesForToolTargetBuildPhase]
  ;
#-------------------------------------------------- Build group list
#--- "Generated by GALGAS" Group
  @Xcode_PBXGroup generatedByGalgasGroup [new !"Generated by GALGAS" ![@stringlist emptyList]] ;
  groupList += !generatedByGalgasGroup ;
  mainGroupChildrenList += ![generatedByGalgasGroup key] ;
#--- "Hand Coded Sources" Group
  @Xcode_PBXGroup handCodedSourcesGroup [new !"Hand Coded Sources" ![@stringlist emptyList]] ;
  groupList += !handCodedSourcesGroup ;
  mainGroupChildrenList += ![handCodedSourcesGroup key] ;
#--- "LIBPM" Group
  @stringlist libpmGroupChildrenList [emptyList] ;
# "Caches" subgroup
  @Xcode_PBXGroup cachesSubGroup [new !"Caches" ![@stringlist emptyList]] ;
  groupList += !cachesSubGroup ;
  libpmGroupChildrenList += ! [cachesSubGroup key] ;
# "BDD" subgroup
  @Xcode_PBXGroup bddSubGroup [new !"Binary Decision Diagrams" ![@stringlist emptyList]] ;
  groupList += !bddSubGroup ;
  libpmGroupChildrenList += ![bddSubGroup key] ;
# "Collections" subgroup
  @Xcode_PBXGroup collectionSubGroup [new !"Collections" ![@stringlist emptyList]] ;
  groupList += !collectionSubGroup ;
  libpmGroupChildrenList += ![collectionSubGroup key] ;
# "Cocoa GALGAS" subgroup
  @Xcode_PBXGroup cocoaGALGASSubGroup [new !"Cocoa GALGAS" ![@stringlist emptyList]] ;
  groupList += !cocoaGALGASSubGroup ;
  libpmGroupChildrenList += ! [cocoaGALGASSubGroup key] ;
# "Cocoa Utilities" subgroup
  @Xcode_PBXGroup cocoaUtilitiesSubGroup [new !"Cocoa Utilities" ![@stringlist emptyList]] ;
  groupList += !cocoaUtilitiesSubGroup ;
  libpmGroupChildrenList += ! [cocoaUtilitiesSubGroup key] ;
# "Command Line Interface" subgroup
  @Xcode_PBXGroup commandLineInterfaceSubGroup [new !"Command Line Interface" ![@stringlist emptyList]] ;
  groupList += !commandLineInterfaceSubGroup ;
  libpmGroupChildrenList += ! [commandLineInterfaceSubGroup key] ;
# "Files" subgroup
  @Xcode_PBXGroup filesSubGroup [new !"Files" ![@stringlist emptyList]] ;
  groupList += !filesSubGroup ;
  libpmGroupChildrenList += ! [filesSubGroup key] ;
# "GALGAS" subgroup
  @Xcode_PBXGroup GALGASSubGroup [new !"GALGAS" ![@stringlist emptyList]] ;
  groupList += !GALGASSubGroup ;
  libpmGroupChildrenList += ! [GALGASSubGroup key] ;
# "Streams" subgroup
  @Xcode_PBXGroup streamsSubGroup [new !"Streams" ![@stringlist emptyList]] ;
  groupList += !streamsSubGroup ;
  libpmGroupChildrenList += ! [streamsSubGroup key] ;
# "Time" subgroup
  @Xcode_PBXGroup timeSubGroup [new !"Time" ![@stringlist emptyList]] ;
  groupList += !timeSubGroup ;
  libpmGroupChildrenList += ! [timeSubGroup key] ;
# "Tiny XML" subgroup
  @Xcode_PBXGroup tinyXMLSubGroup [new !"Tiny XML" ![@stringlist emptyList]] ;
  groupList += !tinyXMLSubGroup ;
  libpmGroupChildrenList += ! [tinyXMLSubGroup key] ;
# "Unicode" subgroup
  @Xcode_PBXGroup unicodeSubGroup [new !"Unicode" ![@stringlist emptyList]] ;
  groupList += !unicodeSubGroup ;
  libpmGroupChildrenList += ! [unicodeSubGroup key] ;
# "Utilities" subgroup
  @Xcode_PBXGroup utilitiesSubGroup [new !"Utilities" ![@stringlist emptyList]] ;
  groupList += !utilitiesSubGroup ;
  libpmGroupChildrenList += ! [utilitiesSubGroup key] ;
#--- "LIBMP" Group
  @Xcode_PBXGroup libpmGroup [new !"LIBPM" !libpmGroupChildrenList] ;
  groupList += !libpmGroup ;
  mainGroupChildrenList += ![libpmGroup key] ;
#--- "Resources" Group
  @Xcode_PBXGroup resourcesGroup [new !"Resources" ![@stringlist emptyList]] ;
  groupList += !resourcesGroup ;
  mainGroupChildrenList += ! [resourcesGroup key] ;
#--- "Frameworks" Group
  @Xcode_PBXGroup frameworksGroup [new !"Frameworks" ![@stringlist emptyList]] ;
  groupList += !frameworksGroup ;
  mainGroupChildrenList += ! [frameworksGroup key] ;
#--- "Products" Group
  @Xcode_PBXGroup productsGroup [new !"Products" !productGroupChildrenList] ;
  groupList += !productsGroup ;
  mainGroupChildrenList += ! [productsGroup key] ;
#--- Main Group
  @Xcode_PBXGroup mainGroup [new !"" !mainGroupChildrenList] ;
  groupList += !mainGroup ;
#--- Define project default configuration
  @stringlist projectDefaultSettings [emptyList] ;
  projectDefaultSettings  += !"ALWAYS_SEARCH_USER_PATHS = NO;" ;
  projectDefaultSettings  += !"ARCHS = (ppc, i386,);" ;
  projectDefaultSettings  += !"GCC_DEBUGGING_SYMBOLS = default;" ;
  projectDefaultSettings  += !"GCC_GENERATE_DEBUGGING_SYMBOLS = NO;" ;
  projectDefaultSettings  += !"GCC_PRECOMPILE_PREFIX_HEADER = YES;" ;
  projectDefaultSettings  += !"GCC_THREADSAFE_STATICS = NO;" ;
  projectDefaultSettings  += !"GCC_TREAT_IMPLICIT_FUNCTION_DECLARATIONS_AS_ERRORS = YES;" ;
  projectDefaultSettings  += !"GCC_TREAT_NONCONFORMANT_CODE_ERRORS_AS_WARNINGS = YES;" ;
  projectDefaultSettings  += !"GCC_TREAT_WARNINGS_AS_ERRORS = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_64_TO_32_BIT_CONVERSION = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_ABOUT_INVALID_OFFSETOF_MACRO = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_ABOUT_MISSING_NEWLINE = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_ABOUT_MISSING_PROTOTYPES = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_ABOUT_RETURN_TYPE = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_CHECK_SWITCH_STATEMENTS = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_EFFECTIVE_CPLUSPLUS_VIOLATIONS = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_FOUR_CHARACTER_CONSTANTS = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_HIDDEN_VIRTUAL_FUNCTIONS = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_INHIBIT_ALL_WARNINGS = NO;" ;
  projectDefaultSettings  += !"GCC_WARN_INITIALIZER_NOT_FULLY_BRACKETED = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_MISSING_PARENTHESES = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_NON_VIRTUAL_DESTRUCTOR = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_PEDANTIC = NO;" ;
  projectDefaultSettings  += !"GCC_WARN_PROTOTYPE_CONVERSION = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_SHADOW = NO;" ;
  projectDefaultSettings  += !"GCC_WARN_SIGN_COMPARE = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_TYPECHECK_CALLS_TO_PRINTF = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_UNINITIALIZED_AUTOS = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_UNKNOWN_PRAGMAS = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_UNUSED_FUNCTION = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_UNUSED_LABEL = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_UNUSED_PARAMETER = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_UNUSED_VALUE = YES;" ;
  projectDefaultSettings  += !"GCC_WARN_UNUSED_VARIABLE = YES;" ;
  projectDefaultSettings  += !"HEADER_SEARCH_PATHS = (../galgas_sources/GALGAS_OUTPUT, ../hand_coded_sources, ../../libpm,);" ;
  projectDefaultSettings  += !"PREBINDING = NO;" ;
  projectDefaultSettings  += !"SDKROOT = /Developer/SDKs/MacOSX10.4u.sdk;" ;

  @Xcode_XCBuildConfiguration defaultProjectConfiguration [new !projectDefaultSettings] ;
  buildConfigurationList += !defaultProjectConfiguration ;
#--- Build project model
  @Xcode_rootObject p [new
    !inProjectName
    ![["project" md5] rightSubString !24]
    !groupList
    ![mainGroup key]
    !buildConfigurationList
    !targetList
    ![defaultProjectConfiguration key]
    !PBXFileReferenceList
    !PBXSourcesBuildPhase_list
  ] ;
#--- Build string
  @string fileNewContents ;
  [p build ?fileNewContents] ;
#--- 
#  log in_pbxproj_filePath ;
  [[in_pbxproj_filePath stringByDeletingLastPathComponent] makeDirectory] ;
  [fileNewContents writeToFileWhenDifferentContents !in_pbxproj_filePath] ;
end routine ;

#---------------------------------------------------------------------------*

end semantics ;

