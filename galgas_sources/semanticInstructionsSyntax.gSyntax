#---------------------------------------------------------------------------*
#                                                                           *
#  'metamodel' component parser, as model builder                           *
#                                                                           *
#  Copyright (C) 2007, ..., 2010 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax semanticInstructionsSyntax ("galgas_scanner.gLexique") :

import "semanticsTypesForAST.gSemantics" ;
import "galgas_cli_options.gOption" ;

#------------------------------------------------------------------------------*
#                                                                              *
#          Rules prototypes                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <expression> !@semanticExpressionAST outExpression ;

nonterminal <formal_parameter_list> !@formalParameterListAST outFormalParameterList ;

nonterminal <formal_input_parameter_list> !@formalInputParameterListAST outFormalInputParameterList ;

nonterminal <actual_parameter_list>
  !@actualParameterListAST outActualParameterList
  ?!@localVariableDeclarationListAST ioLocalVariableDeclarationList
;

nonterminal <output_expression_list>
  !@semanticExpressionListAST outExpressionList
;

#---------------------------------------------------------------------------*
#                                                                           *
#    S E M A N T I C    I N S T R U C T I O N    L I S T                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction_list> !@semanticInstructionListAST outInstructionsList :
  outInstructionsList := [@semanticInstructionListAST emptyList] ;
  repeat
  while
    $;$ ;
  while
    @semanticInstructionAST instruction ;
    @localVariableDeclarationListAST localVariableDeclarationList [emptyList] ;
    <semantic_instruction> ? instruction !?localVariableDeclarationList ;
    foreach localVariableDeclarationList do
      outInstructionsList += ![@localVariableDeclarationAST new ![mTypeName location] !mTypeName !mVariableName !mIsUndefinedConstant] ;
    end foreach ;
    outInstructionsList += !instruction ;
  end repeat ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    ' L O C A L '    D E C L A R A T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

nonterminal <local_declaration> !@bool outIsGlobal ;

rule <local_declaration> !@bool outIsGlobal :
  select
    outIsGlobal := true ;
  or
    $local$ ;
    outIsGlobal := false ;
  end select ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E X T E R N    R O U T I N E    D E C L A R A T I O N                  *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationListAST ioSemanticDeclarations
:
  $extern$ ;
  $routine$ ;
  $identifier$ ? @lstring mActionName indexing routineDefinition ;
  @formalParameterListAST mFormalParameterList ;
  <formal_parameter_list> ? mFormalParameterList ;
  $;$ ;
  ioSemanticDeclarations += ![@externRoutineDeclarationAST new
    !true # Is global
    !mActionName
    !mFormalParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    R O U T I N E    D E C L A R A T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationListAST ioSemanticDeclarations
:
  @bool isGlobal ;
  <local_declaration> ?isGlobal ;
  $routine$ ;
  $identifier$ ? @lstring mRoutineName indexing routineDefinition ;
  @formalParameterListAST mFormalParameterList ;
  <formal_parameter_list> ? mFormalParameterList ;
  $:$ ;
  @semanticInstructionListAST mRoutineInstructionList ;
  <semantic_instruction_list> ? mRoutineInstructionList ;
  $end$ ;
  const @location endOfInstructionList := here ;
  $routine$ ;
  $;$ ;
  ioSemanticDeclarations += ![@routineDeclarationAST new
    !isGlobal
    !mRoutineName
    !mFormalParameterList
    !mRoutineInstructionList
    !endOfInstructionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    F U N C T I O N    D E C L A R A T I O N                               *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationListAST ioSemanticDeclarations
:
  @bool isGlobal ;
  <local_declaration> ?isGlobal ;
  $function$ ;
  $identifier$ ? @lstring mRoutineName indexing functionDefinition ;
  @formalInputParameterListAST formalInputParameterList ;
  <formal_input_parameter_list> ? formalInputParameterList ;
  $->$ ;
  $type_name$ ?@lstring resultTypeName ;
  $identifier$ ?@lstring resultVariableName ;
  $:$ ;
  @semanticInstructionListAST functionInstructionList ;
  <semantic_instruction_list> ? functionInstructionList ;
  $end$ ;
  const @location endOfFunctionInstructionList := here ;
  $function$ ;
  $;$ ;
  ioSemanticDeclarations += ![@functionDeclarationAST new
    !isGlobal
    !mRoutineName
    !formalInputParameterList
    !resultTypeName
    !resultVariableName
    !functionInstructionList
    !endOfFunctionInstructionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    O N C E    F U N C T I O N    D E C L A R A T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationListAST ioSemanticDeclarations
:
  @bool isGlobal ;
  <local_declaration> ?isGlobal ;
  $once$ ;
  $identifier$ ? @lstring mRoutineName indexing functionDefinition ;
  $->$ ;
  $type_name$ ?@lstring resultTypeName ;
  $identifier$ ?@lstring resultVariableName ;
  $:$ ;
  @semanticInstructionListAST mRoutineInstructionList ;
  <semantic_instruction_list> ? mRoutineInstructionList ;
  $end$ ;
  $once$ ;
  const @location endOfFunctionInstructionList := here ;
  $;$ ;
  ioSemanticDeclarations += ![@onceFunctionDeclarationAST new
    !isGlobal
    !mRoutineName
    !resultTypeName
    !resultVariableName
    !mRoutineInstructionList
    !endOfFunctionInstructionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E X T E R N    F U N C T I O N    D E C L A R A T I O N                *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationListAST ioSemanticDeclarations
:
  $extern$ ;
  $function$ ;
  $identifier$ ? @lstring mActionName indexing functionDefinition ;
  @formalInputParameterListAST formalInputParameterList ;
  <formal_input_parameter_list> ? formalInputParameterList ;
  $->$ ;
  $type_name$ ?@lstring resultTypeName ;
  $identifier$ ?@lstring resultVariableName ;
  $;$ ;
  ioSemanticDeclarations += ![@externFunctionDeclarationAST new
    !true # Is global
    !mActionName
    !formalInputParameterList
    !resultTypeName
    !resultVariableName
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  L O C A L    V A R I A B L E    D E C L A R A T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $var$ ;
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  outInstruction := [@localVariableDeclarationAST new ![typeName location] !typeName !variableName !false] ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  outInstruction := [@localVariableDeclarationAST new ![typeName location] !typeName !variableName !false] ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $var$ ;
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  $:=$ ;
  @semanticExpressionAST sourceExpression ;
  <expression> ? sourceExpression ;
  outInstruction := [@localVariableOrConstantDeclarationWithAssignmentAST new
    ![typeName location]
    !false
    !typeName
    !variableName
    !sourceExpression
  ] ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $var$ ;
  $identifier$ ?@lstring variableName ;
  $:=$ ;
  @semanticExpressionAST sourceExpression ;
  <expression> ? sourceExpression ;
  outInstruction := [@localVariableDeclarationWithAssignmentAST new
    ![variableName location]
    !variableName
    !sourceExpression
  ] ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  $:=$ ;
  @semanticExpressionAST sourceExpression ;
  <expression> ? sourceExpression ;
  outInstruction := [@localVariableOrConstantDeclarationWithAssignmentAST new
    ![typeName location]
    !false
    !typeName
    !variableName
    !sourceExpression
  ] ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  $[$ ;
  $default$ ;
  $]$ ;
  outInstruction := [@localVariableOrConstantDeclarationWithDefaultConstructorAST new
    ![typeName location]
    !false # Is not Constant
    !typeName
    !variableName
  ] ;
  if [option galgas_cli_options.warnsOnOldStyleConstantDeclaration value] then
    warning variableName : "This construction is deprecated in GALGAS 2.6.0 and above: replace with \"var "
     . variableName . " := [@" . typeName . " default] ;" ;
  end if ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  $[$ ;
  @lstring constructorName ;
  $identifier$ ? constructorName ;
  @semanticExpressionListAST expressionList ;
  <output_expression_list> ?expressionList ;
  outInstruction := [@localVariableOrConstantDeclarationWithConstructorCallAST new
    ![typeName location]
    !false # Is not Constant
    !typeName
    !variableName
    !constructorName
    !expressionList
  ] ;
  $]$ ;
  if [option galgas_cli_options.warnsOnOldStyleConstantDeclaration value] then
    warning variableName : "This construction is deprecated in GALGAS 2.6.0 and above: replace with \"var "
     . variableName . " := [@" . typeName . " constructor...] ;" ;
  end if ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  L O C A L    C O N S T A N T    D E C L A R A T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $const$ ;
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  $:=$ ;
  @semanticExpressionAST sourceExpression ;
  <expression> ? sourceExpression ;
  outInstruction := [@localVariableOrConstantDeclarationWithAssignmentAST new
    ![typeName location]
    !true # Is Constant
    !typeName
    !variableName
    !sourceExpression
  ] ;
 $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $const$ ;
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  $[$ ;
  @lstring constructorName ;
  $identifier$ ? constructorName ;
  @semanticExpressionListAST expressionList ;
  <output_expression_list> ?expressionList ;
  $]$ ;
  outInstruction := [@localVariableOrConstantDeclarationWithConstructorCallAST new
    ![typeName location]
    !true # is constant
    !typeName
    !variableName
    !constructorName
    !expressionList
  ] ;
  if [option galgas_cli_options.warnsOnOldStyleConstantDeclaration value] then
    warning variableName : "This construction is deprecated in GALGAS 2.6.0 and above: replace with \"const "
     . variableName . " := [@" . typeName . " constructor...] ;" ;
  end if ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $const$ ;
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  $[$ ;
  $default$ ;
  $]$ ;
  outInstruction := [@localVariableOrConstantDeclarationWithDefaultConstructorAST new
    ![typeName location]
    !true # Is Constant
    !typeName
    !variableName
  ] ;
  if [option galgas_cli_options.warnsOnOldStyleConstantDeclaration value] then
    warning variableName : "This construction is deprecated in GALGAS 2.6.0 and above: replace with \"const "
     . variableName . " := [@" . typeName . " default] ;" ;
  end if ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  L O C A L    C O N S T A N T    D E C L A R A T I O N                    *
#  with implicit target type                                                *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $const$ ;
  $identifier$ ?@lstring variableName ;
  $:=$ ;
  @semanticExpressionAST sourceExpression ;
  <expression> ? sourceExpression ;
  outInstruction := [@localConstantDeclarationWithAssignmentAST new
    ![variableName location]
    !variableName
    !sourceExpression
  ] ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    A S S I G N M E N T    I N S T R U C T I O N                           *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $identifier$ ?@lstring mTargetVariableName ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $:=$ ;
  @semanticExpressionAST mSourceExpression ;
  <expression> ? mSourceExpression ;
  $;$ ;
  outInstruction := [@assignmentInstructionAST new
    ![mTargetVariableName location]
    !mTargetVariableName
    !mStructAttributeList
    !mSourceExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    S E L F    A S S I G N M E N T    I N S T R U C T I O N                *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $self$ ;
  const @location instructionLocation := here ;
  $:=$ ;
  @semanticExpressionAST mSourceExpression ;
  <expression> ? mSourceExpression ;
  $;$ ;
  outInstruction := [@selfAssignmentInstructionAST new
    !instructionLocation
    !mSourceExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    D R O P    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $drop$ ;
  const @location instructionLocation := here ;
  @lstringlist dropList [emptyList] ;
  repeat
    $identifier$ ?@lstring variableName ;
    dropList += !variableName ;
  while
    $,$ ;
  end repeat ;
  $;$ ;
  outInstruction := [@dropInstructionAST new !instructionLocation !dropList] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    I N C R E M E N T    I N S T R U C T I O N                             *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $identifier$ ? @lstring mReceiverName ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $++$ ;
  $;$ ;
  outInstruction := [@incrementInstructionAST new
    ![mReceiverName location]
    !mReceiverName
    !mStructAttributeList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    S E L F    I N C R E M E N T    I N S T R U C T I O N                  *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $self$ ;
  const @location instructionLocation := here ;
  $++$ ;
  $;$ ;
  outInstruction := [@selfIncrementInstructionAST new
    !instructionLocation
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    D E C R E M E N T    I N S T R U C T I O N                             *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $identifier$ ?@lstring mReceiverName ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $--$ ;
  $;$ ;
  outInstruction := [@decrementInstructionAST new
    ![mReceiverName location]
    !mReceiverName
    !mStructAttributeList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    S E L F    D E C R E M E N T    I N S T R U C T I O N                  *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $self$ ;
  const @location instructionLocation := here ;
  $--$ ;
  $;$ ;
  outInstruction := [@selfDecrementInstructionAST new
    !instructionLocation
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    F O R E A C H    I N S T R U C T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <foreach_enumerated_object>
  ?!@foreachInstructionEnumeratedObjectListAST ioForeachInstructionEnumeratedObjectList
:
  @lstring enumerationOrder ;
  select
    enumerationOrder := [@lstring new !"" !here] ; # Default order
  or
    $<$ ;
    enumerationOrder := [@lstring new !"up" !here] ;
  or
    $>$ ;
    enumerationOrder := [@lstring new !"down" !here] ;
  or
    $variant$ ? enumerationOrder ;
    warning here:"obsolete construction : now use '>' or '<'" ;
  end select ;
  @semanticExpressionAST mEnumeratedExpression ;
  <expression> ? mEnumeratedExpression ;
  const @location endOfEnumerationExpression := here ;
  @foreachInstructionEnumeratedObjectElementListAST mElementList [emptyList] ;
  @bool mEndsWithEllipsis ;
  @lstring prefix ;
  select
    mEndsWithEllipsis := false ;
    prefix := [@lstring new !"" !here] ;
  or
    $:$ ;
    $identifier$ ? prefix ;
    mEndsWithEllipsis := false ;
  or
    prefix := [@lstring new !"" !here] ;
    $($ ;
    repeat
    while
      $*$ ;
      mElementList += ![@lstring new !"" !here] ![@lstring new !"" !here] ;
    while
      @luint count ;
      $unsigned_literal_integer$ ?count ;
      $*$ ;
      @uint n := [count uint] ;
      if n == 0 then
        error count: "this value should be > 0" ;
      end if ;
      loop n :
      while n > 0 do
        mElementList += ![@lstring new !"" !here] ![@lstring new !"" !here] ;
        n -- ;
      end loop ;
    while
      @lstring typeName ;
      $type_name$ ?typeName ;
      @lstring constantName ;
      $identifier$ ?constantName ;
      mElementList += !typeName !constantName ;
    end repeat ;
    select
      mEndsWithEllipsis := false ;
    or
      $...$ ;
      mEndsWithEllipsis := true ;
    end select ;
    $)$ ;
  end select ;
  ioForeachInstructionEnumeratedObjectList +=
    !enumerationOrder
    !mEnumeratedExpression
    !endOfEnumerationExpression
    !mElementList
    !here
    !mEndsWithEllipsis
    !prefix
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $foreach$ ;
  const @location instructionLocation := here ;
  @foreachInstructionEnumeratedObjectListAST mEnumeratedObjectList [emptyList] ;
  repeat
    <foreach_enumerated_object> !?mEnumeratedObjectList ;
  while
    $,$ ;
  end repeat ;
  @lstring mIndexVariableName ;
  select
    mIndexVariableName := [@lstring new !"" !here] ;
  or
    $index$ ;
    $identifier$ ? mIndexVariableName ;
  end select ;
  @semanticExpressionAST mWhileExpression ;
  select
    mWhileExpression := [@trueExpressionAST new !here] ;
  or
    $while$ ;
    <expression> ? mWhileExpression ;
  end select ;
  const @location endof_while_expression := here ;
  @semanticInstructionListAST mBeforeInstructionList ;
  select
    mBeforeInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $before$ ;
    <semantic_instruction_list> ? mBeforeInstructionList ;
  end select ;
  const @location endof_before_branch := here ;
  $do$ ;
  @semanticInstructionListAST mDoInstructionList ;
  <semantic_instruction_list> ? mDoInstructionList ;
  const @location endof_do_branch := here ;
  @semanticInstructionListAST mBetweenInstructionList ;
  select
    mBetweenInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $between$ ;
    <semantic_instruction_list> ? mBetweenInstructionList ;
  end select ;
  const @location endof_between_branch := here ;
  @semanticInstructionListAST mAfterInstructionList ;
  select
    mAfterInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $after$ ;
    <semantic_instruction_list> ? mAfterInstructionList ;
  end select ;
  const @location endof_after_branch := here ;
  $end$ ;
  const @location endof_foreach_instruction := here ;
  $foreach$ ;
  $;$ ;
  outInstruction := [@foreachInstructionAST new
    !instructionLocation
    !mEnumeratedObjectList
    !mIndexVariableName
    !mWhileExpression
    !endof_while_expression
    !mBeforeInstructionList
    !endof_before_branch
    !mBetweenInstructionList
    !endof_between_branch
    !mDoInstructionList
    !endof_do_branch
    !mAfterInstructionList
    !endof_after_branch
    !endof_foreach_instruction
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    ( N E W )    F O R E A C H    I N S T R U C T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <new_foreach_enumerated_object>
  ?!@forInstructionEnumeratedObjectListAST ioForeachInstructionEnumeratedObjectList
:
  $identifier$ ? @lstring enumerationVariable ;
  $in$ ;
  @bool ascending ;
  select
    ascending := true ;
  or
    $reverse$ ;
    ascending := false ;
  end select ;
  @semanticExpressionAST enumeratedExpression ;
  <expression> ? enumeratedExpression ;
  ioForeachInstructionEnumeratedObjectList +=
    !ascending
    !enumerationVariable
    !enumeratedExpression
    !here
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $for$ ;
  const @location instructionLocation := here ;
  @forInstructionEnumeratedObjectListAST mEnumeratedObjectList [emptyList] ;
  repeat
    <new_foreach_enumerated_object> !?mEnumeratedObjectList ;
  while
    $,$ ;
  end repeat ;
  @semanticExpressionAST mWhileExpression ;
  select
    mWhileExpression := [@trueExpressionAST new !here] ;
  or
    $while$ ;
    <expression> ? mWhileExpression ;
  end select ;
  const @location endof_while_expression := here ;
  @semanticInstructionListAST mBeforeInstructionList ;
  select
    mBeforeInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $before$ ;
    <semantic_instruction_list> ? mBeforeInstructionList ;
  end select ;
  const @location endof_before_branch := here ;
  $do$ ;
  @lstring indexVariableName ;
  select
    indexVariableName := [@lstring new !"" !here] ;
  or
    $index$ ;
    $identifier$ ? indexVariableName ;
  end select ;
  @semanticInstructionListAST mDoInstructionList ;
  <semantic_instruction_list> ? mDoInstructionList ;
  const @location endof_do_branch := here ;
  @semanticInstructionListAST mBetweenInstructionList ;
  select
    mBetweenInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $between$ ;
    <semantic_instruction_list> ? mBetweenInstructionList ;
  end select ;
  const @location endof_between_branch := here ;
  @semanticInstructionListAST mAfterInstructionList ;
  select
    mAfterInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $after$ ;
    <semantic_instruction_list> ? mAfterInstructionList ;
  end select ;
  const @location endof_after_branch := here ;
  $end$ ;
  const @location endof_foreach_instruction := here ;
  $for$ ;
  $;$ ;
  outInstruction := [@forInstructionAST new
    !instructionLocation
    !mEnumeratedObjectList
    !indexVariableName
    !mWhileExpression
    !endof_while_expression
    !mBeforeInstructionList
    !endof_before_branch
    !mBetweenInstructionList
    !endof_between_branch
    !mDoInstructionList
    !endof_do_branch
    !mAfterInstructionList
    !endof_after_branch
    !endof_foreach_instruction
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    I F    I N S T R U C T I O N                                           *
#                                                                           *
#---------------------------------------------------------------------------*

rule <if_branch> !@semanticInstructionAST outInstruction :
  const @location instructionLocation := here ;
  @semanticExpressionAST if_expression ;
  <expression> ?if_expression ;
  $then$ ;
  @semanticInstructionListAST then_instructionList ;
  <semantic_instruction_list> ? then_instructionList ;
  const @location endOf_then_branchLocation := here ;
  @semanticInstructionListAST else_instructionList ;
  select
    else_instructionList := [@semanticInstructionListAST emptyList] ;
  or
    $else$ ;
    <semantic_instruction_list> ? else_instructionList ;
  or
    $elsif$ ;
    @semanticInstructionAST instruction ;
    <if_branch> ? instruction ;
    else_instructionList := [@semanticInstructionListAST emptyList] ;
    else_instructionList += !instruction ;
  end select ;
  outInstruction := [@ifInstructionAST new
    !instructionLocation
    !if_expression
    !then_instructionList
    !endOf_then_branchLocation
    !else_instructionList
    !here
  ] ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $if$ ;
  <if_branch> ?outInstruction ;
  $end$ ;
  $if$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M E T H O D    C A L L    I N S T R U C T I O N                        *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST ioLocalVariableDeclarationList
:
  $[$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mReceiverExpression ;
  <expression> ? mReceiverExpression ;
  $identifier$ ?@lstring mMethodName ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList !?ioLocalVariableDeclarationList ;
  $]$ ;
  $;$ ;
  outInstruction := [@methodCallInstructionAST new
    !instructionLocation
    !mReceiverExpression
    !mMethodName
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M O D I F I E R    C A L L    I N S T R U C T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST ioLocalVariableDeclarationList
:
  $[$ ;
  const @location instructionLocation := here ;
  $!?$ ;
  $identifier$ ?@lstring mReceiverName ;
  @lstringlist mReceiverStructAttributes [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ?@lstring structAttribute ;
    mReceiverStructAttributes += !structAttribute ;
  end repeat ;
  $identifier$ ?@lstring mModifierName ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList !?ioLocalVariableDeclarationList ;
  $]$ ;
  $;$ ;
  outInstruction := [@modifierCallInstructionAST new
    !instructionLocation
    !mReceiverName
    !mReceiverStructAttributes
    !mModifierName
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    " T H I S "   M O D I F I E R    C A L L    I N S T R U C T I O N      *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST ioLocalVariableDeclarationList
:
  $[$ ;
  const @location instructionLocation := here ;
  $!?$ ;
  $self$ ;
  $identifier$ ?@lstring mModifierName ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList !?ioLocalVariableDeclarationList ;
  $]$ ;
  $;$ ;
  outInstruction := [@selfModifierCallInstructionAST new
    !instructionLocation
    !mModifierName
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E R R O R    I N S T R U C T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $error$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mReceiverExpression ;
  <expression> ? mReceiverExpression ;
  $:$ ;
  @semanticExpressionAST mErrorExpression ;
  <expression> ? mErrorExpression ;
  @lstringlist mBuiltVariableList [emptyList] ;
  select
  or
    $:$ ;
    repeat
     @lstring builtVariableName ;
     $identifier$ ? builtVariableName ;
     mBuiltVariableList += !builtVariableName ;
    while
      $,$ ;
    end repeat ;
  end select ;
  $;$ ;
  outInstruction := [@errorInstructionAST new
    !instructionLocation
    !mReceiverExpression
    !mErrorExpression
    !mBuiltVariableList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    W A R N I N G    I N S T R U C T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $warning$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mReceiverExpression ;
  <expression> ? mReceiverExpression ;
  $:$ ;
  @semanticExpressionAST mWarningExpression ;
  <expression> ? mWarningExpression ;
  $;$ ;
  outInstruction := [@warningInstructionAST new
    !instructionLocation
    !mReceiverExpression
    !mWarningExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    R O U T I N E    C A L L    I N S T R U C T I O N                      *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST ioLocalVariableDeclarationList
:
  $identifier$ ?@lstring mRoutineName indexing routineCall ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList !?ioLocalVariableDeclarationList ;
  $;$ ;
  outInstruction := [@routineCallInstructionAST new
    ![mRoutineName location]
    !mRoutineName
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    W I T H    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $with$ ;
  const @location instructionLocation := here ;
  $!?$ ;
  $identifier$ ?@lstring mTargetObjectName ;
  @lstringlist structAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ?@lstring mStructFieldName ;
    structAttributeList += !mStructFieldName ;
  end repeat ;
  $identifier$ ?@lstring mSearchMethodName ;
  $!$ ;
  @semanticExpressionAST mKeyExpression ;
  <expression> ? mKeyExpression ;
  @lstring withPrefix ;
  select
    withPrefix := [@lstring new !"" !here] ;
  or
    $:$ ;
    $identifier$ ? withPrefix ;
  end select ;
  $do$ ;
  @semanticInstructionListAST mDoBranchInstructions ;
  <semantic_instruction_list> ? mDoBranchInstructions ;
  const @location endOf_do_instructions := here ;
  @semanticInstructionListAST mElseBranchInstructions ;
  select
    mElseBranchInstructions := [@semanticInstructionListAST emptyList] ;
  or
    $else$ ;
    <semantic_instruction_list> ? mElseBranchInstructions ;
  end select ;
  const @location endOf_else_instructions := here ;
  outInstruction := [@readWriteAccessWithInstructionAST new
    !instructionLocation
    !withPrefix
    !mTargetObjectName
    !structAttributeList
    !mSearchMethodName
    !mKeyExpression
    !mDoBranchInstructions
    !endOf_do_instructions
    !mElseBranchInstructions
    !endOf_else_instructions
  ] ;
  $end$ ;
  $with$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $with$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST receiverExpression ;
  <expression> ? receiverExpression ;
  $identifier$ ?@lstring mSearchMethodName ;
  $!$ ;
  @semanticExpressionAST mKeyExpression ;
  <expression> ? mKeyExpression ;
  @lstring withPrefix ;
  select
    withPrefix := [@lstring new !"" !here] ;
  or
    $:$ ;
    $identifier$ ? withPrefix ;
  end select ;
  $do$ ;
  @semanticInstructionListAST mDoBranchInstructions ;
  <semantic_instruction_list> ? mDoBranchInstructions ;
  const @location endOfDoInstructions := here ;
  @semanticInstructionListAST mElseBranchInstructions ;
  select
    mElseBranchInstructions := [@semanticInstructionListAST emptyList] ;
  or
    $else$ ;
    <semantic_instruction_list> ? mElseBranchInstructions ;
  end select ;
  outInstruction := [@readAccessWithInstructionAST new
    !instructionLocation
    !withPrefix
    !receiverExpression
    !mSearchMethodName
    !mKeyExpression
    !mDoBranchInstructions
    !endOfDoInstructions
    !mElseBranchInstructions
    !here
  ] ;
  $end$ ;
  $with$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M A T C H    I N S T R U C T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

rule <match_entry> ?!@matchEntryListAST ioMatchEntryList :
  $identifier$ ? @lstring enumerationConstantName ;
  ioMatchEntryList +=
    !false
    !enumerationConstantName
    ![@lstring new !"" !here]
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <match_entry> ?!@matchEntryListAST ioMatchEntryList :
  $type_name$ ?@lstring className ;
  @lstring localConstant ;
  select
    localConstant := [@lstring new !"" !here] ;
  or
    $identifier$ ?localConstant ;
  end select ;
  select
  or
    $($ ;
    repeat
    while
      $type_name$ ?* ;
      $identifier$ ?* ;
    while
      $*$ ;
    while
      $unsigned_literal_integer$ ?* ;
      $*$ ;
    end repeat ;
    select
    or
      $...$ ;
    end select ;
    $)$ ;
  end select ;
  ioMatchEntryList +=
    !true
    !className
    !localConstant
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <match_instruction_branch> ?!@matchInstructionBranchListAST ioMatchInstructionBranchList :
  $when$ ;
  @matchEntryListAST mMatchEntryList [emptyList] ;
  repeat
    <match_entry> !? mMatchEntryList ;
  while
    $::$ ;
  end repeat ;
  const @location endOfMatchEntryList := here ;
  $:$ ;
  @semanticInstructionListAST mMatchBranchInstructionList ;
  <semantic_instruction_list> ? mMatchBranchInstructionList ;
  ioMatchInstructionBranchList +=
    !mMatchEntryList
    !endOfMatchEntryList
    !mMatchBranchInstructionList
    !here
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $match$ ;
  @semanticExpressionListAST matchedExpressionNameList [emptyList] ;
  const @location instructionLocation := here ;
  @semanticExpressionAST receiverExpression ;
  <expression> ? receiverExpression ;
  matchedExpressionNameList += !receiverExpression !here ;
  $::$ ;
  repeat
    @semanticExpressionAST receiverExpression ;
    <expression> ? receiverExpression ;
    matchedExpressionNameList += !receiverExpression !here ;
  while
    $::$ ;
  end repeat ;
  $:$ ;
  @matchInstructionBranchListAST matchInstructionBranchList [emptyList] ;
  repeat
    <match_instruction_branch> !? matchInstructionBranchList ;
  while
  end repeat ;
  @semanticInstructionListAST elseInstructionList ;
  select
    elseInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $else$ ;
    <semantic_instruction_list> ? elseInstructionList ;
  end select ;
  $end$ ;
  $match$ ;
  const @location endOf_match_instruction := here ;
  $;$ ;
  outInstruction := [@matchInstructionAST new
    !instructionLocation
    !matchedExpressionNameList
    !matchInstructionBranchList
    !elseInstructionList
    !endOf_match_instruction
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    C A S T    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <cast_instruction_branch> ?!@castInstructionBranchListAST ioCastInstructionBranchList :
  $when$ ;
  @dynamicTypeComparisonKind kind ;
  select
    $==$ ; kind := [@dynamicTypeComparisonKind equal] ;
  or
    $>=$ ; kind := [@dynamicTypeComparisonKind inherited] ;
  or
    $>$ ; kind := [@dynamicTypeComparisonKind strictlyInherited] ;
  end select ;
  $type_name$ ? @lstring mTypeName ;
  @lstring mConstantVarName ; # Empty string if no constant var
  select
    $identifier$ ?mConstantVarName ;
  or
    mConstantVarName := [@lstring new !"" !here] ;
  end select ;
  $:$ ;
  <semantic_instruction_list> ?@semanticInstructionListAST mCastBranchInstructionList ;
  ioCastInstructionBranchList +=
    !mTypeName
    !mConstantVarName
    !mCastBranchInstructionList
    !kind
    !here
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <cast_else_or_default> !@semanticInstructionListAST outElseOrDefault
:
  outElseOrDefault := [@semanticInstructionListAST emptyList] ;
end rule ;

#---------------------------------------------------------------------------*

rule <cast_else_or_default> !@semanticInstructionListAST outElseOrDefault
:
  $else$ ;
  <semantic_instruction_list> ? outElseOrDefault ;
end rule ;

#---------------------------------------------------------------------------*

rule <cast_else_or_default> !@semanticInstructionListAST outElseOrDefault :
  $default$ ;
  const @location instructionLocation := here ;
  $error$ ;
  @semanticExpressionAST errorLocationExpression ;
  <expression> ? errorLocationExpression ;
  outElseOrDefault := [@semanticInstructionListAST emptyList] ;
  outElseOrDefault += ![@errorInstructionAST new
    !instructionLocation
    !errorLocationExpression
    ![@literalStringExpressionAST new
      !instructionLocation
      ![@stringlist listWithValue !"cast instruction run-time error"]
     ]
    ![@lstringlist emptyList]
  ] ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $cast$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mExpression ;
  <expression> ? mExpression ;
  @castInstructionBranchListAST mCastInstructionBranchList [emptyList] ;
  repeat
    <cast_instruction_branch> !? mCastInstructionBranchList ;
  while
  end repeat ;
  @semanticInstructionListAST mElseOrDefault ;
  <cast_else_or_default> ? mElseOrDefault ;
  $end$ ;
  const @location endOfInstructionLocation := here ;
  $cast$ ;
  $;$ ;
  outInstruction := [@structuredCastInstructionAST new
    !instructionLocation
    !mExpression
    !mCastInstructionBranchList
    !mElseOrDefault
    !endOfInstructionLocation
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    +=    I N S T R U C T I O N                                            *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  @lstring mReceiverName ;
  $identifier$ ? mReceiverName ;
  const @location instructionLocation := here ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $+=$ ;
  @semanticExpressionListAST mExpressionList ;
  <output_expression_list> ?mExpressionList ;
  $;$ ;
  outInstruction := [@plusEqualInstructionAST new
    !instructionLocation
    !mReceiverName
    !mStructAttributeList
    !mExpressionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    SELF +=    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $self$ ;
  const @location instructionLocation := here ;
  $+=$ ;
  @semanticExpressionListAST mExpressionList ;
  <output_expression_list> ?mExpressionList ;
  $;$ ;
  outInstruction := [@selfPlusEqualInstructionAST new
    !instructionLocation
    !mExpressionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    C O N C A T    I N S T R U C T I O N    . =                            *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $identifier$ ? @lstring mTargetVariableName ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $.=$ ;
  @semanticExpressionAST mSourceExpression ;
  <expression> ? mSourceExpression ;
  $;$ ;
  outInstruction := [@concatInstructionAST new
    ![mTargetVariableName location]
    !mTargetVariableName
    !mStructAttributeList
    !mSourceExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    S E L F   C O N C A T    I N S T R U C T I O N    SELF .=              *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $self$ ;
  const @location instructionLocation := here ;
  $.=$ ;
  @semanticExpressionAST mSourceExpression ;
  <expression> ? mSourceExpression ;
  $;$ ;
  outInstruction := [@selfConcatInstructionAST new
    !instructionLocation
    !mSourceExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    L O O P    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $loop$ ;
  const @location instructionLocation := here ;
#--- Variant
  <expression> ?@semanticExpressionAST mVariantExpression ;
  const @location mEndOfVariantExpression := here ;
  $:$ ;
#---
    <semantic_instruction_list> ?@semanticInstructionListAST mFirstInstructions ;
#---
  const @location mEndOfFirstInstructions := here ;
  $while$ ;
  @semanticExpressionAST mLoopExpression ;
  <expression> ? mLoopExpression ;
  const @location mEndOfLoopExpression := here ;
  $do$ ;
  @semanticInstructionListAST mSecondInstructions ;
  <semantic_instruction_list> ? mSecondInstructions ;
  const @location mEndOfSecondInstructions := here ;
  $end$ ;
  const @location mEndOfLoopInstruction := here ;
  $loop$ ;
  $;$ ;
  outInstruction := [@loopInstructionAST new
    !instructionLocation
    !mVariantExpression
    !mEndOfVariantExpression
    !mFirstInstructions
    !mEndOfFirstInstructions
    !mLoopExpression
    !mEndOfLoopExpression
    !mSecondInstructions
    !mEndOfSecondInstructions
    !mEndOfLoopInstruction
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M E S S A G E    I N S T R U C T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $message$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mExpression ;
  <expression> ? mExpression ;
  $;$ ;
  outInstruction := [@messageInstructionAST new
    !instructionLocation
    !mExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    L O G    I N S T R U C T I O N                                         *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $log$ ;
  const @location instructionLocation := here ;
  @logListAST logList [emptyList] ;
  repeat
    select
      $identifier$ ? @lstring loggedVariableName ;
      logList += !loggedVariableName ![@varInExpressionAST new !loggedVariableName] ;
    or
      $literal_string$ ? @lstring logMessage ;
      $:$ ;
      @semanticExpressionAST logExpression ;
      <expression> ?logExpression ;
      logList += !logMessage !logExpression ;
    end select ;
  while
    $,$ ;
  end repeat ;
  $;$ ;
  outInstruction := [@logInstructionAST new
    !instructionLocation
    !logList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  G R A M M A R    I N S T R U C T I O N                                   *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST ioLocalVariableDeclarationList
:
  $grammar$ ;
  const @location instructionLocation := here ;
  $identifier$ ?@lstring mGrammarComponentName indexing grammarComponentReference;
  @lstring mLabelName ;
  select
    mLabelName := [@lstring new !"" !here] ;
  or
    $label$ ;
    $identifier$ ?mLabelName ;
  end select ;
  @bool sourceExpressionIsFile ;
  select
    $in$ ; sourceExpressionIsFile := true ;
  or
    $on$ ; sourceExpressionIsFile := false ;
  end select ;
  @semanticExpressionAST mSourceExpression ;
  <expression> ? mSourceExpression ;
  const @location endOfSourceExpression := here ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList !?ioLocalVariableDeclarationList ;
  $;$ ;
  outInstruction := [@grammarInstructionAST new
    !instructionLocation
    !mGrammarComponentName
    !mLabelName
    !mSourceExpression
    !endOfSourceExpression
    !sourceExpressionIsFile
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  S W I T C H    I N S T R U C T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST unused ioLocalVariableDeclarationList
:
  $switch$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST switchExpression ;
  <expression> ? switchExpression ;
  const @location endOfSwitchExpression := here ;
  @switchBranchesAST switchBranches [emptyList] ;
  repeat
    $when$ ;
    @lstringlist constantNameList [emptyList] ;
    repeat
      $identifier$ ? @lstring constantName ;
      constantNameList += !constantName ;
    while
      $,$ ;
    end repeat ;
    $:$ ;
    @semanticInstructionListAST instructions ;
    <semantic_instruction_list> ? instructions ;
    switchBranches += !constantNameList !instructions !here ;
  while
  end repeat ;
  $end$ ;
  $switch$ ;
  const @location endOfSwitchInstruction := here ;
  $;$ ;
  outInstruction := [@switchInstructionAST new
    !instructionLocation
    !switchExpression
    !endOfSwitchExpression
    !switchBranches
    !endOfSwitchInstruction
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  T Y P E    M E T H O D    C A L L    I N S T R U C T I O N               *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction>
  !@semanticInstructionAST outInstruction
  ?!@localVariableDeclarationListAST ioLocalVariableDeclarationList
:
  $[$ ;
  const @location instructionLocation := here ;
  $type_name$ ? @lstring mTypeName indexing typeReferenceInTypeMethod ;
  $identifier$ ? @lstring mMethodName ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList !?ioLocalVariableDeclarationList ;
  $]$ ;
  $;$ ;
  outInstruction := [@typeMethodCallInstructionAST new
    !instructionLocation
    !mTypeName
    !mMethodName
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;
