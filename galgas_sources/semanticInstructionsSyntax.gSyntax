#---------------------------------------------------------------------------*
#                                                                           *
#  'metamodel' component parser, as model builder                           *
#                                                                           *
#  Copyright (C) 2007, ..., 2010 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax semanticInstructionsSyntax :
with "galgas_scanner.gLexique" ;

import lexique galgas_scanner in "galgas_scanner.gLexique" ;
import semantics semanticsTypesForAST in "semanticsTypesForAST.gSemantics" ;

#------------------------------------------------------------------------------*
#                                                                              *
#          Rules prototypes                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <expression> !@semanticExpressionAST outExpression ;

nonterminal <formal_parameter_list> !@formalParameterListAST outFormalParameterList ;

nonterminal <actual_parameter_list> !@actualParameterListAST outActualParameterList ;

nonterminal <semantic_instruction_list> !@semanticInstructionListAST outInstructionsList ;

nonterminal <semantic_instruction> !@semanticInstructionAST outInstruction ;

nonterminal <output_expression_list> !@outSemanticExpressionListAST outExpressionList ;

nonterminal <semantic_declaration>
 ?!@semanticDeclarationStructAST ioSemanticDeclarations
;

#---------------------------------------------------------------------------*
#                                                                           *
#    S E M A N T I C    I N S T R U C T I O N    L I S T                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction_list> !@semanticInstructionListAST outInstructionsList :
  outInstructionsList := [@semanticInstructionListAST emptyList] ;
  repeat
  while
    @semanticInstructionAST instruction ;
    <semantic_instruction> ? instruction ;
    outInstructionsList += !instruction ;
  end repeat ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    R O U T I N E    D E C L A R A T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationStructAST ioSemanticDeclarations
:
  $routine$ ;
  $identifier$ ? @lstring mRoutineName ;
  @formalParameterListAST mFormalParameterList ;
  <formal_parameter_list> ? mFormalParameterList ;
  $:$ ;
  @semanticInstructionListAST mRoutineInstructionList ;
  <semantic_instruction_list> ? mRoutineInstructionList ;
  $end$ ;
  $routine$ ;
  $;$ ;
  ioSemanticDeclarations->mSemanticDeclarationList += ![@routineDeclarationAST new
    !mRoutineName
    !mFormalParameterList
    !mRoutineInstructionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E X T E R N    R O U T I N E    D E C L A R A T I O N                  *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationStructAST ioSemanticDeclarations
:
  $extern$ ;
  $routine$ ;
  $identifier$ ? @lstring mActionName ;
  @formalParameterListAST mFormalParameterList ;
  <formal_parameter_list> ? mFormalParameterList ;
  $;$ ;
  ioSemanticDeclarations->mSemanticDeclarationList += ![@externRoutineDeclarationAST new
    !mActionName
    !mFormalParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    R O U T I N E    P R O T O T Y P E    D E C L A R A T I O N            *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationStructAST ioSemanticDeclarations
:
  $routine$ ;
  $identifier$ ? @lstring mActionName ;
  @formalParameterListAST mFormalParameterList ;
  <formal_parameter_list> ? mFormalParameterList ;
  $;$ ;
  ioSemanticDeclarations->mSemanticDeclarationList += ![@routinePrototypeDeclarationAST new
    !mActionName
    !mFormalParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    F U N C T I O N    D E C L A R A T I O N                               *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationStructAST ioSemanticDeclarations
:
  $function$ ;
  $identifier$ ? @lstring mRoutineName ;
  @formalParameterListAST mFormalParameterList ;
  <formal_parameter_list> ? mFormalParameterList ;
  $->$ ;
  $type_name$ ?@lstring resultTypeName ;
  $identifier$ ?@lstring resultVariableName ;
  $:$ ;
  @semanticInstructionListAST mRoutineInstructionList ;
  <semantic_instruction_list> ? mRoutineInstructionList ;
  $end$ ;
  $function$ ;
  $;$ ;
  ioSemanticDeclarations->mSemanticDeclarationList += ![@functionDeclarationAST new
    !mRoutineName
    !mFormalParameterList
    !resultTypeName
    !resultVariableName
    !mRoutineInstructionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    O N C E    F U N C T I O N    D E C L A R A T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationStructAST ioSemanticDeclarations
:
  $once$ ;
  $identifier$ ? @lstring mRoutineName ;
  $->$ ;
  $type_name$ ?@lstring resultTypeName ;
  $identifier$ ?@lstring resultVariableName ;
  select
    $;$ ;
  or
    $:$ ;
    @semanticInstructionListAST mRoutineInstructionList ;
    <semantic_instruction_list> ? mRoutineInstructionList ;
    $end$ ;
    $once$ ;
    $;$ ;
    ioSemanticDeclarations->mSemanticDeclarationList += ![@lazyFunctionDeclarationAST new
      !mRoutineName
      !resultTypeName
      !resultVariableName
      !mRoutineInstructionList
    ] ;
  end select ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E X T E R N    F U N C T I O N    D E C L A R A T I O N                *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration>
 ?!@semanticDeclarationStructAST ioSemanticDeclarations
:
  $extern$ ;
  $function$ ;
  $identifier$ ? @lstring mActionName ;
  @formalParameterListAST mFormalParameterList ;
  <formal_parameter_list> ? mFormalParameterList ;
  $->$ ;
  $type_name$ ?@lstring resultTypeName ;
  $identifier$ ?@lstring resultVariableName ;
  $;$ ;
  ioSemanticDeclarations->mSemanticDeclarationList += ![@externFunctionDeclarationAST new
    !mActionName
    !mFormalParameterList
    !resultTypeName
    !resultVariableName
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  L O C A L    V A R I A B L E    D E C L A R A T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  select
    outInstruction := [@localVariableDeclarationAST new ![typeName location] !typeName !variableName] ;
  or
    $:=$ ;
    @semanticExpressionAST sourceExpression ;
    <expression> ? sourceExpression ;
    outInstruction := [@localVariableDeclarationWithAssignmentAST new ![typeName location] !typeName !variableName !sourceExpression] ;
  or
    $[$ ;
    @lstring constructorName ;
    $identifier$ ? constructorName ;
    @outSemanticExpressionListAST expressionList ;
    <output_expression_list> ? expressionList ;
    outInstruction := [@localVariableDeclarationWithInitializationAST new ![typeName location] !typeName !variableName !constructorName !expressionList] ;
    $]$ ;
  end select ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  L O C A L    C O N S T A N T    D E C L A R A T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $const$ ;
  $type_name$ ?@lstring typeName ;
  $identifier$ ?@lstring variableName ;
  select
    $:=$ ;
    @semanticExpressionAST sourceExpression ;
    <expression> ? sourceExpression ;
    outInstruction := [@localConstantDeclarationWithSourceExpressionAST new  ![typeName location] !typeName !variableName !sourceExpression] ;
  or
    $[$ ;
    @lstring constructorName ;
    $identifier$ ? constructorName ;
    @outSemanticExpressionListAST expressionList ;
    <output_expression_list> ? expressionList ;
    outInstruction := [@localConstantDeclarationWithConstructorCallAST new ![typeName location] !typeName !variableName !constructorName !expressionList] ;
    $]$ ;
  end select ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    A S S I G N M E N T    I N S T R U C T I O N                           *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $identifier$ ?@lstring mTargetVariableName ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $:=$ ;
  @semanticExpressionAST mSourceExpression ;
  <expression> ? mSourceExpression ;
  $;$ ;
  outInstruction := [@assignmentInstructionAST new
    ![mTargetVariableName location]
    !mTargetVariableName
    !mStructAttributeList
    !mSourceExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    I N C R E M E N T    I N S T R U C T I O N                             *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $identifier$ ? @lstring mReceiverName ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $++$ ;
  $;$ ;
  outInstruction := [@incrementInstructionAST new ![mReceiverName location] !mReceiverName !mStructAttributeList] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    D E C R E M E N T    I N S T R U C T I O N                             *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $identifier$ ?@lstring mReceiverName ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $--$ ;
  $;$ ;
  outInstruction := [@decrementInstructionAST new ![mReceiverName location] !mReceiverName !mStructAttributeList] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    F O R E A C H    I N S T R U C T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

nonterminal <foreach_enumerated_object>
  ?!@foreachInstructionEnumeratedObjectListAST ioForeachInstructionEnumeratedObjectList
;

#---------------------------------------------------------------------------*

rule <foreach_enumerated_object>
  ?!@foreachInstructionEnumeratedObjectListAST ioForeachInstructionEnumeratedObjectList
:
  @bool ascending ;
  select
    ascending := true ;
  or
    $<$ ;
    ascending := true ;
  or
    $>$ ;
    ascending := false ;
  end select ;
  @semanticExpressionAST mEnumeratedExpression ;
  <expression> ? mEnumeratedExpression ;
  @foreachInstructionEnumeratedObjectElementListAST mElementList [emptyList] ;
  @bool mEndsWithEllipsis ;
  @lstring prefix ;
  select
    mEndsWithEllipsis := false ;
    prefix := [@lstring new !"" !here] ;
  or
    $prefixedby$ ;
    $identifier$ ? prefix ;
    mEndsWithEllipsis := false ;
  or
    prefix := [@lstring new !"" !here] ;
    $($ ;
    repeat
    while
      $*$ ;
      mElementList += ![@foreachInstructionJokerAST new] ;
    while
      @luint count ;
      $unsigned_literal_integer$ ?count ;
      $*$ ;
      @uint n := [count uint] ;
      if n == 0 then
        error count: "this value should be > 0" ;
      end if ;
      loop n :
      while n > 0 do
        mElementList += ![@foreachInstructionJokerAST new] ;
        n -- ;
      end loop ;
    while
      @lstring typeName ;
      $type_name$ ?typeName ;
      @lstring constantName ;
      $identifier$ ?constantName ;
      @foreachInstructionEnumeratedConstantAST c [new !typeName !constantName] ;
      mElementList += !c ;
    end repeat ;
    select
      mEndsWithEllipsis := false ;
    or
      $...$ ;
      mEndsWithEllipsis := true ;
    end select ;
    $)$ ;
  end select ;
  ioForeachInstructionEnumeratedObjectList +=
    !ascending
    !mEnumeratedExpression
    !mElementList
    !mEndsWithEllipsis
    !prefix
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $foreach$ ;
  const @location instructionLocation := here ;
  @foreachInstructionEnumeratedObjectListAST mEnumeratedObjectList [emptyList] ;
  repeat
    <foreach_enumerated_object> !?mEnumeratedObjectList ;
  while
    $,$ ;
  end repeat ;
  @lstring mIndexVariableName ;
  select
    mIndexVariableName := [@lstring new !"" !here] ;
  or
    $:$ ;
    $identifier$ ? mIndexVariableName ;
  end select ;
  @semanticExpressionAST mWhileExpression ;
  select
    mWhileExpression := [@trueExpressionAST new] ;
  or
    $while$ ;
    <expression> ? mWhileExpression ;
  end select ;
  @semanticInstructionListAST mBeforeInstructionList ;
  select
    mBeforeInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $before$ ;
    <semantic_instruction_list> ? mBeforeInstructionList ;
  end select ;
  $do$ ;
  @semanticInstructionListAST mDoInstructionList ;
  <semantic_instruction_list> ? mDoInstructionList ;
  @semanticInstructionListAST mBetweenInstructionList ;
  select
    mBetweenInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $between$ ;
    <semantic_instruction_list> ? mBetweenInstructionList ;
  end select ;
  @semanticInstructionListAST mAfterInstructionList ;
  select
    mAfterInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $after$ ;
    <semantic_instruction_list> ? mAfterInstructionList ;
  end select ;
  $end$ ;
  $foreach$ ;
  $;$ ;
  outInstruction := [@foreachInstructionAST new
    !instructionLocation
    !mEnumeratedObjectList
    !mIndexVariableName
    !mWhileExpression
    !mBeforeInstructionList
    !mBetweenInstructionList
    !mDoInstructionList
    !mAfterInstructionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    S E N D    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $send$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST expression ;
  <expression> ? expression ;
  $;$ ;
  outInstruction := [@sendInstructionAST new
    !instructionLocation
    !expression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    I F    I N S T R U C T I O N                                           *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $if$ ;
  const @location instructionLocation := here ;
  @ifBranchListAST mBranchList [emptyList] ;
  repeat
    @semanticExpressionAST mIFexpressionList ;
    <expression> ?mIFexpressionList ;
    $then$ ;
    @semanticInstructionListAST mIFinstructionList ;
    <semantic_instruction_list> ? mIFinstructionList ;
    mBranchList += !mIFexpressionList !mIFinstructionList ;
  while
    $elsif$ ;
  end repeat ;
  @semanticInstructionListAST mElseInstructionList ;
  select
    mElseInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $else$ ;
    <semantic_instruction_list> ? mElseInstructionList ;
  end select ;
  $end$ ;
  $if$ ;
  $;$ ;
  outInstruction := [@ifInstructionAST new
    !instructionLocation
    !mBranchList
    !mElseInstructionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M E T H O D    C A L L    I N S T R U C T I O N                        *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $[$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mReceiverExpression ;
  <expression> ? mReceiverExpression ;
  $identifier$ ?@lstring mMethodName ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList ;
  $]$ ;
  $;$ ;
  outInstruction := [@methodCallInstructionAST new
    !instructionLocation
    !mReceiverExpression
    !mMethodName
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M O D I F I E R    C A L L    I N S T R U C T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $[$ ;
  const @location instructionLocation := here ;
  $!?$ ;
  $identifier$ ?@lstring mReceiverName ;
  @lstringlist mReceiverStructAttributes [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ?@lstring structAttribute ;
    mReceiverStructAttributes += !structAttribute ;
  end repeat ;
  $identifier$ ?@lstring mModifierName ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList ;
  $]$ ;
  $;$ ;
  outInstruction := [@modifierCallInstructionAST new
    !instructionLocation
    !mReceiverName
    !mReceiverStructAttributes
    !mModifierName
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E R R O R    I N S T R U C T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $error$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mReceiverExpression ;
  <expression> ? mReceiverExpression ;
  $:$ ;
  @semanticExpressionAST mErrorExpression ;
  <expression> ? mErrorExpression ;
  @lstringlist mBuiltVariableList [emptyList] ;
  select
  or
    $:$ ;
    repeat
     @lstring builtVariableName ;
     $identifier$ ? builtVariableName ;
     mBuiltVariableList += !builtVariableName ;
    while
      $,$ ;
    end repeat ;
  end select ;
  $;$ ;
  outInstruction := [@errorInstructionAST new
    !instructionLocation
    !mReceiverExpression
    !mErrorExpression
    !mBuiltVariableList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    W A R N I N G    I N S T R U C T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $warning$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mReceiverExpression ;
  <expression> ? mReceiverExpression ;
  $:$ ;
  @semanticExpressionAST mWarningExpression ;
  <expression> ? mWarningExpression ;
  $;$ ;
  outInstruction := [@warningInstructionAST new
    !instructionLocation
    !mReceiverExpression
    !mWarningExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    R O U T I N E    C A L L    I N S T R U C T I O N                      *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $identifier$ ?@lstring mRoutineName ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList ;
  $;$ ;
  outInstruction := [@routineCallInstructionAST new
    ![mRoutineName location]
    !mRoutineName
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    W I T H    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $with$ ;
  const @location instructionLocation := here ;
  @lstring withPrefix ;
  select
    withPrefix := [@lstring new !"" !here] ;
  or
    $identifier$ ? withPrefix ;
    $:$ ;
  end select ;
  $!?$ ;
  $identifier$ ?@lstring mTargetObjectName ;
  @lstringlist structAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ?@lstring mStructFieldName ;
    structAttributeList += !mStructFieldName ;
  end repeat ;
  $identifier$ ?@lstring mSearchMethodName ;
  $!$ ;
  @semanticExpressionAST mKeyExpression ;
  <expression> ? mKeyExpression ;
  $do$ ;
  @semanticInstructionListAST mDoBranchInstructions ;
  <semantic_instruction_list> ? mDoBranchInstructions ;
  @semanticInstructionListAST mElseBranchInstructions ;
  select
    mElseBranchInstructions := [@semanticInstructionListAST emptyList] ;
  or
    $else$ ;
    <semantic_instruction_list> ? mElseBranchInstructions ;
  end select ;
  outInstruction := [@withInstructionAST new
    !instructionLocation
    !withPrefix
    !mTargetObjectName
    !structAttributeList
    !mSearchMethodName
    !mKeyExpression
    !mDoBranchInstructions
    !mElseBranchInstructions
  ] ;
  $end$ ;
  $with$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M A T C H    I N S T R U C T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

nonterminal <match_entry> ?!@matchEntryListAST ioMatchEntryList ;

#---------------------------------------------------------------------------*

rule <match_entry> ?!@matchEntryListAST ioMatchEntryList :
  $identifier$ ? @lstring mConstantName ;
  ioMatchEntryList += ![@constantMatchEntryAST new !mConstantName] ;
end rule ;

#---------------------------------------------------------------------------*

rule <match_entry> ?!@matchEntryListAST ioMatchEntryList :
  $type_name$ ?@lstring mClassName ;
  $($ ;
  @foreachInstructionEnumeratedObjectElementListAST mElements [emptyList] ;
  repeat
  while
    $*$ ;
    mElements += ![@foreachInstructionJokerAST new] ;
  while
    @luint count ;
    $unsigned_literal_integer$ ?count ;
    $*$ ;
    @uint n := [count uint] ;
    if n == 0 then
      error count: "this value should be > 0" ;
    end if ;
    loop n :
    while n > 0 do
      mElements += ![@foreachInstructionJokerAST new] ;
      n -- ;
    end loop ;
  while
    @lstring typeName ;
    $type_name$ ?typeName ;
    @lstring constantName ;
    $identifier$ ?constantName ;
    @foreachInstructionEnumeratedConstantAST c [new !typeName !constantName] ;
    mElements += !c ;
  end repeat ;
  @bool mEndsWithEllipsis ;
  select
    mEndsWithEllipsis := false ;
  or
    $...$ ;
    mEndsWithEllipsis := true ;
  end select ;
  $)$ ;
  ioMatchEntryList += ![@classMatchEntryAST new
    !mClassName
    !mElements
    !mEndsWithEllipsis
  ] ;
end rule ;

#---------------------------------------------------------------------------*

nonterminal <match_instruction_branch> ?!@matchInstructionBranchListAST ioMatchInstructionBranchList ;

#---------------------------------------------------------------------------*

rule <match_instruction_branch> ?!@matchInstructionBranchListAST ioMatchInstructionBranchList :
  $when$ ;
  @matchEntryListAST mMatchEntryList [emptyList] ;
  repeat
    <match_entry> !? mMatchEntryList ;
  while
    $::$ ;
  end repeat ;
  $:$ ;
  @semanticInstructionListAST mMatchBranchInstructionList ;
  <semantic_instruction_list> ? mMatchBranchInstructionList ;
  ioMatchInstructionBranchList +=
    !mMatchEntryList
    !mMatchBranchInstructionList
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $match$ ;
  const @location instructionLocation := here ;
  @lstring matchedVariableName ;
  $identifier$ ? matchedVariableName ;
  @lstringlist mMatchedVariableNameList [emptyList] ;
  mMatchedVariableNameList += !matchedVariableName ;
  $::$ ;
  repeat
    $identifier$ ? matchedVariableName ;
    mMatchedVariableNameList += !matchedVariableName ;
  while
    $::$ ;
  end repeat ;
  $:$ ;
  @matchInstructionBranchListAST mMatchInstructionBranchList [emptyList] ;
  repeat
    <match_instruction_branch> !? mMatchInstructionBranchList ;
  while
  end repeat ;
  @semanticInstructionListAST mElseInstructionList ;
  select
    mElseInstructionList := [@semanticInstructionListAST emptyList] ;
  or
    $else$ ;
    <semantic_instruction_list> ? mElseInstructionList ;
  end select ;
  $end$ ;
  $match$ ;
  $;$ ;
  outInstruction := [@matchInstructionAST new
    !instructionLocation
    !mMatchedVariableNameList
    !mMatchInstructionBranchList
    !mElseInstructionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    C A S T    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

nonterminal <cast_instruction_branch> ?!@castInstructionBranchListAST ioCastInstructionBranchList ;

#---------------------------------------------------------------------------*

rule <cast_instruction_branch> ?!@castInstructionBranchListAST ioCastInstructionBranchList :
  $when$ ;
  @bool mUseKindOfClass ;
  select
    $>=$ ;
    mUseKindOfClass := true ;
  or
    $==$ ;
    mUseKindOfClass := false ;
  end select ;
  $type_name$ ? @lstring mTypeName ;
  @lstring mConstantVarName ; # Empty string if no constant var
  @bool mConstantVarNameIsUnused ; # true if no constant var
  select
    $identifier$ ?mConstantVarName ;
    mConstantVarNameIsUnused := false ;
  or
    $unused$ ;
    $identifier$ ?mConstantVarName ;
    mConstantVarNameIsUnused := true ;
  or
    mConstantVarName := [@lstring new !"" !here] ;
    mConstantVarNameIsUnused := true ;
  end select ;
  $do$ ;
  @semanticInstructionListAST mCastBranchInstructionList ;
  <semantic_instruction_list> ? mCastBranchInstructionList ;
  ioCastInstructionBranchList +=
    !mUseKindOfClass
    !mTypeName
    !mConstantVarName
    !mConstantVarNameIsUnused
    !mCastBranchInstructionList
  ;
end rule ;

#---------------------------------------------------------------------------*

nonterminal <cast_else_or_default> !@elseOrDefaultForCastInstructionAST outElseOrDefault ;

#---------------------------------------------------------------------------*

rule <cast_else_or_default> !@elseOrDefaultForCastInstructionAST outElseOrDefault
:
  $else$ ;
  @semanticInstructionListAST mElseInstructionList ;
  <semantic_instruction_list> ? mElseInstructionList ;
  outElseOrDefault := [@elseForCastInstructionAST new !mElseInstructionList] ;
end rule ;

#---------------------------------------------------------------------------*

rule <cast_else_or_default> !@elseOrDefaultForCastInstructionAST outElseOrDefault :
  $default$ ;
  $error$ ;
  @semanticExpressionAST mErrorLocationExpression ;
  <expression> ? mErrorLocationExpression ;
  outElseOrDefault := [@defaultForCastInstructionAST new !mErrorLocationExpression] ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $cast$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mExpression ;
  <expression> ? mExpression ;
  $:$ ;
  @castInstructionBranchListAST mCastInstructionBranchList [emptyList] ;
  repeat
    <cast_instruction_branch> !? mCastInstructionBranchList ;
  while
  end repeat ;
  @elseOrDefaultForCastInstructionAST mElseOrDefault ;
  <cast_else_or_default> ? mElseOrDefault ;
  $end$ ;
  $cast$ ;
  $;$ ;
  outInstruction := [@castInstructionAST new
    !instructionLocation
    !mExpression
    !mCastInstructionBranchList
    !mElseOrDefault
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    A D D    I N S T R U C T I O N    + =                                  *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  @lstring mReceiverName ;
  $identifier$ ? mReceiverName ;
  const @location instructionLocation := here ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $+=$ ;
  @outSemanticExpressionListAST mExpressionList ;
  <output_expression_list> ? mExpressionList ;
  $;$ ;
  outInstruction := [@addInstructionAST new
    !instructionLocation
    !mReceiverName
    !mStructAttributeList
    !mExpressionList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    R E M O V E    I N S T R U C T I O N      - =                          *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  @lstring mReceiverName ;
  $identifier$ ? mReceiverName ;
  const @location instructionLocation := here ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $-=$ ;
  $!$ ;
  @semanticExpressionAST mExpression ;
  <expression> ? mExpression ;
  $;$ ;
  outInstruction := [@removeInstructionAST new
    !instructionLocation
    !mReceiverName
    !mStructAttributeList
    !mExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    A P P E N D    I N S T R U C T I O N    . =                            *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $identifier$ ? @lstring mTargetVariableName ;
  @lstringlist mStructAttributeList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ? @lstring attributeName ;
    mStructAttributeList += !attributeName ;
  end repeat ;
  $.=$ ;
  @semanticExpressionAST mSourceExpression ;
  <expression> ? mSourceExpression ;
  $;$ ;
  outInstruction := [@appendInstructionAST new
    ![mTargetVariableName location]
    !mTargetVariableName
    !mStructAttributeList
    !mSourceExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    L O O P    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $loop$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mVariantExpression ;
  <expression> ? mVariantExpression ;
  $:$ ;
  @semanticInstructionListAST mFirstInstructions ;
  <semantic_instruction_list> ? mFirstInstructions ;
  $while$ ;
  @semanticExpressionAST mLoopExpression ;
  <expression> ? mLoopExpression ;
  $do$ ;
  @semanticInstructionListAST mSecondInstructions ;
  <semantic_instruction_list> ? mSecondInstructions ;
  $end$ ;
  $loop$ ;
  $;$ ;
  outInstruction := [@loopInstructionAST new
    !instructionLocation
    !mVariantExpression
    !mFirstInstructions
    !mLoopExpression
    !mSecondInstructions
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M E S S A G E    I N S T R U C T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $message$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST mExpression ;
  <expression> ? mExpression ;
  $;$ ;
  outInstruction := [@messageInstructionAST new
    !instructionLocation
    !mExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    L O G    I N S T R U C T I O N                                         *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $log$ ;
  const @location instructionLocation := here ;
  @logListAST logList [emptyList] ;
  repeat
    select
      $identifier$ ? @lstring loggedVariableName ;
      logList += !loggedVariableName ![@varInExpressionAST new !loggedVariableName ![@lstringlist emptyList]] ;
    or
      $literal_string$ ? @lstring logMessage ;
      $:$ ;
      @semanticExpressionAST logExpression ;
      <expression> ?logExpression ;
      logList += !logMessage !logExpression ;
    end select ;
  while
    $,$ ;
  end repeat ;
  $;$ ;
  outInstruction := [@logInstructionAST new
    !instructionLocation
    !logList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  G R A M M A R    I N S T R U C T I O N                                   *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $grammar$ ;
  const @location instructionLocation := here ;
  @lstring mGrammarComponentName ;
  $identifier$ ?mGrammarComponentName ;
  @lstring mLabelName ;
  select
    mLabelName := [@lstring new !"" !here] ;
  or
    $label$ ;
    $identifier$ ?mLabelName ;
  end select ;
  @bool sourceExpressionIsFile ;
  select
    $in$ ; sourceExpressionIsFile := true ;
  or
    $on$ ; sourceExpressionIsFile := false ;
  end select ;
  @semanticExpressionAST mSourceExpression ;
  <expression> ? mSourceExpression ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList ;
#--- Dependency (obsolete ?)
  select
  or
    $dependency$ ;
    <expression> ? * ;
    $in$ ;
    <expression> ? * ;
  end select ;
  $;$ ;
  outInstruction := [@grammarInstructionAST new
    !instructionLocation
    !mGrammarComponentName
    !mLabelName
    !mSourceExpression
    !sourceExpressionIsFile
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  S W I T C H    I N S T R U C T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $switch$ ;
  const @location instructionLocation := here ;
  @semanticExpressionAST switchExpression ;
  <expression> ? switchExpression ;
  @switchBranchesAST switchBranches [emptyList] ;
  repeat
    $when$ ;
    @lstringlist constantNameList [emptyList] ;
    repeat
      $identifier$ ? @lstring constantName ;
      constantNameList += !constantName ;
    while
      $,$ ;
    end repeat ;
    $:$ ;
    @semanticInstructionListAST instructions ;
    <semantic_instruction_list> ? instructions ;
    switchBranches += !constantNameList !instructions ;
  while
  end repeat ;
  $end$ ;
  $switch$ ;
  $;$ ;
  outInstruction := [@switchInstructionAST new
    !instructionLocation
    !switchExpression
    !switchBranches
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  T Y P E    M E T H O D    C A L L    I N S T R U C T I O N               *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstructionAST outInstruction :
  $[$ ;
  const @location instructionLocation := here ;
  $type_name$ ? @lstring mTypeName ;
  $identifier$ ? @lstring mMethodName ;
  @actualParameterListAST mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList ;
  $]$ ;
  $;$ ;
  outInstruction := [@typeMethodInstructionAST new
    !instructionLocation
    !mTypeName
    !mMethodName
    !mActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*

#nonterminal <sdfgh> ;
#rule <sdfgh> : $bof$ ; end rule ;


end syntax ;
