#---------------------------------------------------------------------------*
#                                                                           *
#  'expression' component parser, as model builder                          *
#                                                                           *
#  Copyright (C) 2007, ..., 2010 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax expressionSyntax :
with "galgas_scanner.gLexique" ;

import lexique galgas_scanner in "galgas_scanner.gLexique" ;
import semantics semanticsTypesForAST in "semanticsTypesForAST.gSemantics" ;

#------------------------------------------------------------------------------*
#                                                                              *
#          Rules prototypes                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <expression> !@semanticExpressionAST outExpression ;

nonterminal <concatenation_term> !@semanticExpressionAST outExpression ;

nonterminal <relation_term> !@semanticExpressionAST outExpression ;

nonterminal <relation_factor> !@semanticExpressionAST outExpression ;

nonterminal <simple_expression> !@semanticExpressionAST outExpression ;

nonterminal <term> !@semanticExpressionAST outExpression ;

nonterminal <factor> !@semanticExpressionAST outExpression ;

nonterminal <output_expression_list>
  !@semanticExpressionListAST outExpressionList
;

#------------------------------------------------------------------------------*
#                                                                              *
#   E X P R E S S I O N                                                        *
#                                                                              *
#------------------------------------------------------------------------------*

rule <expression> !@semanticExpressionAST outExpression :
  <concatenation_term> ? outExpression ;
  repeat
  while
    @location operatorLocation := here ;
    $.$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <concatenation_term> ? rightOperand ;
    outExpression := [@concatExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   C O N C A T E N A T I O N    T E R M                                       *
#                                                                              *
#------------------------------------------------------------------------------*

rule <concatenation_term> !@semanticExpressionAST outExpression :
  <relation_term> ? outExpression ;
  repeat
  while
    @location operatorLocation := here ;
    $|$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <relation_term> ? rightOperand ;
    outExpression := [@orExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $^$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <relation_term> ? rightOperand ;
    outExpression := [@xorExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   R E L A T I O N    T E R M                                                 *
#                                                                              *
#------------------------------------------------------------------------------*

rule <relation_term> !@semanticExpressionAST outExpression :
  <relation_factor> ? outExpression ;
  repeat
  while
    @location operatorLocation := here ;
    $&$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <relation_factor> ? rightOperand ;
    outExpression := [@andExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   R E L A T I O N    F A C T O R                                             *
#                                                                              *
#------------------------------------------------------------------------------*

rule <relation_factor> !@semanticExpressionAST outExpression :
  <simple_expression> ? outExpression ;
  repeat
  while
    @location operatorLocation := here ;
    $==$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <simple_expression> ? rightOperand ;
    outExpression := [@equalExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $!=$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <simple_expression> ? rightOperand ;
    outExpression := [@notEqualExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $<=$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <simple_expression> ? rightOperand ;
    outExpression := [@lowerOrEqualExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $>=$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <simple_expression> ? rightOperand ;
    outExpression := [@greaterOrEqualExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $>$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <simple_expression> ? rightOperand ;
    outExpression := [@strictGreaterExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $<$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <simple_expression> ? rightOperand ;
    outExpression := [@strictLowerExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   S I M P L E    E X P R E S S I O N                                         *
#                                                                              *
#------------------------------------------------------------------------------*

rule <simple_expression> !@semanticExpressionAST outExpression :
  <term> ? outExpression ;
  repeat
  while
    @location operatorLocation := here ;
    $<<$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <term> ? rightOperand ;
    outExpression := [@leftShiftExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $>>$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <term> ? rightOperand ;
    outExpression := [@rightShiftExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $+$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <term> ? rightOperand ;
    outExpression := [@addExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $-$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <term> ? rightOperand ;
    outExpression := [@subExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   T E R M                                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

rule <term> !@semanticExpressionAST outExpression :
  <factor> ? outExpression ;
  repeat
  while
    @location operatorLocation := here ;
    $*$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <factor> ? rightOperand ;
    outExpression := [@multiplicationExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $/$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <factor> ? rightOperand ;
    outExpression := [@divisionExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $mod$ ;
    @semanticExpressionAST leftOperand := outExpression ;
    @semanticExpressionAST rightOperand ;
    <factor> ? rightOperand ;
    outExpression := [@moduloExpressionAST new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    - factor                                               *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  @location operatorLocation := here ;
  $-$ ;
  <factor> ? outExpression ;
  outExpression := [@unaryMinusExpressionAST new !operatorLocation !outExpression] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    <var>                                                  *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $identifier$ ? @lstring identifier ;
  @lstringlist structFieldList [emptyList] ;
  repeat
  while
    $->$ ;
    $identifier$ ?@lstring fieldName ;
    structFieldList += !fieldName ;
  end repeat ;
  outExpression := [@varInExpressionAST new !identifier !structFieldList] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    S E L F                                                *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $self$ ;
  outExpression := [@selfInExpressionAST new !here] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    ( EXPRESSION )                                         *
#                       (cast EXP if >= @T else error ERROR_LOCATION_EXP)      *
#                       (cast EXP if == @T else error ERROR_LOCATION_EXP)      *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $($ ;
  select
    <expression> ? outExpression ;
  or
    $cast$ ;
    @semanticExpressionAST receiverExpression ;
    <expression> ? receiverExpression ;
    const @location endOfReceiverExpression := here ;
    $if$ ;
    @bool useKindOfClass ;
    select
      $>=$ ;
      useKindOfClass := true ;
    or
      $==$ ;
      useKindOfClass := false ;
    end select ;
    @lstring typeName ;
    $type_name$ ? typeName ;
    $else$ ;
    $error$ ;
    @semanticExpressionAST errorLocationExpression ;
    <expression> ? errorLocationExpression ;
    outExpression := [@castInExpressionAST new
      !receiverExpression
      !endOfReceiverExpression
      !useKindOfClass
      !typeName
      !errorLocationExpression
      !here
    ] ;
  end select ;
  $)$ ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    not FACTOR                                             *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  @location operatorLocation := here ;
  $not$ ;
  <factor> ? outExpression ;
  outExpression := [@notExpressionAST new !operatorLocation !outExpression] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    ~ FACTOR                                               *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  @location operatorLocation := here ;
  $~$ ;
  <factor> ? outExpression ;
  outExpression := [@complementExpressionAST new !operatorLocation !outExpression] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    true                                                   *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $true$ ;
  outExpression := [@trueExpressionAST new !here] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    false                                                  *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $false$ ;
  outExpression := [@falseExpressionAST new !here] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :   literal string sequence "..."  "..."  "..."             *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  @stringlist literalStringList [emptyList] ;
  repeat
    @lstring literalString ;
    $literal_string$ ?literalString ;
    literalStringList += ![literalString string] ;
  while
  end repeat ;
  outExpression := [@literalStringExpressionAST new !here !literalStringList] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a 32-bit unsigned literal integer value                *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  @luint literalInt ;
  $unsigned_literal_integer$ ?literalInt ;
  outExpression := [@literalUIntExpressionAST new !literalInt] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a 32-bit signed literal integer value                  *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  @lsint literalInt ;
  $signed_literal_integer$ ?literalInt ;
  outExpression := [@literalSIntExpressionAST new !literalInt] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a 64-bit unsigned literal integer value                *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  @luint64 literalInt ;
  $unsigned_literal_integer64$ ?literalInt ;
  outExpression := [@literalUInt64ExpressionAST new !literalInt] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a 64-bit signed literal integer value                  *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  @lsint64 literalInt ;
  $signed_literal_integer64$ ?literalInt ;
  outExpression := [@literalSInt64ExpressionAST new !literalInt] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a literal character value                              *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  @lchar literalChar ;
  $literal_char$ ?literalChar ;
  outExpression := [@literalCharExpressionAST new !literalChar] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a double value                                         *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  @ldouble literalDouble ;
  $literal_double$ ? literalDouble ?* ;
  outExpression := [@literalDoubleExpressionAST new !literalDouble] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    here                                                   *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $here$ ;
  outExpression := [@hereExpressionAST new !here] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    if exp then exp else exp end                           *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $if$ ;
  @semanticExpressionAST ifExpression ;
  <expression> ? ifExpression ;
  const @location operatorLocation := here ;
  $then$ ;
  @semanticExpressionAST thenExpression ;
  <expression> ? thenExpression ;
  $else$ ;
  @semanticExpressionAST elseExpression ;
  <expression> ? elseExpression ;
  $end$ ;
  outExpression := [@ifExpressionAST new
    !operatorLocation
    !ifExpression
    !thenExpression
    !elseExpression
  ] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#                         F A C T O R                                          *
#   description         :   [expression description]                           *
#   reader call         :   [expression reader !... ]                          *
#   constructor call    :   [@class method !... ]                              *
#   option value        :   [option option_component_name.option_name]         *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $[$ ;
  select
  #--- Get source identity characteristics
    @semanticExpressionAST receiverExpression ;
    <expression> ? receiverExpression ;
  #--- Get reader name
    @lstring readerName ;
    $identifier$ ? readerName ;
  #--- Actual parameters
    @semanticExpressionListAST expressionList ;
    <output_expression_list> ?expressionList ;
    outExpression := [@readerCallExpressionAST new !receiverExpression !readerName !expressionList] ;
  or
  #--- Class name
    @lstring className ;
    $type_name$ ?className ;
  #--- Constructor name
    @lstring constructorName ;
    $identifier$ ? constructorName ;
  #--- Actual arguments
    @semanticExpressionListAST expressionList ;
    <output_expression_list> ?expressionList ;
    outExpression := [@constructorExpressionAST new
      !className
      !constructorName
      !expressionList
    ] ;
  or
    $option$ ;
    select
      $.$ ;
      @lstring optionName ;
      $identifier$ ?optionName ;
      $identifier$ ?@lstring readerName ;
      outExpression := [@optionExpressionAST new ![@lstring new !"galgas_cli_options" !here] !optionName !readerName] ;
    or
      $identifier$ ?@lstring optionComponentName ;
      $.$ ;
      $identifier$ ?@lstring optionName ;
      $identifier$ ?@lstring readerName ;
      outExpression := [@optionExpressionAST new !optionComponentName !optionName !readerName] ;
    end select ;
  or
    $filewrapper$ ;
    @lstring filewrapperName ;
    $identifier$ ? filewrapperName ;
    select
      outExpression := [@filewrapperObjectInstanciationInExpressionAST new
        !filewrapperName
      ] ;
    or
      $.$ ;
      select
        @lstring filePath ;
        $literal_string$ ? filePath ;
        outExpression := [@filewrapperInExpressionAST new
          !filewrapperName
          !filePath
        ] ;
      or
        @lstring filewrapperTemplateName ;
        $identifier$ ? filewrapperTemplateName ;
        @semanticExpressionListAST expressionList ;
        <output_expression_list> ?expressionList ;
        outExpression := [@filewrapperTemplateInExpressionAST new
          !filewrapperName
          !filewrapperTemplateName
          !expressionList
        ] ;
      end select ;
    end select ;
  end select ;
  $]$ ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#                         F A C T O R                                          *
#                                                                              *
#   function call        :   function_name [!... ]                             *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $identifier$ ? @lstring functionName ;
  $[$ ;
  @semanticExpressionListAST expressionList ;
  <output_expression_list> ?expressionList ;
  outExpression := [@functionCallExpressionAST new
    !functionName
    !expressionList
  ] ;
  $]$ ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#                         F A C T O R                                          *
#                                                                              *
#   literal type         :   `@type                                            *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $`$ ;
  $type_name$ ? @lstring typeName ;
  outExpression := [@literalTypeInExpressionAST new
    !typeName
  ] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#                         F A C T O R                                          *
#                                                                              *
#   extract exp to @aType                                                      *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> !@semanticExpressionAST outExpression :
  $extract$ ;
  @semanticExpressionAST receiverExpression ;
  <expression> ? receiverExpression ;
  const @location endOfExpression := here ;
  $:$ ;
  $type_name$ ? @lstring typeName ;
  outExpression := [@extractObjectInExpressionAST new
    !receiverExpression
    !endOfExpression
    !typeName
  ] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   O U T P U T    E X P R E S S I O N    L I S T                              *
#                                                                              *
#------------------------------------------------------------------------------*

rule <output_expression_list>
  !@semanticExpressionListAST outExpressionList
:
  outExpressionList := [@semanticExpressionListAST emptyList] ;
  repeat
  while
    $!$ ;
    @semanticExpressionAST expression ;
    <expression> ? expression ;
    outExpressionList += !expression !here ;
  end repeat ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;
