#---------------------------------------------------------------------------*
#                                                                           *
#  'option' component parser, as model builder                              *
#                                                                           *
#  Copyright (C) 2007, ..., 2007 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax optionModelBuilder :
with "galgas_scanner.gLexique" ;

import lexique galgas_scanner in "galgas_scanner.gLexique" ;
import semantics optionMetamodel in "optionMetamodel.gSemantics" ;

nonterminal <option_component_start_symbol> !@optionComponentRoot outOptionComponentRoot;

nonterminal <option> ?!@commandLineOptionList ioCommandLineOptionList ;

#---------------------------------------------------------------------------*

rule <option_component_start_symbol>
 !@optionComponentRoot outOptionComponentRoot
:
  $option$ ;
#--- Option Component Name
  $identifier$ ?@lstring mOptionComponentName ;
  $:$ ;
#--- Parse the option list
  @commandLineOptionList mOptions [emptyList] ;
  repeat
  while 
    <option> !?mOptions ;
  end repeat ;
  $end$ ;
  $option$ ;
  $;$ ;
  outOptionComponentRoot := [@optionComponentRoot new
    !mOptionComponentName
    !mOptions
  ] ;
end rule ;

#---------------------------------------------------------------------------*

rule <option> ?!@commandLineOptionList ioCommandLineOptionList :
  $type_name$ ?@lstring mOptionTypeName  ;
  $identifier$ ?@lstring mOptionInternalName ;
  $:$ ;
  $literal_char$ ?@lchar mOptionInvocationLetter ;
  $,$ ;
  $literal_string$ ?@lstring mOptionInvocationString ;
  $->$ ;
  $literal_string$ ?@lstring mOptionComment ;
  @lstring defaultValue [new !"" !here] ;
  @optionDefaultValueEnum optionDefaultValueKind ;
  select
    optionDefaultValueKind := [@optionDefaultValueEnum noDefaultValue] ;
  or
    $default$ ;
    select
      $literal_string$ ? defaultValue ;
      optionDefaultValueKind := [@optionDefaultValueEnum stringDefaultValue] ;
    or
      $unsigned_literal_integer$ ? @luint v ;
      [!?defaultValue setString ![[v uint] string]] ;
      optionDefaultValueKind := [@optionDefaultValueEnum unsignedDefaultValue] ;
    end select ;
  end select ;
  $;$ ;
  ioCommandLineOptionList +=
    !mOptionTypeName
    !mOptionInternalName
    !mOptionInvocationLetter
    !mOptionInvocationString
    !mOptionComment
    !defaultValue
    !optionDefaultValueKind
  ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;
