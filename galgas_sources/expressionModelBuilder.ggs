#---------------------------------------------------------------------------*
#                                                                           *
#  'expression' component parser, as model builder                          *
#                                                                           *
#  Copyright (C) 2007, ..., 2007 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax expressionModelBuilder :
import lexique galgas_scanner in "galgas_scanner.gLexique" ;
import metamodel expressionMetamodel in "expressionMetamodel.gMetamodel" ;

#------------------------------------------------------------------------------*
#                                                                              *
#          Rules prototypes                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <expression> -> @semanticExpression ;

nonterminal <concatenation_term> -> @semanticExpression ;

nonterminal <relation_term> -> @semanticExpression ;

nonterminal <relation_factor> -> @semanticExpression ;

nonterminal <simple_expression> -> @semanticExpression ;

nonterminal <term> -> @semanticExpression ;

nonterminal <factor> -> @semanticExpression ;

nonterminal <output_expression_list> -> @outExpressionList ;

#------------------------------------------------------------------------------*
#                                                                              *
#   E X P R E S S I O N                                                        *
#                                                                              *
#------------------------------------------------------------------------------*

rule <expression> -> @semanticExpression expression :
  <concatenation_term> -> expression ;
  repeat
  while
    @location operatorLocation := here ;
    $.$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <concatenation_term> -> rightOperand ;
    expression := [@concatExpression new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   C O N C A T E N A T I O N    T E R M                                       *
#                                                                              *
#------------------------------------------------------------------------------*

rule <concatenation_term> -> @semanticExpression expression :
  <relation_term> -> expression ;
  repeat
  while
    @location operatorLocation := here ;
    $|$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <relation_term> -> rightOperand ;
    expression := [@orExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $^$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <relation_term> -> rightOperand ;
    expression := [@xorExpression new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   R E L A T I O N    T E R M                                                 *
#                                                                              *
#------------------------------------------------------------------------------*

rule <relation_term> -> @semanticExpression expression :
  <relation_factor> -> expression ;
  repeat
  while
    @location operatorLocation := here ;
    $&$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <relation_factor> -> rightOperand ;
    expression := [@andExpression new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   R E L A T I O N    F A C T O R                                             *
#                                                                              *
#------------------------------------------------------------------------------*

rule <relation_factor> -> @semanticExpression expression :
  <simple_expression> -> expression ;
  repeat
  while
    @location operatorLocation := here ;
    $==$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <simple_expression> -> rightOperand ;
    expression := [@equalExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $!=$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <simple_expression> -> rightOperand ;
    expression := [@notEqualExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $<=$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <simple_expression> -> rightOperand ;
    expression := [@lowerOrEqualExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $>=$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <simple_expression> -> rightOperand ;
    expression := [@greaterOrEqualExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $>$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <simple_expression> -> rightOperand ;
    expression := [@strictGreaterExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $<$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <simple_expression> -> rightOperand ;
    expression := [@strictLowerExpression new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   S I M P L E    E X P R E S S I O N                                         *
#                                                                              *
#------------------------------------------------------------------------------*

rule <simple_expression> -> @semanticExpression expression :
  <term> -> expression ;
  repeat
  while
    @location operatorLocation := here ;
    $<<$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <term> -> rightOperand ;
    expression := [@leftShiftExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $>>$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <term> -> rightOperand ;
    expression := [@rightShiftExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $+$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <term> -> rightOperand ;
    expression := [@addExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $-$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <term> -> rightOperand ;
    expression := [@subExpression new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   T E R M                                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

rule <term> -> @semanticExpression expression :
  <factor> -> expression ;
  repeat
  while
    @location operatorLocation := here ;
    $*$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <factor> -> rightOperand ;
    expression := [@multiplicationExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $/$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <factor> -> rightOperand ;
    expression := [@divisionExpression new !operatorLocation !leftOperand !rightOperand] ;
  while
    @location operatorLocation := here ;
    $mod$ ;
    @semanticExpression leftOperand := expression ;
    @semanticExpression rightOperand ;
    <factor> -> rightOperand ;
    expression := [@moduloExpression new !operatorLocation !leftOperand !rightOperand] ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    - factor                                               *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @location operatorLocation := here ;
  $-$ ;
  <factor> -> expression ;
  expression := [@unaryMinusExpression new !operatorLocation !expression] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    <var>                                                  *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @lstring identifier ;
  $identifier$ ? identifier ;
  expression := [@varInExpression new !identifier] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    S E L F                                                *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  $self$ ;
  expression := [@selfInExpression new] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    ( EXPRESSION )                                         *
#                       (cast EXP if >= @T else error ERROR_LOCATION_EXP)      *
#                       (cast EXP if == @T else error ERROR_LOCATION_EXP)      *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  $($ ;
  select
    <expression> -> expression ;
  or
    $cast$ ;
    @semanticExpression receiverExpression ;
    <expression> -> receiverExpression ;
    $if$ ;
    @bool useKindOfClass ;
    select
      $>=$ ;
      useKindOfClass := true ;
    or
      $==$ ;
      useKindOfClass := false ;
    end select ;
    @lstring typeName ;
    $type_name$ ? typeName ;
    $else$ ;
    $error$ ;
    @semanticExpression errorLocationExpression ;
    <expression> -> errorLocationExpression ;
    expression := [@castInExpression new
      !receiverExpression
      !useKindOfClass
      !typeName
      !errorLocationExpression
    ] ;
  end select ;
  $)$ ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    not FACTOR                                             *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @location operatorLocation := here ;
  $not$ ;
  <factor> -> expression ;
  expression := [@notExpression new !operatorLocation !expression] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    ~ FACTOR                                               *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @location operatorLocation := here ;
  $~$ ;
  <factor> -> expression ;
  expression := [@negateExpression new !operatorLocation !expression] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    true                                                   *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @location operatorLocation := here ;
  $true$ ;
  expression := [@trueExpression new !operatorLocation] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    false                                                  *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @location operatorLocation := here ;
  $false$ ;
  expression := [@falseExpression new !operatorLocation] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :   literal string sequence "..."  "..."  "..."             *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @stringlist literalStringList [emptyList] ;
  repeat
    @lstring literalString ;
    $literal_string$ ?literalString ;
    literalStringList += ![literalString string] ;
  while
  end repeat ;
  expression := [@literalStringExpression new !literalStringList] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a 32-bit unsigned literal integer value                *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @luint literalInt ;
  $unsigned_literal_integer$ ?literalInt ;
  expression := [@literalUIntExpression new !literalInt] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a 32-bit signed literal integer value                  *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @lsint literalInt ;
  $signed_literal_integer$ ?literalInt ;
  expression := [@literalSIntExpression new !literalInt] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a 64-bit unsigned literal integer value                *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @luint64 literalInt ;
  $unsigned_literal_integer64$ ?literalInt ;
  expression := [@literalUInt64Expression new !literalInt] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a 64-bit signed literal integer value                  *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @lsint64 literalInt ;
  $signed_literal_integer64$ ?literalInt ;
  expression := [@literalSInt64Expression new !literalInt] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a literal character value                              *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @lchar literalChar ;
  $literal_char$ ?literalChar ;
  expression := [@literalCharExpression new !literalChar] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    a double value                                         *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  @ldouble literalDouble ;
  $literal_double$ ? literalDouble ?* ;
  expression := [@literalDoubleExpression new !literalDouble] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   F A C T O R    :    here                                                   *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  $here$ ;
  expression := [@hereExpression new] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#                         F A C T O R                                          *
#   description         :   [expression description]                           *
#   reader call         :   [expression reader !... ]                          *
#   constructor call    :   [@class method !... ]                              *
#   option value        :   [option option_component_name.option_name]         *
#                                                                              *
#------------------------------------------------------------------------------*

rule <factor> -> @semanticExpression expression :
  $[$ ;
  select
  #--- Get source identity characteristics
    @semanticExpression receiverExpression ;
    <expression> -> receiverExpression ;
    select
      $description$ ;
      expression := [@descriptionInExpression new !receiverExpression !here] ;
    or
    #--- Get reader name
      @lstring readerName ;
      $identifier$ ? readerName ;
    #--- Actual parameters
      @outExpressionList expressionList ;
      <output_expression_list> -> expressionList ;
      expression := [@readerCallExpression new !receiverExpression !readerName !expressionList] ;
    end select ;
  or
  #--- Class name
    @lstring className ;
    $type_name$ ?className ;
  #--- Constructor name
    @lstring constructorName ;
    $identifier$ ? constructorName ;
  #--- Actual arguments
    @outExpressionList expressionList ;
    <output_expression_list> -> expressionList ;
    expression := [@constructorExpression new
      !className
      !constructorName
      !expressionList
    ] ;
  or
    $option$ ;
    select
      $.$ ;
      @lstring optionName ;
      $identifier$ ?optionName ;
      expression := [@optionExpression new ![@lstring new !"generic_galgas_cli_options" !here] !optionName] ;
    or
      @lstring optionComponentName ;
      $identifier$ ? optionComponentName ;
      $.$ ;
      @lstring optionName ;
      $identifier$ ?optionName ;
      expression := [@optionExpression new !optionComponentName !optionName] ;
    end select ;
  end select ;
  $]$ ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#   O U T P U T    E X P R E S S I O N    L I S T                              *
#                                                                              *
#------------------------------------------------------------------------------*

rule <output_expression_list>  -> @outExpressionList :
  repeat
  while
    $!$ ;
    <expression> +> mExpression ;
  end repeat ;
  mEndOfExpressionLocation := here ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;
