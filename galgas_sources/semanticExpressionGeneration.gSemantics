#---------------------------------------------------------------------------*
#                                                                           *
#  Generate predefined types                                                *
#                                                                           *
#  Copyright (C) 2010, ..., 2010 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

semantics semanticExpressionGeneration :
  import "semanticAnalysis.gSemantics" ;

  import semantics semanticAnalysis in "semanticAnalysis.gSemantics" ;

#---------------------------------------------------------------------------*

once compilerCppName ->@string outCompilerCppName :
  outCompilerCppName := "inCompiler" ;
end once ;

#---------------------------------------------------------------------------*
#                                                                           *
#           D E T E C T    ' T R U E '    E X P R E S S I O N               *
#                                                                           *
#---------------------------------------------------------------------------*

reader @semanticExpressionForGeneration isTrueExpression
  ->@bool outResult
:
  outResult := false ;
end reader ;

#---------------------------------------------------------------------------*

override reader @trueExpressionForGeneration isTrueExpression
  ->@bool outResult
:
  outResult := true ;
end reader ;

#---------------------------------------------------------------------------*
#                                                                           *
#           E X P R E S S I O N    G E N E R A T I O N                      *
#                                                                           *
#---------------------------------------------------------------------------*

method @semanticExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  outCppName := "?exp name?" ;
end method ;

#---------------------------------------------------------------------------*

override method @selfCopyInExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  outCppName := mSelfCppName ;
  [!?ioUnusedVariableCppNameSet removeKey !selfMarkName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @hereExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_location " . outCppName 
                   . " (" . compilerCppName [] . "->here ()) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @trueExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @trueExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_bool " . outCppName . " (true) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @falseExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @falseExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_bool " . outCppName . " (false) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalCharExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @literalCharExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_char " . outCppName
                   . " (TO_UNICODE (" . [[mCharacter uint] string] . ")) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalStringExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @literalStringExpressionForGeneration\n" ;
#  ioGeneratedCode .= "static const char kString_" . [[mLocation locationIndex] string] . " ["
#                   . [[mString length] + 1 string] . "] = " . [mString utf8Representation] . ";\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_string " . outCppName
                   . " ("
                   . [mString utf8Representation]
                   . ") ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalDoubleExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @literalDoubleExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_double " . outCppName
                   . " (" . [mValue string]. ") ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalUIntExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @literalUIntExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_uint " . outCppName
                   . " ((PMUInt32) " . [mValue string]. "U) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalUInt64ExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @literalUInt64ExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_uint_36__34_ " . outCppName
                   . " ((PMUInt64) " . [mValue string]. "ULL) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalSIntExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @literalSIntExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_sint " . outCppName
                   . " ((PMSInt32) " . [mValue string]. "L) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalSInt64ExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @literalSInt64ExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_sint_36__34_ " . outCppName
                   . " ((PMSInt64) " . [mValue string]. "LL) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @constructorExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  @stringlist parameterList [emptyList] ;
  foreach mEffectiveParameterList do
    @string parameter ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?parameter] ;
    parameterList += !parameter ;  
  end foreach ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation]
                   . " " . outCppName
                   . " (GALGAS_"  . [mConstructorType identifierRepresentation]
                   . "::constructor_" . [mConstructorName identifierRepresentation]
                   . " (" ;
  foreach parameterList
    do ioGeneratedCode .= mValue ;
    between ioGeneratedCode .= ", " ;
  end foreach ;
  @bool needsComma := [parameterList length] > 0 ;
  if mHasCompilerArgument then
    if needsComma then
      ioGeneratedCode .= ", " ;
    end if ;
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    ioGeneratedCode .= compilerCppName [] ;
    needsComma := true ;
  end if ;
  if needsComma then
    ioGeneratedCode .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
  else
    ioGeneratedCode .= "SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @readerCallExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @readerCallExpressionForGeneration\n" ;
#--- Receiver expression
  @string receiverCppName ;
  [mReceiverExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?receiverCppName] ;
#--- Reader operands
  @stringlist readerArgumentCppNameList [emptyList] ;
  foreach mExpressions do
    @string argumentCppName ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?argumentCppName] ;
    readerArgumentCppNameList += !argumentCppName ;
  end foreach ;
#--- Generate Reader call
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName . " (" . receiverCppName ;
  foreach mFieldList do
    ioGeneratedCode .= ".mAttribute_" . [mValue identifierRepresentation] ;
  end foreach ;
  ioGeneratedCode .=  ".reader_" . [mReaderName identifierRepresentation] . " (" ;
  foreach readerArgumentCppNameList
    do ioGeneratedCode .= mValue ;
    between ioGeneratedCode .= ", " ;
  end foreach ;
  if [readerArgumentCppNameList length] == 0 then
    if mHasCompilerArgument then
      [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
      ioGeneratedCode .= compilerCppName [] . " COMMA_" ;
    end if ;
  elsif mHasCompilerArgument then
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    ioGeneratedCode .= ", " . compilerCppName [] . " COMMA_" ;
  else
    ioGeneratedCode .= " COMMA_" ;
  end if ;
  ioGeneratedCode .= "SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @optionValueExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @optionValueExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName 
                  . " (gOption_" . [mOptionComponentName identifierRepresentation]
                  . "_" . [mOptionEntryName identifierRepresentation] . ".reader_value ()) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @optionCharExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @optionCharExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_char " . outCppName 
                  . "  (gOption_" . [mOptionComponentName identifierRepresentation]
                  . "_" . [mOptionEntryName identifierRepresentation] . ".reader_char ()) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @optionStringExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @optionStringExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_string " . outCppName 
                  . "  (gOption_" . [mOptionComponentName identifierRepresentation]
                  . "_" . [mOptionEntryName identifierRepresentation] . ".reader_string ()) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @optionCommentExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @optionCommentExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_string " . outCppName 
                  . "  = gOption_" . [mOptionComponentName identifierRepresentation]
                  . "_" . [mOptionEntryName identifierRepresentation] . ".reader_comment ()) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @concatExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @concatExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . leftTemporaryOperand . ".operator_concat ("
                  . rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @orExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @orExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . leftTemporaryOperand . ".operator_or ("
                  . rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @xorExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @xorExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . leftTemporaryOperand . ".operator_xor ("
                  .  rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @andExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @andExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . leftTemporaryOperand . ".operator_and ("
                  . rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @equalExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @equalExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_bool " . outCppName . " (kIsEqual, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . ")) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @notEqualExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @notEqualExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_bool " . outCppName . " (kIsNotEqual, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . ")) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @lowerOrEqualExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @lowerOrEqualExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_bool " . outCppName . " (kIsInfOrEqual, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . ")) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @greaterOrEqualExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @greaterOrEqualExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_bool " . outCppName . " (kIsSupOrEqual, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . ")) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @strictGreaterExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @strictGreaterExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_bool " . outCppName . " (kIsStrictSup, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . ")) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @strictLowerExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @strictLowerExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_bool " . outCppName . " (kIsStrictInf, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . ")) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @rightShiftExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [[mLeftExpression mType] identifierRepresentation] . " " . outCppName . " ("
                  . leftTemporaryOperand . ".right_shift_operation ("
                  . rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @leftShiftExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [[mLeftExpression mType] identifierRepresentation] . " " . outCppName . " ("
                  . leftTemporaryOperand . ".left_shift_operation ("
                   .  rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @addExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @addExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . leftTemporaryOperand . ".add_operation ("
                  . rightTemporaryOperand . ", " . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @subExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @subExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . leftTemporaryOperand . ".substract_operation ("
                  . rightTemporaryOperand . ", " . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @multiplicationExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @multiplicationExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . leftTemporaryOperand . " .multiply_operation ("
                  . rightTemporaryOperand . ", " . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @divisionExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @divisionExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . leftTemporaryOperand . ".divide_operation ("
                  . rightTemporaryOperand . ", " . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @moduloExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @moduloExpressionForGeneration\n" ;
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . leftTemporaryOperand . ".modulo_operation ("
                  . rightTemporaryOperand . ", " . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @unaryMinusExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @unaryMinusExpressionForGeneration\n" ;
#--- Left operand
  @string operand ;
  [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?operand] ;
#--- Generate Comparison
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . operand . ".operator_unary_minus ("
                  . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @notExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @notExpressionForGeneration\n" ;
#--- Operand
  @string operand ;
  [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?operand] ;
#--- Generate
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . operand . ".operator_not (SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @tildeExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @complementExpressionForGeneration\n" ;
#--- Operand
  @string operand ;
  [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?operand] ;
#--- Generate
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " (" . operand . ".operator_tilde (SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @ifExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @ifExpressionForGeneration\n" ;
#--- 'if' expression
  @string ifExpression ;
  [mIfExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?ifExpression] ;
#--- Result variable
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [[mThenExpression mType] identifierRepresentation] . " " . outCppName . " ;\n" ;
  ioGeneratedCode .= "if (" . ifExpression . ".isValidAndTrue ()) {\n" ;
#--- 'then' expression
  @string thenExpression ;
  [!?ioGeneratedCode incIndentation !2] ;
  [mThenExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?thenExpression] ;
  ioGeneratedCode .= outCppName . " = " . thenExpression . " ;\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
#--- 'else' expression
  ioGeneratedCode .= "}else{\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
  @string elseExpression ;
  [mElseExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?elseExpression] ;
  ioGeneratedCode .= outCppName . " = " . elseExpression . " ;\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalTypeInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @literalTypeInExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_type " . outCppName . " (& kTypeDescriptor_GALGAS_"
                  . [mTypeName identifierRepresentation] . ") ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @extractObjectInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @extractObjectInExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
#--- Receiver expression
  @string receiverExpression ;
  [mReceiverExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?receiverExpression] ;
#--- Error expression
  @string errorLocationExpression ;
  [mErrorLocationExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?errorLocationExpression] ;
#--- Generate code
  ioGeneratedCode .= "const GALGAS_" . [mType identifierRepresentation] . " " . outCppName
                  . " = GALGAS_" . [mType identifierRepresentation] . "::extractObject (" . receiverExpression
                  . ", " . compilerCppName [] . ", " . errorLocationExpression  . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . ")) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @castInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @castInExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
#--- Receiver expression
  @string receiverExpression ;
  [mReceiverExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?receiverExpression] ;
  ioGeneratedCode .= "GALGAS_" . [mTypeName identifierRepresentation] . " " . outCppName . " ;\n" ;
  ioGeneratedCode .= "if (" . receiverExpression . ".isValid ()) {\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
#--- 
  if mUseKindOfClass then
    ioGeneratedCode .= "if (NULL != dynamic_cast <GALGAS_" . [mTypeName identifierRepresentation] . " *> (" . receiverExpression . ".ptr (HERE))) {\n" ;
  else
    ioGeneratedCode .= "if (" . receiverExpression . ".ptr (HERE)->typeDescriptor () == & kTypeDescriptor_GALGAS_" . [mTypeName identifierRepresentation] . ") {\n" ;
  end if ;
  ioGeneratedCode .= "  " . outCppName . " = " . receiverExpression . " ;\n" ;
  ioGeneratedCode .= "}else{\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
  @string errorLocationExpression ;
  [mErrorLocationExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?errorLocationExpression] ;
  ioGeneratedCode .= compilerCppName [] . "->castInExpressionErrorAtLocation (" . errorLocationExpression
                  . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . ")) ;\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  ioGeneratedCode .= "}\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @functionCallExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @functionCallExpressionForGeneration\n" ;
#---
  @stringlist parameterList [emptyList] ;
  foreach mExpressions do
    @string parameter ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?parameter] ;
    parameterList += !parameter ;  
  end foreach ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [mType identifierRepresentation]
                   . " " . outCppName
                   . " (function_" . [mFunctionName identifierRepresentation]
                   . " (" ;
  foreach parameterList
    do ioGeneratedCode .= mValue . ", " ;
  end foreach ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  ioGeneratedCode .= compilerCppName [] ;
  ioGeneratedCode .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @varInExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  outCppName := mCppVarName ;
  [!?ioUnusedVariableCppNameSet removeKey !mNameForCheckingFormalParameterUsing] ;
  foreach mStructFieldList do
    outCppName .= ".mAttribute_" . [[mValue string] identifierRepresentation] ;
  end foreach ;
end method ;

#---------------------------------------------------------------------------*

override method @filewrapperInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @filewrapperInExpressionForGeneration\n" ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_filewrapper " . outCppName . " (gWrapperDirectory_0_" . [mFilewrapperName identifierRepresentation] . ") ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @filewrapperTemplateInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @filewrapperTemplateInExpressionForGeneration\n" ;
#---
  @stringlist parameterList [emptyList] ;
  foreach mActualOutputParameterList do
    @string parameter ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?parameter] ;
    parameterList += !parameter ;  
  end foreach ;
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_string " . outCppName
                  . " (filewrapperTemplate_" . [mFilewrapperName identifierRepresentation] . "_" . [mFilewrapperTemplateName identifierRepresentation]
                  . " (" . compilerCppName [] ;
  foreach parameterList do
    ioGeneratedCode .= ", " . mValue ;
  end foreach ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  ioGeneratedCode .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @filewrapperStaticPathInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  ioGeneratedCode .= "//--- @filewrapperStaticPathInExpressionForGeneration\n" ;
#---
  outCppName := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_string " . outCppName
  . " (gWrapperFileContent_" . [mFilewrapperFileIndex string] . "_" . [mFilewrapperName identifierRepresentation] . ") ;\n" ;
end method ;

#---------------------------------------------------------------------------*

end semantics ;
