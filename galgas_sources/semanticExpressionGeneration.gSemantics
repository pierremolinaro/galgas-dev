#---------------------------------------------------------------------------*
#                                                                           *
#  Generate predefined types                                                *
#                                                                           *
#  Copyright (C) 2010, ..., 2010 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

semantics semanticExpressionGeneration :
  import "semanticAnalysis.gSemantics" ;

#---------------------------------------------------------------------------*

once compilerCppName ->@string outCompilerCppName :
  outCompilerCppName := "inCompiler" ;
end once ;

#---------------------------------------------------------------------------*
#                                                                           *
#           D E T E C T    ' T R U E '    E X P R E S S I O N               *
#                                                                           *
#---------------------------------------------------------------------------*

reader @semanticExpressionForGeneration isTrueExpression
  ->@bool outResult
:
  outResult := false ;
end reader ;

#---------------------------------------------------------------------------*

override reader @trueExpressionForGeneration isTrueExpression
  ->@bool outResult
:
  outResult := true ;
end reader ;

#---------------------------------------------------------------------------*
#                                                                           *
#           E X P R E S S I O N    G E N E R A T I O N                      *
#                                                                           *
#---------------------------------------------------------------------------*

abstract method @semanticExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
;

#---------------------------------------------------------------------------*

override method @selfCopyInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  ioGeneratedCode .= "//--- @selfCopyInExpressionForGeneration\n" ;
  outCppExpression := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "const GALGAS_" . [mType identifierRepresentation] . " " . outCppExpression . " = " . mSelfCppName . " ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !selfMarkName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @hereExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  outCppExpression := compilerCppName [] . "->here ()" ;
end method ;

#---------------------------------------------------------------------------*

override method @trueExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_bool (true)" ;
end method ;

#---------------------------------------------------------------------------*

override method @falseExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_bool (false)" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalCharExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_char (TO_UNICODE (" . [[mCharacter uint] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalStringExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  if mString == "" then
    outCppExpression := "GALGAS_string::makeEmptyString ()" ;
  else
    outCppExpression := "GALGAS_string (" . [mString utf8Representation] . ")" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @literalDoubleExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_double (" . [mValue string]. ")" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalUIntExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_uint ((PMUInt32) " . [mValue string]. "U)" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalUInt64ExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_uint_36__34_ ((PMUInt64) " . [mValue string]. "ULL)" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalSIntExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_sint ((PMSInt32) " . [mValue string]. "L)" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalSInt64ExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_sint_36__34_ ((PMSInt64) " . [mValue string]. "LL)" ;
end method ;

#---------------------------------------------------------------------------*

override method @constructorExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  @stringlist parameterList [emptyList] ;
  foreach mEffectiveParameterList do
    @string parameter ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?parameter] ;
    parameterList += !parameter ;  
  end foreach ;
  outCppExpression := "GALGAS_"  . [mConstructorType identifierRepresentation]
                   . "::constructor_" . [mConstructorName identifierRepresentation]
                   . " (" ;
  foreach parameterList
    do outCppExpression .= mValue ;
    between outCppExpression .= ", " ;
  end foreach ;
  @bool needsComma := [parameterList length] > 0 ;
  if mHasCompilerArgument then
    if needsComma then
      outCppExpression .= ", " ;
    end if ;
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    outCppExpression .= compilerCppName [] ;
    needsComma := true ;
  end if ;
  if needsComma then
    outCppExpression .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
  else
    outCppExpression .= "SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @superReaderCallExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  ioGeneratedCode .= "//--- @superReaderCallExpressionForGeneration\n" ;
  outCppExpression := "{TO DO}" ;
end method ;

#---------------------------------------------------------------------------*

override method @thisReaderCallExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  ioGeneratedCode .= "//--- @thisReaderCallExpressionForGeneration\n" ;
#--- Reader operands
  @stringlist readerArgumentCppNameList [emptyList] ;
  foreach mExpressions do
    @string argumentCppName ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?argumentCppName] ;
    readerArgumentCppNameList += !argumentCppName ;
  end foreach ;
#--- Generate Reader call
  switch mKind
  when definedAsCategory :
    [!?ioUnusedVariableCppNameSet removeKey !selfMarkName []] ;
    outCppExpression := "callCategoryReader_" . [mReaderName identifierRepresentation]
       . " (" . mCurrentObjectCppPointer . ", " ;
  when definedAsMember :
    outCppExpression :=  "reader_" . [mReaderName identifierRepresentation] . " (" ;
  end switch ;
  foreach readerArgumentCppNameList
    do outCppExpression .= mValue ;
    between outCppExpression .= ", " ;
  end foreach ;
  if [readerArgumentCppNameList length] == 0 then
    if mHasCompilerArgument then
      [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
      outCppExpression .= compilerCppName [] . " COMMA_" ;
    end if ;
  elsif mHasCompilerArgument then
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    outCppExpression .= ", " . compilerCppName [] . " COMMA_" ;
  else
    outCppExpression .= " COMMA_" ;
  end if ;
  outCppExpression .= "SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @readerCallExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Receiver expression
  @string receiverCppName ;
  [mReceiverExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?receiverCppName] ;
#--- Reader operands
  @stringlist readerArgumentCppNameList [emptyList] ;
  foreach mExpressions do
    @string argumentCppName ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?argumentCppName] ;
    readerArgumentCppNameList += !argumentCppName ;
  end foreach ;
#--- Generate Reader call
  switch mKind
  when definedAsCategory :
    outCppExpression := "callCategoryReader_" . [mReaderName identifierRepresentation]
       . " ((const cPtr_" . [[mReceiverExpression mType] identifierRepresentation] . " *) " . receiverCppName . ".ptr (), " ;
  when definedAsMember :
    outCppExpression := receiverCppName ;
    foreach mFieldList do
      outCppExpression .= ".mAttribute_" . [mValue identifierRepresentation] ;
    end foreach ;
    outCppExpression .=  ".reader_" . [mReaderName identifierRepresentation] . " (" ;
  end switch ;
  foreach readerArgumentCppNameList
    do outCppExpression .= mValue ;
    between outCppExpression .= ", " ;
  end foreach ;
  if [readerArgumentCppNameList length] == 0 then
    if mHasCompilerArgument then
      [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
      outCppExpression .= compilerCppName [] . " COMMA_" ;
    end if ;
  elsif mHasCompilerArgument then
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    outCppExpression .= ", " . compilerCppName [] . " COMMA_" ;
  else
    outCppExpression .= " COMMA_" ;
  end if ;
  outCppExpression .= "SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @optionValueExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_" . [mType identifierRepresentation] . " (gOption_" . [mOptionComponentName identifierRepresentation]
                   . "_" . [mOptionEntryName identifierRepresentation] . ".reader_value ())" ;
end method ;

#---------------------------------------------------------------------------*

override method @optionCharExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_char (gOption_" . [mOptionComponentName identifierRepresentation]
                  . "_" . [mOptionEntryName identifierRepresentation] . ".reader_char ())" ;
end method ;

#---------------------------------------------------------------------------*

override method @optionStringExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_string (gOption_" . [mOptionComponentName identifierRepresentation]
                  . "_" . [mOptionEntryName identifierRepresentation] . ".reader_string ())" ;
end method ;

#---------------------------------------------------------------------------*

override method @optionCommentExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#  ioGeneratedCode .= "//--- @optionCommentExpressionForGeneration\n" ;
#  outCppExpression := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  outCppExpression := "gOption_" . [mOptionComponentName identifierRepresentation]
                  . "_" . [mOptionEntryName identifierRepresentation] . ".reader_comment ()" ;
end method ;

#---------------------------------------------------------------------------*

override method @concatExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . ".operator_concat ("
                   . rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @orExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . ".operator_or ("
                   . rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @xorExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . ".operator_xor ("
                  .  rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @andExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . ".operator_and ("
                  . rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @equalExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := "GALGAS_bool (kIsEqual, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @notEqualExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := "GALGAS_bool (kIsNotEqual, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @lowerOrEqualExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := "GALGAS_bool (kIsInfOrEqual, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @greaterOrEqualExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := "GALGAS_bool (kIsSupOrEqual, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @strictGreaterExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := "GALGAS_bool (kIsStrictSup, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @strictLowerExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := "GALGAS_bool (kIsStrictInf, " . leftTemporaryOperand . ".objectCompare (" . rightTemporaryOperand . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @rightShiftExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . ".right_shift_operation ("
                   . rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @leftShiftExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . ".left_shift_operation ("
                   .  rightTemporaryOperand . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @addExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . ".add_operation ("
                   . rightTemporaryOperand . ", " . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @subExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . ".substract_operation ("
                  . rightTemporaryOperand . ", " . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @multiplicationExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . " .multiply_operation ("
                  . rightTemporaryOperand . ", " . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @divisionExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . ".divide_operation ("
                  . rightTemporaryOperand . ", " . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @moduloExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string leftTemporaryOperand ;
  [mLeftExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?leftTemporaryOperand] ;
#--- Right operand
  @string rightTemporaryOperand ;
  [mRightExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?rightTemporaryOperand] ;
#--- Generate Comparison
  outCppExpression := leftTemporaryOperand . ".modulo_operation ("
                  . rightTemporaryOperand . ", " . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @unaryMinusExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Left operand
  @string operand ;
  [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?operand] ;
#--- Generate Comparison
  outCppExpression := operand . ".operator_unary_minus ("
                  . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @notExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Operand
  @string operand ;
  [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?operand] ;
#--- Generate
  outCppExpression := operand . ".operator_not (SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @structFieldAccessExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Operand
  @string operand ;
  [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?operand] ;
#--- Generate
  outCppExpression := operand . ".mAttribute_" . [mStructFieldName identifierRepresentation] ;
end method ;

#---------------------------------------------------------------------------*

override method @tildeExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#--- Operand
  @string operand ;
  [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?operand] ;
#--- Generate
  outCppExpression := operand . ".operator_tilde (SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @ifExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  ioGeneratedCode .= "//--- @ifExpressionForGeneration\n" ;
#--- 'if' expression
  @string ifExpression ;
  [mIfExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?ifExpression] ;
#--- Result variable
  outCppExpression := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "GALGAS_" . [[mThenExpression mType] identifierRepresentation] . " " . outCppExpression . " ;\n" ;
  const @string testVar := "test_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "const enumGalgasBool " . testVar . " = " . ifExpression . ".boolEnum () ;\n" ;
  ioGeneratedCode .= "if (kBoolTrue == " . testVar . ") {\n" ;
#--- 'then' expression
  @string thenExpression ;
  [!?ioGeneratedCode incIndentation !2] ;
  [mThenExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?thenExpression] ;
  ioGeneratedCode .= outCppExpression . " = " . thenExpression . " ;\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
#--- 'else' expression
  ioGeneratedCode .= "}else if (kBoolFalse == " . testVar . ") {\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
  @string elseExpression ;
  [mElseExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?elseExpression] ;
  ioGeneratedCode .= outCppExpression . " = " . elseExpression . " ;\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @literalTypeInExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_type (& kTypeDescriptor_GALGAS_"
                  . [mTypeName identifierRepresentation] . ")" ;
end method ;

#---------------------------------------------------------------------------*

override method @extractObjectInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  ioGeneratedCode .= "//--- @extractObjectInExpressionForGeneration\n" ;
  outCppExpression := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
#--- Receiver expression
  @string receiverExpression ;
  [mReceiverExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?receiverExpression] ;
#--- Error expression
  @string errorLocationExpression ;
  [mErrorLocationExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?errorLocationExpression] ;
#--- Generate code
  outCppExpression := "GALGAS_" . [mType identifierRepresentation] . "::extractObject (" . receiverExpression
                  . ", " . compilerCppName [] . ", " . errorLocationExpression  . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @castInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  ioGeneratedCode .= "//--- @castInExpressionForGeneration\n" ;
  outCppExpression := "temp_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
#--- Receiver expression
  @string receiverExpression ;
  [mReceiverExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?receiverExpression] ;
  ioGeneratedCode .= "GALGAS_" . [mTypeName identifierRepresentation] . " " . outCppExpression . " ;\n" ;
  ioGeneratedCode .= "if (" . receiverExpression . ".isValid ()) {\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
#--- 
  if mUseKindOfClass then
    ioGeneratedCode .= "if (NULL != dynamic_cast <const cPtr_" . [mTypeName identifierRepresentation] . " *> (" . receiverExpression . ".ptr ())) {\n" ;
  else
    ioGeneratedCode .= "if (" . receiverExpression . ".ptr (HERE)->typeDescriptor () == & kTypeDescriptor_GALGAS_" . [mTypeName identifierRepresentation] . ") {\n" ;
  end if ;
  ioGeneratedCode .= "  " . outCppExpression . " = (cPtr_" . [mTypeName identifierRepresentation] . " *) " . receiverExpression . ".ptr () ;\n" ;
  ioGeneratedCode .= "}else{\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
  @string errorLocationExpression ;
  [mErrorLocationExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?errorLocationExpression] ;
  ioGeneratedCode .= compilerCppName [] . "->castInExpressionErrorAtLocation (" . errorLocationExpression
                  . " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . ")) ;\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  ioGeneratedCode .= "}\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @functionCallExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#---
  @stringlist parameterList [emptyList] ;
  foreach mExpressions do
    @string parameter ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?parameter] ;
    parameterList += !parameter ;  
  end foreach ;
  outCppExpression := "function_" . [mFunctionName identifierRepresentation] . " (" ;
  foreach parameterList
    do outCppExpression .= mValue . ", " ;
  end foreach ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  outCppExpression .= compilerCppName [] ;
  outCppExpression .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . "))" ;
end method ;

#---------------------------------------------------------------------------*

override method @varInExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := mCppVarName ;
  [!?ioUnusedVariableCppNameSet removeKey !mNameForCheckingFormalParameterUsing] ;
end method ;

#---------------------------------------------------------------------------*

override method @filewrapperInExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
  outCppExpression := "GALGAS_filewrapper (gWrapperDirectory_0_" . [mFilewrapperName identifierRepresentation] . ")" ;
end method ;

#---------------------------------------------------------------------------*

override method @filewrapperTemplateInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#---
  @stringlist parameterList [emptyList] ;
  foreach mActualOutputParameterList do
    @string parameter ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?parameter] ;
    parameterList += !parameter ;  
  end foreach ;
  outCppExpression := "GALGAS_string (filewrapperTemplate_" . [mFilewrapperName identifierRepresentation] . "_" . [mFilewrapperTemplateName identifierRepresentation]
                  . " (" . compilerCppName [] ;
  foreach parameterList do
    outCppExpression .= ", " . mValue ;
  end foreach ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  outCppExpression .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mLocation line] string] . ")))" ;
end method ;

#---------------------------------------------------------------------------*

override method @filewrapperStaticPathInExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
:
#---
  outCppExpression := "GALGAS_string (gWrapperFileContent_" . [mFilewrapperFileIndex string] . "_" . [mFilewrapperName identifierRepresentation] . ")" ;
end method ;

#---------------------------------------------------------------------------*

end semantics ;
