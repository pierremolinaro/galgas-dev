#---------------------------------------------------------------------------*
#                                                                           *
#  'semantics' component parser, as model builder                           *
#                                                                           *
#  Copyright (C) 2007, ..., 2007 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax syntaxModelBuilder :
import lexique galgas_scanner in "galgas_scanner.gLexique" ;
import semantics expressionMetamodel in "expressionMetamodel.gSemantics" ;
import semantics instructionMetamodel in "instructionMetamodel.gSemantics" ;
import semantics parameterArgumentMetamodel in "parameterArgumentMetamodel.gSemantics" ;
import semantics syntaxMetamodel in "syntaxMetamodel.gSemantics" ;
import semantics commonSemantics in "commonSemantics.gSemantics" ;

#------------------------------------------------------------------------------*
#                                                                              *
#          Rules prototypes                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <syntax_component_start_symbol> !@syntaxComponentRoot outSyntaxComponentRoot ;

nonterminal <nonterminal_declaration> ?!@nonterminalDeclarationList ioNonterminalDeclarationList ;

nonterminal <syntax_rule_declaration> ?!@syntaxRuleList ioRuleList ;

nonterminal <syntax_instruction_list> !@syntaxInstructionList outSyntaxInstructionList ;

nonterminal <semantic_instruction> !@semanticInstruction outInstruction ;

nonterminal <syntax_instruction> !@syntaxInstruction outInstruction ;

nonterminal <formal_parameter_list> !@formalParameterList outFormalParameterList ;

nonterminal <actual_parameter_list> !@actualParameterList outActualParameterList ;

nonterminal <semantic_declaration> !@semanticDeclaration outSemanticDeclaration ;

#------------------------------------------------------------------------------*
#                                                                              *
#      S Y N T A X    C O M P O N E N T                                        *
#                                                                              *
#------------------------------------------------------------------------------*

rule <syntax_component_start_symbol> !@syntaxComponentRoot outSyntaxComponentRoot :
  $syntax$ ;
#--- Semantics Component Name
  $identifier$ ? @lstring mSyntaxComponentName ;
  $:$ ;
#--- Loop on import declarations
  repeat
  while
    $import$ ;
    select
      $semantics$ ;
    or
      $lexique$ ;
    or
      $metamodel$ ;
    or
      $grammar$ ;
    or
      $option$ ;
    end select ;
    $identifier$ ?* ;
    $in$ ;
    $literal_string$ ? * ;
    $;$ ;
  end repeat ;
#--- Loop on declarations
  @nonterminalDeclarationList mNonterminalDeclarationList [emptyList] ;
  @syntaxRuleList mRuleList [emptyList] ;
  @semanticDeclarationList mSemanticDeclarationList [emptyList] ;
  repeat
  while
    <nonterminal_declaration> !? mNonterminalDeclarationList ;
  while
    <syntax_rule_declaration> !? mRuleList ;
  while
    @semanticDeclaration semanticDeclaration ;
    <semantic_declaration> ? semanticDeclaration ;
    mSemanticDeclarationList += !semanticDeclaration ;
  end repeat ;
#--- end
  $end$ ;
  $syntax$ ;
  $;$ ;
  outSyntaxComponentRoot := [@syntaxComponentRoot new
    !mSyntaxComponentName
    !mNonterminalDeclarationList
    !mRuleList
    !mSemanticDeclarationList
  ] ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#      N O N T E R M I N A L    D E C L A R A T I O N                          *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <nonterminal_label_declaration> ??@lstring inLabelName ?!@nonTerminalLabelList ioNonTerminalLabelList ;

#---------------------------------------------------------------------------*

rule <nonterminal_label_declaration> ??@lstring inLabelName ?!@nonTerminalLabelList ioNonTerminalLabelList :
  @formalParameterList mFormalParameters ;
  <formal_parameter_list> ? mFormalParameters ;
  @lstring mReturnedEntityName ; # Empty string if no returned entity
  select
    mReturnedEntityName := [@lstring new !"" !here] ;
  or
    $->$ ;
    $type_name$ ? mReturnedEntityName ;
  end select ;
  ioNonTerminalLabelList +=
    !inLabelName
    !mFormalParameters
    !mReturnedEntityName # Empty string if no returned entity
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <nonterminal_declaration> ?!@nonterminalDeclarationList ioNonterminalDeclarationList :
  @bool mHasParseLabel := false ;
  $nonterminal$ ;
  $non_terminal_symbol$ ? @lstring mNonterminalName ;
  @nonTerminalLabelList mLabels [emptyList] ;
  <nonterminal_label_declaration> ![@lstring new !"" !here] !?mLabels ;
  repeat
  while
    $label$ ;
    select
      @lstring labelName ;
      $identifier$ ?labelName ;
      <nonterminal_label_declaration> !labelName !?mLabels ;
    or
      $parse$ ;
      if mHasParseLabel then
        error here: "the 'parse' id already defined for this nonterminal" ;
      end if ;
      mHasParseLabel := true ;
    end select ;
  end repeat ;
  $;$ ;
  ioNonterminalDeclarationList +=
    !mNonterminalName
    !mLabels
    !mHasParseLabel
  ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#      S Y N T A X    R U L E    D E C L A R A T I O N                         *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <syntax_rule_label>
  ??@lstring inLabelName
  ?!@syntaxRuleLabelList ioLabelList
;

#------------------------------------------------------------------------------*

rule <syntax_rule_label>
  ??@lstring inLabelName
  ?!@syntaxRuleLabelList ioLabelList
:
  @formalParameterList mFormalParameters ;
  <formal_parameter_list> ? mFormalParameters ;
  @lstring mReturnedEntityName ; # Empty string if no returned entity
  @lstring mReturnedVariableName ;  # Empty string if no returned variable
  select
    mReturnedEntityName := [@lstring new !"" !here] ;
    mReturnedVariableName := [@lstring new !"" !here] ;
  or
    $->$ ;
    $type_name$ ? mReturnedEntityName ;
    select
      mReturnedVariableName := [@lstring new !"" !here] ;
    or
      $identifier$ ? mReturnedVariableName ;
    end select ;
  end select ;
  $:$ ;
  @syntaxInstructionList mSyntaxInstructionList ;
  <syntax_instruction_list> ? mSyntaxInstructionList ;
  ioLabelList +=
    !inLabelName
    !mFormalParameters
    !mReturnedEntityName # Empty string if no returned entity
    !mReturnedVariableName  # Empty string if no returned variable
    !mSyntaxInstructionList
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <syntax_rule_declaration> ?!@syntaxRuleList ioRuleList :
  $rule$ ;
  $non_terminal_symbol$ ? @lstring mNonterminalName ;
  @lstring labelName [new !"" !here] ;
  @syntaxRuleLabelList mLabelList [emptyList] ;
  repeat
    <syntax_rule_label> !labelName !? mLabelList ;
  while
    $label$ ;
    $identifier$ ?labelName ;
  end repeat ;
  $end$ ;
  $rule$ ;
  $;$ ;
  ioRuleList +=
    !mNonterminalName
    !mLabelList
  ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#   S Y N T A X    I N S T R U C T I O N    L I S T                         *
#                                                                           *
#---------------------------------------------------------------------------*

rule <syntax_instruction_list> !@syntaxInstructionList outSyntaxInstructionList :
  outSyntaxInstructionList := [@syntaxInstructionList emptyList] ;
  repeat
  while
    @semanticInstruction instruction ;
    <semantic_instruction> ? instruction ;
    outSyntaxInstructionList += !instruction ;
  while
    @syntaxInstruction instruction ;
    <syntax_instruction> ? instruction ;
    outSyntaxInstructionList += !instruction ;
  end repeat ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#   N O N T E R M I N A L    C A L L    I N S T R U C T I O N               *
#                                                                           *
#---------------------------------------------------------------------------*

rule <syntax_instruction> !@syntaxInstruction outInstruction :
  $non_terminal_symbol$ ? @lstring mNonterminalName ;
  @lstring mLabelName ; # No label : empty string
  select
    mLabelName := [@lstring new !"" !here] ;
  or
    $parse$ ;
    mLabelName := [@lstring new !"parse" !here] ;
  or
    $identifier$ ? mLabelName ;
  end select ;
  @actualParameterList mActualParameterList ;
  <actual_parameter_list> ? mActualParameterList ;
  @lstring mReturnedValueTargetVariableName ; # Empty String if no returned value
  @bool mReturnedValueIsAssigned ; # if false, is appended
  select
    mReturnedValueTargetVariableName := [@lstring new !"" !here] ;
    mReturnedValueIsAssigned := true ; # Any Value
  or
    $->$ ;
    $identifier$ ? mReturnedValueTargetVariableName ;
    mReturnedValueIsAssigned := true ;
  or
    $+>$ ;
    $identifier$ ? mReturnedValueTargetVariableName ;
    mReturnedValueIsAssigned := false ;
  end select ;
  $;$ ;
  outInstruction := [@nonterminalCallInstruction new
    !mNonterminalName
    !mLabelName
    !mActualParameterList
    !mReturnedValueTargetVariableName
    !mReturnedValueIsAssigned
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#   T E R M I N A L    C H E C K    I N S T R U C T I O N                   *
#                                                                           *
#---------------------------------------------------------------------------*

rule <syntax_instruction> !@syntaxInstruction outInstruction :
  $terminal$ ? @lstring mTerminalName ;
  @lstringlist mReturnedValueTargetVariableNames [emptyList] ;
  repeat
  while
    $?$ ;
    select
      $*$ ;
      mReturnedValueTargetVariableNames += ![@lstring new !"" !here] ;
    or
      @lstring returnedValueTargetVariableName ;
      $identifier$ ? returnedValueTargetVariableName ;
      mReturnedValueTargetVariableNames += !returnedValueTargetVariableName ;
    end select ;
  end repeat ;
  $;$ ;
  outInstruction := [@terminalCheckInstruction new
    !mTerminalName
    !mReturnedValueTargetVariableNames
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#   R E P E A T    I N S T R U C T I O N                                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <syntax_instruction> !@syntaxInstruction outInstruction :
  $repeat$ ;
  @syntaxInstructionList mRepeatedInstructionList ;
  <syntax_instruction_list> ? mRepeatedInstructionList ;
  @listOfSyntaxInstructionList mRepeatBranchList [emptyList] ;
  repeat
    $while$ ;
    @syntaxInstructionList mInstructionList ;
    <syntax_instruction_list> ? mInstructionList ;
    mRepeatBranchList += ! mInstructionList ;
  while
  end repeat ;
  $end$ ;
  $repeat$ ;
  $;$ ;
  outInstruction := [@repeatInstruction new
    !mRepeatedInstructionList
    !mRepeatBranchList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#   S E L E C T    I N S T R U C T I O N                                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <syntax_instruction> !@syntaxInstruction outInstruction :
  $select$ ;
  @listOfSyntaxInstructionList mSelectBranchList [emptyList] ;
  @syntaxInstructionList instructionList ;
  <syntax_instruction_list> ? instructionList ;
  mSelectBranchList += !instructionList ;
  repeat
    $or$ ;
    <syntax_instruction_list> ? instructionList ;
    mSelectBranchList += !instructionList ;
  while
  end repeat ;
  $end$ ;
  $select$ ;
  $;$ ;
  outInstruction := [@selectInstruction new
    !mSelectBranchList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#   P A R S E    D O    I N S T R U C T I O N                               *
#                                                                           *
#---------------------------------------------------------------------------*

rule <syntax_instruction> !@syntaxInstruction outInstruction :
  $parse$ ;
  $do$ ;
  @listOfSyntaxInstructionList mParseDoBranchList [emptyList] ;
  @syntaxInstructionList instructionList ;
  <syntax_instruction_list> ? instructionList ;
  mParseDoBranchList += !instructionList ;
  repeat
    $rewind$ ;
    <syntax_instruction_list> ? instructionList ;
    mParseDoBranchList += !instructionList ;
  while
  end repeat ;
  $end$ ;
  $parse$ ;
  $;$ ;
  outInstruction := [@parseDoInstruction new
    !mParseDoBranchList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#   B L O C K    I N S T R U C T I O N                                      *
#                                                                           *
#---------------------------------------------------------------------------*

rule <syntax_instruction> !@syntaxInstruction outInstruction :
  $block$ ;
  $identifier$ ? @lstring mReceiverVariable ;
  $.$ ;
  $identifier$ ? @lstring mBlockMethodName ;
  $($ ;
  @actualParameterList mPrologActualParameterList ;
  <actual_parameter_list> ? mPrologActualParameterList ;
  $)$ ;
  $:$ ;
  @syntaxInstructionList mInstructionList ;
  <syntax_instruction_list> ? mInstructionList ;
  $end$ ;
  $block$ ;
  $($ ;
  @actualParameterList mEpilogActualParameterList ;
  <actual_parameter_list> ? mEpilogActualParameterList ;
  $)$ ;
  $;$ ;
  outInstruction := [@blockInstruction new
    !mReceiverVariable
    !mBlockMethodName
    !mPrologActualParameterList
    !mInstructionList
    !mEpilogActualParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;