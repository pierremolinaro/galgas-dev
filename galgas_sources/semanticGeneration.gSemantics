#---------------------------------------------------------------------------*
#                                                                           *
#  Generate predefined types                                                *
#                                                                           *
#  Copyright (C) 2010, ..., 2010 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

semantics semanticGeneration :
  import "semanticInstructionGeneration.gSemantics" ;

  import semantics semanticInstructionGeneration in "semanticInstructionGeneration.gSemantics" ;

#---------------------------------------------------------------------------*
#                                                                           *
# P R O C E D U R E    I M P L E M E N A T I O N                            *
#                                                                           *
#---------------------------------------------------------------------------*

function generateProcedure
  ??@string inProcedureName
  ??@formalParameterListForGeneration inFormalArgumentList
  ??@semanticInstructionListForGeneration inInstructionList
  ??@bool inProcedureIsConst
  ??@string inCompilerTypeName
  ??@bool inGenerateLocationArgs
  ??@bool inGenerateResetTemplateString
  ->@string outGeneratedCode
:
#--- Build unused variable set
  @stringset unusedVariableCppNameSet [emptySet] ;
  if not inGenerateResetTemplateString then
    unusedVariableCppNameSet += !compilerCppName [] ;
  end if ;
  foreach inFormalArgumentList do
    if mFormalArgumentPassingMode != [@formalArgumentPassingModeAST argumentOut] then
      unusedVariableCppNameSet += !mFormalArgumentCppName ;
    end if ;
  end foreach ;
#--- Generate body
  @uint temporaryVariableIndex := 0 ;
  @string routineBody := "" ;
  [!?routineBody incIndentation !2] ;
  foreach inInstructionList do
    [mInstruction generateInstruction !?temporaryVariableIndex !?unusedVariableCppNameSet !?routineBody] ;
  end foreach ;
  [!?routineBody decIndentation !2] ;
#--- Generate header
  outGeneratedCode := "void " . inProcedureName . " (" ;
  const @uint colRef := [outGeneratedCode currentColumn] ;
#---
  foreach inFormalArgumentList do
    switch mFormalArgumentPassingMode 
    when argumentConstantIn :
      outGeneratedCode .= "GALGASap_" . [mFormalArgumentType identifierRepresentation] . " " ;
    when argumentInOut :
      outGeneratedCode .= "GALGASap_" . [mFormalArgumentType identifierRepresentation] . " & " ;
    when argumentOut :
      outGeneratedCode .= "GALGASap_" . [mFormalArgumentType identifierRepresentation] . " & " ;
    when argumentIn :
      outGeneratedCode .= "GALGASap_" . [mFormalArgumentType identifierRepresentation] . " " ;
    end switch ;
    if [unusedVariableCppNameSet hasKey !mFormalArgumentCppName] then
      outGeneratedCode .= "/* " . mFormalArgumentCppName . " */" ;
    else
      outGeneratedCode .= mFormalArgumentCppName ;
    end if ;
    outGeneratedCode .= ",\n" ;
    [!?outGeneratedCode appendSpacesUntilColumn !colRef] ;
  end foreach ;
#--- Compiler argument
  outGeneratedCode .= inCompilerTypeName . " * " ;
  if [unusedVariableCppNameSet hasKey !compilerCppName []] then
    outGeneratedCode .= "/* " . compilerCppName [] . " */" ;
  else
    outGeneratedCode .= compilerCppName [] ;
  end if ;
  if inGenerateLocationArgs then
    outGeneratedCode .= "\n" ;
    [!?outGeneratedCode appendSpacesUntilColumn !colRef] ;
    outGeneratedCode .= "COMMA_UNUSED_LOCATION_ARGS" ;
  end if ;
  outGeneratedCode .= ")" ;
  if inProcedureIsConst then
    outGeneratedCode .= " const" ;
  end if ;
  outGeneratedCode .= " {\n" ;
#--- Release 'out' arguments
  foreach inFormalArgumentList index IDX do
    switch mFormalArgumentPassingMode 
    when argumentOut :
      outGeneratedCode .= "  " . mFormalArgumentCppName . ".drop () ; // Release 'out' argument\n" ;
    when argumentConstantIn, argumentIn, argumentInOut :
    end switch ;
  end foreach ;
#--- Generate routine body
  outGeneratedCode .= routineBody ;
#---
  if inGenerateResetTemplateString then
    outGeneratedCode .= "  " . compilerCppName [] . "->resetTemplateString () ;\n" ;
  end if ;
#---
  outGeneratedCode .= "}\n\n" ;
end function ;

#---------------------------------------------------------------------------*
#                                                                           *
# F U N C T I O N    I M P L E M E N A T I O N                              *
#                                                                           *
#---------------------------------------------------------------------------*

function generateFunction
  ??@string inProcedureName
  ??@formalInputParameterListForGeneration inFormalArgumentList
  ??@semanticInstructionListForGeneration inInstructionList
  ??@string inCompilerTypeName
  ??@string inResultTypeName
  ??@string inResultVariableCppName
  ->@string outGeneratedCode
:
#--- Build unused variable set
  @stringset unusedVariableCppNameSet [emptySet] ;
  unusedVariableCppNameSet += !compilerCppName [] ;
#--- Release 'in' and 'const in' arguments at the end of the routine
  foreach inFormalArgumentList do
    unusedVariableCppNameSet += !mFormalArgumentCppName ;
  end foreach ;
#--- Generate body
  @uint temporaryVariableIndex := 0 ;
  @string routineBody := "" ;
  [!?routineBody incIndentation !2] ;
  foreach inInstructionList do
    [mInstruction generateInstruction !?temporaryVariableIndex !?unusedVariableCppNameSet !?routineBody] ;
  end foreach ;
  [!?routineBody decIndentation !2] ;
#--- Generate header
  outGeneratedCode := "GALGASap_" . [inResultTypeName identifierRepresentation] . " " ;
  outGeneratedCode .= inProcedureName . " (" ;
  const @uint colRef := [outGeneratedCode currentColumn] ;
#---
  foreach inFormalArgumentList do
    outGeneratedCode .= "GALGASap_" . [mFormalArgumentType identifierRepresentation] . " & " ;
    if [unusedVariableCppNameSet hasKey !mFormalArgumentCppName] then
      outGeneratedCode .= "/* " . mFormalArgumentCppName . " */" ;
    else
      outGeneratedCode .= mFormalArgumentCppName ;
    end if ;
    outGeneratedCode .= ",\n" ;
    [!?outGeneratedCode appendSpacesUntilColumn !colRef] ;
  end foreach ;
#--- Compiler argument
  outGeneratedCode .= inCompilerTypeName . " * " ;
  if [unusedVariableCppNameSet hasKey !compilerCppName []] then
    outGeneratedCode .= "/* " . compilerCppName [] . " */" ;
  else
    outGeneratedCode .= compilerCppName [] ;
  end if ;
  outGeneratedCode .= "\n" ;
  [!?outGeneratedCode appendSpacesUntilColumn !colRef] ;
  outGeneratedCode .= "COMMA_UNUSED_LOCATION_ARGS)" ;
  outGeneratedCode .= " {\n" ;
#--- Declare returned variable
  outGeneratedCode .= "  GALGASap_" . [inResultTypeName identifierRepresentation]
                   .  " " . inResultVariableCppName . " ; // Returned variable\n" ;
#--- Generate routine body
  outGeneratedCode .= routineBody ;
#--- Generate return instruction
  outGeneratedCode .= "//---\n" ;
  outGeneratedCode .= "  return " . inResultVariableCppName . " ;\n" ;
#---
  outGeneratedCode .= "}\n\n" ;
end function ;

#---------------------------------------------------------------------------*
#                                                                           *
#           G E N E R A T I O N    F I L E W R A P P E R S                  *
#                                                                           *
#---------------------------------------------------------------------------*

filewrapper typeGenerationTemplate in "../generation_templates/type_generation" {
} {
  
  template unifiedClassBodyForType "unified-class-body-for-type.h.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@bool IS_CONCRETE # false if abstract class
    ?@constructorMap CONSTRUCTOR_MAP
    ?@readerMap READER_MAP
    ?@modifierMap MODIFIER_MAP
    ?@instanceMethodMap INSTANCE_METHOD_MAP
    ?@classMethodMap CLASS_METHOD_MAP
    ?@enumerationDescriptorList ENUMERATION_DESCRIPTOR_LIST # Empty List if cannot be enumerated
    ?@uint SUPPORTED_OPERATORS
    ?@unifiedTypeMapProxyList ADD_ASSIGN_ARGUMENT_LIST # Empty list if operator is not supported
    ?@withAccessorMap WITH_ACCESSOR_MAP
  ;

  template genericTypeImplementation "generic-type.cpp.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@unifiedTypeMapProxy SUPER_TYPE_INDEX
  ;

#---------- STRUCTURE
  template structTypeHeader "GALGAS_struct.h.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedAttributeList ATTRIBUTE_LIST
  ;

  template structTypeSpecificImplementation "GALGAS_struct.cpp.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedAttributeList ATTRIBUTE_LIST
  ;

#---------- LIST
  template listTypeHeader "GALGAS_list.h.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedAttributeList ATTRIBUTE_LIST
  ;

  template listTypeSpecificImplementation "GALGAS_list.cpp.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedAttributeList ATTRIBUTE_LIST
  ;

#---------- SORTED LIST
  template sortedlistTypeHeader "GALGAS_sortedlist.h.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedAttributeList ATTRIBUTE_LIST
  ;

  template sortedlistTypeSpecificImplementation "GALGAS_sortedlist.cpp.gTemplate"
    ?@string TYPE_IDENTIFIER
    ?@typedAttributeList ATTRIBUTE_LIST
    ?@sortDescriptorListForGeneration SORT_ATTRIBUTE_LIST
  ;

#---------- ENUM
  template enumTypeHeader "GALGAS_enum.h.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@stringlist CONSTANT_LIST
  ;

  template enumTypeSpecificImplementation "GALGAS_enum.cpp.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@stringlist CONSTANT_LIST
    ?@enumMessageListForGeneration ENUM_MESSAGE_LIST
  ;
}

#---------------------------------------------------------------------------*

filewrapper typeGenerationTemplateEX in "../generation_templates/ex_type_generation" {
} {
  template autoPointerClassImplementation "GALGASap_type.cpp.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@string SUPER_TYPE_IDENTIFIER
    ?@uint SUPPORTED_OPERATORS
    ?@readerMap READER_MAP
    ?@nativeAttributeList NATIVE_ATTRIBUTE_LIST
  ;

  template unifiedTypeImplementation "unified-type.cpp.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@bool IS_CONCRETE # false if abstract class
    ?@unifiedTypeMapProxy SUPER_TYPE_INDEX
    ?@typeKindEnum TYPE_KIND
    ?@typedAttributeList CURRENT_TYPE_ATTRIBUTE_LIST
    ?@constructorMap CONSTRUCTOR_MAP
    ?@readerMap READER_MAP
    ?@modifierMap MODIFIER_MAP
    ?@instanceMethodMap INSTANCE_METHOD_MAP
    ?@classMethodMap CLASS_METHOD_MAP
    ?@enumerationDescriptorList ENUMERATION_DESCRIPTOR_LIST # Empty List if cannot be enumerated
    ?@uint SUPPORTED_OPERATORS
    ?@unifiedTypeMapProxyList ADD_ASSIGN_ARGUMENT_LIST # Empty list if operator is not supported
    ?@nativeAttributeList NATIVE_ATTRIBUTE_LIST
  ;

  template predefinedTypeSpecificImplementation "predefined-type-specific.cpp.gTemplate"
    ?@string TYPE_IDENTIFIER
    ?@modifierMap MODIFIER_MAP
    ?@uint SUPPORTED_OPERATORS
    ?@unifiedTypeMapProxyList ADD_ASSIGN_ARGUMENT_LIST # Empty list if operator is not supported
    ?@nativeAttributeList NATIVE_ATTRIBUTE_LIST
  ;

  template listmapTypeSpecificImplementation "GALGAS_listmap.cpp.gTemplate"
    ?@string TYPE_IDENTIFIER
    ?@string ASSOCIATED_LISTTYPE_IDENTIFIER
    ?@typedAttributeList ATTRIBUTE_LIST
  ;

  template mapTypeSpecificDeclaration "GALGAS_map-specific.h.gTemplate"
    ?@string TYPE_IDENTIFIER
    ?@typedAttributeList ATTRIBUTE_LIST
  ;

  template mapTypeSpecificImplementation "GALGAS_map.cpp.gTemplate"
    ?@string TYPE_IDENTIFIER
    ?@typedAttributeList ATTRIBUTE_LIST
    ?@insertMethodListAST INSERT_METHOD_LIST
    ?@mapSearchMethodListAST SEARCH_METHOD_LIST
    ?@mapRemoveMethodListAST REMOVE_METHOD_LIST
    ?@bool HAS_INSERT_OR_REPLACE 
    ?@withAccessorMap WITH_ACCESSOR_MAP
  ;

  template mapProxyTypeSpecificImplementation "GALGAS_mapproxy.cpp.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@string ASSOCIATED_MAP_TYPE_IDENTIFIER
    ?@typedAttributeList ATTRIBUTE_LIST
    ?@mapSearchMethodListAST SEARCH_METHOD_LIST
  ;

  template classTypeSpecificImplementation "GALGAS_class.cpp.gTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@string SUPER_TYPE_IDENTIFIER
    ?@typedAttributeList CURRENT_ATTRIBUTE_LIST
    ?@typedAttributeList ALL_ATTRIBUTE_LIST
    ?@typedAttributeList INHERITED_ATTRIBUTE_LIST
    ?@bool IS_ABSTRACT
    ?@methodDefinitionListForGeneration CONCRETE_METHOD_LIST
  ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#           G E N E R A T I O N    F I L E W R A P P E R S                  *
#                                                                           *
#---------------------------------------------------------------------------*

filewrapper semanticComponentGenerationTemplate in "../generation_templates/semantic_generation" {
} {
  template routineDeclaration "routine-declaration.h.gTemplate"
    ?@string ROUTINE_NAME
    ?@formalParameterListForGeneration FORMAL_ARGUMENT_LIST
  ;

  template functionDeclaration "function-declaration.h.gTemplate"
    ?@string FUNCTION_NAME
    ?@formalInputParameterListForGeneration FORMAL_ARGUMENT_LIST
    ?@string RETURN_TYPE_IDENTIFIER
  ;

  template routineImplementation "routine-implementation.cpp.gTemplate"
    ?@string ROUTINE_NAME
    ?@string ROUTINE_IMPLEMENTATION
  ;

  template functionImplementation "function-implementation.cpp.gTemplate"
    ?@string FUNCTION_NAME
    ?@string FUNCTION_IMPLEMENTATION
    ?@formalInputParameterListForGeneration FORMAL_ARGUMENT_LIST
    ?@string RETURN_TYPE_IDENTIFIER
  ;
}

#---------------------------------------------------------------------------*
#                                                                           *
# A P P E N D    D E C L A R A T I O N                                      *
#                                                                           *
#---------------------------------------------------------------------------*

reader @semanticDeclarationForGeneration appendDeclaration -> @string outHeader :
  outHeader := "" ;
end reader ;

#---------------------------------------------------------------------------*

override reader @routinePrototypeDeclarationForGeneration appendDeclaration -> @string outHeader :
  outHeader := [filewrapper semanticComponentGenerationTemplate.routineDeclaration
    !mRoutineName
    !mFormalArgumentList
  ] ; 
end reader ;

#---------------------------------------------------------------------------*

override reader @functionPrototypeDeclarationForGeneration appendDeclaration -> @string outHeader :
  outHeader := [filewrapper semanticComponentGenerationTemplate.functionDeclaration
    !mFunctionName
    !mFormalArgumentList
    ![mReturnType identifierRepresentation]
  ] ; 
end reader ;

#---------------------------------------------------------------------------*

override reader @onceFunctionDeclarationForGeneration appendDeclaration -> @string outHeader :
  outHeader := [filewrapper semanticComponentGenerationTemplate.functionDeclaration
    !mFunctionName
    ![@formalInputParameterListForGeneration emptyList]
    ![mReturnType identifierRepresentation]
  ] ; 
end reader ;

#---------------------------------------------------------------------------*

override reader @structTypeForGeneration appendDeclaration ->@string outHeader :
  outHeader := [filewrapper typeGenerationTemplate.structTypeHeader
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    !mTypedAttributeList
  ] ;
  outHeader .= [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    ![mTypeIndex mIsConcrete]
    ![mTypeIndex mConstructorMap]
    ![mTypeIndex mReaderMap]
    ![mTypeIndex mModifierMap]
    ![mTypeIndex mInstanceMethodMap]
    ![mTypeIndex mClassMethodMap]
    ![mTypeIndex mEnumerationDescriptor]
    ![mTypeIndex mHandledOperatorFlags]
    ![mTypeIndex mAddAssignOperatorArguments]
    ![mTypeIndex mWithAccessorMap]
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @listTypeForGeneration appendDeclaration ->@string outHeader :
  outHeader := [filewrapper typeGenerationTemplate.listTypeHeader
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    !mTypedAttributeList
  ] ;
  outHeader .= [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    ![mTypeIndex mIsConcrete]
    ![mTypeIndex mConstructorMap]
    ![mTypeIndex mReaderMap]
    ![mTypeIndex mModifierMap]
    ![mTypeIndex mInstanceMethodMap]
    ![mTypeIndex mClassMethodMap]
    ![mTypeIndex mEnumerationDescriptor]
    ![mTypeIndex mHandledOperatorFlags]
    ![mTypeIndex mAddAssignOperatorArguments]
    ![mTypeIndex mWithAccessorMap]
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @sortedListTypeForGeneration appendDeclaration ->@string outHeader :
  outHeader := [filewrapper typeGenerationTemplate.sortedlistTypeHeader
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    !mTypedAttributeList
  ] ;
  outHeader .= [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    ![mTypeIndex mIsConcrete]
    ![mTypeIndex mConstructorMap]
    ![mTypeIndex mReaderMap]
    ![mTypeIndex mModifierMap]
    ![mTypeIndex mInstanceMethodMap]
    ![mTypeIndex mClassMethodMap]
    ![mTypeIndex mEnumerationDescriptor]
    ![mTypeIndex mHandledOperatorFlags]
    ![mTypeIndex mAddAssignOperatorArguments]
    ![mTypeIndex mWithAccessorMap]
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @enumTypeForGeneration appendDeclaration ->@string outHeader :
  outHeader := [filewrapper typeGenerationTemplate.enumTypeHeader
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    !mConstantList
  ] ;
  outHeader .= [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    ![mTypeIndex mIsConcrete]
    ![mTypeIndex mConstructorMap]
    ![mTypeIndex mReaderMap]
    ![mTypeIndex mModifierMap]
    ![mTypeIndex mInstanceMethodMap]
    ![mTypeIndex mClassMethodMap]
    ![mTypeIndex mEnumerationDescriptor]
    ![mTypeIndex mHandledOperatorFlags]
    ![mTypeIndex mAddAssignOperatorArguments]
    ![mTypeIndex mWithAccessorMap]
  ] ;
 end reader ;

#---------------------------------------------------------------------------*
#                                                                           *
# A P P E N D    G E N E R I C    T Y P E    I M P L E M E N T A T I O N    *
#                                                                           *
#  This code is common for all types                                        *
#---------------------------------------------------------------------------*

reader @semanticDeclarationForGeneration appendTypeGenericImplementation ->@string outImplementation :
  outImplementation := "" ;
end reader ;

#---------------------------------------------------------------------------*

override reader @semanticTypeForGeneration appendTypeGenericImplementation ->@string outImplementation :
  outImplementation := [filewrapper typeGenerationTemplate.genericTypeImplementation
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    ![mTypeIndex mSuperType]
  ] ;
end reader ;


#---------------------------------------------------------------------------*
#                                                                           *
# A P P E N D    S P E C I F I C    I M P L E M E N T A T I O N             *
#                                                                           *
#---------------------------------------------------------------------------*

reader @semanticDeclarationForGeneration appendSpecificImplementation ->@string outImplementation :
  outImplementation := "" ;
end reader ;

#---------------------------------------------------------------------------*

override reader @listmapTypeForGeneration appendSpecificImplementation ->@string outImplementation :
  outImplementation := [filewrapper typeGenerationTemplateEX.listmapTypeSpecificImplementation
    ![mTypeIndex identifierRepresentation]
    ![mAssociatedListTypeIndex identifierRepresentation]
    !mAssociatedListTypedAttributeList
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @mapTypeForGeneration appendSpecificImplementation ->@string outImplementation :
  outImplementation := [filewrapper typeGenerationTemplateEX.mapTypeSpecificImplementation
    ![mTypeIndex identifierRepresentation]
    !mTypedAttributeList
    !mInsertMethodList
    !mSearchMethodList
    !mRemoveMethodList
    !mHasInsertOrReplaceModifier
    !mWithAccessorMap
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @sortedListTypeForGeneration appendSpecificImplementation ->@string outImplementation :
  outImplementation := [filewrapper typeGenerationTemplate.sortedlistTypeSpecificImplementation
    ![mTypeIndex identifierRepresentation]
    !mTypedAttributeList
    !mSortDescriptorList
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @enumTypeForGeneration appendSpecificImplementation ->@string outImplementation :
  outImplementation := [filewrapper typeGenerationTemplate.enumTypeSpecificImplementation
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    !mConstantList
    !mEnumMessageList
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @mapProxyTypeForGeneration appendSpecificImplementation ->@string outImplementation :
  outImplementation := [filewrapper typeGenerationTemplateEX.mapProxyTypeSpecificImplementation
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    ![mAssociatedMapTypeProxy identifierRepresentation]
    !mAssociatedMapTypedAttributeList
    !mSearchMethodList
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @classTypeForGeneration appendSpecificImplementation ->@string outImplementation :
  outImplementation := [filewrapper typeGenerationTemplateEX.classTypeSpecificImplementation
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    !if [mSuperClass isNull] then "class" else [mSuperClass identifierRepresentation] end
    !mTypedAttributeList
    !mAllTypedAttributeList
    ![mAllTypedAttributeList subListWithRange !0 ![mAllTypedAttributeList length] - [mTypedAttributeList length]]
    !mIsAbstract
    !mMethodList
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @structTypeForGeneration appendSpecificImplementation ->@string outImplementation :
  outImplementation := [filewrapper typeGenerationTemplate.structTypeSpecificImplementation
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    !mTypedAttributeList
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @listTypeForGeneration appendSpecificImplementation ->@string outImplementation :
  outImplementation := [filewrapper typeGenerationTemplate.listTypeSpecificImplementation
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    !mTypedAttributeList
  ] ;
 end reader ;

#---------------------------------------------------------------------------*

override reader @routineImplementationForGeneration appendSpecificImplementation -> @string outImplementation :
  outImplementation := [filewrapper semanticComponentGenerationTemplate.routineImplementation
    !mRoutineName
    !generateProcedure [
      !"routine_" . [mRoutineName identifierRepresentation]
      !mFormalArgumentList
      !mRoutineInstructionList
      !false # Do not generate 'const' qualifier
      !"C_Compiler"
      !true # Generate location Argument
      !false # Do not generate 'resetTemplateString'
    ]
  ] ; 
end reader ;

#---------------------------------------------------------------------------*

override reader @functionImplementationForGeneration appendSpecificImplementation -> @string outImplementation :
  outImplementation := [filewrapper semanticComponentGenerationTemplate.functionImplementation
    !mFunctionName
    !generateFunction [
      !"function_" . [mFunctionName identifierRepresentation]
      !mFormalArgumentList
      !mFunctionInstructionList
      !"C_Compiler"
      ![mReturnType key]
      !mResultVariableCppName
    ]
    !mFormalArgumentList
    ![mReturnType identifierRepresentation]
  ] ; 
end reader ;

#---------------------------------------------------------------------------*

override reader @onceFunctionDeclarationForGeneration appendSpecificImplementation -> @string outImplementation :
  outImplementation := [filewrapper semanticComponentGenerationTemplate.functionImplementation
    !mFunctionName
    !generateFunction [
      !"function_" . [mFunctionName identifierRepresentation]
      ![@formalInputParameterListForGeneration emptyList]
      !mFunctionInstructionList
      !"C_Compiler"
      ![mReturnType key]
      !mResultVariableCppName
    ]
    ![@formalInputParameterListForGeneration emptyList]
    ![mReturnType identifierRepresentation]
  ] ; 
end reader ;

#---------------------------------------------------------------------------*

end semantics ;
