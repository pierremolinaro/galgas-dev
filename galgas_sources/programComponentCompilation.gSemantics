#---------------------------------------------------------------------------*
#                                                                           *
#  Program component compilation                                            *
#                                                                           *
#  Copyright (C) 2010, ..., 2010 Pierre Molinaro.                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

semantics programComponentCompilation :
import "semanticAnalysisOfSyntaxComponent.gSemantics" ;
import "programSLRgrammar.gGrammar" ;

#---------------------------------------------------------------------------*
#                                                                           *
#           G E N E R A T I O N    F I L E W R A P P E R S                  *
#                                                                           *
#---------------------------------------------------------------------------*

filewrapper programFileGenerationTemplate in "../generation_templates/program_generation" {
}{
}{
  template programFileHeader "program-file.h.gTemplate"
    ?@string COMPONENT_NAME
    ?@stringlist IMPORTED_COMPONENT_LIST
  ;

  template programFileImplementation "program-file.cpp.gTemplate"
    ?@string COMPONENT_NAME
    ?@stringlist IMPORTED_COMPONENT_LIST
    ?@programRuleList PROGRAM_RULE_LIST
    ?@bool HAS_INDEXING
  ;

}

#---------------------------------------------------------------------------*
#                                                                           *
#           C O M P I L E    ' B E F O R E '                                *
#                                                                           *
#---------------------------------------------------------------------------*

routine compileBeforeClause
  ??@analysisContext inAnalysisContext
  ??@prologueEpilogueList inPrologueList
  ?!@semanticDeclarationSortedListForGeneration ioDecoratedDeclarationListForGeneration
:
#--- Compile 'before' routines
  var semanticInstructionListForGeneration := [@semanticInstructionListForGeneration emptyList] ;
  for * in inPrologueList do
    analyzeRoutineBody
      !inAnalysisContext
      ![@formalParameterListAST emptyList] # Routine signature : no argument
      ![@localConstantList emptyList] # No local constant
      ![@typedAttributeList emptyList] # No attribute (not a method)
      ![@typedAttributeList emptyList] # No attribute (not a method)
      !""
      !mInstructionList
      !mEndOfInstructionList
      ??@semanticInstructionListForGeneration blockSemanticInstructionListForGeneration
      ?*
    ;
    semanticInstructionListForGeneration += ![@semanticBlockInstructionForGeneration new
      !blockSemanticInstructionListForGeneration
    ] ;
  end for ;
#---
  ioDecoratedDeclarationListForGeneration +=
    ![@routineImplementationForGeneration new
      !false # Has no header
      !"proc-before"
      !"before"
      ![@formalParameterListForGeneration emptyList]
      !semanticInstructionListForGeneration
    ]
  !""
  ;
end routine ;

#---------------------------------------------------------------------------*
#                                                                           *
#           C O M P I L E    P R O G R A M    R U L E S                     *
#                                                                           *
#---------------------------------------------------------------------------*

routine compileProgramRules
  ??@analysisContext inAnalysisContext
  ??@programRuleList inProgramRules
  ?!@semanticDeclarationSortedListForGeneration ioDecoratedDeclarationListForGeneration
  ?!@bool ioHasIndexing
:
  @uint ruleIndex := 0 ;
  foreach inProgramRules do
    if mIndexingGrammar->string != "" then
      ioHasIndexing := true ;
      [inAnalysisContext->mSemanticContext->mGrammarMap searchKey !mIndexingGrammar ?* ??@bool hasIndexing ?*] ;
      if not hasIndexing then
        error mIndexingGrammar : "the '" . mIndexingGrammar . "' grammar does not enable indexing" ;
      end if ;
    end if ;
    @formalParameterListAST argumentList [emptyList] ;
    argumentList +=
      ![@formalArgumentPassingModeAST argumentConstantIn]
      ![@lstring new !"lstring" !here]
      !mSourceFileVariableName
      !mSourceFileVariableNameIsUnused
    ;
    @semanticInstructionListForGeneration semanticInstructionListForGeneration ;
    @formalParameterListForGeneration routineSignature ;
    analyzeRoutineBody
      !inAnalysisContext
      !argumentList
      ![@localConstantList emptyList] # No local constant
      ![@typedAttributeList emptyList] # No attribute (not a method)
      ![@typedAttributeList emptyList] # No attribute (not a method)
      !""
      !mInstructionList
      !mEndOfInstructionList
      ?semanticInstructionListForGeneration
      ?routineSignature
    ;
    ioDecoratedDeclarationListForGeneration +=
      ![@routineImplementationForGeneration new
        !false # Has no header
        !"proc-programrule"
        !"programRule_" . [ruleIndex string]
        !routineSignature
        !semanticInstructionListForGeneration
      ]
    !""
    ;
    ruleIndex ++ ;
  end foreach ;
end routine ;

#---------------------------------------------------------------------------*
#                                                                           *
#           C O M P I L E    ' A F T E R '                                  *
#                                                                           *
#---------------------------------------------------------------------------*

routine compileAfterClause
  ??@analysisContext inAnalysisContext
  ??@prologueEpilogueList inEpilogueList
  ?!@semanticDeclarationSortedListForGeneration ioDecoratedDeclarationListForGeneration
:
#--- Compile 'before' routines
  var semanticInstructionListForGeneration := [@semanticInstructionListForGeneration emptyList] ;
  for * in inEpilogueList do
    analyzeRoutineBody
      !inAnalysisContext
      ![@formalParameterListAST emptyList] # Routine signature : no argument
      ![@localConstantList emptyList] # No local constant
      ![@typedAttributeList emptyList] # No attribute (not a method)
      ![@typedAttributeList emptyList] # No attribute (not a method)
      !""
      !mInstructionList
      !mEndOfInstructionList
      ??@semanticInstructionListForGeneration blockSemanticInstructionListForGeneration
      ?*
    ;
    semanticInstructionListForGeneration += ![@semanticBlockInstructionForGeneration new
      !blockSemanticInstructionListForGeneration
    ] ;
  end for ;
#---
  ioDecoratedDeclarationListForGeneration +=
    ![@routineImplementationForGeneration new
      !false # Has no header
      !"proc-after"
      !"after"
      ![@formalParameterListForGeneration emptyList]
      !semanticInstructionListForGeneration
    ]
  !""
  ;
end routine ;

#---------------------------------------------------------------------------*
#                                                                           *
#           C O M P I L E    P R O G R A M    C O M P O N E N T             *
#                                                                           *
#---------------------------------------------------------------------------*

routine compileProgramComponentFromAST
  ??@lstring inSourceFile
  ??@string inOutputDirectory
  ??@programComponentAST inProgramComponentAST
  ?!@parsedComponentStruct ioParsedComponentStruct
:
  const @string basename := [[inSourceFile lastPathComponent] stringByDeletingPathExtension] ;
#--- Check component name
  const @lstring componentName := [inProgramComponentAST mProgramComponentName] ;
  if [componentName string] != basename then
    warning componentName: "GALGAS checks "
      "the component name ('" . [componentName string]
    . "') against the source file base name ('" . basename . "'): they should be identical"
    ;
  end if ; 
#--- Recursively parse all imported files
  @semanticDeclarationListAST semanticDeclarationList := [inProgramComponentAST mSemanticDeclarationList] ;
  @stringset parsedFileSet [setWithString ![inSourceFile string]] ;
  @lstringlist optionComponentFileNameList [emptyList] ;
  @grammarComponentASTList grammarComponentASTList [emptyList] ;
  @stringlist headerIncludeList [emptyList] ;
  @stringlist implementationIncludeList [emptyList] ;
  recursivelyImportSemanticComponentAndCheckNoCircularityInImportSemantics
    !inSourceFile
    !?ioParsedComponentStruct
    !?optionComponentFileNameList
    !?grammarComponentASTList
    !?semanticDeclarationList
    !?parsedFileSet
    !?headerIncludeList
    !?implementationIncludeList
    ![inProgramComponentAST mImportedComponentFileNameList]
    ![inSourceFile stringByDeletingLastPathComponent]
  ;
#------ Add predefined types to semantics declarations
  @semanticDeclarationListAST semanticDeclarationListWithPredefinedTypes := semanticDeclarationList ;
  appendPredefinedTypesASTs !?semanticDeclarationListWithPredefinedTypes ;
#------ Build semantic context
  @semanticContext semanticContext ;
  buildSemanticContext
    ![inSourceFile string]
    !semanticDeclarationListWithPredefinedTypes
    ![inProgramComponentAST mEndOfSourceFile]
    ?semanticContext
  ;
#--- Perform semantic analysis of grammar components
  buildGrammarComponentMapForSemanticAnalysis
    !grammarComponentASTList
    !?semanticContext
  ;
#--- Build option component map for analysis
  buildOptionComponentMapForSemanticAnalysis
    !optionComponentFileNameList
    !?ioParsedComponentStruct
    !?semanticContext->mOptionComponentMapForSemanticAnalysis
  ;
#--- Build lexique component map for semantic analysis
  buildLexiqueComponentMapForSemanticAnalysis
    !ioParsedComponentStruct
    ?@lexiqueComponentMapForSemanticAnalysis lexiqueMap
  ;
  semanticContext->mLexiqueComponentMapForSemanticAnalysis := lexiqueMap ;
#------ Semantic Analysis
  @semanticDeclarationSortedListForGeneration decoratedDeclarationListForGeneration ;
  performSemanticAnalysis
    ![inProgramComponentAST mSemanticDeclarationList]
    !semanticContext
    ?decoratedDeclarationListForGeneration
  ;
#--- Analysis context
  const analysisContext :=  [@analysisContext new
    !semanticContext
    !buildPredefinedTypes [!semanticContext]
    ![@unifiedTypeMapProxy null]
    !""
    ![@unifiedTypeMapProxy null]
  ] ;
#--- Analyse 'before' instructions
  compileBeforeClause
    !analysisContext
    !inProgramComponentAST->mPrologueList
    !?decoratedDeclarationListForGeneration
  ;
#--- Analyse program rules
  @bool hasIndexing := false ;
  compileProgramRules
    !analysisContext
    !inProgramComponentAST->mProgramRules
    !?decoratedDeclarationListForGeneration
    !?hasIndexing
  ;
#--- Analyse 'after' instructions
  compileAfterClause
    !analysisContext
    !inProgramComponentAST->mEpilogueList
    !?decoratedDeclarationListForGeneration
  ;
#------ Generate Header File
  if [@uint errorCount] == 0 then
    @string headerString := [filewrapper programFileGenerationTemplate.programFileHeader
      ![componentName string]
      !headerIncludeList
    ] ;
    foreach decoratedDeclarationListForGeneration do
      var unusedInclusionSet := [@stringset emptySet] ;
      [mDeclaration appendDeclaration1 !?unusedInclusionSet ??@string code] ;
      headerString .= code ;
    end foreach ;
    foreach decoratedDeclarationListForGeneration do
      var inclusionSet2 := [@stringset emptySet] ;
      [mDeclaration appendDeclaration2 !false !inOutputDirectory !?inclusionSet2 ??@string headerString2] ;
      headerString .= headerString2 ;
#      headerString .= [mDeclaration appendDeclaration2 !inOutputDirectory !false] ;
    end foreach ;
    [@string generateFileWithPattern
      !inOutputDirectory
      ![componentName string] . ".h"
      !"//"
      !"\n\n" # Defaut user zone1
      !headerString
      !"\n\n" # Defaut user zone2
      !"#endif\n"
    ] ;
  end if ;
#------ Generate Implementation File
  if [@uint errorCount] == 0 then
    @string implementationString := [filewrapper programFileGenerationTemplate.programFileImplementation
      ![componentName string]
      !implementationIncludeList
      ![inProgramComponentAST mProgramRules]
      !hasIndexing
    ] ;
    foreach decoratedDeclarationListForGeneration do
      implementationString .= [mDeclaration appendTypeGenericImplementation] ;
      var unusedInclusionSet := [@stringset emptySet] ;
      [mDeclaration appendSpecificImplementation !?unusedInclusionSet ??@string code] ;
      implementationString += code ;
    end foreach ;
    [@string generateFileWithPattern
      !inOutputDirectory
      ![componentName string] . ".cpp"
      !"//"
      !"\n\n" # Defaut user zone1
      !implementationString
      !"\n\n" # Defaut user zone2
      !"\n"
    ] ;
  end if ;
end routine ;

#---------------------------------------------------------------------------*

routine compileProgramComponentFromSourceFile
  ??@lstring inSourceFile
  ??@string inOutputDirectory
  ?!@parsedComponentStruct ioParsedComponentStruct
:
#--- Parse source file
  const basename := [[inSourceFile lastPathComponent] stringByDeletingPathExtension] ;
  const basenameKey := [@lstring new !basename !here] ;
  @programComponentAST programComponentAST ;
  if [ioParsedComponentStruct->mParsedProgramComponentMap hasKey !basename] then
    [ioParsedComponentStruct->mParsedProgramComponentMap searchKey !basenameKey ?* ?programComponentAST] ;
  else
    grammar programSLRgrammar in inSourceFile ?programComponentAST ;
    [!?ioParsedComponentStruct->mParsedProgramComponentMap insertKey !basenameKey !inSourceFile !programComponentAST] ;
  end if ;
#---
  compileProgramComponentFromAST
    !inSourceFile
    !inOutputDirectory
    !programComponentAST
    !?ioParsedComponentStruct
  ;
end routine ;

#---------------------------------------------------------------------------*
#                                                                           *
#           C O M P I L E    P R O G R A M    C O M P O N E N T             *
#                                                                           *
#---------------------------------------------------------------------------*

routine compileProgramComponentGalgas3
  ??@string inOutputDirectory
  ??@prologueEpilogueList inPrologueList
  ??@programRuleList inProgramRules
  ??@prologueEpilogueList inEpilogueList
  ??@semanticContext inSemanticContext
  ?!@stringlist ioToolProductFileList
  ?!@stringset ioAllProductFileSet
:
  const useOneHugeHeader := [option galgas_cli_options.generateOneHeader value] ;
#--- Analysis context
  const analysisContext := [@analysisContext new
    !inSemanticContext
    !buildPredefinedTypes [!inSemanticContext]
    ![@unifiedTypeMapProxy null]
    !""
    ![@unifiedTypeMapProxy null]
  ] ;
#--- Analyse 'before' instructions
  var decoratedDeclarationListForGeneration := [@semanticDeclarationSortedListForGeneration emptySortedList] ;
  compileBeforeClause
    !analysisContext
    !inPrologueList
    !?decoratedDeclarationListForGeneration
  ;
#--- Analyse program rules
  @bool hasIndexing := false ;
  compileProgramRules
    !analysisContext
    !inProgramRules
    !?decoratedDeclarationListForGeneration
    !?hasIndexing
  ;
#--- Analyse 'after' instructions
  compileAfterClause
    !analysisContext
    !inEpilogueList
    !?decoratedDeclarationListForGeneration
  ;
#------ Generate Header File
  const programFileName := "program" ;
  if [@uint errorCount] == 0 then
    var inclusionSet1 := [@stringset emptySet] ;
    var inclusionSet2 := [@stringset emptySet] ;
    var headerString := "" ;
    foreach decoratedDeclarationListForGeneration do
      [mDeclaration appendDeclaration1 !?inclusionSet1 ??@string code] ;
      headerString .= code ;
    end foreach ;
    foreach decoratedDeclarationListForGeneration do
      [mDeclaration appendDeclaration2 !true !inOutputDirectory !?inclusionSet2 ??@string headerString2] ;
      headerString .= headerString2 ;
    end foreach ;
    if useOneHugeHeader then
      inclusionSet1 := [@stringset setWithString !"all-declarations"] ;
      inclusionSet2 := [@stringset emptySet] ;
    end if ;
    const headerIncludes:= [filewrapper programFileGenerationTemplate.programFileHeader
      !programFileName
      ![inclusionSet1 stringList] + [inclusionSet2 stringList]
    ] ;

#    var headerIncludeList := [@stringlist emptyList] ;
#    if useOneHugeHeader then
#      headerIncludeList += !"all-declarations" ;
#    end if ;
#    @string headerString := [filewrapper programFileGenerationTemplate.programFileHeader
#      !programFileName
#      !headerIncludeList
#    ] ;
#    foreach decoratedDeclarationListForGeneration do
#      var inclusionSet := [@stringset emptySet] ;
#      [mDeclaration appendDeclaration1 !?inclusionSet ??@string code] ;
#      if not useOneHugeHeader then
#        for (s) in inclusionSet do
#          headerString .= "#include \"" . s . ".h\"\n" ;
#        end for ;
#      end if ;
#      headerString .= code ;
#    end foreach ;
#    foreach decoratedDeclarationListForGeneration do
#      var inclusionSet2 := [@stringset emptySet] ;
#      [mDeclaration appendDeclaration2 !true !inOutputDirectory !?inclusionSet2 ??@string headerString2] ;
#      if not useOneHugeHeader then
#        for (s) in inclusionSet2 do
#          headerString .= "#include \"" . s . ".h\"\n" ;
#        end for ;
#      end if ;
#      headerString .= headerString2 ;
#    end foreach ;
    ioAllProductFileSet += !programFileName + ".h" ;
    [@string generateFileWithPattern
      !inOutputDirectory
      !programFileName + ".h"
      !"//"
      !"\n\n" # Defaut user zone1
      !headerIncludes . headerString
      !"\n\n" # Defaut user zone2
      !"#endif\n"
    ] ;
  end if ;
#-------------------------------------------------------- Generate Implementation File
  if [@uint errorCount] == 0 then
    var inclusionSet := [@stringset emptySet] ;
    var implementationString := "" ;
    foreach decoratedDeclarationListForGeneration do
      implementationString += [mDeclaration appendTypeGenericImplementation] ;
      [mDeclaration appendSpecificImplementation !?inclusionSet ??@string code] ;
      implementationString += code ;
    end foreach ;
    if useOneHugeHeader then
      inclusionSet := [@stringset emptySet] ;
      for * in inSemanticContext->mOptionComponentMapForSemanticAnalysis do
        if not mIsPredefined then
          inclusionSet += !"option-" . lkey->string ;
        end if ;
      end for ;
      for * in inSemanticContext->mGrammarMap do
        inclusionSet += !"grammar-" . lkey->string ;
      end for ;
    end if ;
    const header := [filewrapper programFileGenerationTemplate.programFileImplementation
      !programFileName
      ![inclusionSet stringList]
      !inProgramRules
      !hasIndexing
    ] ;
    ioToolProductFileList += !programFileName . ".cpp" ;
    ioAllProductFileSet += !programFileName + ".cpp" ;
    [@string generateFileWithPattern
      !inOutputDirectory
      !programFileName . ".cpp"
      !"//"
      !"\n\n" # Defaut user zone1
      !header + implementationString
      !"\n\n" # Defaut user zone2
      !"\n"
    ] ;
  end if ;
end routine ;

#---------------------------------------------------------------------------*

end semantics ;
