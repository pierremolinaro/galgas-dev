#---------------------------------------------------------------------------*
#                                                                           *
#  Program component compilation                                            *
#                                                                           *
#  Copyright (C) 2010, ..., 2010 Pierre Molinaro.                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

semantics programComponentCompilation :
import "semanticAnalysisOfSyntaxComponent.gSemantics" ;
import "programSLRgrammar.gGrammar" ;

#---------------------------------------------------------------------------*
#                                                                           *
#           G E N E R A T I O N    F I L E W R A P P E R S                  *
#                                                                           *
#---------------------------------------------------------------------------*

local filewrapper programFileGenerationTemplate in "../generation_templates/program_generation" {
}{
}{
  template programFileHeader "program-file.h.gTemplate"
    ?@string COMPONENT_NAME
    ?@stringlist IMPORTED_COMPONENT_LIST
    ?@lstringlist TYPE_PREDECLARATION_LIST
  ;

  template programFileImplementation "program-file.cpp.gTemplate"
    ?@string COMPONENT_NAME
    ?@stringlist IMPORTED_COMPONENT_LIST
    ?@programRuleList PROGRAM_RULE_LIST
    ?@bool HAS_INDEXING
  ;

}

#---------------------------------------------------------------------------*
#                                                                           *
#           C O M P I L E    ' B E F O R E '                                *
#                                                                           *
#---------------------------------------------------------------------------*

local routine compileBeforeClause
  ??@analysisContext inAnalysisContext
  ??@programComponentAST inProgramComponentAST
  ?!@semanticDeclarationSortedListForGeneration ioDecoratedDeclarationListForGeneration
:
#--- Check there is zero or one 'before' declaration
  @semanticInstructionListForGeneration semanticInstructionListForGeneration ;
  if [[inProgramComponentAST mPrologueList] length] > 1 then
    const @location locationOfSecondItem := [[inProgramComponentAST mPrologueList] mEndOfInstructionListAtIndex !1] ;
    error locationOfSecondItem: "there are several 'before' declarations" : semanticInstructionListForGeneration ;
  elsif [[inProgramComponentAST mPrologueList] length] == 0 then
    semanticInstructionListForGeneration := [@semanticInstructionListForGeneration emptyList] ;
  else
    analyzeRoutineBody
      !inAnalysisContext
      ![@formalParameterListAST emptyList] # Routine signature : no argument
      ![@localConstantList emptyList] # No local constant
      ![@typedAttributeList emptyList] # No attribute (not a method)
      ![@typedAttributeList emptyList] # No attribute (not a method)
      !""
      ![[inProgramComponentAST mPrologueList] mInstructionListAtIndex !0]
      ![[inProgramComponentAST mPrologueList] mEndOfInstructionListAtIndex !0]
      ?semanticInstructionListForGeneration
      ?*
    ;
  end if ;
#---
  ioDecoratedDeclarationListForGeneration += ![@routineImplementationForGeneration new
    !true # Is global declaration
    !"before"
    ![@formalParameterListForGeneration emptyList]
    !semanticInstructionListForGeneration
  ]
  !""
  ;
end routine ;

#---------------------------------------------------------------------------*
#                                                                           *
#           C O M P I L E    P R O G R A M    R U L E S                     *
#                                                                           *
#---------------------------------------------------------------------------*

local routine compileProgramRules
  ??@analysisContext inAnalysisContext
  ??@programComponentAST inProgramComponentAST
  ?!@semanticDeclarationSortedListForGeneration ioDecoratedDeclarationListForGeneration
  ?!@bool ioHasIndexing
:
  @uint ruleIndex := 0 ;
  foreach [inProgramComponentAST mProgramRules] do
    if mIndexingGrammar->string != "" then
      ioHasIndexing := true ;
      @bool hasIndexing ;
      [inAnalysisContext->mSemanticContext->mGrammarMap searchKey !mIndexingGrammar ?* ?hasIndexing] ;
      if not hasIndexing then
        error mIndexingGrammar : "the '" . mIndexingGrammar . "' grammar does not enable indexing" ;
      end if ;
    end if ;
    @formalParameterListAST argumentList [emptyList] ;
    argumentList +=
      ![@formalArgumentPassingModeAST argumentConstantIn]
      ![@lstring new !"lstring" !here]
      !mSourceFileVariableName
      !mSourceFileVariableNameIsUnused
    ;
    @semanticInstructionListForGeneration semanticInstructionListForGeneration ;
    @formalParameterListForGeneration routineSignature ;
    analyzeRoutineBody
      !inAnalysisContext
      !argumentList
      ![@localConstantList emptyList] # No local constant
      ![@typedAttributeList emptyList] # No attribute (not a method)
      ![@typedAttributeList emptyList] # No attribute (not a method)
      !""
      !mInstructionList
      !mEndOfInstructionList
      ?semanticInstructionListForGeneration
      ?routineSignature
    ;
    ioDecoratedDeclarationListForGeneration += ![@routineImplementationForGeneration new
      !true # Is global declaration
      !"programRule_" . [ruleIndex string]
      !routineSignature
      !semanticInstructionListForGeneration
    ]
    !""
    ;
    ruleIndex ++ ;
  end foreach ;
end routine ;

#---------------------------------------------------------------------------*
#                                                                           *
#           C O M P I L E    ' A F T E R '                                  *
#                                                                           *
#---------------------------------------------------------------------------*

local routine compileAfterClause
  ??@analysisContext inAnalysisContext
  ??@programComponentAST inProgramComponentAST
  ?!@semanticDeclarationSortedListForGeneration ioDecoratedDeclarationListForGeneration
:
#--- Check there is zero or one 'before' declaration
  @semanticInstructionListForGeneration semanticInstructionListForGeneration ;
  if [[inProgramComponentAST mEpilogueList] length] > 1 then
    const @location locationOfSecondItem := [[inProgramComponentAST mEpilogueList] mEndOfInstructionListAtIndex !1] ;
    error locationOfSecondItem: "there are several 'after' declarations" : semanticInstructionListForGeneration ;
  elsif [[inProgramComponentAST mEpilogueList] length] == 0 then
    semanticInstructionListForGeneration := [@semanticInstructionListForGeneration emptyList] ;
  else
    analyzeRoutineBody
      !inAnalysisContext
      ![@formalParameterListAST emptyList] # Routine signature : no argument
      ![@localConstantList emptyList] # No local constant
      ![@typedAttributeList emptyList] # No attribute (not a method)
      ![@typedAttributeList emptyList] # No attribute (not a method)
      !""
      ![[inProgramComponentAST mEpilogueList] mInstructionListAtIndex !0]
      ![[inProgramComponentAST mEpilogueList] mEndOfInstructionListAtIndex !0]
      ?semanticInstructionListForGeneration
      ?*
    ;
  end if ;
#---
  ioDecoratedDeclarationListForGeneration += ![@routineImplementationForGeneration new
    !true # Is global declaration
    !"after"
    ![@formalParameterListForGeneration emptyList]
    !semanticInstructionListForGeneration
  ]
  !""
  ;
end routine ;

#---------------------------------------------------------------------------*
#                                                                           *
#           C O M P I L E    P R O G R A M    C O M P O N E N T             *
#                                                                           *
#---------------------------------------------------------------------------*

routine compileProgramComponentFromAST
  ??@lstring inSourceFile
  ??@string inOutputDirectory
  ??@programComponentAST inProgramComponentAST
  ?!@parsedComponentStruct ioParsedComponentStruct
:
  const @string basename := [[inSourceFile lastPathComponent] stringByDeletingPathExtension] ;
#--- Check component name
  const @lstring componentName := [inProgramComponentAST mProgramComponentName] ;
  if [componentName string] != basename then
    warning componentName: "GALGAS checks "
      "the component name ('" . [componentName string]
    . "') against the source file base name ('" . basename . "'): they should be identical"
    ;
  end if ; 
#--- Recursively parse all imported files
  @semanticDeclarationListAST semanticDeclarationList := [inProgramComponentAST mSemanticDeclarationList] ;
  @stringset parsedFileSet [setWithString ![inSourceFile string]] ;
  @lstringlist optionComponentFileNameList [emptyList] ;
  @grammarComponentASTList grammarComponentASTList [emptyList] ;
  @stringlist headerIncludeList [emptyList] ;
  @stringlist implementationIncludeList [emptyList] ;
  recursivelyImportSemanticComponentAndCheckNoCircularityInImportSemantics
    !inSourceFile
    !?ioParsedComponentStruct
    !?optionComponentFileNameList
    !?grammarComponentASTList
    !?semanticDeclarationList
    !?parsedFileSet
    !?headerIncludeList
    !?implementationIncludeList
    ![inProgramComponentAST mImportedComponentFileNameList]
    ![inSourceFile stringByDeletingLastPathComponent]
  ;
#------ Add predefined types to semantics declarations
  @semanticDeclarationListAST semanticDeclarationListWithPredefinedTypes := semanticDeclarationList ;
  appendPredefinedTypesASTs !?semanticDeclarationListWithPredefinedTypes ;
#------ Build semantic context
  @semanticContext semanticContext ;
  buildSemanticContext
    ![inSourceFile string]
    !semanticDeclarationListWithPredefinedTypes
    ![inProgramComponentAST mEndOfSourceFile]
    ?semanticContext
  ;
#--- Perform semantic analysis of grammar components
  buildGrammarComponentMapForSemanticAnalysis
    !grammarComponentASTList
    !?semanticContext
  ;
#--- Build option component map for analysis
  buildOptionComponentMapForSemanticAnalysis
    !optionComponentFileNameList
    !?ioParsedComponentStruct
    !?semanticContext->mOptionComponentMapForSemanticAnalysis
  ;
#--- Build lexique component map for semantic analysis
  buildLexiqueComponentMapForSemanticAnalysis
    !ioParsedComponentStruct
    ?@lexiqueComponentMapForSemanticAnalysis lexiqueMap
  ;
  semanticContext->mLexiqueComponentMapForSemanticAnalysis := lexiqueMap ;
#------ Semantic Analysis
  @semanticDeclarationSortedListForGeneration decoratedDeclarationListForGeneration ;
  performSemanticAnalysis
    ![inProgramComponentAST mSemanticDeclarationList]
    !semanticContext
    ?decoratedDeclarationListForGeneration
  ;
#--- Analysis context
  const analysisContext :=  [@analysisContext new
    !semanticContext
    !buildPredefinedTypes [!semanticContext]
    ![@unifiedTypeMapProxy null]
    !""
    ![@unifiedTypeMapProxy null]
  ] ;
#--- Analyse 'before' instructions
  compileBeforeClause
    !analysisContext
    !inProgramComponentAST
    !?decoratedDeclarationListForGeneration
  ;
#--- Analyse program rules
  @bool hasIndexing := false ;
  compileProgramRules
    !analysisContext
    !inProgramComponentAST
    !?decoratedDeclarationListForGeneration
    !?hasIndexing
  ;
#--- Analyse 'after' instructions
  compileAfterClause
    !analysisContext
    !inProgramComponentAST
    !?decoratedDeclarationListForGeneration
  ;
#------ Generate Header File
  if [@uint errorCount] == 0 then
    @string headerString := [filewrapper programFileGenerationTemplate.programFileHeader
      ![componentName string]
      !headerIncludeList
      ![semanticContext->mTypeMap allKeyList]
    ] ;
    foreach decoratedDeclarationListForGeneration do
      headerString .= [mDeclaration appendDeclaration1] ;
    end foreach ;
    foreach decoratedDeclarationListForGeneration do
      headerString .= [mDeclaration appendDeclaration2 !inOutputDirectory] ;
    end foreach ;
    [@string generateFileWithPattern
      !inOutputDirectory
      ![componentName string] . ".h"
      !"//"
      !"\n\n" # Defaut user zone1
      !headerString
      !"\n\n" # Defaut user zone2
      !"#endif\n"
    ] ;
  end if ;
#------ Generate Implementation File
  if [@uint errorCount] == 0 then
    @string implementationString := [filewrapper programFileGenerationTemplate.programFileImplementation
      ![componentName string]
      !implementationIncludeList
      ![inProgramComponentAST mProgramRules]
      !hasIndexing
    ] ;
    foreach decoratedDeclarationListForGeneration do
      implementationString .= [mDeclaration appendTypeGenericImplementation] ;
      implementationString .= [mDeclaration appendSpecificImplementation] ;
    end foreach ;
    [@string generateFileWithPattern
      !inOutputDirectory
      ![componentName string] . ".cpp"
      !"//"
      !"\n\n" # Defaut user zone1
      !implementationString
      !"\n\n" # Defaut user zone2
      !"\n"
    ] ;
  end if ;
end routine ;

#---------------------------------------------------------------------------*

routine compileProgramComponentFromSourceFile
  ??@lstring inSourceFile
  ??@string inOutputDirectory
  ?!@parsedComponentStruct ioParsedComponentStruct
:
#--- Parse source file
  const basename := [[inSourceFile lastPathComponent] stringByDeletingPathExtension] ;
  const basenameKey := [@lstring new !basename !here] ;
  @programComponentAST programComponentAST ;
  if [ioParsedComponentStruct->mParsedProgramComponentMap hasKey !basename] then
    [ioParsedComponentStruct->mParsedProgramComponentMap searchKey !basenameKey ?* ?programComponentAST] ;
  else
    grammar programSLRgrammar in inSourceFile ?programComponentAST ;
    [!?ioParsedComponentStruct->mParsedProgramComponentMap insertKey !basenameKey !inSourceFile !programComponentAST] ;
  end if ;
#---
  compileProgramComponentFromAST
    !inSourceFile
    !inOutputDirectory
    !programComponentAST
    !?ioParsedComponentStruct
  ;
end routine ;

#---------------------------------------------------------------------------*

end semantics ;
