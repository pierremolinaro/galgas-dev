#---------------------------------------------------------------------------*
#                                                                           *
#  GALGAS treewalking component parser definition                           *
#                                                                           *
#  Copyright (C) 2007 Pierre Molinaro.                                      *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax treewalking_parser :

import lexique galgas_scanner in "galgas_scanner.ggs" ;

import semantics semantics_semantics in "semantics_semantics.ggs" ;

#-------------------------------------------------------------------*

nonterminal <galgas_component>
  ?!@M_lexiqueComponents ioLexiqueMapForUse
  ?!@M_semanticsComponents ioSemanticsComponentsMap
  ?!@M_syntaxComponents ioSyntaxComponentsMap
  ?!@M_grammarComponents ioGrammarComponentsMap
  ?!@M_optionComponents ioOptionsComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
label importLexique
  ?!@M_lexiqueComponents ioLexiqueMapForUse
label importSyntax
  ?!@M_syntaxComponents ioSyntaxComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
label importSemantics
  ?!@M_semanticsComponents ioSemanticsComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
label importGrammarForSemantics
  ?!@M_grammarComponents ioGrammarComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_optionComponents ioOptionsComponentsMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
label importOptions
  ?!@M_optionComponents ioOptionsComponentsMap
;

nonterminal <parse_treewalking_document>
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
  ??@bool inGenerateTreewalkingCode
label parse
;

nonterminal <parse_semantics_component_for_importing>
  ??@lstring inFileName
  ?!@M_semanticsComponents ioSemanticsMapForUse
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
label parse
;

nonterminal <treewalking_routine>
  ??@M_semanticsEntitiesForUse inSemanticsEntitiesMap
  ??@entityToImplementMap inMetamodelEntityMap
  ??@M_optionComponents inOptionsComponentsMapForUse
  ??@treewalkingRoutineHeaderMap inTreewalkingRoutineHeaderMap
  ?!@treewalkingRoutineToGenerateList ioTreewalkingRoutineToGenerateList
  ??@M_treewalkingComponents inTreewalkingComponentMap
label firstLocalPhase
  ??@M_semanticsEntitiesForUse inSemanticsEntitiesMap
  ?!@treewalkingRoutineHeaderMap ioTreewalkingRoutineHeaderMap
label parse
;

nonterminal <formal_arguments_list>
  ??@M_semanticsEntitiesForUse ioComponentSemanticsEntitiesMap
  !@L_EXsignature listeDeTypes
  ?!@typeVariablesMap localVariableMap
  !@typeListeTypesEtNomsArgMethode listeTypeEtNomsArguments
  ?!@typeTableEnAvant tableEnAvant
  !@L_signature outSignature
label importGrammarForSemantics
  !@L_signature outSignature
label parse
;

nonterminal <semantic_instructions_list>
  ??@treewalkingRoutineHeaderMap inTreewalkingRoutineHeaderMap
  ??@typeTableEnAvant inTableEnAvant
  ??@M_semanticsEntitiesForUse ioComponentSemanticsEntitiesMap
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ??@entityPropertyMap ioPropertyMap
  ??@entityToImplementMap inMetamodelEntityMap
  ??@M_treewalkingComponents inTreewalkingComponentMap
label parse
;

#-------------------------------------------------------------------*
#                   treewalking component                           *
#-------------------------------------------------------------------*

rule <galgas_component>
  ?!@M_lexiqueComponents unused ioLexiqueMapForUse
  ?!@M_semanticsComponents unused ioSemanticsComponentsMap
  ?!@M_syntaxComponents unused ioSyntaxComponentsMap
  ?!@M_grammarComponents unused ioGrammarComponentsMap
  ?!@M_optionComponents unused ioOptionsComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
:
  <parse_treewalking_document>
    !?ioMetamodelComponentMap
    !?ioTreewalkingComponentMap
    !true
  ;
label importLexique
  ?!@M_lexiqueComponents unused ioLexiqueMapForUse
:
  <parse_treewalking_document> parse ;
label importSyntax
  ?!@M_syntaxComponents unused ioSyntaxComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
:
  <parse_treewalking_document>
    !?ioMetamodelComponentMap
    !?ioTreewalkingComponentMap
    !false
  ;
label importSemantics
  ?!@M_semanticsComponents unused ioSemanticsComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
:
  <parse_treewalking_document>
    !?ioMetamodelComponentMap
    !?ioTreewalkingComponentMap
    !false
  ;
label importGrammarForSemantics
  ?!@M_grammarComponents unused ioGrammarComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_optionComponents unused ioOptionsComponentsMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
:
  <parse_treewalking_document>
    !?ioMetamodelComponentMap
    !?ioTreewalkingComponentMap
    !false
  ;
label importOptions
  ?!@M_optionComponents unused ioOptionsComponentsMap
:
  <parse_treewalking_document> parse ;
end rule ;


#-------------------------------------------------------------------*

rule <parse_treewalking_document>
  ?!@M_metamodelsComponents unused ioMetamodelComponentMap
  ?!@M_treewalkingComponents ioTreewalkingComponentMap
  ??@bool inGenerateTreewalkingCode
:
#--- Tree walking component header
  $treewalking$ ;
  @lstring treewalkingName ;
  $identifier$ ? treewalkingName ;
#--- Metamodel to walking trought
  $($ ;
  @lstring metamodelName ;
  $identifier$ ? metamodelName ;
  $)$ ;
  $:$ ;
  @stringset filesToImportInHeaderSet [setWithString ![metamodelName string]] ;
#--- Imported Metamodel
  @M_semanticsComponents semanticsComponentMap [emptyMap] ;
  @M_metamodelsComponents metamodelComponentMap [emptyMap] ;
  @M_treewalkingComponents treewalkingComponentMap [emptyMap] ;
  $import$ ;
  @lstring fileName ;
  $literal_string$ ? fileName ;
  $;$ ;  
  <parse_semantics_component_for_importing>
    !fileName
    !?semanticsComponentMap
    !?metamodelComponentMap
    !?treewalkingComponentMap
  ;
  @lstring metamodelRootEntity ;
  @entityToImplementMap metamodelEntityMap ;
  @stringset importedMetamodels ;
  if [metamodelComponentMap hasKey ![metamodelName string]] then
    [metamodelComponentMap searchKey
       !metamodelName
       ?metamodelRootEntity
       ?metamodelEntityMap
       ?importedMetamodels
    ] ;
    @stringset superMetamodels [emptySet] ;
    foreach importedMetamodels (@string kSuperMetamodelName) :
      performMetamodelInclusion
        !metamodelComponentMap
        ![@lstring new !kSuperMetamodelName !here]
        !?superMetamodels
        !?metamodelEntityMap
      ;
    end foreach ;
  else
    error fileName: "this file does not define the '" . [metamodelName string] . "' metamodel"
     -> metamodelRootEntity, metamodelEntityMap, importedMetamodels ;
  end if ;
#--- Import semantics component(s)
  @M_optionComponents optionsComponentsMapForUse [emptyMap] ;
  @M_semanticsEntitiesForUse semanticsEntitiesMap ;
  enterBuiltinTypes ?semanticsEntitiesMap ;
  repeat
  while
    $import$ ;
    $semantics$ ;
    @lstring semanticsComponentName ;
    $identifier$ ? semanticsComponentName ;
    filesToImportInHeaderSet += ![semanticsComponentName string] ;
    $in$ ;    
    @lstring fileName ;
    $literal_string$ ? fileName ;
    $;$ ;  
    <parse_semantics_component_for_importing>
      !fileName
      !?semanticsComponentMap
      !?metamodelComponentMap
      !?treewalkingComponentMap
    ;
    @M_semanticsEntitiesForUse importedSemanticsEntitiesMap ;
    [semanticsComponentMap searchKey !semanticsComponentName ?6* ?importedSemanticsEntitiesMap] ;
    foreach importedSemanticsEntitiesMap (@lstring kKey @AC_semanticsEntity kEntity) :
      if not [semanticsEntitiesMap hasKey ![kKey string]] then
        [!?semanticsEntitiesMap insertKey !kKey  !kEntity] ;
      end if ;
    end foreach ;
  end repeat ;
#---------------------- parse routine definitions
#--- FIRST PASS
  @treewalkingRoutineToGenerateList treewalkingRoutineToGenerateList [emptyList] ;
  @treewalkingRoutineHeaderMap treewalkingRoutineHeaderMap [emptyMap] ;
  @routineDispatcherSortedList routineDispatcherSortedList [emptySortedList] ;
  parse do
#--- FIRST PASS
    repeat
    while
      <treewalking_routine> firstLocalPhase
        !semanticsEntitiesMap
        !?treewalkingRoutineHeaderMap
      ;
    end repeat ;
  rewind
#--- SECOND PASS
    repeat
    while
      <treewalking_routine>
        !semanticsEntitiesMap
        !metamodelEntityMap
        !optionsComponentsMapForUse
        !treewalkingRoutineHeaderMap
        !?treewalkingRoutineToGenerateList
        !ioTreewalkingComponentMap
      ;
    end repeat ;
  end parse ;
#--- Get Root routine signature
  @L_EXsignature rootRoutineSignature ;
  @typeListeTypesEtNomsArgMethode rootRoutineArgumentList ;
  if [treewalkingRoutineHeaderMap hasKey ![metamodelRootEntity string]] then
    [treewalkingRoutineHeaderMap searchKey !metamodelRootEntity ?rootRoutineSignature ?rootRoutineArgumentList] ;
  else
    error here:"a routine should be defined for the '@" . [metamodelRootEntity string] . "' root class"
    -> rootRoutineSignature, rootRoutineArgumentList ;
  end if ;
#--- Generate code, and additional checkings ?
  if inGenerateTreewalkingCode then
  #--- check if a class defines a routine, a routine defined by a super class 
  #    should have the same signature
    foreach treewalkingRoutineHeaderMap (@lstring kKey @L_EXsignature kRoutineSignature *) :
      @uint entityID ;
      @string metamodelName ;
      [metamodelEntityMap searchKey !kKey ?6* ?metamodelName ?entityID] ;
      routineDispatcherSortedList += 
        ![entityID string]
        !entityID
        ![kKey string]
        ![kKey string]
        !metamodelName
      ;
      @lstring entityName := kKey ;
      loop [metamodelEntityMap count] : while [entityName string] != "" do
        @lstring superEntityName ;
        [metamodelEntityMap searchKey !entityName ?3* ?superEntityName ?4*] ;
        if [treewalkingRoutineHeaderMap hasKey ![superEntityName string]] then
          @L_EXsignature superRoutineSignature ;
          [treewalkingRoutineHeaderMap searchKey !superEntityName ?superRoutineSignature ?*] ;
          verifierCompatibiliteSignatures
            !kRoutineSignature
            !superRoutineSignature
            ![kKey location]
          ;
        end if ;
        entityName  := superEntityName ;
      end loop ;
    end foreach ;
  #--- If a class does not define a routine, use the super class 
  #    routine (if it exists)
    foreach metamodelEntityMap (@lstring kEntityName 3* @lstring kSuperEntityName 4*) :
      @bool hasRoutine := [treewalkingRoutineHeaderMap hasKey ![kEntityName string]] ;
      if not hasRoutine then
        @lstring superEntityName := kSuperEntityName ;
        loop [treewalkingRoutineHeaderMap count] : while (not hasRoutine) & ([superEntityName string] != "") do
          hasRoutine := [treewalkingRoutineHeaderMap hasKey ![superEntityName string]] ;
          if not hasRoutine then
            @lstring superSuperEntityName ;
            [metamodelEntityMap searchKey !superEntityName ?3* ?superSuperEntityName ?4*] ;
            superEntityName := superSuperEntityName ;
          end if ;
        end loop ;
        if hasRoutine then
          @uint entityID ;
          @string metamodelName ;
          [metamodelEntityMap searchKey !kEntityName ?6* ?metamodelName ?entityID] ;
          routineDispatcherSortedList += 
            ![entityID string]
            !entityID
            ![superEntityName string]
            ![kEntityName string]
            !metamodelName
          ;
        end if ;
      end if ;
    end foreach ;
  #--- Generate code
    generate_treewalking
      !treewalkingName
      !filesToImportInHeaderSet
      !treewalkingRoutineToGenerateList
      !routineDispatcherSortedList
      !rootRoutineArgumentList
      !metamodelRootEntity
    ;
  end if ;
#--- Add to treewalking component map
  if not [ioTreewalkingComponentMap hasKey ![treewalkingName string]] then
    [!?ioTreewalkingComponentMap insertKey
      !treewalkingName
      ![metamodelRootEntity string]
      !rootRoutineSignature
    ] ;
  end if ;
#--- End of treewalking component
  $end$ ;
  $treewalking$ ;
  $;$ ;
end rule ;

#-------------------------------------------------------------------*

rule <treewalking_routine>
  ??@M_semanticsEntitiesForUse inSemanticsEntitiesMap
  ??@entityToImplementMap inMetamodelEntityMap
  ??@M_optionComponents inOptionsComponentsMapForUse
  ??@treewalkingRoutineHeaderMap inTreewalkingRoutineHeaderMap
  ?!@treewalkingRoutineToGenerateList ioTreewalkingRoutineToGenerateList
  ??@M_treewalkingComponents inTreewalkingComponentMap
:
  $rule$ ;
  @lstring metamodelClassName ;
  $type_name$ ? metamodelClassName ;
  @entityPropertyMap allMetamodelPropertyMap ;
  [inMetamodelEntityMap searchKey !metamodelClassName ?allMetamodelPropertyMap ?7*] ;
#---- Formal arguments -----------------------------------------
  @typeTableEnAvant tableEnAvant [emptyMap] ;
  @L_EXsignature listeDeTypes ;
  @typeVariablesMap localVariableMap [emptyMap] ;
  @typeListeTypesEtNomsArgMethode formalArgumentList ;
  @L_signature signatureForGrammarComponent ;
  <formal_arguments_list>
    !inSemanticsEntitiesMap
    ?listeDeTypes
    !?localVariableMap
    ?formalArgumentList
    !?tableEnAvant
    ?signatureForGrammarComponent
  ;
#--- Add metamodel attributes as local constants
  foreach allMetamodelPropertyMap (@lstring kKey @AC_galgasType kType) :
    extract kType :
    when @typeGalgas_singleReferenceEntity (*) ->
    when @typeGalgas_multipleReferenceEntity (3*) ->
    else
      @typeCplusPlusName cppName := [@typeCurrentObjectName new ![kKey location] ![kKey string]] ;
      [!?localVariableMap insertUsedConstInArgument !kKey !kType !cppName] ;
    end extract ;
  end foreach ;
#--- Instruction list
  $:$ ;
  @typeInstructionList instructionList [emptyList] ;
  <semantic_instructions_list>
    !inTreewalkingRoutineHeaderMap
    !tableEnAvant
    !inSemanticsEntitiesMap
    !inOptionsComponentsMapForUse
    !?localVariableMap
    !?instructionList
    !allMetamodelPropertyMap
    !inMetamodelEntityMap
    !inTreewalkingComponentMap
  ;
#---
  ioTreewalkingRoutineToGenerateList +=
    !metamodelClassName
    !formalArgumentList
    !instructionList
  ;
#--- End of rule
  $end$ ;
  $rule$ ;
  $;$ ;
label firstLocalPhase
  ??@M_semanticsEntitiesForUse inSemanticsEntitiesMap
  ?!@treewalkingRoutineHeaderMap ioTreewalkingRoutineHeaderMap
:
  $rule$ ;
  @lstring metamodelClassName ;
  $type_name$ ? metamodelClassName ;
#---- Formal arguments -----------------------------------------
  @typeTableEnAvant tableEnAvant [emptyMap] ;
  @L_EXsignature listeDeTypes ;
  @typeVariablesMap localVariableMap [emptyMap] ;
  @typeListeTypesEtNomsArgMethode formalArgumentList ;
  @L_signature signatureForGrammarComponent ;
  <formal_arguments_list>
    !inSemanticsEntitiesMap
    ?listeDeTypes
    !?localVariableMap
    ?formalArgumentList
    !?tableEnAvant
    ?signatureForGrammarComponent
  ;
  [!?ioTreewalkingRoutineHeaderMap insertKey !metamodelClassName !listeDeTypes !formalArgumentList] ;
#--- Instruction list
  $:$ ;
  <semantic_instructions_list> parse ;
#---
  $end$ ;
  $rule$ ;
  $;$ ;
end rule ;

#-------------------------------------------------------------------*

#rule <file>:
#       $a$               

#-------------------------------------------------------------------*

end syntax ;
