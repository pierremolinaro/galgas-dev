#---------------------------------------------------------------------------*
#                                                                           *
#  Generate predefined types                                                *
#                                                                           *
#  Copyright (C) 2010, ..., 2010 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

semantics semanticInstructionGeneration :
  import "semanticExpressionGeneration.gSemantics" ;

#---------------------------------------------------------------------------*

abstract method @actualParameterForGeneration generateActualParameter
  ?!@uint ioTemporaryVariableIndex
  ?!@string ioImplementation
  ?!@stringlist ioJokerParametersToReleaseList
  ?!@stringlist ioOutputVariableList
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
;

#---------------------------------------------------------------------------*

override method @outputActualParameterForGeneration generateActualParameter
  ?!@uint ioTemporaryVariableIndex
  ?!@string ioImplementation
  ?!@stringlist unused ioJokerParametersToReleaseList
  ?!@stringlist unused ioOutputVariableList
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  [mOutputActualParameterExpression generateExpression !?ioImplementation !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?outCppName] ;
end method ;

#---------------------------------------------------------------------------*

override method @outputInputActualParameterForGeneration generateActualParameter
  ?!@uint unused ioTemporaryVariableIndex
  ?!@string unused ioImplementation
  ?!@stringlist unused ioJokerParametersToReleaseList
  ?!@stringlist unused ioOutputVariableList
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  outCppName := mOutputInputVariableCppName ;
  foreach mStructAttributeList do
    outCppName .= ".mAttribute_" . [mValue identifierRepresentation] ;
  end foreach ;
  [!?ioUnusedVariableCppNameSet removeKey !mOutputInputVariableCppName] ;
end method ;

#---------------------------------------------------------------------------*

override method @inputActualParameterForGeneration generateActualParameter
  ?!@uint unused ioTemporaryVariableIndex
  ?!@string unused ioImplementation
  ?!@stringlist unused ioJokerParametersToReleaseList
  ?!@stringlist ioOutputVariableList
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppName
:
  outCppName := mInputActualCppName ;
  ioOutputVariableList += !mInputActualCppName ;
  [!?ioUnusedVariableCppNameSet removeKey !outCppName] ;
end method ;

#---------------------------------------------------------------------------*

override method @inputJokerParameterForGeneration generateActualParameter
  ?!@uint unused ioTemporaryVariableIndex
  ?!@string ioImplementation
  ?!@stringlist ioJokerParametersToReleaseList
  ?!@stringlist ioOutputVariableList
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppName
:
  outCppName := mInputActualCppName ;
  ioImplementation .= "GALGAS_" . [mFormalArgumentType identifierRepresentation] . " " . outCppName . " ; // Joker input parameter\n" ;
  ioJokerParametersToReleaseList += !mInputActualCppName ;
  ioOutputVariableList += !mInputActualCppName ;
end method ;

#---------------------------------------------------------------------------*
#                                                                           *
#           I N S T R U C T I O N    G E N E R A T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

abstract method @semanticInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
;

#---------------------------------------------------------------------------*

function generateRoutineBody
  ??@bool inGenerateCheckValid
  ??@semanticInstructionListForGeneration inInstructionList
  ->@string outGeneratedCode
:
  @stringset unusedVariableCppNameSet [emptySet] ;
  outGeneratedCode := "" ;
  @uint temporaryVariableIndex := 0 ;
  [!?outGeneratedCode incIndentation !2] ;
  foreach inInstructionList do
    [mInstruction generateInstruction !inGenerateCheckValid !?temporaryVariableIndex !?unusedVariableCppNameSet !?outGeneratedCode] ;
  end foreach ;
  [!?outGeneratedCode decIndentation !2] ;
end function ;

#---------------------------------------------------------------------------*

routine generateInstructionList
  ??@bool inGenerateCheckValid
  ??@semanticInstructionListForGeneration inInstructionList
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  [!?ioGeneratedCode incIndentation !2] ;
  foreach inInstructionList do
    [mInstruction generateInstruction !inGenerateCheckValid !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet !?ioGeneratedCode] ;
  end foreach ;
  [!?ioGeneratedCode decIndentation !2] ;
end routine ;

#---------------------------------------------------------------------------*

override method @localVariableOrConstantDeclarationWithSourceExpressionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  @string sourceVar ;
  [mSourceExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?sourceVar] ;
  ioGeneratedCode .= "GALGAS_" . [mTargetType identifierRepresentation] . " " . mCppVariableName
                    . " = " . sourceVar . " ;\n" ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    ioGeneratedCode .= "  " . mCppVariableName . ".checkIsValid (HERE) ;\n" ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @localVariableDeclarationForGeneration generateInstruction
  ??@bool unused inGenerateCheckValid
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  ioGeneratedCode .= "GALGAS_" . [mVariableType identifierRepresentation] . " " . mCppVariableName . " ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @localVariableOrConstantDeclarationWithDefaultConstructorForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  ioGeneratedCode .= "GALGAS_" . [mTargetType identifierRepresentation]
                  . " " . mTargetVariableCppName
                  . " = GALGAS_"  . [mTargetType identifierRepresentation]
                  . "::constructor_"
                  . [[mTargetType mDefaultConstructorName] identifierRepresentation]
                  . " (SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    ioGeneratedCode .= "  " . mTargetVariableCppName . ".checkIsValid (HERE) ;\n" ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @localVariableOrConstantDeclarationWithConstructorCallForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  @stringlist parameterList [emptyList] ;
  foreach mConstructorArguments do
    @string parameter ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?parameter] ;
    parameterList += !parameter ;  
  end foreach ;
  ioGeneratedCode .= "GALGAS_" . [mTargetType identifierRepresentation]
                   . " " . mTargetVariableCppName
                   . " = GALGAS_"  . [mTargetType identifierRepresentation]
                   . "::constructor_" . [mConstructorName identifierRepresentation]
                   . " (" ;
  foreach parameterList
    do ioGeneratedCode .= mValue ;
    between ioGeneratedCode .= ", " ;
  end foreach ;
  @bool needsComma := [parameterList length] > 0 ;
  if mHasCompilerArgument then
    if needsComma then
      ioGeneratedCode .= ", " ;
    end if ;
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    ioGeneratedCode .= compilerCppName [] ;
    needsComma := true ;
  end if ;
  if needsComma then
    ioGeneratedCode .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
  else
    ioGeneratedCode .= "SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
  end if ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    ioGeneratedCode .= "  " . mTargetVariableCppName . ".checkIsValid (HERE) ;\n" ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @assignmentInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  @string sourceVar ;
  [mSourceExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?sourceVar] ;
  [!?ioUnusedVariableCppNameSet removeKey !mNameForCheckingFormalParameterUsing] ;
#---
  @string receiverCppName := mTargetCppName ;
  foreach mStructAttributeList do
    receiverCppName .= ".mAttribute_" . [[mValue string] identifierRepresentation] ;
  end foreach ;
  ioGeneratedCode .= receiverCppName . " = " . sourceVar . " ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !mTargetCppName] ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    ioGeneratedCode .= "  " . receiverCppName . ".checkIsValid (HERE) ;\n" ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @dropInstructionForGeneration generateInstruction
  ??@bool unused inGenerateCheckValid
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  foreach mDropList do
    ioGeneratedCode .= mValue . ".drop () ; // " . mComment . "\n" ;
  end foreach ;
end method ;

#---------------------------------------------------------------------------*

override method @concatInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  @string sourceVar ;
  [mSourceExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?sourceVar] ;
  [!?ioUnusedVariableCppNameSet removeKey !mNameForCheckingFormalParameterUsing] ;
  @string targetCppName := mTargetVariableCppName ;
  foreach mStructAttributeList do
    targetCppName .= ".mAttribute_" . mValue ;
  end foreach ;
  ioGeneratedCode .= targetCppName . ".dotAssign_operation (" . sourceVar
                  . " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !mTargetVariableCppName] ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    ioGeneratedCode .= "  " . targetCppName . ".checkIsValid (HERE) ;\n" ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @grammarInstructionWithSourceFileForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  ioGeneratedCode .= "{\n" ;
#--- Source string
  [mSourceFileExpression generateExpression
    !?ioGeneratedCode
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    ?@string sourceVar
  ] ;
#--- Parameters
  @stringlist parameterCppNameList [emptyList] ;
  @stringlist jokerParametersToReleaseList [emptyList] ;
  @stringlist inputVariableList [emptyList] ;
  foreach mActualParameterList do
    @string parameterCppName ;
    [mActualParameter generateActualParameter
      !?ioTemporaryVariableIndex
      !?ioGeneratedCode
      !?jokerParametersToReleaseList
      !?inputVariableList
      !?ioUnusedVariableCppNameSet
      ?parameterCppName
    ] ;
    parameterCppNameList += !parameterCppName ;
  end foreach ;
  ioGeneratedCode .= "cGrammar_" . [mGrammarName identifierRepresentation]
                  .  "::_performSourceFileParsing_" . [mLabelName identifierRepresentation] 
                  .  " (" . compilerCppName [] . ", " . sourceVar ;
  foreach parameterCppNameList do
    ioGeneratedCode .= ", " . mValue ;
  end foreach ;
  ioGeneratedCode .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  ioGeneratedCode .= "}\n" ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    foreach inputVariableList do
      ioGeneratedCode .= "  " . mValue . ".checkIsValid (HERE) ;\n" ;
    end foreach ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @grammarInstructionWithSourceExpressionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  ioGeneratedCode .= "{\n" ;
#--- Source string
  @string sourceVar ;
  [mSourceStringExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?sourceVar] ;
#--- Parameters
  @stringlist parameterCppNameList [emptyList] ;
  @stringlist jokerParametersToReleaseList [emptyList] ;
  @stringlist inputVariableList [emptyList] ;
  foreach mActualParameterList do
    @string parameterCppName ;
    [mActualParameter generateActualParameter !?ioTemporaryVariableIndex !?ioGeneratedCode !?jokerParametersToReleaseList !?inputVariableList !?ioUnusedVariableCppNameSet ?parameterCppName] ;
    parameterCppNameList += !parameterCppName ;
  end foreach ;
  ioGeneratedCode .= "cGrammar_" . [mGrammarName identifierRepresentation]
                  .  "::_performSourceStringParsing_" . [mLabelName identifierRepresentation] 
                  .  " (" . compilerCppName [] . ", " . sourceVar ;
  foreach parameterCppNameList do
    ioGeneratedCode .= ", " . mValue ;
  end foreach ;
  ioGeneratedCode .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    foreach inputVariableList do
      ioGeneratedCode .= "  " . mValue . ".checkIsValid (HERE) ;\n" ;
    end foreach ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @ifInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Generate test expression
  @string cppVarName ;
  [mIFexpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?cppVarName] ;
  const @string testVar := "test_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
  ioGeneratedCode .= "const enumGalgasBool " . testVar . " = " . cppVarName . ".boolEnum () ;\n" ;
#--- If test expression is not valuated or false, execute 'else' part
  ioGeneratedCode .= "if (kBoolTrue == " . testVar . ") {\n" ;
  generateInstructionList
    !inGenerateCheckValid
    !m_then_instructionList
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !?ioGeneratedCode
  ;
  if [m_else_instructionList length] > 0 then
    ioGeneratedCode .= "}else if (kBoolFalse == " . testVar . ") {\n" ;
    generateInstructionList
      !inGenerateCheckValid
      !m_else_instructionList
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;
  end if ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @errorInstructionForGeneration generateInstruction
  ??@bool unused inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Receiver expression
  @string receiverCppVarName ;
  [mReceiverExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?receiverCppVarName] ;
#--- Receiver expression
  @string messageCppVarName ;
  [mErrorExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?messageCppVarName] ;
#--- Use location reader ?
  if mUseLocationReader then
    const @string locationVar := "location_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
    ioGeneratedCode .= "GALGAS_location " . locationVar . " (" . receiverCppVarName . ".reader_location (HERE)) ; // Implicit use of 'location' reader\n" ;
    receiverCppVarName := locationVar ;
  end if ;
#--- Generate error instruction
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  ioGeneratedCode .= compilerCppName [] . "->emitSemanticError (" . receiverCppVarName . ", " . messageCppVarName
                   . " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
#--- Release dropped variables
  foreach mBuiltVariableCppNameList do
    [!?ioUnusedVariableCppNameSet removeKey !mValue] ;
    ioGeneratedCode .= mValue . ".drop () ; // Release error dropped variable\n" ;
  end foreach ;
end method ;

#---------------------------------------------------------------------------*

override method @warningInstructionForGeneration generateInstruction
  ??@bool unused inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Receiver expression
  @string receiverCppVarName ;
  [mReceiverExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?receiverCppVarName] ;
#--- Receiver expression
  @string messageCppVarName ;
  [mWarningExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?messageCppVarName] ;
#--- Use location reader ?
  if mUseLocationReader then
    const @string locationVar := "location_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
    ioGeneratedCode .= "GALGAS_location " . locationVar . " (" . receiverCppVarName . ".reader_location (HERE)) ; // Implicit use of 'location' reader\n" ;
    receiverCppVarName := locationVar ;
  end if ;
#--- Generate warning instruction
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  ioGeneratedCode .= compilerCppName [] . "->emitSemanticWarning (" . receiverCppVarName . ", " . messageCppVarName
                  . " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @methodCallInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Receiver expression
  @string receiverCppName ;
  [mReceiverExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?receiverCppName] ;
  [!?ioUnusedVariableCppNameSet removeKey !receiverCppName] ;
#---
  @stringlist parameterCppNameList [emptyList] ;
  @stringlist jokerParametersToReleaseList [emptyList] ;
  @stringlist inputVariableList [emptyList] ;
  foreach mActualParameterList do
    @string parameterCppName ;
    [mActualParameter generateActualParameter !?ioTemporaryVariableIndex !?ioGeneratedCode !?jokerParametersToReleaseList !?inputVariableList !?ioUnusedVariableCppNameSet ?parameterCppName] ;
    parameterCppNameList += !parameterCppName ;
  end foreach ;
#--- Generate method call
  switch mKind
  when definedAsCategory :
    if [mReceiverType mTypeKindEnum] == [@typeKindEnum classType] then
      ioGeneratedCode .= "callCategoryMethod_" . [mMethodName identifierRepresentation] . " ((const cPtr_" . [[mReceiverExpression mType] identifierRepresentation] . " *) " . receiverCppName . ".ptr (), " ;
    else
      ioGeneratedCode .= "categoryMethod_" . [mMethodName identifierRepresentation] . " (" . receiverCppName . ", " ;
    end if ;
  when definedAsMember :
    ioGeneratedCode .= receiverCppName . ".method_" . [mMethodName identifierRepresentation] . " (" ;
  end switch ;
  foreach parameterCppNameList
    do ioGeneratedCode .= mValue ;
    between ioGeneratedCode .= ", " ;
  end foreach ;
  if [parameterCppNameList length] == 0 then
    if mHasCompilerArgument then
      [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
      ioGeneratedCode .= compilerCppName [] ;
      ioGeneratedCode .= " COMMA_" ;
    end if ;
  elsif mHasCompilerArgument then
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    ioGeneratedCode .= ", " . compilerCppName [] ;
    ioGeneratedCode .= " COMMA_" ;
  else
    ioGeneratedCode .= " COMMA_" ;
  end if ;
  ioGeneratedCode .= "SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    foreach inputVariableList do
      ioGeneratedCode .= "  " . mValue . ".checkIsValid (HERE) ;\n" ;
    end foreach ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @routineCallInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  ioGeneratedCode .= "{\n" ;
#--- Parameters
  @stringlist parameterCppNameList [emptyList] ;
  @stringlist jokerParametersToReleaseList [emptyList] ;
  @stringlist inputVariableList [emptyList] ;
  foreach mActualParameterList do
    @string parameterCppName ;
    [mActualParameter generateActualParameter !?ioTemporaryVariableIndex !?ioGeneratedCode !?jokerParametersToReleaseList !?inputVariableList !?ioUnusedVariableCppNameSet ?parameterCppName] ;
    parameterCppNameList += !parameterCppName ;
  end foreach ;
#--- Generate method call
  ioGeneratedCode .= "routine_" . [[mRoutineName string] identifierRepresentation] . " (" ;
  foreach parameterCppNameList do
    ioGeneratedCode .= mValue . ", " ;
  end foreach ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  ioGeneratedCode .=  compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[[mRoutineName location] line] string] . ")) ;\n" ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    foreach inputVariableList do
      ioGeneratedCode .= "  " . mValue . ".checkIsValid (HERE) ;\n" ;
    end foreach ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @modifierCallInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  ioGeneratedCode .= "{\n" ;
  @string receiverCppName := mReceiverCppName ;
  foreach mReceiverStructAttributes do
    receiverCppName .= ".mAttribute_" . [[mValue string] identifierRepresentation] ;
  end foreach ;
  [!?ioUnusedVariableCppNameSet removeKey !mReceiverBaseName] ;
#  ioGeneratedCode .= "  //Receiver '" . mReceiverBaseName . "'\n" ;
#--- Parameters
  @stringlist parameterCppNameList [emptyList] ;
  @stringlist jokerParametersToReleaseList [emptyList] ;
  @stringlist inputVariableList [emptyList] ;
  foreach mActualParameterList do
    @string parameterCppName ;
    [mActualParameter generateActualParameter
      !?ioTemporaryVariableIndex
      !?ioGeneratedCode
      !?jokerParametersToReleaseList
      !?inputVariableList
      !?ioUnusedVariableCppNameSet
      ?parameterCppName
    ] ;
    parameterCppNameList += !parameterCppName ;
  end foreach ;
#--- Generate method call
  switch mKind
  when definedAsCategory :
    if [mReceiverType mTypeKindEnum] == [@typeKindEnum classType] then
      if ([[mReceiverType mAllTypedAttributeList] length] > 0) & [mReceiverType mIsConcrete] then
        ioGeneratedCode .= receiverCppName . ".insulate (HERE) ;\n" ;
      end if ;
      ioGeneratedCode .= "callCategoryModifier_" . [mModifierName identifierRepresentation]
                      . " ((cPtr_" . [mReceiverType identifierRepresentation] . " *) " . receiverCppName . ".ptr (), " ;
    else
      ioGeneratedCode .= "categoryModifier_" . [mModifierName identifierRepresentation] . " (" . receiverCppName . ", " ;
    end if ;
  when definedAsMember :
    ioGeneratedCode .= receiverCppName . ".modifier_" . [[mModifierName string] identifierRepresentation] . " (" ;
  end switch ;
  foreach parameterCppNameList
    do ioGeneratedCode .= mValue ;
    between ioGeneratedCode .= ", " ;
  end foreach ;
  if mHasCompilerArgument then
    if [parameterCppNameList length] != 0 then
      ioGeneratedCode .=  ", " ;
    end if ;
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    ioGeneratedCode .=  compilerCppName [] ;
    ioGeneratedCode .= " COMMA_" ;
  elsif [parameterCppNameList length] != 0 then
    ioGeneratedCode .= " COMMA_" ;
  end if ;
  ioGeneratedCode .= "SOURCE_FILE_AT_LINE (" . [[[mModifierName location] line] string] . ")) ;\n" ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    foreach inputVariableList do
      ioGeneratedCode .= "  " . mValue . ".checkIsValid (HERE) ;\n" ;
    end foreach ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @selfModifierCallInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  ioGeneratedCode .= "{\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !mSelfCppName] ;
#--- Parameters
  @stringlist parameterCppNameList [emptyList] ;
  @stringlist jokerParametersToReleaseList [emptyList] ;
  @stringlist inputVariableList [emptyList] ;
  foreach mActualParameterList do
    @string parameterCppName ;
    [mActualParameter generateActualParameter !?ioTemporaryVariableIndex !?ioGeneratedCode !?jokerParametersToReleaseList !?inputVariableList !?ioUnusedVariableCppNameSet ?parameterCppName] ;
    parameterCppNameList += !parameterCppName ;
  end foreach ;
#--- Generate modifier call
  switch mKind
  when definedAsCategory :
    if [mReceiverType mTypeKindEnum] == [@typeKindEnum classType] then
      ioGeneratedCode .= "callCategoryModifier_" . [mModifierName identifierRepresentation] . " (" . mSelfCppName . ", " ;
    else
      ioGeneratedCode .= "categoryModifier_" . [mModifierName identifierRepresentation] . " (" . mSelfCppName . ", " ;
    end if ;
  when definedAsMember :
    if [mReceiverType mTypeKindEnum] == [@typeKindEnum classType] then
      ioGeneratedCode .= mSelfCppName . "->modifier_" . [mModifierName identifierRepresentation] . " (" ;
    else
      ioGeneratedCode .= mSelfCppName . ".modifier_" . [mModifierName identifierRepresentation] . " (" ;
    end if ;
  end switch ;
  foreach parameterCppNameList
    do ioGeneratedCode .= mValue ;
    between ioGeneratedCode .= ", " ;
  end foreach ;
  if mHasCompilerArgument then
    if [parameterCppNameList length] != 0 then
      ioGeneratedCode .=  ", " ;
    end if ;
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    ioGeneratedCode .=  compilerCppName [] ;
    ioGeneratedCode .= " COMMA_" ;
  elsif [parameterCppNameList length] != 0 then
    ioGeneratedCode .= " COMMA_" ;
  end if ;
  ioGeneratedCode .= "SOURCE_FILE_AT_LINE (" . [[[mModifierName location] line] string] . ")) ;\n" ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    foreach inputVariableList do
      ioGeneratedCode .= "  " . mValue . ".checkIsValid (HERE) ;\n" ;
    end foreach ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @typeMethodInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  ioGeneratedCode .= "{\n" ;
#--- Parameters
  @stringlist parameterCppNameList [emptyList] ;
  @stringlist jokerParametersToReleaseList [emptyList] ;
  @stringlist inputVariableList [emptyList] ;
  foreach mActualParameterList do
    @string parameterCppName ;
    [mActualParameter generateActualParameter !?ioTemporaryVariableIndex !?ioGeneratedCode !?jokerParametersToReleaseList !?inputVariableList !?ioUnusedVariableCppNameSet ?parameterCppName] ;
    parameterCppNameList += !parameterCppName ;
  end foreach ;
#--- Generate method call
  ioGeneratedCode .= "GALGAS_" . [[mTypeName string] identifierRepresentation]
                  . "::class_method_" . [[mMethodName string] identifierRepresentation] . " (" ;
  foreach parameterCppNameList
    do ioGeneratedCode .= mValue ;
    between ioGeneratedCode .= ", " ;
  end foreach ;
  if mHasCompilerArgument then
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    if [parameterCppNameList length] > 0 then
      ioGeneratedCode .= ", " ;
    end if ;
    ioGeneratedCode .=  compilerCppName [] ;
  end if ;
  if mHasCompilerArgument | ([parameterCppNameList length] > 0) then
    ioGeneratedCode .= " COMMA_" ;
  end if ;
  ioGeneratedCode .= "SOURCE_FILE_AT_LINE (" . [[[mMethodName location] line] string] . ")) ;\n" ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    foreach inputVariableList do
      ioGeneratedCode .= "  " . mValue . ".checkIsValid (HERE) ;\n" ;
    end foreach ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @structuredCastInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Cast expression
  [mExpression generateExpression
    !?ioGeneratedCode
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    ?@string castCppVarName
  ] ;
#---
  ioGeneratedCode .= "if (" . castCppVarName . ".isValid ()) {\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
  foreach mCastBranchList do
    switch mTypeComparisonKind
    when equal :
      ioGeneratedCode .= "if (" . castCppVarName
      . ".dynamicTypeDescriptor () == & kTypeDescriptor_GALGAS_"
      . [mType identifierRepresentation] . ") {\n" ;
    when inherited :
      ioGeneratedCode .= "if (NULL != dynamic_cast <const cPtr_"
      . [mType identifierRepresentation]
      . " *> (" . castCppVarName . ".ptr ())) {\n" ;
    when strictlyInherited :
      ioGeneratedCode .= "if ((" . castCppVarName
      . ".dynamicTypeDescriptor () != & kTypeDescriptor_GALGAS_"
      . [mType identifierRepresentation] . ") && (NULL != dynamic_cast <const cPtr_"
      . [mType identifierRepresentation]
      . " *> (" . castCppVarName . ".ptr ()))) {\n" ;
    end switch ;
    if [mCastedVarCppName length] > 0 then
      ioGeneratedCode .=
        "  GALGAS_" . [mType identifierRepresentation]
      . " " . mCastedVarCppName . " ((cPtr_" . [mType identifierRepresentation]
      . " *) " . castCppVarName . ".ptr ()) ;\n" ;
    end if ;
    generateInstructionList
      !inGenerateCheckValid
      !mInstructionList
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;
  between ioGeneratedCode .= "}else " ;
  end foreach ;
#--- 'else' branch
  if [mElseInstructionList length] > 0 then
    ioGeneratedCode .= "}else{\n" ;
    generateInstructionList
      !inGenerateCheckValid
      !mElseInstructionList
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;  
  end if ;
#---
  ioGeneratedCode .= "}\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @plusEqualnstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Parameters
  @stringlist parameterList [emptyList] ;
  foreach mExpressionList do
    @string parameter ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?parameter] ;
    parameterList += !parameter ;  
  end foreach ;
#--- Receiver
  [!?ioUnusedVariableCppNameSet removeKey !mNameForCheckingFormalParameterUsing] ;
  @string receiverCppName := mReceiverCppName ;
  foreach mStructAttributeList do
    receiverCppName .= ".mAttribute_" . [[mValue string] identifierRepresentation] ;
  end foreach ;
#--- Call add assign (+=) operator
  ioGeneratedCode .= receiverCppName . ".addAssign_operation (" ;
  [!?ioUnusedVariableCppNameSet removeKey !receiverCppName] ;
  foreach parameterList
    do ioGeneratedCode .= mValue ;
    between ioGeneratedCode .= ", " ;
  end foreach ;
  ioGeneratedCode .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n"  ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    ioGeneratedCode .= "  " . receiverCppName . ".checkIsValid (HERE) ;\n" ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @incrementInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Receiver
  [!?ioUnusedVariableCppNameSet removeKey !mReceiverCppName] ;
  @string receiverCppName := mReceiverCppName ;
  foreach mStructAttributeList do
    receiverCppName .= ".mAttribute_" . [[mValue string] identifierRepresentation] ;
  end foreach ;
#--- Invoke incrementation
  ioGeneratedCode .= receiverCppName . ".increment_operation (" . compilerCppName []
                  . " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    ioGeneratedCode .= "  " . receiverCppName . ".checkIsValid (HERE) ;\n" ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @decrementInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Receiver
  [!?ioUnusedVariableCppNameSet removeKey !mReceiverCppName] ;
  @string receiverCppName := mReceiverCppName ;
  foreach mStructAttributeList do
    receiverCppName .= ".mAttribute_" . [[mValue string] identifierRepresentation] ;
  end foreach ;
#--- Invoke incrementation
  ioGeneratedCode .= receiverCppName . ".decrement_operation (" . compilerCppName []
                  . " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  if inGenerateCheckValid then
    ioGeneratedCode .= "#ifndef DO_NOT_GENERATE_CHECKINGS\n" ;
    ioGeneratedCode .= "  " . receiverCppName . ".checkIsValid (HERE) ;\n" ;
    ioGeneratedCode .= "#endif\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @messageInstructionForGeneration generateInstruction
  ??@bool unused inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Message expression
  @string messageCppVarName ;
  [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?messageCppVarName] ;
#--- Invoke 'printMessage' method of C_Compiler
  ioGeneratedCode .= compilerCppName [] . "->printMessage (" . messageCppVarName ;
  ioGeneratedCode .= " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n"  ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
end method ;

#---------------------------------------------------------------------------*

override method @loopInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Variant expression
  @string variantCppVarName ;
  [mVariantExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?variantCppVarName] ;
  ioGeneratedCode .= "if (" . variantCppVarName . ".isValid ()) {\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
  const @string variantVar := "variant_" . [[mInstructionLocation locationIndex] string] ;
  ioGeneratedCode .= "PMUInt32 " . variantVar . " = " . variantCppVarName . ".uintValue () ;\n" ;
  const @string loopVar := "loop_" . [[mInstructionLocation locationIndex] string] ;
  ioGeneratedCode .= "bool " . loopVar . " = true ;\n"
                  . "while (" . loopVar . ") {\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
  generateInstructionList
    !inGenerateCheckValid
    !mFirstInstructions
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !?ioGeneratedCode
  ;
  @string loopExpressionVar ;
  [mLoopExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?loopExpressionVar] ;
  ioGeneratedCode .= loopVar . " = " . loopExpressionVar . ".isValid () ;\n"
                   . "if (" . loopVar . ") {\n"
                   . "  " . loopVar . " = " . loopExpressionVar . ".boolValue () ;\n"
                   . "}\n" ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  ioGeneratedCode .= "if (" . loopVar . " && (0 == " . variantVar . ")) {\n"
                     "  " . loopVar . " = false ;\n"
                     "  " . compilerCppName [] . "->loopRunTimeVariantError (SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n"
                     "}\n"
                   . "if (" . loopVar . ") {\n" ;
  ioGeneratedCode .= "  " . variantVar . " -- ;\n" ;
  generateInstructionList
    !inGenerateCheckValid
    !mSecondInstructions
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !?ioGeneratedCode
  ;
  ioGeneratedCode .= "}\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  ioGeneratedCode .= "}\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @readOnlyWithInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Receiver expression
  @string receiverVarCppName ;
  [mReceiverExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?receiverVarCppName] ;
  const @unifiedTypeMapProxy receiverType := [mReceiverExpression mType] ;
#--- key expression
  @string keyVarCppName ;
  [mKeyExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?keyVarCppName] ;
#---
  ioGeneratedCode .= "const cMapElement_" . [receiverType identifierRepresentation]
                  . " * " . mObjectArrayCppName . " = (const cMapElement_" . [receiverType identifierRepresentation]
                  . " *) " . receiverVarCppName . "." ;
  if mAccessName == "hasKey" then
    ioGeneratedCode .= "readAccessForWithInstruction_hasKey (" . keyVarCppName . ") ;\n" ;
  else
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
    ioGeneratedCode .= "performSearch (" . keyVarCppName . ", " . compilerCppName []
                     . ", kSearchErrorMessage_" . [receiverType identifierRepresentation] . "_" . [mAccessName identifierRepresentation]
                     . " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
  end if ;
  ioGeneratedCode .= "if (NULL != " . mObjectArrayCppName . ") {\n"
                  .  "    macroValidSharedObject (" . mObjectArrayCppName . ", const cMapElement_" . [receiverType identifierRepresentation] . ") ;\n" ;
  generateInstructionList
    !inGenerateCheckValid
    !mDoBranchInstructions
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !?ioGeneratedCode
  ;
  if [mElseBranchInstructions length] > 0 then
    ioGeneratedCode .= "}else{\n" ;
    generateInstructionList
      !inGenerateCheckValid
      !mElseBranchInstructions
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;
  end if ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @readWriteWithInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  @string receiverCppName := mReceiverVariableCppName ;
  foreach mStructAttributeList do
    receiverCppName .= ".mAttribute_" . [[mValue string] identifierRepresentation] ;
  end foreach ;
#--- key expression
  @string keyVarCppName ;
  [mKeyExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?keyVarCppName] ;
#---
  foreach [mReceiverType mCurrentTypedAttributeList] index IDX do
    ioGeneratedCode .= "// index " . [IDX+1 string] . ": attribute '" . mAttributeName . "', type @" . [mAttributeTypeProxy key] . "\n" ;
  end foreach ;
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
  [!?ioUnusedVariableCppNameSet removeKey !mReceiverVariableCppName] ;
  ioGeneratedCode .= "cMapElement_" . [mReceiverType identifierRepresentation]
                  . " * " . mObjectArrayCppName . " = (cMapElement_" . [mReceiverType identifierRepresentation]
                  . " *) " . receiverCppName . ".readWriteAccessForWithInstruction_" . [mAccessName identifierRepresentation] . " (" . compilerCppName []
                  .  ", " . keyVarCppName . " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n"
                  .  "if (NULL != " . mObjectArrayCppName . ") {\n"
                  .  "    macroValidSharedObject (" . mObjectArrayCppName . ", cMapElement_" . [mReceiverType identifierRepresentation] . ") ;\n" ;
  generateInstructionList
   !inGenerateCheckValid
   !mDoBranchInstructions
   !?ioTemporaryVariableIndex
   !?ioUnusedVariableCppNameSet
   !?ioGeneratedCode
  ;
  if [mElseBranchInstructions length] > 0 then
    ioGeneratedCode .= "}else{\n" ;
    generateInstructionList
      !inGenerateCheckValid
      !mElseBranchInstructions
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;
  end if ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @foreachInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  @stringlist enumerationVarCppNameList [emptyList] ;
  foreach mEnumeratedObjectList do
    @string enumerationVar ;
    [mEnumeratedExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?enumerationVar] ;
    enumerationVarCppNameList += !enumerationVar ;
  end foreach ;
  foreach mEnumeratedObjectList, enumerationVarCppNameList do
    ioGeneratedCode .= "cEnumerator_" . [[mEnumeratedExpression mType] identifierRepresentation] . " " . mEnumeratorCppName . " (" . mValue
                    . ", kEnumeration_" . mEnumerationOrder . ") ;\n" ;
  end foreach ;
#--- Analyse 'while' expression
  const @bool whileExpressionIsAllwaysTrue := [mWhileExpression isTrueExpression] ;
#--- Has 'before', 'after' or expression ? If yes, generate 'before' instructions
  if (([mBeforeInstructionList length] + [mAfterInstructionList length]) > 0) | not whileExpressionIsAllwaysTrue then
  #--- Index
    if mIndexVariableCppName != "" then
      ioGeneratedCode .= "GALGAS_uint " . mIndexVariableCppName . " ((PMUInt32) 0) ;\n" ;
    end if ;
    const @string boolVarCppName := "bool_" . [ioTemporaryVariableIndex string] ; ioTemporaryVariableIndex ++ ;
    if whileExpressionIsAllwaysTrue then
      ioGeneratedCode .= "const bool " . boolVarCppName . " = true ;\n" ;
    else
      @string whileVar ;
      [mWhileExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?whileVar] ;
      ioGeneratedCode .= "bool " . boolVarCppName . " = " . whileVar . ".isValidAndTrue () ;\n" ;
    end if ;
    ioGeneratedCode .= "if (" ;
    foreach mEnumeratedObjectList do
      ioGeneratedCode .= mEnumeratorCppName . ".hasCurrentObject () && " ;
    end foreach ;
    ioGeneratedCode .= boolVarCppName . ") {\n" ;
    generateInstructionList
      !inGenerateCheckValid
      !mBeforeInstructionList
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;
    [!?ioGeneratedCode incIndentation !2] ;
    ioGeneratedCode .= "while (" ;
    foreach mEnumeratedObjectList do
      ioGeneratedCode .= mEnumeratorCppName . ".hasCurrentObject () && " ;
    end foreach ;
    ioGeneratedCode .= boolVarCppName . ") {\n" ;
  #--- Generate 'do' instruction
    generateInstructionList
      !inGenerateCheckValid
      !mDoInstructionList
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;
  #--- Goto next object
    foreach mEnumeratedObjectList do
      ioGeneratedCode .= "  " . mEnumeratorCppName . ".gotoNextObject () ;\n" ;
    end foreach ;
    if mIndexVariableCppName != "" then
      ioGeneratedCode .= "  " . mIndexVariableCppName . ".increment () ;\n" ;
    end if ;
  #--- Compute 'while' expression
    if not whileExpressionIsAllwaysTrue then
      ioGeneratedCode .= "  if (" ;
      foreach mEnumeratedObjectList
      do  ioGeneratedCode .= mEnumeratorCppName . ".hasCurrentObject ()" ;
      between ioGeneratedCode .= " && " ;
      end foreach ;
      ioGeneratedCode .= ") {\n" ;
      @string whileVar ;
      [mWhileExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?whileVar] ;
      ioGeneratedCode .= "    " . boolVarCppName . " = " . whileVar . ".isValidAndTrue () ;\n" ;
      ioGeneratedCode .= "  }\n" ;
    end if ;
  #--- Generate 'between' instruction
    if [mBetweenInstructionList length] > 0 then
      [!?ioGeneratedCode incIndentation !2] ;
      ioGeneratedCode .= "if (" ;
      foreach mEnumeratedObjectList do
        ioGeneratedCode .= mEnumeratorCppName . ".hasCurrentObject () && " ;
      end foreach ;
      ioGeneratedCode .= boolVarCppName . ") {\n" ;
      generateInstructionList
        !inGenerateCheckValid
        !mBetweenInstructionList
        !?ioTemporaryVariableIndex
        !?ioUnusedVariableCppNameSet
        !?ioGeneratedCode
      ;
      ioGeneratedCode .= "}\n" ;
      [!?ioGeneratedCode decIndentation !2] ;
    end if ;
  #--- End of foreach instruction
    ioGeneratedCode .= "}\n" ;
  #--- Generate 'after' instruction
    [!?ioGeneratedCode decIndentation !2] ;
    generateInstructionList
      !inGenerateCheckValid
      !mAfterInstructionList
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;
    ioGeneratedCode .= "}\n" ;
  else #----- foreach instruction without 'before', 'after', while expression
    if mIndexVariableCppName != "" then
      ioGeneratedCode .= "GALGAS_uint " . mIndexVariableCppName . " ((PMUInt32) 0) ;\n" ;
    end if ;
    ioGeneratedCode .= "while (" ;
    foreach mEnumeratedObjectList
      do ioGeneratedCode .= mEnumeratorCppName . ".hasCurrentObject ()" ;
      between ioGeneratedCode .= " && " ;
    end foreach ;
    ioGeneratedCode .= ") {\n" ;
  #--- Generate 'do' instruction
    generateInstructionList
      !inGenerateCheckValid
      !mDoInstructionList
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;
  #--- Generate 'between' instruction
    if [mBetweenInstructionList length] > 0 then
      [!?ioGeneratedCode incIndentation !2] ;
      ioGeneratedCode .= "if (" ;
      foreach mEnumeratedObjectList
        do ioGeneratedCode .= mEnumeratorCppName . ".hasNextObject ()" ;
        between ioGeneratedCode .= " && " ;
      end foreach ;
      ioGeneratedCode .= ") {\n" ;
      generateInstructionList
        !inGenerateCheckValid
        !mBetweenInstructionList
        !?ioTemporaryVariableIndex
        !?ioUnusedVariableCppNameSet
        !?ioGeneratedCode
      ;
      ioGeneratedCode .= "}\n" ;
      [!?ioGeneratedCode decIndentation !2] ;
    end if ;
  #--- End of foreach instruction
    foreach mEnumeratedObjectList do
      ioGeneratedCode .= "  " . mEnumeratorCppName . ".gotoNextObject () ;\n" ;
    end foreach ;
    if mIndexVariableCppName != "" then
      [!?ioUnusedVariableCppNameSet removeKey !compilerCppName []] ;
      ioGeneratedCode .= "  " . mIndexVariableCppName . ".increment_operation (" . compilerCppName [] . " COMMA_SOURCE_FILE_AT_LINE (" . [[mInstructionLocation line] string] . ")) ;\n" ;
    end if ;
    ioGeneratedCode .= "}\n" ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @logInstructionForGeneration generateInstruction
  ??@bool unused inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  @string logVar ;
  [mLogExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?logVar] ;
  ioGeneratedCode .= logVar . ".log (" . [mLogMessage utf8Representation] . " COMMA_SOURCE_FILE_AT_LINE (" . [[[mLogMessage location] line] string] . ")) ;\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @switchInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
#--- Switch expression
  @string switchVar ;
  [mSwitchExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?switchVar] ;
  ioGeneratedCode .= "if (" . switchVar . ".isValid ()) {\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
  ioGeneratedCode .= "switch (" . switchVar . ".enumValue ()) {\n" ;
  ioGeneratedCode .= "case GALGAS_" . [[mSwitchExpression mType] identifierRepresentation] . "::kNotBuilt:\n" ;
  ioGeneratedCode .= "  break ;\n" ;
  foreach mBranches do
    foreach mSwitchConstantList do
      ioGeneratedCode .= "case GALGAS_" . [[mSwitchExpression mType] identifierRepresentation] . "::kEnum_" . [[mValue string] identifierRepresentation] . ": " ;
    end foreach ;
    ioGeneratedCode .= "{\n" ;
    generateInstructionList
      !inGenerateCheckValid
      !mInstructions
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;
    ioGeneratedCode .= "  } break ;\n" ;
  end foreach ;
  ioGeneratedCode .= "}\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

override method @matchInstructionForGeneration generateInstruction
  ??@bool inGenerateCheckValid
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?!@string ioGeneratedCode
:
  @stringlist receiverList [emptyList] ;
  foreach mMatchedExpressionList do
    @string var ;
    [mExpression generateExpression !?ioGeneratedCode !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?var] ;
    receiverList += !var ;
  end foreach ;
  ioGeneratedCode .= "if (" ;
  foreach receiverList
    do ioGeneratedCode .= mValue . ".isValid ()" ;
    between ioGeneratedCode .= " && " ;
  end foreach ;
  ioGeneratedCode .= ") {\n" ;
  [!?ioGeneratedCode incIndentation !2] ;
  foreach mMatchInstructionBranchList do
    ioGeneratedCode .= "if (" ;
    foreach mMatchListForGeneration, receiverList do
      if mIsType then
        ioGeneratedCode .= "(NULL != dynamic_cast <const cPtr_" . [mTypeNameOrEnumerationConstantName identifierRepresentation]
                        . " *> (" . mValue . ".ptr ()))" ;
      else
        ioGeneratedCode .= "(" . mValue . ".enumValue () == GALGAS_"
                        . [mLocalConstantName identifierRepresentation]
                        . "::kEnum_" . [mTypeNameOrEnumerationConstantName identifierRepresentation] . ")" ;
      end if ;
      between ioGeneratedCode .= " && " ;
    end foreach ;
    ioGeneratedCode .= ") {\n" ;
    generateInstructionList
      !inGenerateCheckValid
      !mMatchBranchInstructionList
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      !?ioGeneratedCode
    ;
  between ioGeneratedCode .= "}else " ;
  end foreach ;
  ioGeneratedCode .= "}else{\n" ;
  generateInstructionList
    !inGenerateCheckValid
    !mElseInstructionList
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !?ioGeneratedCode
  ;
  ioGeneratedCode .= "}\n" ;
  [!?ioGeneratedCode decIndentation !2] ;
  ioGeneratedCode .= "}\n" ;
end method ;

#---------------------------------------------------------------------------*

end semantics ;
