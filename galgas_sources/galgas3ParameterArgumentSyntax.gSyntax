#---------------------------------------------------------------------------*
#                                                                           *
#  Copyright (C) 2014, ..., 2014 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax galgas3ParameterArgumentSyntax ("galgas3Scanner.gLexique") :

import "semanticsTypesForAST.gSemantics" ;

#------------------------------------------------------------------------------*

nonterminal <expression> !@semanticExpressionAST outExpressionAST ;

#------------------------------------------------------------------------------*
#                                                                              *
#          F O R M A L    P A R A M E T E R    L I S T                         *
#                                                                              *
#------------------------------------------------------------------------------*

rule <formal_parameter_list> !@formalParameterListAST outFormalParameterList :
  outFormalParameterList := [@formalParameterListAST emptyList] ;
  repeat
  while
    @lstring selector ;
    @formalArgumentPassingModeAST mFormalParameterPassingMode ;
    select
      $?$ ?selector ;
      mFormalParameterPassingMode := [@formalArgumentPassingModeAST argumentIn] ;
    or
      $?$ ?selector ;
      $let$ ;
      mFormalParameterPassingMode := [@formalArgumentPassingModeAST argumentConstantIn] ;
    or
      $?!$ ?selector ;
      mFormalParameterPassingMode := [@formalArgumentPassingModeAST argumentInOut] ;
    or
      $!$ ?selector ;
      mFormalParameterPassingMode := [@formalArgumentPassingModeAST argumentOut] ;
    end select ;
    $type_name$ ?@lstring mFormalParameterTypeName ;
    @bool mIsUnused ;
    select
      mIsUnused := false ;
    or
      $unused$ ;
      mIsUnused := true ;
    end select ;
    $identifier$ ?@lstring mFormalParameterName ;
    outFormalParameterList +=
      !selector
      !mFormalParameterPassingMode
      !mFormalParameterTypeName
      !mFormalParameterName
      !mIsUnused
    ;
  end repeat ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  A C T U A L    P A R A M E T E R    L I S T                              *
#                                                                           *
#---------------------------------------------------------------------------*

rule <actual_parameter_list>
  !@actualParameterListAST outActualParameterList
:
  outActualParameterList := [@actualParameterListAST emptyList] ;
  repeat
  while
    $?$ ?const selector ;
    select
      $*$ ;
      outActualParameterList += ![@inputSingleJokerActualParameterAST new !selector] ;
    or
      @luint count ;
      $unsigned_literal_integer$ ? count ;
      $*$ ;
      var n := [count uint] ;
      if selector->string != "" then
        error selector : "the selector should be '?'" ;
      end if ;
     if n == 0 then
        error count: "the count value should be > 0" ;
      else
        loop n :
        while n > 0 do
          outActualParameterList += ![@inputJokerActualParameterAST new !selector->location !n] ;
          n -- ;
        end loop ;
      end if ;
    end select ;
  while
    $!$ ?const selector ;
    <expression> ??@semanticExpressionAST e ;
    const @location endOfExpressionLocation := here ;
    outActualParameterList += ![@outputActualParameterAST new
      !selector
      !e
      !endOfExpressionLocation
    ] ;
  while
    $!?$ ?const selector ;
    @lstring outputInputActualParameterName ;
    $identifier$ ?outputInputActualParameterName ;
    @lstringlist structAttributeList [emptyList] ;
    repeat
    while
      $.$ ;
      $identifier$ ?var structAttributeName ;
      structAttributeList += !structAttributeName ;
    end repeat ;
    outActualParameterList += ![@outputInputActualParameterAST new 
      !selector
      !outputInputActualParameterName
      !structAttributeList
    ] ;
  while
    $!?$ ?const selector ;
    $*$ ;
    outActualParameterList += ![@outputInputSingleJokerParameterAST new 
      !selector
    ] ;
  while
    $!?$ ?const selector ;
    $unsigned_literal_integer$ ??@luint count ;
    $*$ ;
    var n := [count uint] ;
    if selector->string != "" then
      error selector : "the selector should be '!?'" ;
    end if ;
    if n == 0 then
      error count: "the count value should be > 0" ;
    else
      loop n :
      while n > 0 do
        outActualParameterList += ![@outputInputJokerParameterAST new !selector->location !n] ;
        n -- ;
      end loop ;
    end if ;
  while # input parameter, existing variable
    $?$ ?const selector ;
    $identifier$ ?const inputActualParameterName ;
    outActualParameterList += ![@inputActualExistingVariableParameterAST new
      !selector
      !inputActualParameterName
    ] ;
  while  # input parameter, new variable
    $?$ ?const selector ;
    $var$ ;
    @lstring declarationTypeName ;
    select
      declarationTypeName := ["" nowhere] ;
    or
      $type_name$ ?declarationTypeName ;
    end select ;
    $identifier$ ?const inputActualParameterName ;
    outActualParameterList += ![@inputActualNewVariableParameterAST new
      !selector
      !declarationTypeName
      !inputActualParameterName
    ] ;
  while  # input parameter, new variable, explicit type
    $?$ ?const selector ;
    $type_name$ ??@lstring declarationTypeName ;
    $identifier$ ?const inputActualParameterName ;
    outActualParameterList += ![@inputActualNewVariableParameterAST new
      !selector
      !declarationTypeName
      !inputActualParameterName
    ] ;
  while # input parameter, new constant
    $?$ ?const selector ;
    $let$ ;
    @lstring declarationTypeName ;
    select
      declarationTypeName := ["" nowhere] ;
    or
      $type_name$ ?declarationTypeName ;
    end select ;
    $identifier$ ?const inputActualParameterName ;
    outActualParameterList += ![@inputActualNewConstantParameterAST new
      !selector
      !declarationTypeName
      !inputActualParameterName
    ] ;
  end repeat ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  F O R M A L    I N P U T    P A R A M E T E R S                          *
#                                                                           *
#---------------------------------------------------------------------------*

rule <formal_input_parameter_list> !@formalInputParameterListAST outFormalInputParameterList :
  outFormalInputParameterList := [@formalInputParameterListAST emptyList] ;
  repeat
  while
    @bool isConstant ;
    $?$ ?const selector ;
    select
      isConstant := false ;
    or
      $let$ ;
      isConstant := true ;
    end select ;
    $type_name$ ?@lstring mFormalParameterTypeName ;
    @bool mIsUnused ;
    select
      mIsUnused := false ;
    or
      $unused$ ;
      mIsUnused := true ;
    end select ;
    $identifier$ ?@lstring mFormalParameterName ;
    outFormalInputParameterList +=
      !selector
      !mFormalParameterTypeName
      !mFormalParameterName
      !mIsUnused
      !isConstant
    ;
  end repeat ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;
