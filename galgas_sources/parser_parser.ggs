#---------------------------------------------------------------------------*
#                                                                           *
#  GALGAS parser component parser definition                                *
#                                                                           *
#  Copyright (C) 1997, ..., 2007 Pierre Molinaro.                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax parser_parser :

import lexique galgas_scanner in "galgas_scanner.ggs" ;

import semantics parser_semantics in "parser_semantics.ggs" ;

#------------------------------------------------------------------------------*
#                                                                              *
#          Rules prototypes                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <galgas_component>
  ?!@M_lexiqueComponents ioLexiqueMapForUse
  ?!@M_semanticsComponents ioSemanticsComponentsMap
  ?!@M_syntaxComponents ioSyntaxComponentsMap
  ?!@M_grammarComponents ioGrammarComponentsMap
  ?!@M_optionComponents ioOptionsComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_constraintComponents ioConstraintComponentMap
label importLexique
  ?!@M_lexiqueComponents ioLexiqueMapForUse
label importSyntax
  ?!@M_syntaxComponents ioSyntaxComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
label importSemantics
  ?!@M_semanticsComponents ioSemanticsComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_constraintComponents ioConstraintComponentMap
label importGrammarForSemantics
  ?!@M_grammarComponents ioGrammarComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_constraintComponents ioConstraintComponentMap
label importOptions
  ?!@M_optionComponents ioOptionsComponentsMap
;

nonterminal <semantics_declaration>
  ?!@M_semanticsEntitiesForUse ioComponentSemanticsEntitiesMap
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeTableNomRoutinesDeclarees tableNomRoutinesDeclarees
  ?!@typeTableEnAvant tableEnAvant
  ?!@typeTableRoutinesAimplementer tableRoutinesAimplementer
label importSemantics
  ?!@ModelMap ioModelMap
  ?!@ActionMap unused ioExternActionMap
  ?!@M_semanticsEntitiesForUse ioComponentSemanticsEntitiesMap
  ?!@typeTableNomRoutinesDeclarees tableNomRoutinesDeclarees
  ?!@typeTableEnAvant tableEnAvant
  ?!@typeTableRoutinesAimplementer tableRoutinesAimplementer
label parse
;

nonterminal <syntax_declaration>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?!@M_semanticsEntitiesForUse ioEntitiesMap
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeTableEnAvant tableEnAvant
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@M_nonTerminalSymbolsForGrammar ioNonTerminalSymbolsMap
  ?!@L_productionRules_ForGrammarComponent ioProductionRulesListForGrammar
  ?@lstring inSyntaxComponentName
  ?@entityToImplementMap inAllMetamodelClassMap
label importSyntax
  ?!@M_nonTerminalSymbolsForGrammar ioNonTerminalSymbolsMap
  ?!@L_productionRules_ForGrammarComponent ioProductionRulesListForGrammar
label parse
;

nonterminal <parse_lexique_for_importing>
  ??@lstring inFileName
  ?!@M_lexiqueComponents ioLexiqueMapForUse
label parse
;

nonterminal <import_headers_semantics_and_grammars>
  !@stringset ioIncludedSemanticsComponents
  !@stringset outIncludedMetamodelComponents
  !@stringset ioIncludedOptionComponents
  !@stringset ioIncludedGrammarComponents
  ?!@M_semanticsComponents ioSemanticsComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_constraintComponents ioConstraintComponentMap
  ?!@M_grammarComponents ioGrammarComponentsMap
  ?!@M_optionComponents ioOptionsComponents
  !@stringset outInclusionsForImplementationFile
  !@stringset outInclusionsForHeaderFile
  !@M_optionComponents ioOptionsComponentsMapForUse
  !@ModelMap outModelMap
  !@ActionMap outActionMap

  !@M_semanticsEntitiesForUse ioEntitiesMap
  !@typeTableEnAvant tableEnAvant
  !@entityToImplementMap outAllMetamodelClassMap
label importSemantics
  ?!@M_semanticsComponents ioSemanticsComponentsMap
  !@stringset outIncludedSemanticsComponents
  !@stringset outIcludedOptionComponents
  !@ModelMap outModelMap
  !@ActionMap outActionMap
  ?!@M_semanticsEntitiesForUse ioComponentSemanticsEntitiesMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_constraintComponents ioConstraintComponentMap
label parse
;

nonterminal <semantic_instruction>
  ??@typeTableEnAvant inTableEnAvant
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@entityPropertyMap ioPropertyMap
  ?!@stringset ioConstraintPropertyDefinitionSet
  ?!@callInstructionSharedPropertySignatureMap ioCallInstructionSharedPropertySignatureMap
label parse
;

nonterminal <syntax_instruction>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?@bool inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap inAllMetamodelClassMap
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
label parse
;

nonterminal <expression>
  ??@M_semanticsEntitiesForUse ioComponentSemanticsEntitiesMap
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  !@typeExpression outExpression
  !@AC_galgasType outResultType
label parse
;

nonterminal <input_parameters_list>
  !@L_assignedVariables listeAffectationParametresEffectifs
  !@typeCplusPlusNameList outAllVariablesList
  ?@typeListeAttributsSemantiques listeAttributsSemantiques
  ?!@typeVariablesMap ioVariablesMap
label parse
;

nonterminal <actual_parameters_list>
  ??@L_EXsignature listeTypesParametresFormels
  !@typeExpressionList outExpressionList
  ?!@typeVariablesMap ioVariablesMap
  ??@M_optionComponents inOptionsComponentsMapForUse
  ??@M_semanticsEntitiesForUse ioComponentSemanticsEntitiesMap
  ?!@typeInstructionList ioInstructionList

label parse
;

nonterminal <formal_arguments_list>
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  !@L_EXsignature listeDeTypes
  ?!@typeVariablesMap tableArgumentsMethode
  !@typeListeTypesEtNomsArgMethode listeTypeEtNomsArguments
  ?!@typeTableEnAvant tableEnAvant
  !@L_signature outSignature
label importGrammarForSemantics
  !@L_signature outSignature
label parse
;

nonterminal <syntax_instructions_list>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?@bool inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap inAllMetamodelClassMap
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
label parse
;
 
nonterminal <syntax_instructions_list_no_verif>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?@bool inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap inAllMetamodelClassMap
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
label parse
;

nonterminal <get_lexique_infos>
  !@M_terminalSymbolsMapForUse tableTerminaux
  ?!@M_lexiqueComponents ioLexiqueMapForUse
  !@lstring outLexiqueName
label getLexiqueName
  !@lstring outLexiqueName
label parse
;

action checkLabelSignatures
  ?!@typeAltProductionsMap ioAltProductionMap ;

action checkParseRewindSignatures
  ?!@L_parse_rewind_signature_list ioParseRewindSignature
 ;

#------------------------------------------------------------------------------*
#                                                                              *
#                      SYNTAX COMPONENT                                        *
#                                                                              *
#------------------------------------------------------------------------------*

rule <galgas_component>
  ?!@M_lexiqueComponents ioLexiqueMapForUse
  ?!@M_semanticsComponents ioSemanticsComponentsMap
  ?!@M_syntaxComponents ioSyntaxComponentsMap
  ?!@M_grammarComponents ioGrammarComponentsMap
  ?!@M_optionComponents ioOptionsComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
  ?!@M_constraintComponents ioConstraintComponentMap
:
  @typeEntitiesToGenerateList listeEntitesAengendrer ;
  @typeTableRoutinesAimplementer tableRoutinesAimplementer [emptyMap] ;
  @typeTableNomRoutinesDeclarees tableNomRoutinesDeclarees [emptyMap] ;
  $syntax$ ;
#--- Get syntax comopnent name
  @lstring componentName ;
  $identifier$ ? componentName ;
  $:$ ;
  @M_terminalSymbolsMapForUse tableTerminaux ;
#--- Define lexique
  @lstring  lexiqueName ;
  <get_lexique_infos>
    ?tableTerminaux
    !?ioLexiqueMapForUse
    ?lexiqueName
  ;
  @string defLexique := [lexiqueName string] ;
#--- Parse Inclusions
  @stringset inclusionsForHeaderFile ;
  @stringset inclusionsForImplementationFile ;
  @typeTableEnAvant tableEnAvant ;
  @M_semanticsEntitiesForUse entitiesMap ;
  @M_optionComponents ioOptionsComponentsMapForUse ;
  @stringset includedSemanticsComponents ;
  @stringset includedMetamodelComponents ;
  @stringset includedOptionComponents ;
  @stringset includedGrammarComponents ;
  @ModelMap modelMap ;
  @ActionMap actionMap ;
  @entityToImplementMap allMetamodelClassMap ;
  <import_headers_semantics_and_grammars>
    ?includedSemanticsComponents
    ?includedOptionComponents
    ?includedMetamodelComponents
    ?includedGrammarComponents
    !?ioSemanticsComponentsMap
    !?ioMetamodelComponentMap
    !?ioConstraintComponentMap
    !?ioGrammarComponentsMap
    !?ioOptionsComponentsMap
    ?inclusionsForImplementationFile
    ?inclusionsForHeaderFile
    ?ioOptionsComponentsMapForUse
    ?modelMap
    ?actionMap
    ?entitiesMap
    ?tableEnAvant
    ?allMetamodelClassMap
  ;
  listeEntitesAengendrer := [@typeEntitiesToGenerateList emptyList] ;
  @M_nonTerminalSymbolsForGrammar nonTerminalSymbolsMapForGrammar [emptyMap] ;
  @L_productionRules_ForGrammarComponent productionRulesListForGrammar [emptyList] ;
  @M_nonTerminalSymbolForSyntax nonTerminalSymbolsMapForSyntax [emptyMap] ;
  repeat
  while
    <syntax_declaration>
      !?nonTerminalSymbolsMapForSyntax
      !?entitiesMap
      !ioOptionsComponentsMapForUse
      !?tableEnAvant
      !?listeEntitesAengendrer
      !?tableTerminaux
      !?nonTerminalSymbolsMapForGrammar
      !?productionRulesListForGrammar
      !componentName
      !allMetamodelClassMap
    ;
  while
    <semantics_declaration>
      !?entitiesMap
      !?listeEntitesAengendrer
      !ioOptionsComponentsMapForUse
      !?tableNomRoutinesDeclarees
      !?tableEnAvant
      !?tableRoutinesAimplementer
    ;
  end repeat ;
#--- Check that all routines have been implemented
  foreach tableNomRoutinesDeclarees (@lstring nomRoutine) :
    if not [tableRoutinesAimplementer hasKey ![nomRoutine string]] then
      error nomRoutine : "the routine '" . [nomRoutine string] .
                         "' is not implemented within the component" ;
    end if ;
  end foreach ;
#--- Enter syntax component into syntax components map
  [!?ioSyntaxComponentsMap insertKey
    !componentName
    !nonTerminalSymbolsMapForGrammar
    !productionRulesListForGrammar
    !lexiqueName
  ] ;
#--- Generate component
  action generateSemanticsComponent
    !?componentName
    !?inclusionsForHeaderFile
    !?defLexique
    !?listeEntitesAengendrer
    !?inclusionsForImplementationFile
  ;
#--- Fin du fichier
  $end$ ;
  $syntax$ ;
  $;$ ;
label importSyntax
  ?!@M_syntaxComponents ioSyntaxComponentsMap
  ?!@M_metamodelsComponents ioMetamodelComponentMap
:
  $syntax$ ;
  @lstring syntaxComponentName ;
  $identifier$ ? syntaxComponentName ;
  $:$ ;
#--- Lexique
  @lstring lexiqueName ;
  <get_lexique_infos> getLexiqueName ? lexiqueName ;
#--- Import Semantics, Metamodels
  @M_semanticsComponents semanticsComponentsMap [emptyMap] ;
  @M_grammarComponents grammarComponentsMap [emptyMap] ;
  @M_optionComponents optionsComponentsMap [emptyMap] ;  
  @stringset inclusionsForHeaderFile ;
  @stringset inclusionsForImplementationFile ;
  @typeTableEnAvant tableEnAvant ;
  @M_semanticsEntitiesForUse entitiesMap ;
  @M_optionComponents ioOptionsComponentsMapForUse ;
  @M_constraintComponents constraintComponentMap [emptyMap] ;
  @stringset includedSemanticsComponents ;
  @stringset includedMetamodelComponents ;
  @stringset includedOptionComponents ;
  @stringset includedGrammarComponents ;
  @ModelMap modelMap ;
  @ActionMap actionMap ;
  @entityToImplementMap allMetamodelClassMap ;
  <import_headers_semantics_and_grammars>
    ?includedSemanticsComponents
    ?includedOptionComponents
    ?includedMetamodelComponents
    ?includedGrammarComponents
    !?semanticsComponentsMap
    !?ioMetamodelComponentMap
    !?constraintComponentMap
    !?grammarComponentsMap
    !?optionsComponentsMap
    ?inclusionsForImplementationFile
    ?inclusionsForHeaderFile
    ?ioOptionsComponentsMapForUse
    ?modelMap
    ?actionMap
    ?entitiesMap
    ?tableEnAvant
    ?allMetamodelClassMap
  ;
#--- 
  @L_productionRules_ForGrammarComponent productionRulesListForGrammar [emptyList] ;
  @M_nonTerminalSymbolsForGrammar nonTerminalSymbolsMapForGrammar [emptyMap] ;
  repeat
  while
    <syntax_declaration> importSyntax
      !?nonTerminalSymbolsMapForGrammar
      !?productionRulesListForGrammar
    ;
  while
    <semantics_declaration> parse ;
  end repeat ;
#--- Enter syntax component into syntax components map
  [!?ioSyntaxComponentsMap insertKey
    !syntaxComponentName
    !nonTerminalSymbolsMapForGrammar
    !productionRulesListForGrammar
    !lexiqueName
  ] ;
  $end$ ;
  $syntax$ ;
  $;$ ;
label importSemantics
  ?!@M_semanticsComponents unused ioSemanticsComponentsMap
  ?!@M_metamodelsComponents unused ioMetamodelComponentMap
  ?!@M_constraintComponents unused ioConstraintComponentMap
:
  $syntax$ ;
  $identifier$ ? * ;
  $:$ ;
  <get_lexique_infos> parse ;
  <import_headers_semantics_and_grammars> parse ;
  repeat
  while
    <syntax_declaration> parse ;
  while
    <semantics_declaration> parse ;
  end repeat ;
  $end$ ;
  $syntax$ ;
  $;$ ;
label importLexique
  ?!@M_lexiqueComponents unused ioLexiqueMapForUse
:
  $syntax$ ;
  $identifier$ ? * ;
  $:$ ;
  <get_lexique_infos> parse ;
  <import_headers_semantics_and_grammars> parse ;
  repeat
  while
    <syntax_declaration> parse ;
  while
    <semantics_declaration> parse ;
  end repeat ;
  $end$ ;
  $syntax$ ;
  $;$ ;
label importGrammarForSemantics
  ?!@M_grammarComponents unused ioGrammarComponentsMap
  ?!@M_metamodelsComponents unused ioMetamodelComponentMap
  ?!@M_constraintComponents unused ioConstraintComponentMap
:
  $syntax$ ;
  $identifier$ ? * ;
  $:$ ;
  <get_lexique_infos> parse ;
  <import_headers_semantics_and_grammars> parse ;
  repeat
  while
    <syntax_declaration> parse ;
  while
    <semantics_declaration> parse ;
  end repeat ;
  $end$ ;
  $syntax$ ;
  $;$ ;
label importOptions
  ?!@M_optionComponents unused ioOptionsComponentsMap
:
  $syntax$ ;
  $identifier$ ? * ;
  $:$ ;
  <get_lexique_infos> parse ;
  <import_headers_semantics_and_grammars> parse ;
  repeat
  while
    <syntax_declaration> parse ;
  while
    <semantics_declaration> parse ;
  end repeat ;
  $end$ ;
  $syntax$ ;
  $;$ ;
end rule ;

#------------------------------------------------------------------------------*
#                                                                              *
#                   GET LEXIQUE INFOS                                          *
#                                                                              *
#------------------------------------------------------------------------------*

rule <get_lexique_infos>
  !@M_terminalSymbolsMapForUse tableTerminaux
  ?!@M_lexiqueComponents ioLexiqueMapForUse
  !@lstring outLexiqueName
:
#--- Get lexique name
  $import$ ;
  $lexique$ ;
  $identifier$ ?outLexiqueName ;
#--- 'in' clause ?
  @M_lexiqueComponents lexiqueComponentsMap ;
  select
    lexiqueComponentsMap := ioLexiqueMapForUse ;
  or
    $in$ ;
    @lstring fileName ;
    $literal_string$ ? fileName ;
    lexiqueComponentsMap := [@M_lexiqueComponents emptyMap] ;
    <parse_lexique_for_importing>
      !fileName
      !?lexiqueComponentsMap
    ;
  end select ;
  $;$ ;
#--- Get lexique from lexique components map
  [lexiqueComponentsMap searchKey !outLexiqueName ?tableTerminaux] ;
label getLexiqueName
  !@lstring outLexiqueName
:
  $import$ ;
  $lexique$ ;
  $identifier$ ? outLexiqueName ;
  select
  or
    $in$ ;
    $literal_string$ ? * ;
    <parse_lexique_for_importing> parse ;
  end select ;
  $;$ ;
end rule ;

#------------------------------------------------------------------------------*
#                                Instruction BLOC                              *
#------------------------------------------------------------------------------*

rule <syntax_instruction>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?@bool inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap inAllMetamodelClassMap
:
  $block$ ;
#--- Nom de la map
  @lstring nomTable ;
  $identifier$ ? nomTable ;
  @AC_galgasType typeEntite ;
  @typeCplusPlusName nomCppVariableTable ;
  [!?ioVariablesMap searchForReadWriteAccess !nomTable ?typeEntite ?nomCppVariableTable] ; 
  @typeTableBlocsDeTable tableMethodesBloc ;
  extract typeEntite->@typeGalgasMapType (?* ?* ?tableMethodesBloc ?* ?*) error nomTable : messageGalgasType ;
#--- Methode block
  $.$ ;
  @lstring nomMethodeBloc ;
  $identifier$ ? nomMethodeBloc ;
  @L_EXsignature signatureDebut ;
  @L_EXsignature signatureFin ;
  [tableMethodesBloc searchKey !nomMethodeBloc ?signatureDebut ?signatureFin] ;
#--- Analyser la list d'arguments du prologue
  @typeExpressionList prologueExpressionList ;
  $($ ;
  <actual_parameters_list>
    !signatureDebut
    ?prologueExpressionList
    !?ioVariablesMap
    !inOptionsComponentsMapForUse
    !ioEntitiesMap
    !?ioInstructionList
  ;
  $)$ ;
#--- Generate prologue instruction
  @typeInstruction instruction ;
  instruction := [@typeMapBlockPrologueInstruction new !nomCppVariableTable
                                                      !nomMethodeBloc
                                                      !prologueExpressionList] ;
  ioInstructionList += !instruction ;
#--- Liste d'instructions incluses
  $:$ ;
  <syntax_instructions_list_no_verif>
    !?ioNonTerminalSymbolMapForSyntax
    !inCheckNonTerminalsAreDeclaredWithParseLabel
    !ioEntitiesMap
    !?listeEntitesAengendrer
    !inOptionsComponentsMapForUse
    !?ioVariablesMap
    !?ioInstructionList
    !?tableTerminaux
    !?ioSyntaxSignature
    !inAllMetamodelClassMap
  ;
  $end$ ;
  $block$ ;
#--- Analyser la list d'arguments de l'epilogue
  @typeExpressionList epilogueExpressionList ;
  $($ ;
  <actual_parameters_list>
    !signatureFin
    ?epilogueExpressionList
    !?ioVariablesMap
    !inOptionsComponentsMapForUse
    !ioEntitiesMap
    !?ioInstructionList
  ;
  $)$ ;
#--- Verifier que la map est toujours accessible en lecture/ecriture
  [!?ioVariablesMap searchForReadWriteAccess !nomTable ?* ?*] ;
#--- Generate epilogue instruction
  instruction := [@typeMapBlockEpilogueInstruction new !nomCppVariableTable
                                                      !nomMethodeBloc
                                                      !epilogueExpressionList] ;
  ioInstructionList += !instruction ;
  $;$ ;
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
:
  $block$ ;
  $identifier$ ? * ;
  $.$ ;
  $identifier$ ? * ;
  $($ ;
  <actual_parameters_list> parse ;
  $)$ ;
  $:$ ;
  <syntax_instructions_list_no_verif> importSyntax !?ioInstructionList ;
  $end$ ;
  $block$ ;
  $($ ;
  <actual_parameters_list> parse ;
  $)$ ;
  $;$ ;
end rule ;

#------------------------------------------------------------------------------*
#     N O N T E R M I N A L    D E C L A R A T I O N                           *
#------------------------------------------------------------------------------*

rule <syntax_declaration>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?!@M_semanticsEntitiesForUse ioEntitiesMap
  ??@M_optionComponents unused inOptionsComponentsMapForUse
  ?!@typeTableEnAvant tableEnAvant
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ?!@M_terminalSymbolsMapForUse unused tableTerminaux
  ?!@M_nonTerminalSymbolsForGrammar ioNonTerminalSymbolsMap
  ?!@L_productionRules_ForGrammarComponent unused ioProductionRulesListForGrammar
  ?@lstring unused inSyntaxComponentName
  ?@entityToImplementMap inAllMetamodelClassMap
:
  $nonterminal$ ;
  @lstring ruleName ;
  $non_terminal_symbol$ ? ruleName ;
  @L_EXsignature listeDeTypesFormels ;
  @typeVariablesMap tableArgumentsMethode [emptyMap] ;
  @typeListeTypesEtNomsArgMethode listeTypeEtNomsArguments ;
  @L_signature signatureForGrammar ;
  <formal_arguments_list>
    !ioEntitiesMap
    ?listeDeTypesFormels 
    !?tableArgumentsMethode
    ?listeTypeEtNomsArguments
    !?tableEnAvant
    ?signatureForGrammar
  ;
  @lstring returnedEntity ;
  @lstring mdaResultVariableName ;
  select
    returnedEntity := [@lstring new !"" !here] ;
    mdaResultVariableName := [@lstring new !"" !here] ;
  or
    $->$ ;
    $mda_type_name$ ? returnedEntity ;
    [inAllMetamodelClassMap searchKey !returnedEntity ?* ?* ?* ?* ?* ?* ?* ?*] ;
    select
      mdaResultVariableName := [@lstring new !"" !here] ; # No variable
    or
      $identifier$ ? mdaResultVariableName ;
    end select ;
  end select ;
  @M_nonterminalSymbolAlts parametersMap [emptyMap] ;
  @M_nonterminalSymbolAltsForGrammar parametersMapForGrammar [emptyMap] ;
  @lstring emptyId := [@lstring new !"" !here] ;
  [!?parametersMap insertKey !emptyId !listeDeTypesFormels !returnedEntity] ;
  [!?parametersMapForGrammar insertKey
    !emptyId
    !signatureForGrammar
    !returnedEntity
  ] ;
  repeat
  while
    $label$ ;
    select
      $parse$ ;
      @L_EXsignature listeDeTypesFormels [emptyList] ;
      @L_signature signatureForGrammarComponent [emptyList] ;
      @lstring altName [new !"parse" !here] ;
      [!?parametersMap insertKey !altName !listeDeTypesFormels ![@lstring new !"" !here]] ;
      [!?parametersMapForGrammar insertKey
        !altName
        !signatureForGrammarComponent
        ![@lstring new !"" !here]
      ] ;
    or
      @lstring altName ;
      $identifier$ ? altName ;
      @L_EXsignature listeDeTypesFormels ;
      @typeVariablesMap tableArgumentsMethode [emptyMap] ;
      @typeListeTypesEtNomsArgMethode listeTypeEtNomsArguments ;
      @L_signature signatureForGrammarComponent ;
      <formal_arguments_list>
        !ioEntitiesMap
        ?listeDeTypesFormels 
        !?tableArgumentsMethode
        ?listeTypeEtNomsArguments
        !?tableEnAvant
        ?signatureForGrammarComponent
      ;
      @lstring returnedEntity ;
      select
        returnedEntity := [@lstring new !"" !here] ;
      or
        $->$ ;
        $mda_type_name$ ? returnedEntity ;
        @AC_semanticsEntity e ;
        [ioEntitiesMap searchKey !returnedEntity ?e] ;
        @AC_galgasType type ;
        extract e->@typeEntiteType (?type) error returnedEntity : messageTypeEntite ;
        extract type->@typeGalgas_singleEntity (...) error returnedEntity : messageGalgasType ;
      end select ;
      [!?parametersMap insertKey !altName !listeDeTypesFormels !returnedEntity] ;
      [!?parametersMapForGrammar insertKey
        !altName
        !signatureForGrammarComponent
        !returnedEntity
      ] ;
    end select ;
  end repeat ;
  $;$ ;
  [!?ioNonTerminalSymbolsMap insertKey !ruleName !parametersMapForGrammar] ;
  [!?ioNonTerminalSymbolMapForSyntax insertKey !ruleName !parametersMap] ;
  @typeEntityToGenerate ea := [@typeNonterminalToGenerate new !ruleName !parametersMap] ;
  listeEntitesAengendrer += !ea ;
label importSyntax
  ?!@M_nonTerminalSymbolsForGrammar ioNonTerminalSymbolsMap
  ?!@L_productionRules_ForGrammarComponent unused ioProductionRulesListForGrammar
:
  $nonterminal$ ;
  @lstring nonterminalSymbolName ;
  $non_terminal_symbol$ ? nonterminalSymbolName ;
  @L_signature signature ;
  <formal_arguments_list> importGrammarForSemantics ?signature ;
  @lstring returnedEntity ;
  select
    returnedEntity := [@lstring new !"" !here] ;
  or
    $->$ ;
    $mda_type_name$ ? returnedEntity ;
    select
    or
      $identifier$ ? * ;
    end select ;
  end select ;
  @M_nonterminalSymbolAltsForGrammar nonterminalSymbolParametersMap [emptyMap] ;
  @lstring altName := [@lstring new !"" !here] ;
  [!?nonterminalSymbolParametersMap insertKey
    !altName
    !signature
    !returnedEntity
  ] ;
  repeat
  while
    $label$ ;
    select
      $parse$ ;
      @L_signature signature [emptyList] ;
      @lstring noReturnedEntity := [@lstring new !"" !here] ;
      [!?nonterminalSymbolParametersMap insertKey
        ![@lstring new !"parse" !here] # Label 'parse'
        !signature
        !noReturnedEntity
      ] ;
    or
      $identifier$ ?altName ;
      @L_signature signature ;
      <formal_arguments_list> importGrammarForSemantics ?signature ;
      @lstring returnedEntity ;
      select
        returnedEntity := [@lstring new !"" !here] ;
      or
        $->$ ;
        $mda_type_name$ ? returnedEntity ;
      end select ;
      [!?nonterminalSymbolParametersMap insertKey
        !altName
        !signature
        !returnedEntity
      ] ;
    end select ;
  end repeat ;
  [!?ioNonTerminalSymbolsMap insertKey !nonterminalSymbolName !nonterminalSymbolParametersMap] ;
  $;$ ;
end rule ;

#------------------------------------------------------------------------------*
#     P A R S I N G   P R O D U C T I O N   R U L E                            *
#------------------------------------------------------------------------------*

rule <syntax_declaration>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?!@M_semanticsEntitiesForUse ioEntitiesMap
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeTableEnAvant tableEnAvant
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@M_nonTerminalSymbolsForGrammar unused ioNonTerminalSymbolsMap
  ?!@L_productionRules_ForGrammarComponent ioProductionRulesListForGrammar
  ?@lstring unused inSyntaxComponentName
  ?@entityToImplementMap inAllMetamodelClassMap
:
  $rule$ ;
  @lstring ruleName ;
  $non_terminal_symbol$ ? ruleName ;
  @L_EXsignature listeDeTypesFormels ;
  @typeVariablesMap tableArgumentsMethode [emptyMap] ;
  @typeListeTypesEtNomsArgMethode listeTypeEtNomsArguments ;
  @L_signature signatureForGrammar ;
  <formal_arguments_list>
    !ioEntitiesMap
    ?listeDeTypesFormels 
    !?tableArgumentsMethode
    ?listeTypeEtNomsArguments
    !?tableEnAvant
    ?signatureForGrammar
  ;
  @lstring returnedEntity ;
  @lstring mdaResultVariableName ;
  select
    returnedEntity := [@lstring new !"" !here] ;
    mdaResultVariableName := [@lstring new !"" !here] ;
  or
    $->$ ;
    $mda_type_name$ ? returnedEntity ;
    [inAllMetamodelClassMap searchKey !returnedEntity ?* ?* ?* ?* ?* ?* ?* ?*] ;
    select
      mdaResultVariableName := [@lstring new !"" !here] ; # No variable
    or
      $identifier$ ? mdaResultVariableName ;
    end select ;
  end select ;
#--- Search non terminal symbol definition
  @M_nonterminalSymbolAlts nonterminalSymbolAltMap ;
  [ioNonTerminalSymbolMapForSyntax searchKey !ruleName ?nonterminalSymbolAltMap] ;
#--- Verifier la declaration de la rule et la compatibilite des arguments effectifs avec les arguments formels
  @typeAltProductionsMap altProductionsMap [emptyMap] ;
  @lstring altName := [@lstring new !"" !here] ;
#--- Parse label declared ? If Yes, check that called non terminals are also declared with 'parse' label
  @bool hasParseLabel := [nonterminalSymbolAltMap hasKey !"parse"] ;
  @bool checkNonTerminalsAreDeclaredWithParseLabel := hasParseLabel ;
#--- Parse tag ?
  @string productionTagName ;
  select
    productionTagName := "" ;
  or
    $tag$ ;
    @lstring tagName ;
    $literal_string$ ? tagName ;
    productionTagName := [tagName string] ;
  end select ;
  @location firstBranchErrorLocation := here ;
  $:$ ;
#--- Check rule header
  @L_EXsignature signaturePremiereBrancheFournieParPrototype ;
  @lstring formalReturnedEntity ;
  [nonterminalSymbolAltMap searchKey
    !altName
    ?signaturePremiereBrancheFournieParPrototype
    ?formalReturnedEntity
  ] ;
  verifierCompatibiliteSignatures
    !signaturePremiereBrancheFournieParPrototype
    !listeDeTypesFormels
    !firstBranchErrorLocation
  ;
  if ([formalReturnedEntity string] == "") & ([returnedEntity string] != "") then
    error returnedEntity: "the non terminal declaration does not name any returned entity" ;
  elsif [returnedEntity string] != [formalReturnedEntity string] then
    @stringset superEntityStringSet ;
    [inAllMetamodelClassMap searchKey ! returnedEntity ?* ?* ?* ?* ?* ?superEntityStringSet ?* ?*] ;
    if not [superEntityStringSet hasKey ![formalReturnedEntity string]] then
      # message "superEntityStringSet : " . [superEntityStringSet description] ;
      error returnedEntity: "the '@" . [formalReturnedEntity string] . "' entity or one of its inherited entities is expected here" ;
    end if ;
  end if ;
#--- Insert in variable map attributes for returned entity
  @typeInstructionList listeInstructions [emptyList] ;
  @entityPropertyMap allPropertiesMap [emptyMap] ;
  if [mdaResultVariableName string] != "" then
    [inAllMetamodelClassMap searchKey ! returnedEntity ?* ?* ?* ?* ?* ?* ?* ?*] ;
    @AC_galgasType definitionType := [@typeGalgas_singleEntity new ! returnedEntity] ;
    @typeCplusPlusName nomCppVariable := [@typeAutomaticName new ! mdaResultVariableName] ;
    [!?tableArgumentsMethode insertSingleEntityLocalVariable ! mdaResultVariableName !definitionType !nomCppVariable] ;
    @typeInstruction instruction := [@typeInstructionDeclarationVarLocale new !nomCppVariable !definitionType] ;
    listeInstructions += !instruction ;
  elsif [returnedEntity string] != "" then
    [inAllMetamodelClassMap searchKey !returnedEntity ?allPropertiesMap ?* ?* ?* ?* ?* ?* ?*] ;
    foreach allPropertiesMap (@lstring kKey @metamodelProperty kProperty) :
      extract kProperty :
      when @metamodelAttributeProperty (@AC_galgasType kType) ->
        @lstring nomVariable := [@lstring new ![kKey string] ![returnedEntity location]] ;
        @typeCplusPlusName nomCppVariable := [@typeAutomaticName new !nomVariable] ;
        [!?tableArgumentsMethode insertEntityAttributeLocalVariable !nomVariable !kType !nomCppVariable] ;
        @typeInstruction instruction := [@typeInstructionDeclarationVarLocale new !nomCppVariable !kType] ;
        listeInstructions += !instruction ;
      when @metamodelSingleReferenceProperty (@lstring kReferenceEntityName) ->
        @AC_galgasType definitionType := [@typeGalgas_singleEntity new !kReferenceEntityName] ;
        @lstring nomVariable := [@lstring new ![kKey string] ![returnedEntity location]] ;
        @typeCplusPlusName nomCppVariable := [@typeAutomaticName new !nomVariable] ;
        [!?tableArgumentsMethode insertSingleEntityLocalVariable !nomVariable !definitionType !nomCppVariable] ;
        @typeInstruction instruction := [@typeInstructionDeclarationVarLocale new !nomCppVariable !definitionType] ;
        listeInstructions += !instruction ;
      when @metamodelMultipleReferenceProperty (@lstring kReferenceEntityName * *) ->
        @AC_galgasType definitionType := [@typeGalgas_multipleEntity new !kReferenceEntityName] ;
        @lstring nomVariable := [@lstring new ![kKey string] ![returnedEntity location]] ;
        @typeCplusPlusName nomCppVariable := [@typeAutomaticName new !nomVariable] ;
        [!?tableArgumentsMethode insertListOfEntitiesLocalVariable !nomVariable !definitionType !nomCppVariable] ;
        #log nomVariable, definitionType, nomCppVariable ;
        @typeInstruction instruction := [@typeInstructionDeclarationVarLocale new !nomCppVariable !definitionType] ;
        listeInstructions += !instruction ;
      else
      end extract ;
    end foreach ;
  end if ;
#--- Parse instructions list
  @L_ruleSyntaxSignature syntaxSignature [emptyList] ;
  <syntax_instructions_list>
    !?ioNonTerminalSymbolMapForSyntax
    !checkNonTerminalsAreDeclaredWithParseLabel
    !ioEntitiesMap
    !?listeEntitesAengendrer
    !inOptionsComponentsMapForUse
    !?tableArgumentsMethode
    !?listeInstructions
    !?tableTerminaux
    !?syntaxSignature
    !inAllMetamodelClassMap
  ;
  [!?altProductionsMap insertKey
    !altName
    !listeTypeEtNomsArguments
    !listeInstructions
    !syntaxSignature
    !here
    ![returnedEntity string]
    ![mdaResultVariableName string]
    !allPropertiesMap
  ] ;
  repeat
  while
    $label$ ;
    $identifier$ ?altName ;
    tableArgumentsMethode := [@typeVariablesMap emptyMap] ;
    @L_signature signatureForGrammarComponent ;
    <formal_arguments_list>
      !ioEntitiesMap
      ?listeDeTypesFormels 
      !? tableArgumentsMethode
      ?listeTypeEtNomsArguments
      !?tableEnAvant
      ?signatureForGrammarComponent # unused
    ;
    @lstring returnedEntity ;
    select
      returnedEntity := [@lstring new !"" !here] ;
    or
      $->$ ;
      $type_name$ ? returnedEntity ;
      [inAllMetamodelClassMap searchKey !returnedEntity ?* ?* ?* ?* ?* ?* ?* ?*] ;
    end select ;
    @location errorLocation := here ;
    $:$ ;
    @L_EXsignature signatureFournieParPrototype ;
    @lstring returnedEntityFromPrototype ;
    [nonterminalSymbolAltMap searchKey !altName ?signatureFournieParPrototype ?returnedEntityFromPrototype] ;
    verifierCompatibiliteSignatures
      !signatureFournieParPrototype
      !listeDeTypesFormels
      !errorLocation
    ;
    @typeInstructionList branchInstructionList [emptyList] ;
    @L_ruleSyntaxSignature branchSyntaxSignature [emptyList] ;
    <syntax_instructions_list>
      !?ioNonTerminalSymbolMapForSyntax
      !false
      !ioEntitiesMap
      !?listeEntitesAengendrer
      !inOptionsComponentsMapForUse
      !? tableArgumentsMethode
      !?branchInstructionList
      !?tableTerminaux
      !?branchSyntaxSignature
      !inAllMetamodelClassMap
    ;
    [!?altProductionsMap insertKey
      !altName
      !listeTypeEtNomsArguments
      !branchInstructionList
      !branchSyntaxSignature
      !here
      ![returnedEntity string]
      ![mdaResultVariableName string]
      ![@entityPropertyMap emptyMap]
    ] ;
  end repeat ;
  $end$ ;
  $rule$ ;
  $;$ ;
#--- Check if all label rules have the same syntax signature
  action checkLabelSignatures !?altProductionsMap ;
#--- Check that all label rules are implemented
  foreach nonterminalSymbolAltMap (@lstring nonTerminalAltName @L_EXsignature altSignature @lstring kReturnedEntity) :
    if [nonTerminalAltName string] != "parse" & not [altProductionsMap hasKey ![nonTerminalAltName string]] then
      error here : "the '" . [nonTerminalAltName string] . "' label is not implemented within the rule" ;
    end if ;
  end foreach ;
#--- Generate production
  @typeEntityToGenerate ea := [@typeProductionAengendrer new
    !ruleName
    !altProductionsMap
    !productionTagName
    !hasParseLabel
  ] ;
  listeEntitesAengendrer += !ea ;
#--- Generate list for grammar component
  @uint ruleIndex := 0 ;
  ioProductionRulesListForGrammar +=
   !ruleName
   !ruleIndex
   !syntaxSignature
  ;
label importSyntax
  ?!@M_nonTerminalSymbolsForGrammar unused ioNonTerminalSymbolsMap
  ?!@L_productionRules_ForGrammarComponent ioProductionRulesListForGrammar
:
  $rule$ ;
  @lstring nonterminalSymbolName ;
  $non_terminal_symbol$ ? nonterminalSymbolName ;
  @L_signature signature ;
  <formal_arguments_list> importGrammarForSemantics ?signature ;
  @lstring returnedEntity ;
  select
    returnedEntity := [@lstring new !"" !here] ;
  or
    $->$ ;
    $mda_type_name$ ? returnedEntity ;
    select
    or
      $identifier$ ? * ;
    end select ;
  end select ;
  select
  or
    $tag$ ;
    $literal_string$ ? * ;
  end select ;
  $:$ ;
  @L_ruleSyntaxSignature instructionsList [emptyList] ;
  <syntax_instructions_list> importSyntax !?instructionsList ;
  repeat
  while
    $label$ ;
    $identifier$ ? * ;
    <formal_arguments_list> parse ;
    select
    or
      $->$ ;
      $type_name$ ? * ;
    end select ;
    $:$ ;
    <syntax_instructions_list> parse ;
  end repeat ;
  @uint leftNonterminalSymbolIndex := 0 ;
  ioProductionRulesListForGrammar +=
    !nonterminalSymbolName
    !leftNonterminalSymbolIndex
    !instructionsList
  ;
  $end$ ;
  $rule$ ;
  $;$ ;
end rule ;

#------------------------------------------------------------------------------*
#              LISTE DES INSTRUCTIONS SYNTAXIQUES D'UNE PRODUCTION             *
#------------------------------------------------------------------------------*

rule <syntax_instructions_list>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?@bool inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap inAllMetamodelClassMap
:
  block ioVariablesMap.verifyVariableUsing () :
    <syntax_instructions_list_no_verif>
      !?ioNonTerminalSymbolMapForSyntax
      !inCheckNonTerminalsAreDeclaredWithParseLabel
      !ioEntitiesMap
      !?listeEntitesAengendrer
      !inOptionsComponentsMapForUse
      !?ioVariablesMap
      !?ioInstructionList
      !?tableTerminaux
      !?ioSyntaxSignature
      !inAllMetamodelClassMap
    ;
    @location positionErreur := here ;
  end block (!positionErreur) ;
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
:
  <syntax_instructions_list_no_verif> importSyntax !?ioInstructionList ;
end rule ;

#------------------------------------------------------------------------------*
#          Instruction de verification syntaxique $terminal$ ? ... ;           *
#------------------------------------------------------------------------------*

rule <syntax_instruction>
  ?!@M_nonTerminalSymbolForSyntax unused ioNonTerminalSymbolMapForSyntax
  ?@bool unused inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse unused ioEntitiesMap
  ?!@typeEntitiesToGenerateList unused listeEntitesAengendrer
  ??@M_optionComponents unused inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap unused inAllMetamodelClassMap
:
  @lstring terminalSymbolName ;
  $terminal$ ? terminalSymbolName ;
  @typeListeAttributsSemantiques listeDesAttributsFormels ;
  [tableTerminaux searchKey !terminalSymbolName ?listeDesAttributsFormels] ;
  @L_assignedVariables listeTypeEffectifs ;
  @typeCplusPlusNameList allVariablesList ;
  <input_parameters_list>
    ?listeTypeEffectifs
    ?allVariablesList
    !listeDesAttributsFormels
    !?ioVariablesMap
  ;
  @typeInstruction instruction ;
  instruction := [@typeInstructionVerifSyntaxique new !terminalSymbolName !listeTypeEffectifs] ;
  ioInstructionList += !instruction ;
  @uint terminalSymbolIndex := 0 ;
  @AC_instruction_ForGrammar i := [@T_terminalInstruction_forGrammarComponent new ![terminalSymbolName location] !terminalSymbolName !terminalSymbolIndex] ;
  ioSyntaxSignature += !i ;
  $;$ ;
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
:
  @lstring terminalSymbolName ;
  $terminal$ ? terminalSymbolName ;
  <input_parameters_list> parse ;
  @AC_instruction_ForGrammar i ;
  @uint terminalSymbolIndex := 0 ;
  i := [@T_terminalInstruction_forGrammarComponent new ![terminalSymbolName location] !terminalSymbolName !terminalSymbolIndex] ;
  ioInstructionList += !i ;
  $;$ ;
end rule ;

#------------------------------------------------------------------------------*
#    N O N - T E R M I N A L    C A L L    I N S T R U C T I O N    <p> ...  ; *
#------------------------------------------------------------------------------*

rule <syntax_instruction>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?@bool inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ?!@typeEntitiesToGenerateList unused listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse unused tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap inAllMetamodelClassMap
:
  @lstring nonterminalSymbolName ;
  $non_terminal_symbol$ ? nonterminalSymbolName ;
  @lstring altName ;
  select
    altName := [@lstring new !"" !here] ;
  or
    $parse$ ;
    altName := [@lstring new !"parse" !here] ;
  or
    $identifier$ ?altName ;
  end select ;
  @M_nonterminalSymbolAlts nonterminalSymbolAltMap ;
  [ioNonTerminalSymbolMapForSyntax searchKey !nonterminalSymbolName ?nonterminalSymbolAltMap] ;
  @L_EXsignature signatureMethode ;
  @lstring returnedEntityFromSignature ;
  [nonterminalSymbolAltMap searchKey !altName ?signatureMethode ?returnedEntityFromSignature] ;
#--- Check if parse label is declared ?
  if inCheckNonTerminalsAreDeclaredWithParseLabel & ([altName string] != "parse") then
    if not [nonterminalSymbolAltMap hasKey !"parse"] then
      error here : "As current rule has 'parse' label, every invoked non terminal symbol must be declared with 'parse' label" ;
    end if ;
  end if ;
#--- Parse effective parameter list
  @typeExpressionList parametersExpressionList ;
  <actual_parameters_list>
    !signatureMethode
    ?parametersExpressionList
    !?ioVariablesMap
    !inOptionsComponentsMapForUse
    !ioEntitiesMap
    !?ioInstructionList
  ;
#--- Parse returned value
  @lstring returnedEntityVariableName ;
  @bool isMultipleEntity := false ;
  select
    returnedEntityVariableName := [@lstring new !"" !here] ;
  or
    $->$ ;
    $identifier$ ? returnedEntityVariableName ;
  or
    $+>$ ;
    $identifier$ ? returnedEntityVariableName ;
    isMultipleEntity := true ;
  end select ;
#--- Check returned value
  if ([returnedEntityFromSignature string] == "") & ([returnedEntityVariableName string] != "") then
    error returnedEntityVariableName: "the non terminal symbol is declared with no returned entity" ;
  elsif ([returnedEntityFromSignature string] != "") & ([returnedEntityVariableName string] == "") then
    error returnedEntityVariableName: "the non terminal symbol is declared to return an instance of '@" .  [returnedEntityFromSignature  string] . "' entity" ;
  elsif ([returnedEntityFromSignature string] != "") & ([returnedEntityVariableName string] != "") then
  #--- Check type consistency
    @lstring variableEntityName ;
    if isMultipleEntity then
      @AC_galgasType type ;
      [!?ioVariablesMap searchForReadWriteAccess !returnedEntityVariableName ?type ?*] ;
      extract type->@typeGalgas_multipleEntity (?variableEntityName) error returnedEntityVariableName : messageGalgasType ;
    else
      @AC_galgasType type ;
      [!?ioVariablesMap searchForWriteAccess !returnedEntityVariableName ?type ?*] ;
      extract type->@typeGalgas_singleEntity (?variableEntityName) error returnedEntityVariableName : messageGalgasType ;
    end if ;
   #--- The returnedEntityFromSignature entity should be a super class of variableEntityName
    @bool ok := [returnedEntityFromSignature string] == [variableEntityName string] ;
    if not ok then
      @lstring testedClass := returnedEntityFromSignature ;
      loop [inAllMetamodelClassMap count] :
      while (not ok) & ([testedClass length] > 0) do
        @lstring superEntity ;
        [inAllMetamodelClassMap searchKey !testedClass ?3* ?superEntity ?4*] ;
        testedClass := superEntity ;
        ok := [testedClass string] == [variableEntityName string] ;
      end loop ;
    end if ;
    if not ok then
      error returnedEntityVariableName : "the type of this variable is '%" . [variableEntityName string]
       . "' and is not a super class of '%" . [returnedEntityFromSignature string] . "'" ;
    end if ;
  end if ;
#--- Generate instruction
  @typeInstruction instruction ;
  instruction := [@typeInstructionAppelNonTerminal new
    !nonterminalSymbolName
    !parametersExpressionList
    !altName
    !returnedEntityVariableName
    !isMultipleEntity
  ] ;
  ioInstructionList += !instruction ;
  @AC_instruction_ForGrammar i := [@T_nonterminalInstruction_forGrammarComponent new
    ![nonterminalSymbolName location]
    !nonterminalSymbolName
    !0
  ] ;
  ioSyntaxSignature += !i ;
  $;$ ;
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
:
  @lstring nonterminalSymbolName ;
  $non_terminal_symbol$ ? nonterminalSymbolName ;
  select
  or
    $parse$ ;
  or
    $identifier$ ? * ;
  end select ;
  <actual_parameters_list> parse ;
  @AC_instruction_ForGrammar i ;
  @uint nonterminalSymbolIndex := 0 ;
  i := [@T_nonterminalInstruction_forGrammarComponent new ![nonterminalSymbolName location]
                                                          !nonterminalSymbolName
                                                          !nonterminalSymbolIndex] ;
  ioInstructionList += !i ;
  select
  or
    $->$ ;
    $identifier$ ? * ;
  or
    $+>$ ;
    $identifier$ ? * ;
  end select ;
  $;$ ;
end rule ;

#------------------------------------------------------------------------------*
#    S E L E C T    I N S T R U C T I O N                                      *
#------------------------------------------------------------------------------*

rule <syntax_instruction>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?@bool inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap inAllMetamodelClassMap
:
  $select$ ;
  @location positionDebut := here ;
  @typeListeBranchesInstructions listeBranchesChoix [emptyList] ;
  @L_branchList_ForGrammarComponent selectList [emptyList] ;
  block ioVariablesMap.testBloc () :
    block ioVariablesMap.testPart () :
      @typeInstructionList firstBranchInstructionList [emptyList] ;
      @L_ruleSyntaxSignature firstBranchSyntaxSignature [emptyList] ;
      <syntax_instructions_list_no_verif>
        !?ioNonTerminalSymbolMapForSyntax
        !inCheckNonTerminalsAreDeclaredWithParseLabel
        !ioEntitiesMap
        !?listeEntitesAengendrer
        !inOptionsComponentsMapForUse
        !?ioVariablesMap
        !?firstBranchInstructionList
        !?tableTerminaux
        !?firstBranchSyntaxSignature
        !inAllMetamodelClassMap
      ;
      listeBranchesChoix += !firstBranchInstructionList ;
      selectList += !firstBranchSyntaxSignature ;
    end block (!here) ;
    $or$ ;
    repeat
      block ioVariablesMap.testPart () :
        @typeInstructionList listeInstructionsBrancheChoix [emptyList] ;
        @L_ruleSyntaxSignature syntaxSignature [emptyList] ;
        <syntax_instructions_list_no_verif>
          !?ioNonTerminalSymbolMapForSyntax
          !inCheckNonTerminalsAreDeclaredWithParseLabel
          !ioEntitiesMap
          !?listeEntitesAengendrer
          !inOptionsComponentsMapForUse
          !?ioVariablesMap
          !?listeInstructionsBrancheChoix
          !?tableTerminaux
          !?syntaxSignature
          !inAllMetamodelClassMap
        ;
        listeBranchesChoix += !listeInstructionsBrancheChoix ;
        selectList += !syntaxSignature ;
      end block (!here) ;
    while
      $or$ ;
    end repeat ;
    $end$ ;
    $select$ ;
    $;$ ;
  end block () ;
#--- Engendrer l'instruction
  @typeInstruction instruction := [@C_select_instruction new !listeBranchesChoix !positionDebut] ;
  ioInstructionList += !instruction ;
  @AC_instruction_ForGrammar i ;
  @uint v := 0 ;
  i := [@T_selectInstruction_forGrammarComponent new !positionDebut !selectList !v] ;
  ioSyntaxSignature += !i ;
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
:
  $select$ ;
  @location positionDebut := here ;
  @L_branchList_ForGrammarComponent selectList [emptyList] ;
  @L_ruleSyntaxSignature syntaxSignature [emptyList] ;
  <syntax_instructions_list_no_verif> importSyntax !?syntaxSignature ;
  selectList += !syntaxSignature ;
  $or$ ;
  repeat
    @L_ruleSyntaxSignature syntaxSignature2 [emptyList] ;
    <syntax_instructions_list_no_verif> importSyntax !?syntaxSignature2 ;
    selectList += !syntaxSignature2 ;
  while
    $or$ ;
  end repeat ;
  $end$ ;
  $select$ ;
  $;$ ;
  @AC_instruction_ForGrammar i ;
  @uint v := 0 ;
  i := [@T_selectInstruction_forGrammarComponent new !positionDebut !selectList !v] ;
  ioInstructionList += !i ;
end rule ;

#------------------------------------------------------------------------------*
#    R E P E A T    I N S T R U C T I O N                                      *
#------------------------------------------------------------------------------*

rule <syntax_instruction>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?@bool inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap inAllMetamodelClassMap
:
  $repeat$ ;
  @location positionDebut := here ;
  @typeListeBranchesInstructions listeBranchesRepeter [emptyList] ;
  @L_branchList_ForGrammarComponent whileList [emptyList] ;
  block ioVariablesMap.repeatBlock () :
    @typeInstructionList listeInstructionsPremiereBranche [emptyList] ;
    @L_ruleSyntaxSignature repeatBranchInstructionsList [emptyList] ;
    block ioVariablesMap.repeatPart () :
      <syntax_instructions_list_no_verif>
        !?ioNonTerminalSymbolMapForSyntax
        !inCheckNonTerminalsAreDeclaredWithParseLabel
        !ioEntitiesMap
        !?listeEntitesAengendrer
        !inOptionsComponentsMapForUse
        !?ioVariablesMap
        !?listeInstructionsPremiereBranche
        !?tableTerminaux
        !?repeatBranchInstructionsList
        !inAllMetamodelClassMap
       ;
      listeBranchesRepeter += !listeInstructionsPremiereBranche ;
      whileList += !repeatBranchInstructionsList ;
      @location positionErreurPremiereBranche := here ;
    end block (!positionErreurPremiereBranche) ;
    $while$ ;
    repeat
      block ioVariablesMap.repeatPart () :
        @typeInstructionList listeInstructionsBrancheRepeter [emptyList];
        @L_ruleSyntaxSignature syntaxSignature [emptyList] ;
        <syntax_instructions_list_no_verif>
          !?ioNonTerminalSymbolMapForSyntax
          !inCheckNonTerminalsAreDeclaredWithParseLabel
          !ioEntitiesMap
          !?listeEntitesAengendrer
          !inOptionsComponentsMapForUse
          !?ioVariablesMap
          !?listeInstructionsBrancheRepeter
          !?tableTerminaux
          !?syntaxSignature
          !inAllMetamodelClassMap
        ;
        listeBranchesRepeter += !listeInstructionsBrancheRepeter ;
        whileList += !syntaxSignature ;
      end block (!here) ;
    while
      $while$ ;
    end repeat ;
    $end$ ;
    $repeat$ ;
  end block () ;
  $;$ ;
#--- Engendrer l'instruction
  @typeInstruction instruction := [@C_repeat_instruction new !listeBranchesRepeter !positionDebut] ;
  ioInstructionList += !instruction ;
  @AC_instruction_ForGrammar i ;
  @uint v := 0 ;
  i := [@T_repeatInstruction_forGrammarComponent new !positionDebut !whileList !v] ;
  ioSyntaxSignature += !i ;
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
:
  $repeat$ ;
  @location positionDebut := here ;
  @L_branchList_ForGrammarComponent repeatList [emptyList] ;
  @L_ruleSyntaxSignature syntaxSignature [emptyList] ;
  <syntax_instructions_list_no_verif> importSyntax !?syntaxSignature ;
  repeatList += !syntaxSignature ;
  $while$ ;
  repeat
    syntaxSignature := [@L_ruleSyntaxSignature emptyList] ;
    <syntax_instructions_list_no_verif> importSyntax !?syntaxSignature ;
    repeatList += !syntaxSignature ;
  while
    $while$ ;
  end repeat ;
  $end$ ;
  $repeat$ ;
  $;$ ;
  @AC_instruction_ForGrammar i ;
  @uint v := 0 ;
  i := [@T_repeatInstruction_forGrammarComponent new !positionDebut !repeatList !v] ;
  ioInstructionList += !i ;
end rule ;

#------------------------------------------------------------------------------*
#    P A R S E    D O ...    R E W I N D ...    I N S T R U C T I O N          *
#    P A R S E    W H E N   ...    E L S E  ...    I N S T R U C T I O N       *
#------------------------------------------------------------------------------*

rule <syntax_instruction>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?@bool inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap inAllMetamodelClassMap
:
  $parse$ ;
  @typeInstruction instruction ;
  @L_parse_rewind_signature_list parse_rewind_signature_list [emptyList] ;
  select
    $do$ ;
    @location instructionStartLocation := here ;
    @typeListeBranchesInstructions branchList [emptyList] ;
  #--- First branch
    @L_branchList_ForGrammarComponent whileList [emptyList] ;
    @typeInstructionList firstBranchInstructionsList [emptyList] ;
    @L_ruleSyntaxSignature firstBranchSyntaxSignature [emptyList] ;
    <syntax_instructions_list_no_verif>
      !?ioNonTerminalSymbolMapForSyntax
      !inCheckNonTerminalsAreDeclaredWithParseLabel
      !ioEntitiesMap
      !?listeEntitesAengendrer
      !inOptionsComponentsMapForUse
      !?ioVariablesMap
      !?firstBranchInstructionsList
      !?tableTerminaux
      !?firstBranchSyntaxSignature
      !inAllMetamodelClassMap
    ;
    branchList += !firstBranchInstructionsList ;
    parse_rewind_signature_list += !firstBranchSyntaxSignature !instructionStartLocation ;
    foreach firstBranchSyntaxSignature (@AC_instruction_ForGrammar grammarInstruction) :
      ioSyntaxSignature += !grammarInstruction ;
    end foreach ;
  #--- One or more 'rewind' branches
    repeat
      $rewind$ ;
      @location branchStartLocation := here ;
      @typeInstructionList instructionsList [emptyList] ;
      @L_ruleSyntaxSignature syntaxSignature [emptyList] ;
      <syntax_instructions_list_no_verif>
        !?ioNonTerminalSymbolMapForSyntax
        !inCheckNonTerminalsAreDeclaredWithParseLabel
        !ioEntitiesMap
        !?listeEntitesAengendrer
        !inOptionsComponentsMapForUse
        !?ioVariablesMap
        !?instructionsList
        !?tableTerminaux
        !?syntaxSignature
        !inAllMetamodelClassMap
      ;
      branchList += !instructionsList ;
      parse_rewind_signature_list += !syntaxSignature  !branchStartLocation ;
    while
    end repeat ;
    instruction := [@C_parse_rewind_instruction new !here !branchList] ;
  or
  #--- Instruction parse when ... when ... else ... end parse ;
    @L_expression_instructionsList_list branchList [emptyList] ;
    block ioVariablesMap.testBloc () :
    #--- 'when' branches
      repeat
        block ioVariablesMap.testPart () :
          $when$ ;
          @typeExpression expression ;
          @AC_galgasType resultType ;
          <expression>
            !ioEntitiesMap
            !inOptionsComponentsMapForUse
            !?ioVariablesMap
            ?expression
            ?resultType
          ;
          $:$ ;
          @location branchStartLocation := here ;
          @typeInstructionList instructionsList [emptyList] ;
          @L_ruleSyntaxSignature syntaxSignature [emptyList] ;
          <syntax_instructions_list_no_verif>
            !?ioNonTerminalSymbolMapForSyntax
            !inCheckNonTerminalsAreDeclaredWithParseLabel
            !ioEntitiesMap
            !?listeEntitesAengendrer
            !inOptionsComponentsMapForUse
            !?ioVariablesMap
            !?instructionsList
            !?tableTerminaux
            !?syntaxSignature
            !inAllMetamodelClassMap
          ;
          branchList += !expression !instructionsList ;
          parse_rewind_signature_list += !syntaxSignature  !branchStartLocation ;
        end block (!here) ;
      while
      end repeat ;
      $else$ ;
      block ioVariablesMap.testPart () :
        @location branchStartLocation := here ;
        @typeInstructionList elseInstructionsList [emptyList] ;
        @L_ruleSyntaxSignature syntaxSignature [emptyList] ;
        <syntax_instructions_list_no_verif>
          !?ioNonTerminalSymbolMapForSyntax
          !inCheckNonTerminalsAreDeclaredWithParseLabel
          !ioEntitiesMap
          !?listeEntitesAengendrer
          !inOptionsComponentsMapForUse
          !?ioVariablesMap
          !?elseInstructionsList
          !?tableTerminaux
          !?syntaxSignature
          !inAllMetamodelClassMap
        ;
        parse_rewind_signature_list += !syntaxSignature  !branchStartLocation ;
        foreach syntaxSignature (@AC_instruction_ForGrammar grammarInstruction) :
          ioSyntaxSignature += !grammarInstruction ;
        end foreach ;
      end block (!here) ;
    end block () ;
    instruction := [@C_parse_when_else_instruction new !branchList !elseInstructionsList] ;
  end select ;
  $end$ ;
  $parse$ ;
  $;$ ;
#--- Generate instruction
  ioInstructionList += !instruction ;
#--- Check all branches have the same syntax signature
  action checkParseRewindSignatures !?parse_rewind_signature_list ;
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
:
  $parse$ ;
  select
    $do$ ;
    <syntax_instructions_list_no_verif> importSyntax !?ioInstructionList ;
    repeat
      $rewind$ ;
      <syntax_instructions_list_no_verif> parse ;
    while
    end repeat ;
  or
    repeat
      $when$ ;
      <expression> parse ;
      $:$ ;
      <syntax_instructions_list_no_verif> parse ;
    while
    end repeat ;
    $else$ ;
    <syntax_instructions_list_no_verif> importSyntax !?ioInstructionList ;
  end select ;
  $end$ ;
  $parse$ ;
  $;$ ;
end rule ;

#------------------------------------------------------------------------------*

rule <syntax_instructions_list_no_verif>
  ?!@M_nonTerminalSymbolForSyntax ioNonTerminalSymbolMapForSyntax
  ?@bool inCheckNonTerminalsAreDeclaredWithParseLabel
  ??@M_semanticsEntitiesForUse ioEntitiesMap
  ?!@typeEntitiesToGenerateList listeEntitesAengendrer
  ??@M_optionComponents inOptionsComponentsMapForUse
  ?!@typeVariablesMap ioVariablesMap
  ?!@typeInstructionList ioInstructionList
  ?!@M_terminalSymbolsMapForUse tableTerminaux
  ?!@L_ruleSyntaxSignature ioSyntaxSignature
  ??@entityToImplementMap inAllMetamodelClassMap
:
  @entityPropertyMap propertyMap [emptyMap] ;
  @stringset constraintPropertyDefinitionSet [emptySet] ;
  @callInstructionSharedPropertySignatureMap callInstructionSharedPropertySignatureMap [emptyMap] ;
  @typeTableEnAvant tableEnAvant [emptyMap] ;
  repeat
  while
    <syntax_instruction>
      !?ioNonTerminalSymbolMapForSyntax
      !inCheckNonTerminalsAreDeclaredWithParseLabel
      !ioEntitiesMap
      !?listeEntitesAengendrer
      !inOptionsComponentsMapForUse
      !?ioVariablesMap
      !?ioInstructionList
      !?tableTerminaux
      !?ioSyntaxSignature
      !inAllMetamodelClassMap
    ;
  while
    <semantic_instruction>
      !tableEnAvant
      !ioEntitiesMap
      !inOptionsComponentsMapForUse
      !?ioVariablesMap
      !?ioInstructionList
      !?propertyMap
      !?constraintPropertyDefinitionSet
      !?callInstructionSharedPropertySignatureMap
    ;
  end repeat ;
label importSyntax
  ?!@L_ruleSyntaxSignature ioInstructionList
:
  repeat
  while
    <syntax_instruction> importSyntax !?ioInstructionList ;
  while
    <semantic_instruction> parse ;
  end repeat ;
end rule ;

#------------------------------------------------------------------------------*

end syntax ;
