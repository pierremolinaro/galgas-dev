#---------------------------------------------------------------------------*
#                                                                           *
#  Generate predefined types                                                *
#                                                                           *
#  Copyright (C) 2010, ..., 2010 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

semantics predefinedTypes :
  import "semanticGeneration.gSemantics" ;

  import semantics semanticGeneration in "semanticGeneration.gSemantics" ;

#---------------------------------------------------------------------------*

routine appendPredefinedListTypeAST
  ?!@semanticDeclarationListAST ioDeclarationListASTs
  ??@string inElementTypeName
:
  @attributeInCollectionListAST attributeList [emptyList] ;
  attributeList +=
    ![@lstring new !inElementTypeName !here]
    ![@lstring new !"mValue" !here]
  ;
  ioDeclarationListASTs += ![@listDeclarationAST new
    ![@lstring new !inElementTypeName . "list" !here]
    !attributeList
  ] ;
end routine ;

#---------------------------------------------------------------------------*

routine appendStructASTForTypeWithLocation
  ?!@semanticDeclarationListAST ioDeclarationListASTs
  ??@string inElementTypeName
:
  @attributeInCollectionListAST attributeList [emptyList] ;
  attributeList += ![@lstring new !inElementTypeName !here] ![@lstring new !inElementTypeName !here] ;
  attributeList += ![@lstring new !"location" !here] ![@lstring new !"location" !here] ;
  ioDeclarationListASTs += ![@structDeclarationAST new
    ![@lstring new !"l" . inElementTypeName !here]
    !attributeList
  ] ;
end routine ;

#---------------------------------------------------------------------------*

routine appendPredefinedTypesASTs
  ?!@semanticDeclarationListAST ioDeclarationListASTs
:
  ioDeclarationListASTs += ![@objectPredefinedTypeAST new] ; # Type, super type
#  ioDeclarationListASTs += ![@genericTypeTypeAST new !"enumerable" !"object"] ;
#  ioDeclarationListASTs += ![@genericTypeTypeAST new !"class" !"object"] ;
#  ioDeclarationListASTs += ![@genericTypeTypeAST new !"struct" !"object"] ;
#  ioDeclarationListASTs += ![@genericTypeTypeAST new !"enum" !"object"] ;
#  ioDeclarationListASTs += ![@genericTypeTypeAST new !"mapproxy" !"object"] ;
#  ioDeclarationListASTs += ![@genericTypeTypeAST new !"list" !"enumerable"] ;
#  ioDeclarationListASTs += ![@genericTypeTypeAST new !"sortedlist" !"enumerable"] ;
#  ioDeclarationListASTs += ![@genericTypeTypeAST new !"map" !"enumerable"] ;
#  ioDeclarationListASTs += ![@genericTypeTypeAST new !"listmap" !"enumerable"] ;

  ioDeclarationListASTs += ![@boolPredefinedTypeAST new !"bool" ![@predefinedTypeKindEnum predefined_bool]] ;
  ioDeclarationListASTs += ![@uintPredefinedTypeAST new !"uint" ![@predefinedTypeKindEnum predefined_uint]] ;
  ioDeclarationListASTs += ![@uint64PredefinedTypeAST new !"uint64" ![@predefinedTypeKindEnum predefined_uint64]] ;
  ioDeclarationListASTs += ![@sintPredefinedTypeAST new !"sint" ![@predefinedTypeKindEnum predefined_sint]] ;
  ioDeclarationListASTs += ![@sint64PredefinedTypeAST new !"sint64" ![@predefinedTypeKindEnum predefined_sint64]] ;
  ioDeclarationListASTs += ![@charPredefinedTypeAST new !"char" ![@predefinedTypeKindEnum predefined_char]] ;
  ioDeclarationListASTs += ![@doublePredefinedTypeAST new !"double" ![@predefinedTypeKindEnum predefined_double]] ;
  ioDeclarationListASTs += ![@stringPredefinedTypeAST new !"string" ![@predefinedTypeKindEnum predefined_string]] ;
  ioDeclarationListASTs += ![@locationPredefinedTypeAST new !"location" ![@predefinedTypeKindEnum predefined_location]] ;
  ioDeclarationListASTs += ![@stringsetPredefinedTypeAST new !"stringset" ![@predefinedTypeKindEnum predefined_stringset]] ;
  ioDeclarationListASTs += ![@functionPredefinedTypeAST new !"function" ![@predefinedTypeKindEnum predefined_function]] ;
  ioDeclarationListASTs += ![@typePredefinedTypeAST new !"type" ![@predefinedTypeKindEnum predefined_type]] ;
  ioDeclarationListASTs += ![@binarysetPredefinedTypeAST new !"binaryset" ![@predefinedTypeKindEnum predefined_binaryset]] ;
  ioDeclarationListASTs += ![@dataPredefinedTypeAST new !"data" ![@predefinedTypeKindEnum predefined_data]] ;

  appendPredefinedListTypeAST !?ioDeclarationListASTs !"luint" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"uint" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"uint64" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"function" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"string" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"lstring" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"type" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"object" ;

  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"bool" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"uint" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"uint64" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"sint" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"sint64" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"char" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"double" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"string" ;
end routine ;

#---------------------------------------------------------------------------*
#                                                                           *
#           G E N E R A T I O N    F I L E W R A P P E R S                  *
#                                                                           *
#---------------------------------------------------------------------------*

filewrapper predefinedTypeGenerationTemplateEX in "../generation_templates/ex_type_generation" {
} {
  template predefinedTypesImplementation "predefined_types.cpp.gTemplate"
  ;
}

#---------------------------------------------------------------------------*

filewrapper predefinedTypeGenerationTemplate in "../generation_templates/type_generation" {
} {
  template primitiveTypesHeaderPrologue "primitive-types-predeclaration-prologue.h.gTemplate"
    ?@stringset TYPE_LIST
  ;

  template predefinedTypesHeaderPrologue "types-predeclaration-prologue.h.gTemplate"
    ?@stringset TYPE_LIST
  ;

  template sint_type "sint-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template sint64_type "sint64-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template uint_type "uint-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template uint64_type "uint64-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template bool_type "bool-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template binaryset_type "binaryset-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template function_type "function-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template type_type "type-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template location_type "location-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template data_type "data-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template char_type "char-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template double_type "double-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template string_type "string-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
  
  template stringset_type "stringset-type.h.gTemplate"
    ?@string TYPE_IDENTIFIER
  ;
}

#---------------------------------------------------------------------------*

reader @semanticDeclarationForGeneration appendPrimitiveTypeDeclaration -> @string outHeader :
  outHeader := "" ;end reader ;

#---------------------------------------------------------------------------*

override reader @primitiveTypeForGeneration appendPrimitiveTypeDeclaration -> @string outHeader :
  switch mKind
  when predefined_uint :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.uint_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_uint64 :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.uint64_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_sint :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.sint_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_sint64 :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.sint64_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_char :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.char_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_double :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.double_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_string :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.string_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_stringset :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.stringset_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_bool :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.bool_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_binaryset :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.binaryset_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_function :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.function_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_location :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.location_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_type :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.type_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  when predefined_data :
    outHeader := [filewrapper predefinedTypeGenerationTemplate.data_type
      ![mTypeIndex identifierRepresentation]
    ] ;
  end switch ;
  outHeader .= [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    ![mTypeIndex key]
    ![mTypeIndex identifierRepresentation]
    ![mTypeIndex mIsConcrete]
    ![mTypeIndex mSuperType]
    ![mTypeIndex mCurrentTypedAttributeList]
    ![mTypeIndex mInheritedTypedAttributeList]
    ![mTypeIndex mConstructorMap]
    ![mTypeIndex mReaderMap]
    ![mTypeIndex mModifierMap]
    ![mTypeIndex mInstanceMethodMap]
    ![mTypeIndex mClassMethodMap]
    ![mTypeIndex mEnumerationDescriptor]
    ![mTypeIndex mHandledOperatorFlags]
    ![mTypeIndex mAddAssignOperatorArguments]
    ![mTypeIndex mNativeAttributeList]
    ![mTypeIndex mEnumConstantList]
    ![mTypeIndex mPrivateDataMemberList]
    ![mTypeIndex mWithAccessorMap]
  ] ;
end reader ;

#---------------------------------------------------------------------------*
#                                                                           *
# G E N E R A T E    P R E D E F I N E D    T Y P E    F I L E S            *
#                                                                           *
#---------------------------------------------------------------------------*

routine generatePredefinedTypeFiles ??@string inDirectory :
  if inDirectory != "" then
    if not [inDirectory directoryExists] then
      error here : "the '" . inDirectory . "' directory does not exist" ;
    else
    #------ Add predefined types to semantics declarations
      @semanticDeclarationListAST predefinedTypeASTlist [emptyList] ;
      appendPredefinedTypesASTs !?predefinedTypeASTlist ;
    #------ Build Semantic Context
      @semanticContext semanticContext ;
      buildSemanticContext
        !predefinedTypeASTlist
        !here
        ?semanticContext
      ;
    #------ Semantic Analysis
      @semanticDeclarationListForGeneration decoratedDeclarationListForGeneration ;
      performSemanticAnalysis
        !predefinedTypeASTlist
        !semanticContext
        ?decoratedDeclarationListForGeneration
      ;
    #------ Generate primitive types declaration file
      @string generatedCode := [filewrapper predefinedTypeGenerationTemplate.primitiveTypesHeaderPrologue
        ![semanticContext->mTypeMap allKeys]
      ] ;
      foreach decoratedDeclarationListForGeneration do
        generatedCode .= [mDeclaration appendPrimitiveTypeDeclaration] ;
      end foreach ;
      if [@uint errorCount] == 0 then
        [@string generateFile
          !inDirectory
          !"primitive-types.h"
          !"//"
          !"\n\n" # Defaut user zone1
          !generatedCode
          !"\n\n" # Defaut user zone2
          !"#endif\n"
        ] ;
      end if ;
    #------ Generate predefined types declaration file
      generatedCode := [filewrapper predefinedTypeGenerationTemplate.predefinedTypesHeaderPrologue
        ![semanticContext->mTypeMap allKeys]
      ] ;
      foreach decoratedDeclarationListForGeneration do
        generatedCode .= [mDeclaration appendDeclaration] ;
      end foreach ;
      if [@uint errorCount] == 0 then
        [@string generateFile
          !inDirectory
          !"predefined-types.h"
          !"//"
          !"\n\n" # Defaut user zone1
          !generatedCode
          !"\n\n" # Defaut user zone2
          !"#endif\n"
        ] ;
      end if ;
    #------ Generate predefined types implementation
      generatedCode := [filewrapper predefinedTypeGenerationTemplateEX.predefinedTypesImplementation] ;
      foreach decoratedDeclarationListForGeneration do
        generatedCode .= [mDeclaration appendSpecificImplementation] ;
        generatedCode .= [mDeclaration appendTypeGenericImplementation] ;
      end foreach ;
      if [@uint errorCount] == 0 then
        [@string generateFile
          !inDirectory
          !"predefined-types.cpp"
          !"//"
          !"\n\n" # Defaut user zone1
          !generatedCode
          !"\n\n" # Defaut user zone2
          !""
        ] ;
      end if ;
    end if ;
  end if ;
end routine ;

#---------------------------------------------------------------------------*

end semantics ;
