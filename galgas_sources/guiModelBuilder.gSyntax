#---------------------------------------------------------------------------*
#                                                                           *
#  'gui' component parser, as model builder                                 *
#                                                                           *
#  Copyright (C) 2007, ..., 2007 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax guiModelBuilder :

import lexique galgas_scanner in "galgas_scanner.gLexique" ;
import semantics guiMetamodel in "guiMetamodel.gSemantics" ;

#---------------------------------------------------------------------------*

nonterminal <gui_component_start_symbol> !@guiComponentRoot outGuiComponentRoot ;

nonterminal <gui_attribute> ?!@guiAttributeList ioAttributes ;

nonterminal <gui_label> ?!@guiLabelList ioLabels ;

#---------------------------------------------------------------------------*
#                                                                           *
#   G U I    C O M P O N E N T                                              *
#                                                                           *
#---------------------------------------------------------------------------*

rule <gui_component_start_symbol>
  !@guiComponentRoot outGuiComponentRoot
:
  $gui$ ;
  $identifier$ ?@lstring mGUIName ;
  $literal_string$ ?@lstring mGUIClass ;
  $:$ ;
#--- Old style imports
#--- Loop on import declarations
  repeat
  while
    $import__$ ;
    $literal_string$ ? * ;
    $;$ ;
  while
    $import$ ;
    select
      $semantics$ ;
    or
      $lexique$ ;
   or
      $grammar$ ;
    or
      $syntax$ ;
    or
      $option$ ;
    end select ;
    $identifier$ ?* ;
    $in$ ;
    $literal_string$ ? * ;
    $;$ ;
  end repeat ;
#--- Attributes and labels
  @guiAttributeList mAttributes [emptyList] ;
  @guiLabelList mLabels [emptyList] ;
  repeat
  while
    <gui_attribute> !?mAttributes ;
  while
    <gui_label> !?mLabels ;
  end repeat ;
#---
  $end$ ;
  $gui$ ;
  $;$ ;
  outGuiComponentRoot := [@guiComponentRoot new
    !mGUIName
    !mGUIClass
    !mAttributes
    !mLabels
  ] ;
end rule ;

#---------------------------------------------------------------------------*

rule <gui_attribute> ?!@guiAttributeList ioAttributes :
  $identifier$ ?@lstring mKey ;
  $:$ ;
  $literal_string$ ?@lstring mValue ;
  $;$ ;
  ioAttributes += !mKey !mValue ;
end rule ;

#---------------------------------------------------------------------------*

rule <gui_label> ?!@guiLabelList ioLabels :
  $label$ ;
  $terminal$ ?@lstring mAttribute1 ;
  $,$ ;
  $terminal$ ?@lstring mAttribute2 ;
  $;$ ;
  ioLabels += !mAttribute1 !mAttribute2 ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;