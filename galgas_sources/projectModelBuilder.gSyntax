#---------------------------------------------------------------------------*
#                                                                           *
#  'project' model builder                                                  *
#                                                                           *
#  Copyright (C) 2007, ..., 2011 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax projectModelBuilder ("galgas_scanner.gLexique") :

import "projectMetamodel.gSemantics" ;

#---------------------------------------------------------------------------*

nonterminal <project_component_start_symbol>
  !@projectComponentAST outProjectComponentAST
;

#---------------------------------------------------------------------------*

rule <project_component_start_symbol>
  !@projectComponentAST outProjectComponentAST
:
  $project$ ;
  $identifier$ ? @lstring projectName ;
  @luint majorVersion ;
  @luint minorVersion ;
  @luint revisionVersion ;
  select
    majorVersion := [@luint new !0 !here] ;
    minorVersion := [@luint new !0 !here] ;
    revisionVersion := [@luint new !0 !here] ;
    warning here: "required from GALGAS 2.4.3: the project version '(n:p:q)', where n, p and q are unsigned integer constants representing respectivly major version, minor version and revision." ;
  or
    $($ ;
    $unsigned_literal_integer$ ? majorVersion ;
    $:$ ;
    $unsigned_literal_integer$ ? minorVersion ;
    $:$ ;
    $unsigned_literal_integer$ ? revisionVersion ;
    $)$ ;
  end select ;
  $->$ ;
  $literal_string$ ? @lstring targetName ;
  $:$ ;
  @projectSourceList projectSourceList [emptyList] ;
  @lstringlist generateFeatureList [emptyList] ;
  @qualifiedFeatureList qualifiedFeatureList [ emptyList] ;
  repeat
  while
    $feature$ ;
    select
      $identifier$ ? @lstring featureName ;
      $:$ ;
      $literal_string$ ? @lstring featureValue ;
      qualifiedFeatureList += !featureName !featureValue ;
    or
      $literal_string$ ? @lstring featureName ;
      generateFeatureList += !featureName ;
    end select ;
    $;$ ;
  while
    @sourceFileKind sourceFileKind ;
    select
      $extern$ ;
      sourceFileKind := [@sourceFileKind externSourceFile] ;
    or
      $gui$ ;
      sourceFileKind := [@sourceFileKind guiSourceFile] ;
    or
      $lexique$ ;
      sourceFileKind := [@sourceFileKind lexiqueSourceFile] ;
    or
      $option$ ;
      sourceFileKind := [@sourceFileKind optionSourceFile] ;
    or
      $semantics$ ;
      sourceFileKind := [@sourceFileKind semanticsSourceFile] ;
    or
      $syntax$ ;
      sourceFileKind := [@sourceFileKind syntaxSourceFile] ;
    or
      $grammar$ ;
      sourceFileKind := [@sourceFileKind grammarSourceFile] ;
    or
      $program$ ;
      sourceFileKind := [@sourceFileKind programSourceFile] ;
    end select ;
    $literal_string$ ?@lstring filePath ;
    $;$ ;
    projectSourceList +=
      !sourceFileKind
      !filePath
    ;
  end repeat ;
  $end$ ;
  outProjectComponentAST := [@projectComponentAST new
    !projectSourceList
    !majorVersion
    !minorVersion
    !revisionVersion
    !generateFeatureList
    !qualifiedFeatureList
    !projectName
    !targetName
    !here
  ] ;
  $project$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;