#---------------------------------------------------------------------------*
#                                                                           *
#  'project' model builder                                                  *
#                                                                           *
#  Copyright (C) 2007, ..., 2007 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax projectModelBuilder :
with "galgas_scanner.gLexique" ;

import lexique galgas_scanner in "galgas_scanner.gLexique" ;
import semantics projectMetamodel in "projectMetamodel.gSemantics" ;
import "projectMetamodel.gSemantics" ;

#---------------------------------------------------------------------------*

nonterminal <project_component_start_symbol>
  !@projectComponentAST outProjectComponentAST
;

#---------------------------------------------------------------------------*

rule <project_component_start_symbol>
  !@projectComponentAST outProjectComponentAST
:
  $project$ ;
  $identifier$ ? @lstring projectName ;
  $->$ ;
  $literal_string$ ? @lstring targetName ;
  $:$ ;
  @projectSourceList projectSourceList [emptyList] ;
  @lstringlist generateFeatureList [emptyList] ;
  repeat
  while
    $feature$ ;
    $literal_string$ ? @lstring featureName ;
    generateFeatureList += !featureName ;
    $;$ ;
  while
    @sourceFileKind sourceFileKind ;
    select
      $extern$ ;
      sourceFileKind := [@sourceFileKind externSourceFile] ;
    or
      $gui$ ;
      sourceFileKind := [@sourceFileKind guiSourceFile] ;
    or
      $lexique$ ;
      sourceFileKind := [@sourceFileKind lexiqueSourceFile] ;
    or
      $option$ ;
      sourceFileKind := [@sourceFileKind optionSourceFile] ;
    or
      $semantics$ ;
      sourceFileKind := [@sourceFileKind semanticsSourceFile] ;
    or
      $syntax$ ;
      sourceFileKind := [@sourceFileKind syntaxSourceFile] ;
    or
      $grammar$ ;
      sourceFileKind := [@sourceFileKind grammarSourceFile] ;
    or
      $program$ ;
      sourceFileKind := [@sourceFileKind programSourceFile] ;
    end select ;
    $literal_string$ ?@lstring filePath ;
    $;$ ;
    projectSourceList +=
      !sourceFileKind
      !filePath
    ;
  end repeat ;
  $end$ ;
  outProjectComponentAST := [@projectComponentAST new
    !projectSourceList
    !generateFeatureList
    !projectName
    !targetName
    !here
  ] ;
  $project$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;