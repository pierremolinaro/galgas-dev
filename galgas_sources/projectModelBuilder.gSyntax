#---------------------------------------------------------------------------*
#                                                                           *
#  'project' model builder                                                  *
#                                                                           *
#  Copyright (C) 2007, ..., 2011 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax projectModelBuilder ("galgas_scanner.gLexique")  feature translate :

import "projectMetamodel.gSemantics" ;

#---------------------------------------------------------------------------*

nonterminal <project_component_start_symbol>
  !@projectComponentAST outProjectComponentAST
;

#---------------------------------------------------------------------------*

rule <project_component_start_symbol>
  !@projectComponentAST outProjectComponentAST
:
  $project$ ;
  $identifier$ ??@lstring projectName :> ?* ?* ;
  $($ ;
  $unsigned_literal_integer$ ??@luint majorVersion ;
  $:$ ;
  $unsigned_literal_integer$ ??@luint minorVersion ;
  $:$ ;
  $unsigned_literal_integer$ ??@luint revisionVersion ;
  $)$ ;
  $->$ ;
  $literal_string$ ? @lstring targetName ;
  $:$ :> ?@string sep ?* ;
  send sep . "{" ;
  @projectSourceList projectSourceList [emptyList] ;
  @lstringlist generateFeatureList [emptyList] ;
  @qualifiedFeatureList qualifiedFeatureList [ emptyList] ;
  repeat
  while
    $feature$ :> ?@string sep ?* ;
    send sep ;
    select
      $identifier$ ? @lstring featureName :> ?* ?* ;
      send "%" . featureName ;
      $:$ ;
      $literal_string$ ? @lstring featureValue ;
      qualifiedFeatureList += !featureName !featureValue ;
    or
      $literal_string$ ? @lstring featureName :> ?* ?* ;
      send "%" . featureName ;
      generateFeatureList += !featureName ;
    end select ;
    $;$ :> ?* ?* ;
  while
    @sourceFileKind sourceFileKind ;
    select
      $extern$ :> ??@string sep ?* ;
      send sep ;
      sourceFileKind := [@sourceFileKind externSourceFile] ;
    or
      $gui$ :> ??@string sep ?* ;
      send sep ;
      sourceFileKind := [@sourceFileKind guiSourceFile] ;
    or
      $lexique$ :> ??@string sep ?* ;
      send sep ;
      sourceFileKind := [@sourceFileKind lexiqueSourceFile] ;
    or
      $option$ :> ??@string sep ?* ;
      send sep ;
      sourceFileKind := [@sourceFileKind optionSourceFile] ;
    or
      $semantics$ :> ??@string sep ?* ;
      send sep ;
      sourceFileKind := [@sourceFileKind semanticsSourceFile] ;
    or
      $syntax$ :> ??@string sep ?* ;
      send sep ;
      sourceFileKind := [@sourceFileKind syntaxSourceFile] ;
    or
      $grammar$ :> ??@string sep ?* ;
      send sep ;
      sourceFileKind := [@sourceFileKind grammarSourceFile] ;
    or
      $program$ :> ??@string sep ?* ;
      send sep ;
      sourceFileKind := [@sourceFileKind programSourceFile] ;
    end select ;
    $literal_string$ ?@lstring filePath :> ?* ?* ;
    send if sourceFileKind == [@sourceFileKind externSourceFile] then
      "\"hand_coded_sources/" . filePath . "\""
    else 
      "\"galgas-sources/" . [filePath stringByDeletingPathExtension] . ".galgas\""
    end ;
    $;$ :> ?* ?* ;
    projectSourceList +=
      !sourceFileKind
      !filePath
    ;
  end repeat ;
  $end$ :> ?sep ?* ;
  send sep . "}" ;
  outProjectComponentAST := [@projectComponentAST new
    !projectSourceList
    !majorVersion
    !minorVersion
    !revisionVersion
    !generateFeatureList
    !qualifiedFeatureList
    !projectName
    !targetName
    !here
  ] ;
  $project$ :> ?* ?* ;
  $;$ :> ?* ?* ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;