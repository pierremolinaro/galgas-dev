#---------------------------------------------------------------------------*
#                                                                           *
#  semantics definitions for semantics component                            *
#                                                                           *
#  Copyright (C) 2010, ..., 2010 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

semantics buildSemanticContext :
  import "semanticsClassMethods.gSemantics" ;
  import "semanticsInstanceMethods.gSemantics" ;
  import "semanticsModifiers.gSemantics" ;
  import "semanticsReaders.gSemantics" ;
  import "semanticsConstructors.gSemantics" ;

  import semantics semanticsClassMethods in "semanticsClassMethods.gSemantics" ;
  import semantics semanticsInstanceMethods in "semanticsInstanceMethods.gSemantics" ;
  import semantics semanticsModifiers in "semanticsModifiers.gSemantics" ;
  import semantics semanticsReaders in "semanticsReaders.gSemantics" ;
  import semantics semanticsConstructors in "semanticsConstructors.gSemantics" ;

#---------------------------------------------------------------------------*

routine appendPredefinedListTypeAST
  ?!@semanticDeclarationListAST ioDeclarationListASTs
  ??@string inElementTypeName
:
  @attributeInCollectionListAST attributeList [emptyList] ;
  attributeList +=
    ![@lstring new !inElementTypeName !here]
    ![@lstring new !"mValue" !here]
  ;
  ioDeclarationListASTs += ![@listDeclarationAST new
    ![@lstring new !inElementTypeName . "list" !here]
    !attributeList
  ] ;
end routine ;

#---------------------------------------------------------------------------*

routine appendStructASTForTypeWithLocation
  ?!@semanticDeclarationListAST ioDeclarationListASTs
  ??@string inElementTypeName
:
  @attributeInCollectionListAST attributeList [emptyList] ;
  attributeList += ![@lstring new !inElementTypeName !here] ![@lstring new !inElementTypeName !here] ;
  attributeList += ![@lstring new !"location" !here] ![@lstring new !"location" !here] ;
  ioDeclarationListASTs += ![@structDeclarationAST new
    ![@lstring new !"l" . inElementTypeName !here]
    !attributeList
  ] ;
end routine ;

#---------------------------------------------------------------------------*

routine appendPredefinedTypesASTs
  ?!@semanticDeclarationListAST ioDeclarationListASTs
:
  ioDeclarationListASTs += ![@genericTypeTypeAST new !"root" !""] ; # Type, super type
  ioDeclarationListASTs += ![@genericTypeTypeAST new !"enumerable" !"root"] ;
  ioDeclarationListASTs += ![@genericTypeTypeAST new !"list" !"enumerable"] ;
  ioDeclarationListASTs += ![@genericTypeTypeAST new !"sortedlist" !"enumerable"] ;
#  ioDeclarationListASTs += ![@genericTypeTypeAST new !"set" !"enumerable"] ;
  ioDeclarationListASTs += ![@genericTypeTypeAST new !"map" !"enumerable"] ;
  ioDeclarationListASTs += ![@genericTypeTypeAST new !"listmap" !"enumerable"] ;

  ioDeclarationListASTs += ![@boolPredefinedTypeAST new !"bool"] ;
  ioDeclarationListASTs += ![@uintPredefinedTypeAST new !"uint"] ;
  ioDeclarationListASTs += ![@uint64PredefinedTypeAST new !"uint64"] ;
  ioDeclarationListASTs += ![@sintPredefinedTypeAST new !"sint"] ;
  ioDeclarationListASTs += ![@sint64PredefinedTypeAST new !"sint64"] ;
  ioDeclarationListASTs += ![@charPredefinedTypeAST new !"char"] ;
  ioDeclarationListASTs += ![@doublePredefinedTypeAST new !"double"] ;
  ioDeclarationListASTs += ![@stringPredefinedTypeAST new !"string"] ;
  ioDeclarationListASTs += ![@locationPredefinedTypeAST new !"location"] ;
  ioDeclarationListASTs += ![@stringsetPredefinedTypeAST new !"stringset"] ;
  ioDeclarationListASTs += ![@objectPredefinedTypeAST new !"object"] ;
  ioDeclarationListASTs += ![@functionPredefinedTypeAST new !"function"] ;
  ioDeclarationListASTs += ![@typePredefinedTypeAST new !"type"] ;
  ioDeclarationListASTs += ![@binarysetPredefinedTypeAST new !"binaryset"] ;
  ioDeclarationListASTs += ![@dataPredefinedTypeAST new !"data"] ;

  appendPredefinedListTypeAST !?ioDeclarationListASTs !"luint" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"uint" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"uint64" ;
#  appendPredefinedListTypeAST !?ioDeclarationListASTs !"sint" ;
#  appendPredefinedListTypeAST !?ioDeclarationListASTs !"sint64" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"object" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"function" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"string" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"lstring" ;
  appendPredefinedListTypeAST !?ioDeclarationListASTs !"type" ;

  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"bool" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"uint" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"uint64" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"sint" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"sint64" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"char" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"double" ;
  appendStructASTForTypeWithLocation !?ioDeclarationListASTs !"string" ;
end routine ;

#---------------------------------------------------------------------------*
#                                                                           *
#  R O U T I N E   M A P                                                    *
#                                                                           *
#---------------------------------------------------------------------------*

map @routineMap {
  @formalParameterSignature mRoutineSignature ;
  insert insertKey error message "the '%K' routine has been already declared in %L" ;
  search searchKey error message "the '%K' routine is not declared" ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#  F U N C T I O N   M A P                                                  *
#                                                                           *
#---------------------------------------------------------------------------*

map @functionMap {
  @unifiedTypeMapIndexList mFunctionSignature ;
  @unifiedTypeMapIndex mResultTypeIndex ;
  insert insertKey error message "the '%K' function has been already declared in %L" ;
  search searchKey error message "the '%K' function is not declared" ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#  F I L E W R A P P E R   M A P                                            *
#                                                                           *
#---------------------------------------------------------------------------*

map @filewrapperTemplateMap {
  @unifiedTypeMapIndexList mTemplateSignature ;
  @lstring mFilewrapperTemplatePath ;
  insert insertKey error message "the '%K' filewrapper template has been already declared in %L" ;
  search searchKey error message "the '%K' filewrapper template is not declared" ;
}

#---------------------------------------------------------------------------*

map @filewrapperMap {
  @lstring mFilewrapperPath ;
  @lstringlist mFilewrapperExtensionList ;
  @filewrapperTemplateMap mFilewrapperTemplateMap ;
  insert insertKey error message "the '%K' filewrapper has been already declared in %L" ;
  search searchKey error message "the '%K' filewrapper is not declared" ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#    D E S C E N D A N T    C L A S S    L I S T M A P                      *
#                                                                           *
#---------------------------------------------------------------------------*

listmap @descendantClassListMap (@unifiedTypeMapIndexList) ;

#---------------------------------------------------------------------------*
#                                                                           *
#    S E M A N T I C    C O N T E X T                                       *
#                                                                           *
#---------------------------------------------------------------------------*

struct @semanticContext {
  @unifiedTypeMap mTypeMap ;
  @routineMap mRoutineMap ;
  @functionMap mFunctionMap ;
  @descendantClassListMap mDescendantClassListMap ;
  @filewrapperMap mFilewrapperMap ;
  @semanticDeclarationListAST mOrderedSemanticDeclarationListAST ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#    C O L L E C T    T Y P E S                                             *
#                                                                           *
#---------------------------------------------------------------------------*

map @collectedTypeMap {
  insert insertKey error message "the '%K' type has been already defined" ;
  search searchKey error message "the '%K' type is not declared" ;
}

#---------------------------------------------------------------------------*

mapindex @collectedTypeMapIndex (@collectedTypeMap) ;

#---------------------------------------------------------------------------*

routine collectAttributeTypes
  ??@attributeInCollectionListAST inAttributeList
  ?!@collectedTypeMap ioCollectedTypeMap
:
  foreach inAttributeList do
    [@collectedTypeMapIndex makeRegularIndex !mAttributeTypeName !?ioCollectedTypeMap ?*] ;
  end foreach ;
end routine ;

#---------------------------------------------------------------------------*

method @semanticDeclarationAST collectType
  ?!@collectedTypeMap unused ioCollectedTypeMap
:
end method ;

#---------------------------------------------------------------------------*

override method @genericTypeTypeAST collectType
  ?!@collectedTypeMap ioCollectedTypeMap
:
  [!?ioCollectedTypeMap insertKey ![@lstring new !mTypeName !here]] ;
  if mSuperTypeName != "" then
    [@collectedTypeMapIndex makeRegularIndex ![@lstring new !mSuperTypeName !here] !?ioCollectedTypeMap ?*] ;
  end if ;
end method ;

#---------------------------------------------------------------------------*

override method @predefinedTypeAST collectType
  ?!@collectedTypeMap ioCollectedTypeMap
:
  [!?ioCollectedTypeMap insertKey ![@lstring new !mPredefinedTypeName !here]] ;
end method ;

#---------------------------------------------------------------------------*

override method @mapDeclarationAST collectType
  ?!@collectedTypeMap ioCollectedTypeMap
:
  [!?ioCollectedTypeMap insertKey !mMapTypeName] ;
  collectAttributeTypes !mAttributeList !?ioCollectedTypeMap ;
end method ;

#---------------------------------------------------------------------------*

override method @mapindexDeclarationAST collectType
  ?!@collectedTypeMap ioCollectedTypeMap
:
  [!?ioCollectedTypeMap insertKey !mMapIndexTypeName] ;
end method ;

#---------------------------------------------------------------------------*

override method @enumDeclarationAST collectType
  ?!@collectedTypeMap ioCollectedTypeMap
:
  [!?ioCollectedTypeMap insertKey !mEnumTypeName] ;
end method ;

#---------------------------------------------------------------------------*

override method @sortedListDeclarationAST collectType
  ?!@collectedTypeMap ioCollectedTypeMap
:
  [!?ioCollectedTypeMap insertKey !mSortedListTypeName] ;
  collectAttributeTypes !mAttributeList !?ioCollectedTypeMap ;
end method ;

#---------------------------------------------------------------------------*

override method @structDeclarationAST collectType
  ?!@collectedTypeMap ioCollectedTypeMap
:
  [!?ioCollectedTypeMap insertKey !mStructTypeName] ;
  collectAttributeTypes !mAttributeList !?ioCollectedTypeMap ;
end method ;

#---------------------------------------------------------------------------*

override method @listDeclarationAST collectType
  ?!@collectedTypeMap ioCollectedTypeMap
:
  [!?ioCollectedTypeMap insertKey !mListTypeName] ;
  collectAttributeTypes !mAttributeList !?ioCollectedTypeMap ;
end method ;

#---------------------------------------------------------------------------*

override method @classDeclarationAST collectType
  ?!@collectedTypeMap ioCollectedTypeMap
:
  [!?ioCollectedTypeMap insertKey !mClassTypeName] ;
  collectAttributeTypes !mAttributeList !?ioCollectedTypeMap ;
  foreach mAbstractMethodList do
    foreach mSignature do
      [@collectedTypeMapIndex makeRegularIndex !mFormalParameterTypeName !?ioCollectedTypeMap ?*] ;
    end foreach ;
  end foreach ;
  foreach mOverridenMethodList do
    foreach mSignature do
      [@collectedTypeMapIndex makeRegularIndex !mFormalParameterTypeName !?ioCollectedTypeMap ?*] ;
    end foreach ;
  end foreach ;
  foreach mMethodList do
    foreach mSignature do
      [@collectedTypeMapIndex makeRegularIndex !mFormalParameterTypeName !?ioCollectedTypeMap ?*] ;
    end foreach ;
  end foreach ;
end method ;

#---------------------------------------------------------------------------*

override method @listmapDeclarationAST collectType
  ?!@collectedTypeMap ioCollectedTypeMap
:
  [!?ioCollectedTypeMap insertKey !mListmapTypeName] ;
  [@collectedTypeMapIndex makeRegularIndex !mAssociatedListTypeName !?ioCollectedTypeMap ?*] ;
end method ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E N T E R    T Y P E S                                                 *
#                                                                           *
#---------------------------------------------------------------------------*

abstract method @semanticDeclarationAST enterType
  ?!@semanticContext unused ioSemanticContext
;

#---------------------------------------------------------------------------*

override method @predefinedTypeAST enterType
  ?!@semanticContext ioSemanticContext
:
  @constructorMap constructorMap ;
  @readerMap readerMap ;
  @modifierMap modifierMap ;
  @instanceMethodMap instanceMethodMap ;
  @classMethodMap classMethodMap ;
  @predefinedTypeAST t := self ; # Bug in GALGAS 1
  [t getConstructorMap !?ioSemanticContext->mTypeMap ?constructorMap] ;
  [t getReaderMap !?ioSemanticContext->mTypeMap ?readerMap] ;
  [t getModifierMap !?ioSemanticContext->mTypeMap ?modifierMap] ;
  [t getInstanceMethodMap !?ioSemanticContext->mTypeMap ?instanceMethodMap] ;
  [t getClassMethodMap !?ioSemanticContext->mTypeMap ?classMethodMap] ;
  @unifiedTypeMapIndexList addAssignArgumentList ;
  [t getAddAssignArgumentList !?ioSemanticContext->mTypeMap ?addAssignArgumentList] ;
  @unifiedTypeMapIndexList minusAssignArgumentList ;
  [t getMinusAssignArgumentList !?ioSemanticContext->mTypeMap ?minusAssignArgumentList] ;
  @enumerationDescriptorList enumerationList ;
  [t getEnumerationList !?ioSemanticContext->mTypeMap ?enumerationList] ;
  @unifiedTypeMapIndex superTypeIndex ;
  [t getSuperType !?ioSemanticContext->mTypeMap ?superTypeIndex] ;
  [!?ioSemanticContext->mTypeMap insertKey
    ![@lstring new !mPredefinedTypeName !here]
    !true # Concrete Type
    !superTypeIndex
    ![@typeKindEnum primitiveType]
    ![@typedAttributeList emptyList]
    ![@typedAttributeList emptyList]
    !constructorMap
    !readerMap
    !modifierMap
    !instanceMethodMap
    !classMethodMap
    !enumerationList
    ![t getSupportedOperatorFlags]
    !addAssignArgumentList
    !minusAssignArgumentList
    ![t getNativeAttributeList]
  ] ;
end method ;

#---------------------------------------------------------------------------*

override method @genericTypeTypeAST enterType
  ?!@semanticContext ioSemanticContext
:
#--- uint type index
#  @unifiedTypeMapIndex uintType ;
#  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"uint" !here] !?ioSemanticContext->mTypeMap ?uintType] ;
#--- Super type index
  @unifiedTypeMapIndex superTypeIndex ;
  if mSuperTypeName == "" then
    superTypeIndex := [@unifiedTypeMapIndex null] ;
  else
    [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !mSuperTypeName !here] !?ioSemanticContext->mTypeMap ?superTypeIndex] ;
  end if ;
#--- Constructors
  @constructorMap constructorMap [emptyMap] ;
#--- Readers
  @readerMap readerMap [emptyMap] ;
#  enterBaseReaderWithoutArgument !?readerMap !?ioSemanticContext->mTypeMap !"length" !"uint" !false ;
#--- Modifiers
  @modifierMap modifierMap [emptyMap] ;
#--- Instance methods
  @instanceMethodMap instanceMethodMap [emptyMap] ;
#--- Enter in type map
  [!?ioSemanticContext->mTypeMap insertKey
    ![@lstring new !mTypeName !here]
    !false # Abstract Type
    !superTypeIndex
    ![@typeKindEnum listType]
    ![@typedAttributeList emptyList]
    ![@typedAttributeList emptyList]
    !constructorMap
    !readerMap
    !modifierMap
    !instanceMethodMap
    ![@classMethodMap emptyMap]
    ![@enumerationDescriptorList emptyList]
    !0
    ![@unifiedTypeMapIndexList emptyList]
    ![@unifiedTypeMapIndexList emptyList]
    ![@nativeAttributeList emptyList]
  ] ;
end method ;

#---------------------------------------------------------------------------*

override method @mapDeclarationAST enterType
  ?!@semanticContext ioSemanticContext
:
  @constructorMap constructorMap [emptyMap] ;
  @readerMap readerMap [emptyMap] ;
  @modifierMap modifierMap [emptyMap] ;
  @instanceMethodMap instanceMethodMap [emptyMap] ;
  @enumerationDescriptorList enumerationDescriptor [emptyList] ;
#--- string type index
  @unifiedTypeMapIndex stringTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"string" !here] !?ioSemanticContext->mTypeMap ?stringTypeIndex] ;
#--- lstring type index
  @unifiedTypeMapIndex lstringTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"lstring" !here] !?ioSemanticContext->mTypeMap ?lstringTypeIndex] ;
#--- current map type index
  @unifiedTypeMapIndex currentMapTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mMapTypeName !?ioSemanticContext->mTypeMap ?currentMapTypeIndex] ;
#--- Constructors
  enterConstructorWithoutArgument !?constructorMap !?ioSemanticContext->mTypeMap !"emptyMap" ![mMapTypeName string] !false ;
#--- Readers
  enterBaseReaderWithoutArgument !?readerMap !?ioSemanticContext->mTypeMap !"count" !"uint" !false ;
#--- Handle attributes
  @typedAttributeList typedAttributeList [emptyList] ;
  @formalParameterSignature insertMethodFormalArgumentList [emptyList] ;
  insertMethodFormalArgumentList += !lstringTypeIndex ![@formalArgumentPassingModeAST argumentConstantIn] ;
  @formalParameterSignature removeMethodFormalArgumentList [emptyList] ;
  removeMethodFormalArgumentList += !lstringTypeIndex ![@formalArgumentPassingModeAST argumentConstantIn] ;
  foreach mAttributeList do
    @unifiedTypeMapIndex attributeTypeIndex ;
    [@unifiedTypeMapIndex makeRegularIndex !mAttributeTypeName !?ioSemanticContext->mTypeMap ?attributeTypeIndex] ;
    typedAttributeList += !attributeTypeIndex !mAttributeName ;
    enumerationDescriptor += !attributeTypeIndex ![mAttributeName string] ;
    insertMethodFormalArgumentList += !attributeTypeIndex ![@formalArgumentPassingModeAST argumentConstantIn] ;
    removeMethodFormalArgumentList += !attributeTypeIndex ![@formalArgumentPassingModeAST argumentOut] ;
  end foreach ;
#--- Handle insert modifiers
  foreach mInsertMethodList do
    if [modifierMap hasKey ![mMethodName string]] then
      error mMethodName:"the '" . mMethodName . "' insert method is already declared" ;
    else
      [!?modifierMap insertOrReplace
        !mMethodName
        !insertMethodFormalArgumentList
        !true
      ] ;
    end if ;
  end foreach ;
#--- Handle remove modifiers
  foreach mRemoveMethodList do
    if [modifierMap hasKey ![mMethodName string]] then
      error mMethodName:"the '" . mMethodName . "' method is already declared as an insert modifier or a remove modifier" ;
    else
      [!?modifierMap insertOrReplace
        !mMethodName
        !removeMethodFormalArgumentList
        !true
      ] ;
    end if ;
  end foreach ;
#--- Handle search methods
  foreach mSearchMethodList do
    [!?instanceMethodMap insertOrReplace
      !mMethodName
      !removeMethodFormalArgumentList
      !true
    ] ;
  end foreach ;
#--- Enter "attributeForKey" readers
  foreach mAttributeList do
    const @lstring accessorName [new ![mAttributeName string] . "ForKey" ![mAttributeName location]] ;
    @unifiedTypeMapIndex attributeTypeIndex ;
    [@unifiedTypeMapIndex makeRegularIndex !mAttributeTypeName !?ioSemanticContext->mTypeMap ?attributeTypeIndex] ;
    [!?readerMap insertOrReplace
      !accessorName
      ![@unifiedTypeMapIndexList listWithValue !stringTypeIndex]
      !true
      !attributeTypeIndex
      ![@readerQualifier isBasic]
      !""
    ] ;
  end foreach ;
#--- Enter "set...Attribute...forKey" modifiers
  foreach mAttributeList do
    const @lstring accessorName [new !"set" . [mAttributeName stringByCapitalizingFirstCharacter] . "ForKey" ![mAttributeName location]] ;
    @unifiedTypeMapIndex attributeTypeIndex ;
    [@unifiedTypeMapIndex makeRegularIndex !mAttributeTypeName !?ioSemanticContext->mTypeMap ?attributeTypeIndex] ;
    @formalParameterSignature accessorFormalArgumentList [emptyList] ;
    accessorFormalArgumentList += !lstringTypeIndex ![@formalArgumentPassingModeAST argumentConstantIn] ;
    accessorFormalArgumentList += !attributeTypeIndex ![@formalArgumentPassingModeAST argumentConstantIn] ;
    [!?modifierMap insertOrReplace
      !accessorName
      !accessorFormalArgumentList
      !true
    ] ;
  end foreach ;
#--- Enter in type map
  @unifiedTypeMapIndex superTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"map" !here] !?ioSemanticContext->mTypeMap ?superTypeIndex] ;
  [!?ioSemanticContext->mTypeMap insertKey
    !mMapTypeName
    !true # Concrete Type
    !superTypeIndex
    ![@typeKindEnum mapType]
    ![@typedAttributeList emptyList]
    !typedAttributeList
    !constructorMap
    !readerMap
    !modifierMap
    !instanceMethodMap
    ![@classMethodMap emptyMap]
    !enumerationDescriptor
    !0
    ![@unifiedTypeMapIndexList emptyList]
    ![@unifiedTypeMapIndexList emptyList]
    ![@nativeAttributeList emptyList]
  ] ;
end method ;

#---------------------------------------------------------------------------*

override method @mapindexDeclarationAST enterType
  ?!@semanticContext ioSemanticContext
:
#--- Check associated map is really a map
  @typedAttributeList mapTypedAttributeList ;
  @typeKindEnum typeKindEnum ;
  [[ioSemanticContext mTypeMap] searchKey
    !mAssociatedMapTypeName
    ?2*
    ?typeKindEnum
    ?*
    ?mapTypedAttributeList
    ?10*
  ] ;
  if typeKindEnum != [@typeKindEnum mapType] then
    error mAssociatedMapTypeName : "the @" . mAssociatedMapTypeName . " should be a map type" ;
    mapTypedAttributeList := [@typedAttributeList emptyList] ;
  end if ;
#---
  @constructorMap constructorMap [emptyMap] ;
  @readerMap readerMap [emptyMap] ;
  @modifierMap modifierMap [emptyMap] ;
  @instanceMethodMap instanceMethodMap [emptyMap] ;
  @classMethodMap classMethodMap [emptyMap] ;
#--- Associated map type index
  @unifiedTypeMapIndex associatedMapTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mAssociatedMapTypeName !?ioSemanticContext->mTypeMap ?associatedMapTypeIndex] ;
#--- lstring type index
  @unifiedTypeMapIndex lstringTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"lstring" !here] !?ioSemanticContext->mTypeMap ?lstringTypeIndex] ;
#--- currentType type index
  @unifiedTypeMapIndex currentTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"lstring" !here] !?ioSemanticContext->mTypeMap ?currentTypeIndex] ;
#--- Handle map type name
  @unifiedTypeMapIndex mapTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mMapIndexTypeName !?ioSemanticContext->mTypeMap ?mapTypeIndex] ;
#--- class method
  @formalParameterSignature argList [emptyList] ;
  argList += !lstringTypeIndex ![@formalArgumentPassingModeAST argumentConstantIn] ;
  argList += !associatedMapTypeIndex ![@formalArgumentPassingModeAST argumentInOut] ;
  argList += !currentTypeIndex ![@formalArgumentPassingModeAST argumentOut] ;
  [!?classMethodMap insertOrReplace
    ![@lstring new !"makeRegularIndex" !here]
    !argList
    !false
  ] ;
#--- null constructor
  enterConstructorWithoutArgument !?constructorMap !?ioSemanticContext->mTypeMap !"null" ![mMapIndexTypeName string] !false ;
#--- readers
  enterInheritedReaderWithoutArgument !?readerMap !?ioSemanticContext->mTypeMap !"isSolved" !"bool" !false ;
  enterInheritedReaderWithoutArgument !?readerMap !?ioSemanticContext->mTypeMap !"isNull" !"bool" !false ;
#--- Enter associated map attribute names as readers
  foreach mapTypedAttributeList do
    const @lstring accessorName [new ![mAttributeName string] . "ForKey" ![mAttributeName location]] ;
    [!?readerMap insertOrReplace
      !accessorName
      ![@unifiedTypeMapIndexList emptyList]
      !true
      !mAttributeTypeIndex
      ![@readerQualifier isBasic]
      !""
    ] ;
  end foreach ;
#--- Enter type in type map
#log mMapIndexTypeName ;
  @unifiedTypeMapIndex superTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"root" !here] !?ioSemanticContext->mTypeMap ?superTypeIndex] ;
  [!?ioSemanticContext->mTypeMap insertKey
    !mMapIndexTypeName
    !true # Concrete Type
    !superTypeIndex
    ![@typeKindEnum mapIndexType]
    ![@typedAttributeList emptyList]
    !mapTypedAttributeList
    !constructorMap
    !readerMap
    !modifierMap
    !instanceMethodMap
    ![@classMethodMap emptyMap]
    ![@enumerationDescriptorList emptyList]
    !0
    ![@unifiedTypeMapIndexList emptyList]
    ![@unifiedTypeMapIndexList emptyList]
    ![@nativeAttributeList emptyList]
  ] ;
end method ;

#---------------------------------------------------------------------------*

override method @enumDeclarationAST enterType
  ?!@semanticContext ioSemanticContext
:
#---
  @constructorMap constructorMap [emptyMap] ;
  @readerMap readerMap [emptyMap] ;
  @modifierMap modifierMap [emptyMap] ;
  @instanceMethodMap instanceMethodMap [emptyMap] ;
  @classMethodMap classMethodMap [emptyMap] ;
#--- currentType type index
  @unifiedTypeMapIndex currentTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mEnumTypeName !?ioSemanticContext->mTypeMap ?currentTypeIndex] ;
#--- Constructors
  foreach mConstantList do
    enterConstructorWithoutArgument !?constructorMap !?ioSemanticContext->mTypeMap ![mValue string] ![mEnumTypeName string] !false ;
  end foreach ;
#--- readers
  foreach mEnumMessageList do
    enterBaseReaderWithoutArgument !?readerMap !?ioSemanticContext->mTypeMap ![mEnumMessageName string] !"string" !false ;
  end foreach ;
#--- Enter type in type map
#log mEnumTypeName ;
  @unifiedTypeMapIndex superTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"root" !here] !?ioSemanticContext->mTypeMap ?superTypeIndex] ;
  [!?ioSemanticContext->mTypeMap insertKey
    !mEnumTypeName
    !true # Concrete Type
    !superTypeIndex
    ![@typeKindEnum mapIndexType]
    ![@typedAttributeList emptyList]
    ![@typedAttributeList emptyList]
    !constructorMap
    !readerMap
    !modifierMap
    !instanceMethodMap
    ![@classMethodMap emptyMap]
    ![@enumerationDescriptorList emptyList]
    !0
    ![@unifiedTypeMapIndexList emptyList]
    ![@unifiedTypeMapIndexList emptyList]
    ![@nativeAttributeList emptyList]
  ] ;
end method ;

#---------------------------------------------------------------------------*

override method @filewrapperDeclarationAST enterType
  ?!@semanticContext ioSemanticContext
:
  @filewrapperTemplateMap filewrapperTemplateMap [emptyMap] ;
  foreach mFilewrapperTemplateList do
    @unifiedTypeMapIndexList templateSignature [emptyList] ;
    foreach mFilewrapperTemplateFormalInputParameters do
      @unifiedTypeMapIndex parameterTypeIndex ;
      [@unifiedTypeMapIndex makeRegularIndex !mFormalParameterTypeName !?ioSemanticContext->mTypeMap ?parameterTypeIndex] ;
      templateSignature += !parameterTypeIndex ;
    end foreach ;
    [!?filewrapperTemplateMap insertKey
      !mFilewrapperTemplateName
      !templateSignature
      !mFilewrapperTemplatePath
    ] ;
  end foreach ;
  [!?ioSemanticContext->mFilewrapperMap insertKey
    !mFilewrapperName
    !mFilewrapperPath
    !mFilewrapperExtensionList
    !filewrapperTemplateMap
  ] ;
end method ;

#---------------------------------------------------------------------------*

override method @sortedListDeclarationAST enterType
  ?!@semanticContext ioSemanticContext
:
  @unifiedTypeMapIndex uintType ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"uint" !here] !?ioSemanticContext->mTypeMap ?uintType] ;
#--- Element type index
  @unifiedTypeMapIndex listTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mSortedListTypeName !?ioSemanticContext->mTypeMap ?listTypeIndex] ;
#--- Enumeration description
  @enumerationDescriptorList enumerationDescriptor [emptyList] ;
  @unifiedTypeMapIndexList constructorAttributeTypeList [emptyList] ;
  @formalParameterSignature modifierFormalArgumentList [emptyList] ;
  @typedAttributeList typedAttributeList [emptyList] ;
  foreach mAttributeList do
    @unifiedTypeMapIndex attributeTypeIndex ;
    [@unifiedTypeMapIndex makeRegularIndex !mAttributeTypeName !?ioSemanticContext->mTypeMap ?attributeTypeIndex] ;
    typedAttributeList += !attributeTypeIndex !mAttributeName ;
    enumerationDescriptor += !attributeTypeIndex ![mAttributeName string] ;
    constructorAttributeTypeList += !attributeTypeIndex ;
    modifierFormalArgumentList += !attributeTypeIndex ![@formalArgumentPassingModeAST argumentOut] ;
  end foreach ;
#--- Constructors
  @constructorMap constructorMap [emptyMap] ;
  enterConstructorWithoutArgument !?constructorMap !?ioSemanticContext->mTypeMap !"emptySortedList" ![mSortedListTypeName string] !false ;
#--- Readers
  @readerMap readerMap [emptyMap] ;
  enterBaseReaderWithoutArgument !?readerMap !?ioSemanticContext->mTypeMap !"length" !"uint" !false ;
#--- Modifiers
  @modifierMap modifierMap [emptyMap] ;
  [!?modifierMap insertOrReplace
    ![@lstring new !"popGreatest" !here]
    !modifierFormalArgumentList
    !true
  ] ;
  [!?modifierMap insertOrReplace
    ![@lstring new !"popSmallest" !here]
    !modifierFormalArgumentList
    !true
  ] ;
#--- Instance methods
  @instanceMethodMap instanceMethodMap [emptyMap] ;
  [!?instanceMethodMap insertOrReplace
    ![@lstring new !"greatest" !here]
    !modifierFormalArgumentList
    !true
  ] ;
  [!?instanceMethodMap insertOrReplace
    ![@lstring new !"smallest" !here]
    !modifierFormalArgumentList
    !true
  ] ;
#--- Enter in type map
#log mSortedListTypeName ;
  @unifiedTypeMapIndex superTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"sortedlist" !here] !?ioSemanticContext->mTypeMap ?superTypeIndex] ;
  [!?ioSemanticContext->mTypeMap insertKey
    !mSortedListTypeName
    !true # Concrete Type
    !superTypeIndex
    ![@typeKindEnum sortedListType]
    ![@typedAttributeList emptyList]
    !typedAttributeList
    !constructorMap
    !readerMap
    !modifierMap
    !instanceMethodMap
    ![@classMethodMap emptyMap]
    !enumerationDescriptor
    !0
    !constructorAttributeTypeList
    ![@unifiedTypeMapIndexList emptyList]
    ![@nativeAttributeList emptyList]
  ] ;
end method ;

#---------------------------------------------------------------------------*

override method @structDeclarationAST enterType
  ?!@semanticContext ioSemanticContext
:
#--- Struct type index
  @unifiedTypeMapIndex structTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mStructTypeName !?ioSemanticContext->mTypeMap ?structTypeIndex] ;
#--- Constructors
  @constructorMap constructorMap [emptyMap] ;
  @unifiedTypeMapIndexList constructorAttributeTypeList [emptyList] ;
  @typedAttributeList typedAttributeList [emptyList] ;
  foreach mAttributeList do
    @unifiedTypeMapIndex attributeTypeIndex ;
    [@unifiedTypeMapIndex makeRegularIndex !mAttributeTypeName !?ioSemanticContext->mTypeMap ?attributeTypeIndex] ;
    constructorAttributeTypeList += !attributeTypeIndex ;
    typedAttributeList += !attributeTypeIndex !mAttributeName ;
  end foreach ;
  [!?constructorMap insertKey
    ![@lstring new !"new" !here]
    !constructorAttributeTypeList
    !false
    !structTypeIndex
  ] ;
#--- Readers
  @readerMap readerMap [emptyMap] ;
  foreach mAttributeList do
    @unifiedTypeMapIndex attributeTypeIndex ;
    [@unifiedTypeMapIndex makeRegularIndex !mAttributeTypeName !?ioSemanticContext->mTypeMap ?attributeTypeIndex] ;
    [!?readerMap insertOrReplace
      !mAttributeName
      ![@unifiedTypeMapIndexList emptyList]
      !false
      !attributeTypeIndex
      ![@readerQualifier isBasic]
      !""
    ] ;
  end foreach ;
#--- Modifiers
  @modifierMap modifierMap [emptyMap] ;
#--- Instance methods
  @instanceMethodMap instanceMethodMap [emptyMap] ;
#--- Enter in type map
  @unifiedTypeMapIndex superTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"root" !here] !?ioSemanticContext->mTypeMap ?superTypeIndex] ;
  [!?ioSemanticContext->mTypeMap insertKey
    !mStructTypeName
    !true # Concrete Type
    !superTypeIndex
    ![@typeKindEnum structType]
    ![@typedAttributeList emptyList]
    !typedAttributeList
    !constructorMap
    !readerMap
    !modifierMap
    !instanceMethodMap
    ![@classMethodMap emptyMap]
    ![@enumerationDescriptorList emptyList]
    !generateVirtualDestructor []
      | generateCppConstructorWithAttributeList []
      | generateDescriptionReaderUtilityMethod []
    ![@unifiedTypeMapIndexList emptyList]
    ![@unifiedTypeMapIndexList emptyList]
    ![@nativeAttributeList emptyList]
  ] ;
end method ;

#---------------------------------------------------------------------------*

override method @listDeclarationAST enterType
  ?!@semanticContext ioSemanticContext
:
#--- Handle attributes
  @typedAttributeList typedAttributeList [emptyList] ;
  foreach mAttributeList do
    @unifiedTypeMapIndex attributeTypeIndex ;
    [@unifiedTypeMapIndex makeRegularIndex !mAttributeTypeName !?ioSemanticContext->mTypeMap ?attributeTypeIndex] ;
    typedAttributeList += !attributeTypeIndex !mAttributeName ;
  end foreach ;
#--- uint type index
  @unifiedTypeMapIndex uintType ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"uint" !here] !?ioSemanticContext->mTypeMap ?uintType] ;
#--- Element type index
  @unifiedTypeMapIndex listTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mListTypeName !?ioSemanticContext->mTypeMap ?listTypeIndex] ;
#--- Attribute description
  @enumerationDescriptorList enumerationDescriptor [emptyList] ;
  @unifiedTypeMapIndexList constructorAttributeTypeList [emptyList] ;
  @formalParameterSignature modifierOutputFormalArgumentList [emptyList] ;
  @formalParameterSignature modifierInputFormalArgumentList [emptyList] ;
  foreach typedAttributeList do
    enumerationDescriptor += !mAttributeTypeIndex ![mAttributeName string] ;
    constructorAttributeTypeList += !mAttributeTypeIndex ;
    modifierOutputFormalArgumentList += !mAttributeTypeIndex ![@formalArgumentPassingModeAST argumentOut] ;
    modifierInputFormalArgumentList += !mAttributeTypeIndex ![@formalArgumentPassingModeAST argumentConstantIn] ;
  end foreach ;
#--- Constructors
  @constructorMap constructorMap [emptyMap] ;
  enterConstructorWithoutArgument !?constructorMap !?ioSemanticContext->mTypeMap !"emptyList" ![mListTypeName string] !false ;
  [!?constructorMap insertKey
    ![@lstring new !"listWithValue" !here]
    !constructorAttributeTypeList
    !false
    !listTypeIndex
  ] ;
#--- Readers
  @readerMap readerMap [emptyMap] ;
  enterInheritedReaderWithoutArgument !?readerMap !?ioSemanticContext->mTypeMap !"length" !"uint" !false ;
  enterBaseReaderWithArgument !?readerMap !?ioSemanticContext->mTypeMap !"subListFromIndex" !"uint" ![mListTypeName string] !true ;
  enterBaseReaderWith2Arguments !?readerMap !?ioSemanticContext->mTypeMap !"subListWithRange" !"uint" !"uint" ![mListTypeName string] !true ;
  foreach typedAttributeList do
    [!?readerMap insertOrReplace
      ![@lstring new ![mAttributeName string] . "AtIndex" ![mAttributeName location]]
      ![@unifiedTypeMapIndexList listWithValue !uintType]
      !true
      !mAttributeTypeIndex
      ![@readerQualifier isBasic]
      !""
    ] ;
  end foreach ;
#--- Modifiers
  @modifierMap modifierMap [emptyMap] ;
  [!?modifierMap insertOrReplace
    ![@lstring new !"popFirst" !here]
    !modifierOutputFormalArgumentList
    !true
  ] ;
  [!?modifierMap insertOrReplace
    ![@lstring new !"popLast" !here]
    !modifierOutputFormalArgumentList
    !true
  ] ;
  [!?modifierMap insertOrReplace
    ![@lstring new !"prependValue" !here]
    !modifierInputFormalArgumentList
    !false
  ] ;
  foreach typedAttributeList do
    @formalParameterSignature setterFormalArgumentList [emptyList] ;
    setterFormalArgumentList += !mAttributeTypeIndex ![@formalArgumentPassingModeAST argumentConstantIn] ;
    setterFormalArgumentList += !uintType ![@formalArgumentPassingModeAST argumentConstantIn] ;
    [!?modifierMap insertOrReplace
      ![@lstring new !"set" . [mAttributeName stringByCapitalizingFirstCharacter] . "AtIndex" ![mAttributeName location]]
      !setterFormalArgumentList
      !true
    ] ;
  end foreach ;
#--- Instance methods
  @instanceMethodMap instanceMethodMap [emptyMap] ;
  [!?instanceMethodMap insertOrReplace
    ![@lstring new !"first" !here]
    !modifierOutputFormalArgumentList
    !true
  ] ;
  [!?instanceMethodMap insertOrReplace
    ![@lstring new !"last" !here]
    !modifierOutputFormalArgumentList
    !true
  ] ;
#--- Enter in type map
#log mListTypeName ;
  @unifiedTypeMapIndex superTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"list" !here] !?ioSemanticContext->mTypeMap ?superTypeIndex] ;
  [!?ioSemanticContext->mTypeMap insertKey
    !mListTypeName
    !true # Concrete Type
    !superTypeIndex
    ![@typeKindEnum listType]
    !typedAttributeList
    ![@typedAttributeList emptyList]
    !constructorMap
    !readerMap
    !modifierMap
    !instanceMethodMap
    ![@classMethodMap emptyMap]
    !enumerationDescriptor
    !binaryConcatOperator [] | doNotGenererateObjectCompare [] | dotAssignOperator [] | generateCppConstructorWithNativeAttributeList []
    !constructorAttributeTypeList
    ![@unifiedTypeMapIndexList emptyList]
    ![@nativeAttributeList emptyList]
  ] ;
end method ;

#---------------------------------------------------------------------------*

override method @classDeclarationAST enterType
  ?!@semanticContext ioSemanticContext
:
  @unifiedTypeMapIndex uintType ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"uint" !here] !?ioSemanticContext->mTypeMap ?uintType] ;
#---
  @readerMap readerMap ;
  @modifierMap modifierMap ;
  @instanceMethodMap instanceMethodMap ;
  @classMethodMap classMethodMap ;
  @typedAttributeList inheritedTypedAttributeList ;
#--- Handle super class
  @unifiedTypeMapIndex superClassIndex ;
  if [mSuperClassName string] == "" then
    [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"root" !here] !?ioSemanticContext->mTypeMap ?superClassIndex] ;
    readerMap := [@readerMap emptyMap] ;
    modifierMap := [@modifierMap emptyMap] ;
    instanceMethodMap := [@instanceMethodMap emptyMap] ;
    classMethodMap := [@classMethodMap emptyMap] ;
    inheritedTypedAttributeList := [@typedAttributeList emptyList] ;
  else
    [@unifiedTypeMapIndex makeRegularIndex !mSuperClassName !?ioSemanticContext->mTypeMap ?superClassIndex] ;
    @typeKindEnum typeKindEnum ;
    [[ioSemanticContext mTypeMap] searchKey
      !mSuperClassName
      ?2*
      ?typeKindEnum
      ?*
      ?inheritedTypedAttributeList
      ?*
      ?readerMap
      ?modifierMap
      ?instanceMethodMap
      ?classMethodMap
      ?5*
    ] ;
    if typeKindEnum != [@typeKindEnum classType] then
      error mSuperClassName : "the @" . mSuperClassName . " should be a class type" ;
    end if ;
  end if ;
#--- Current class Index
  @unifiedTypeMapIndex classIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mClassTypeName !?ioSemanticContext->mTypeMap ?classIndex] ;
#--- Constructor
  @constructorMap constructorMap [emptyMap] ;
  if not mIsAbstract then
    @unifiedTypeMapIndexList constructorAttributeTypeList [emptyList] ;
    foreach inheritedTypedAttributeList do
      constructorAttributeTypeList += !mAttributeTypeIndex ;
    end foreach ;
    foreach mAttributeList do
      @unifiedTypeMapIndex attributeTypeIndex ;
      [@unifiedTypeMapIndex makeRegularIndex !mAttributeTypeName !?ioSemanticContext->mTypeMap ?attributeTypeIndex] ;
      constructorAttributeTypeList += !attributeTypeIndex ;
    end foreach ;
    [!?constructorMap insertKey
      ![@lstring new !"new" !here]
      !constructorAttributeTypeList
      !true
      !classIndex
    ] ;
  end if ;
#--- Append methods
  foreach mAbstractMethodList do
    @formalParameterSignature formalArgumentList [emptyList] ;
    foreach mSignature do
      @unifiedTypeMapIndex typeIndex ;
      [@unifiedTypeMapIndex makeRegularIndex !mFormalParameterTypeName !?ioSemanticContext->mTypeMap ?typeIndex] ;
      formalArgumentList += !typeIndex !mFormalParameterPassingMode ;
    end foreach ;
    [!?instanceMethodMap insertOrReplace
      !mMethodName
      !formalArgumentList
      !true
    ] ;
  end foreach ;
  foreach mOverridenMethodList do
    @formalParameterSignature formalArgumentList [emptyList] ;
    foreach mSignature do
      @unifiedTypeMapIndex typeIndex ;
      [@unifiedTypeMapIndex makeRegularIndex !mFormalParameterTypeName !?ioSemanticContext->mTypeMap ?typeIndex] ;
      formalArgumentList += !typeIndex !mFormalParameterPassingMode ;
    end foreach ;
    [!?instanceMethodMap insertOrReplace
      !mMethodName
      !formalArgumentList
      !true
    ] ;
  end foreach ;
  foreach mMethodList do
    @formalParameterSignature formalArgumentList [emptyList] ;
    foreach mSignature do
      @unifiedTypeMapIndex typeIndex ;
      [@unifiedTypeMapIndex makeRegularIndex !mFormalParameterTypeName !?ioSemanticContext->mTypeMap ?typeIndex] ;
      formalArgumentList += !typeIndex !mFormalParameterPassingMode ;
    end foreach ;
    [!?instanceMethodMap insertOrReplace
      !mMethodName
      !formalArgumentList
      !true
    ] ;
  end foreach ;
#--- Typed attribute list
  @typedAttributeList currentClassTypedAttributeList [emptyList] ;
  @typedAttributeList allTypedAttributeList := inheritedTypedAttributeList ;
  foreach mAttributeList do
    @unifiedTypeMapIndex attributeTypeIndex ;
    [@unifiedTypeMapIndex makeRegularIndex !mAttributeTypeName !?ioSemanticContext->mTypeMap ?attributeTypeIndex] ;
    currentClassTypedAttributeList += !attributeTypeIndex !mAttributeName ;
    allTypedAttributeList += !attributeTypeIndex !mAttributeName ;
  end foreach ;
#--- Enter in type map
#log mClassTypeName ;
  [!?ioSemanticContext->mTypeMap insertKey
    !mClassTypeName
    !not mIsAbstract # Concrete Type
    !superClassIndex
    ![@typeKindEnum classType]
    !allTypedAttributeList
    !currentClassTypedAttributeList
    !constructorMap
    !readerMap
    !modifierMap
    !instanceMethodMap
    ![@classMethodMap emptyMap]
    ![@enumerationDescriptorList emptyList]
    !0
    ![@unifiedTypeMapIndexList emptyList]
    ![@unifiedTypeMapIndexList emptyList]
    ![@nativeAttributeList emptyList]
  ] ;
end method ;

#---------------------------------------------------------------------------*

override method @externRoutineDeclarationAST enterType # Handles also routine declaration
  ?!@semanticContext ioSemanticContext
:
#--- Build signature
  @formalParameterSignature routineSignature [emptyList] ;
  foreach mFormalParameterList do
    @unifiedTypeMapIndex parameterTypeIndex ;
    [@unifiedTypeMapIndex makeRegularIndex !mFormalParameterTypeName !?ioSemanticContext->mTypeMap ?parameterTypeIndex] ;
    routineSignature += !parameterTypeIndex !mFormalParameterPassingMode ;
  end foreach ;
#--- Enter routine in routine map
  [!?ioSemanticContext->mRoutineMap insertKey !mRoutineName !routineSignature] ;
end method ;

#---------------------------------------------------------------------------*

override method @externFunctionDeclarationAST enterType # Handles also function declaration
  ?!@semanticContext ioSemanticContext
:
#--- Build signature
  @unifiedTypeMapIndexList functionSignature [emptyList] ;
  foreach mFormalParameterList do
    @unifiedTypeMapIndex parameterTypeIndex ;
    [@unifiedTypeMapIndex makeRegularIndex !mFormalParameterTypeName !?ioSemanticContext->mTypeMap ?parameterTypeIndex] ;
    functionSignature += !parameterTypeIndex ;
  end foreach ;
#--- Result type
  @unifiedTypeMapIndex resultTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mResultTypeName !?ioSemanticContext->mTypeMap ?resultTypeIndex] ;
#--- Enter function in routine map
  [!?ioSemanticContext->mFunctionMap insertKey !mFunctionName !functionSignature !resultTypeIndex] ;
end method ;

#---------------------------------------------------------------------------*

override method @lazyFunctionDeclarationAST enterType
  ?!@semanticContext ioSemanticContext
:
#--- Result type
  @unifiedTypeMapIndex resultTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mResultTypeName !?ioSemanticContext->mTypeMap ?resultTypeIndex] ;
#--- Enter function in routine map
  [!?ioSemanticContext->mFunctionMap insertKey !mFunctionName ![@unifiedTypeMapIndexList emptyList] !resultTypeIndex] ;
end method ;

#---------------------------------------------------------------------------*

override method @listmapDeclarationAST enterType
  ?!@semanticContext ioSemanticContext
:
#--- Check associated map is really a list
  @typedAttributeList listTypedAttributeList ;
  @typeKindEnum typeKindEnum ;
  [[ioSemanticContext mTypeMap] searchKey
    !mAssociatedListTypeName
    ?2*
    ?typeKindEnum
    ?*
    ?listTypedAttributeList
    ?10*
  ] ;
  if typeKindEnum != [@typeKindEnum listType] then
    error mAssociatedListTypeName : "the '@" . mAssociatedListTypeName . "' type should be a list type" ;
    listTypedAttributeList := [@typedAttributeList emptyList] ;
  end if ;
#---
  @constructorMap constructorMap [emptyMap] ;
  @readerMap readerMap [emptyMap] ;
  @modifierMap modifierMap [emptyMap] ;
  @instanceMethodMap instanceMethodMap [emptyMap] ;
  @classMethodMap classMethodMap [emptyMap] ;
#--- Associated map type index
  @unifiedTypeMapIndex associatedListTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mAssociatedListTypeName !?ioSemanticContext->mTypeMap ?associatedListTypeIndex] ;
#--- currentType type index
  @unifiedTypeMapIndex currentTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex !mListmapTypeName !?ioSemanticContext->mTypeMap ?currentTypeIndex] ;
#--- currentType type index
  @unifiedTypeMapIndex stringTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"string" !here] !?ioSemanticContext->mTypeMap ?stringTypeIndex] ;
#--- readers
  enterInheritedReaderWithoutArgument !?readerMap !?ioSemanticContext->mTypeMap !"count" !"uint" !false ;
#--- Enumerator description
  @enumerationDescriptorList enumeratorDescriptor [emptyList] ;
  enumeratorDescriptor += !stringTypeIndex !"key" ;
  enumeratorDescriptor += !associatedListTypeIndex !"value" ;
#--- Add assign operator description
  @unifiedTypeMapIndexList addAssignOperatorDescription [emptyList] ;
  addAssignOperatorDescription += !stringTypeIndex ;
  foreach listTypedAttributeList do
    addAssignOperatorDescription += !mAttributeTypeIndex ;
  end foreach ;
#--- Enter type in type map
  @unifiedTypeMapIndex superTypeIndex ;
  [@unifiedTypeMapIndex makeRegularIndex ![@lstring new !"listmap" !here] !?ioSemanticContext->mTypeMap ?superTypeIndex] ;
  [!?ioSemanticContext->mTypeMap insertKey
    !mListmapTypeName
    !true # Concrete Type
    !superTypeIndex
    ![@typeKindEnum mapIndexType]
    ![@typedAttributeList emptyList]
    !listTypedAttributeList
    !constructorMap
    !readerMap
    !modifierMap
    !instanceMethodMap
    ![@classMethodMap emptyMap]
    !enumeratorDescriptor
    !0
    !addAssignOperatorDescription
    ![@unifiedTypeMapIndexList emptyList]
    ![@nativeAttributeList emptyList]
  ] ;
end method ;

#---------------------------------------------------------------------------*
#                                                                           *
#    B U I L D    O R D E R E D     S E M A N T I C    L I S T              *
#                                                                           *
#---------------------------------------------------------------------------*

sortedlist @partiallyOrderedSemanticDeclarationListAST {
  @string mDependanceString ;
  @string mTypeString ;
  @semanticDeclarationAST mSemanticDeclaration ;
}{
  mDependanceString <
}

#---------------------------------------------------------------------------*

method @semanticDeclarationAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST unused ioPartiallyOrderedDeclarationList
:
end method ;

#---------------------------------------------------------------------------*

override method @predefinedTypeAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  ioPartiallyOrderedDeclarationList += !"root" !mPredefinedTypeName !self ;
end method ;

#---------------------------------------------------------------------------*

override method @stringsetPredefinedTypeAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  ioPartiallyOrderedDeclarationList += !"enumerable" !mPredefinedTypeName !self ;
end method ;

#---------------------------------------------------------------------------*

override method @genericTypeTypeAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  ioPartiallyOrderedDeclarationList += !mSuperTypeName !mTypeName !self ;
end method ;

#---------------------------------------------------------------------------*

override method @classDeclarationAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  const @string actualSuperTypeName := if [mSuperClassName string] == "" then "root" else [mSuperClassName string] end ;
  ioPartiallyOrderedDeclarationList += !actualSuperTypeName ![mClassTypeName string] !self ;
end method ;

#---------------------------------------------------------------------------*

override method @listDeclarationAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  ioPartiallyOrderedDeclarationList += !"list" ![mListTypeName string] !self ;
end method ;

#---------------------------------------------------------------------------*

override method @structDeclarationAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  ioPartiallyOrderedDeclarationList += !"root" ![mStructTypeName string] !self ;
end method ;

#---------------------------------------------------------------------------*

override method @enumDeclarationAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  ioPartiallyOrderedDeclarationList += !"root" ![mEnumTypeName string] !self ;
end method ;

#---------------------------------------------------------------------------*

override method @sortedListDeclarationAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  ioPartiallyOrderedDeclarationList += !"sortedlist" ![mSortedListTypeName string] !self ;
end method ;

#---------------------------------------------------------------------------*

override method @listmapDeclarationAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  ioPartiallyOrderedDeclarationList += ![mAssociatedListTypeName string] ![mListmapTypeName string] !self ;
end method ;

#---------------------------------------------------------------------------*

override method @mapDeclarationAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  ioPartiallyOrderedDeclarationList += !"map" ![mMapTypeName string] !self ;
end method ;

#---------------------------------------------------------------------------*

override method @mapindexDeclarationAST enterOrderedDeclaration
  ?!@partiallyOrderedSemanticDeclarationListAST ioPartiallyOrderedDeclarationList
:
  ioPartiallyOrderedDeclarationList += ![mAssociatedMapTypeName string] ![mMapIndexTypeName string] !self ;
end method ;

#---------------------------------------------------------------------------*
#                                                                           *
#    B U I L D    S E M A N T I C    C O N T E X T                          *
#                                                                           *
#---------------------------------------------------------------------------*

routine buildSemanticContext
  ??@semanticDeclarationListAST inSemanticDeclarationList
  ??@location inEndOfSourceFile
  !@semanticContext outSemanticContext
:
#--- Collect all types, for checking there is no undefined type
  @collectedTypeMap collectedTypeMap [emptyMap] ;
  foreach inSemanticDeclarationList do
    [mSemanticDeclaration collectType !?collectedTypeMap] ;
  end foreach ;
#--- Check there is no undefined type
  @stringlist undefinedClasses := [collectedTypeMap unsolvedIndexKeyStringList] ;
  if [undefinedClasses length] == 1 then
    @string errorMessage := "there is 1 undefined type:" ;
    foreach undefinedClasses do
      errorMessage .= "\n-  @" . mValue ;
    end foreach ;
    error inEndOfSourceFile : errorMessage ;
  elsif [undefinedClasses length] > 1 then
    @string errorMessage := "there are " . [[undefinedClasses length] string] . " undefined types:" ;
    foreach undefinedClasses do
      errorMessage .= "\n-  @" . mValue ;
    end foreach ;
    error inEndOfSourceFile : errorMessage ;
  end if ;
#------ Build ordered types list, so that
#  - a class appears after its super class
#  - a map index appears after its associated map
#  - a list map appears after its associated list
  @partiallyOrderedSemanticDeclarationListAST partiallyOrderedSemanticDeclarationListAST [emptySortedList] ;
  if [@uint errorCount] == 0 then
    foreach inSemanticDeclarationList do
      [mSemanticDeclaration enterOrderedDeclaration !?partiallyOrderedSemanticDeclarationListAST] ;  
    end foreach ;
  end if ;
#---
  @semanticDeclarationListAST orderedSemanticDeclarationListAST [emptyList] ;
  @stringset alreadyDefinedTypes [emptySet] ;
  alreadyDefinedTypes += !"" ;
  @bool progress := true ;
  loop [partiallyOrderedSemanticDeclarationListAST length] :
  while ([partiallyOrderedSemanticDeclarationListAST length] > 0) & progress do
    progress := false ;
    const @partiallyOrderedSemanticDeclarationListAST r := partiallyOrderedSemanticDeclarationListAST ;
    partiallyOrderedSemanticDeclarationListAST := [@partiallyOrderedSemanticDeclarationListAST emptySortedList] ;
    foreach r do
      if [alreadyDefinedTypes hasKey !mDependanceString] then
        orderedSemanticDeclarationListAST += !mSemanticDeclaration ;
        alreadyDefinedTypes += !mTypeString ;
#        message "ENTERING @" : mTypeString . "\n" ;
        progress := true ;
      else
        partiallyOrderedSemanticDeclarationListAST += !mDependanceString !mTypeString !mSemanticDeclaration ;
      end if ;
    end foreach ;
  end loop ;
#--- Remain in partiallyOrderedSemanticDeclarationListAST only
#    circular definitions
  if [partiallyOrderedSemanticDeclarationListAST length] > 0 then
    @string s := "inheritance circularities involving the " ;
    foreach partiallyOrderedSemanticDeclarationListAST
      do s.= "@" . mTypeString ;
      between s .= ", " ;
    end foreach ;
    s .= " classes; detail:" ;
    foreach partiallyOrderedSemanticDeclarationListAST
      do s.= "\n  - @" . mTypeString . " inherits from @" . mDependanceString ;
    end foreach ;
    error here : s ;
  end if ;
#--- Build initial semantic context
  outSemanticContext := [@semanticContext new
    ![@unifiedTypeMap emptyMap]
    ![@routineMap emptyMap]
    ![@functionMap emptyMap]
    ![@descendantClassListMap emptyMap]
    ![@filewrapperMap emptyMap]
    !orderedSemanticDeclarationListAST
  ] ;
#--- Loop throught all declarations
  if [@uint errorCount] == 0 then
#    message "************** ENTER TYPES ************************\n" ;
    foreach orderedSemanticDeclarationListAST do
      [mSemanticDeclaration enterType !?outSemanticContext] ;
    end foreach ;
#    message "************** TYPES ENTERED ************************\n" ;
  end if ;
end routine ;

#---------------------------------------------------------------------------*

end semantics ;
