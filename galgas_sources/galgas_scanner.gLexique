#---------------------------------------------------------------------------*
#                                                                           *
#  GALGAS scanner definition                                                *
#                                                                           *
#  Copyright (C) 1997, ..., 2009 Pierre Molinaro.                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

lexique galgas_scanner :

@string identifierString ;
@uint uint32value ;
@sint sint32value ;
@uint64 uint64value ;
@sint64 sint64value ;
@char charValue ;
@string tokenString ;

style keywordsStyle -> "Keywords" ;
style delimitersStyle -> "Delimiters" ;
style terminalStyle -> "Terminal Symbols" ;
style nonTerminalStyle -> "Non Terminal Symbols" ;
style integerStyle -> "Integer Constants" ;
style float_csts -> "Floating Point Constants" ;
style characterStyle -> "Character Constants" ;
style stringStyle -> "String Constants" ;
style typeNameStyle -> "Type Names (@...)" ;
style commentStyle -> "Comments" ;

# -------------------------- identifier or key word --------------------
# keywords can be written in lower or upper case
# for identifiers, case is significant

$identifier$ ! tokenString error message "an identifier" ;

list galgasKeyWordList error message "the '%K' keyword" style keywordsStyle :
  "abstract",
  "after",
  "before",
  "between",
  "block",
  "cast",
  "class",
  "const",
  "default",
  "do",
  "drop",
  "else",
  "elsif",
  "end",
  "enum",
  "error",
  "extends",
  "extern",
  "extract",
  "false",
  "feature",
  "filewrapper",
  "foreach",
  "function",
  "grammar",
  "graph",
  "gui",
  "here",
  "if",
  "import",
  "in",
  "index",
  "indexing",
  "insert",
  "label",
  "lexique",
  "list",
  "listmap",
  "local",
  "log",
  "loop",
  "map",
  "mapproxy",
  "match",
  "message",
  "method",
  "mod",
  "modifier",
  "nonterminal",
  "not",
  "on",
  "once",
  "operator",
  "option",
  "or",
  "override",
  "parse",
  "prefixedby",
  "project",
  "program",
  "reader",
  "remove",
  "replace",
  "repeat",
  "rewind",
  "root",
  "routine",
  "rule",
  "search",
  "select",
  "self",
  "selfcopy",
  "semantics",
  "send",
  "sortedlist",
  "state",
  "struct",
  "style",
  "super",
  "switch",
  "syntax", 
  "tag",
  "template",
  "then",
  "true",
  "uniquemap",
  "unused",
  "warning",
  "when",              
  "while",
  "with" ;

rule 'a'->'z' |  'A'->'Z' :
  repeat
    enterCharacterIntoString !?identifierString !toLower (!*) ;
    enterCharacterIntoString !?tokenString !* ;
  while 'a'->'z' | 'A'->'Z' | '_' | '0'->'9' :
  end repeat ;
  send search identifierString in galgasKeyWordList default $identifier$ ;
end rule ;

#------------------------------- Integer, floating Point Constant ------------------
@double floatValue ;
$literal_double$ !floatValue !tokenString error message "a float number" style float_csts ;
$unsigned_literal_integer$ !uint32value error message "a 32-bit unsigned decimal number" style integerStyle ;
$signed_literal_integer$ !sint32value error message "a 32-bit signed decimal number" style integerStyle ;
$unsigned_literal_integer64$ !uint64value error message "a 64-bit unsigned decimal number" style integerStyle ;
$signed_literal_integer64$ !sint64value error message "a 64-bit signed decimal number" style integerStyle ;

message decimalNumberTooLarge : "decimal number too large" ;
message floatNumberConversionError : "invalid float number" ;
message internalError : "internal error" ;

rule "0x" :
  repeat
  while '0'->'9' :
    enterCharacterIntoString !?tokenString !* ;
  while 'a'->'f' :
    enterCharacterIntoString !?tokenString !* ;
  while 'A'->'F' :
    enterCharacterIntoString !?tokenString !* ;
  while '_' :
  end repeat ;
  select
  when "LS" :
    convertHexStringIntoSInt64 !tokenString !?sint64value error decimalNumberTooLarge, internalError ;
    send $signed_literal_integer64$ ;
  when 'S' | 's' :
    convertHexStringIntoSInt !tokenString !?sint32value error decimalNumberTooLarge, internalError ;
    send $signed_literal_integer$ ;
  when 'L' :
    convertHexStringIntoUInt64 !tokenString !?uint64value error decimalNumberTooLarge, internalError ;
    send $unsigned_literal_integer64$ ;
  default
    convertHexStringIntoUInt !tokenString !?uint32value error decimalNumberTooLarge, internalError ;
    send $unsigned_literal_integer$ ;
  end select ;
end rule ;

rule '0'->'9' :
  enterCharacterIntoString !?tokenString !* ;
  repeat
  while '0'->'9' :
    enterCharacterIntoString !?tokenString !* ;
  while '_' :
  end repeat ;
  select
  when 'S' | 's' :
    convertDecimalStringIntoSInt !tokenString !?sint32value error decimalNumberTooLarge, internalError ;
    send $signed_literal_integer$ ;
  when "LS" :
    convertDecimalStringIntoSInt64 !tokenString !?sint64value error decimalNumberTooLarge, internalError ;
    send $signed_literal_integer64$ ;
  when 'L' :
    convertDecimalStringIntoUInt64 !tokenString !?uint64value error decimalNumberTooLarge, internalError ;
    send $unsigned_literal_integer64$ ;
  when '.' :
    enterCharacterIntoString !?tokenString !'.' ;
    repeat
    while '0'->'9' :
      enterCharacterIntoString !?tokenString !* ;
    while '_' :
    end repeat ;
    convertStringToDouble !tokenString !?floatValue error floatNumberConversionError ;
    send $literal_double$ ;
  default
    convertDecimalStringIntoUInt !tokenString !?uint32value error decimalNumberTooLarge, internalError ;
    send $unsigned_literal_integer$ ;
  end select ;
end rule ;


#------------------------------- Floating point Number ------------------
$.$ error message "the '.' delimitor" style delimitersStyle ;
$.=$ error message "the '.=' delimitor" style delimitersStyle ;
$...$ error message "the '...' delimitor" style delimitersStyle ;

#--- Floating Point constant begining with a dot
rule '.' :
  select
  when '0'->'9' :
    enterCharacterIntoString !?tokenString !'0' ;
    enterCharacterIntoString !?tokenString !'.' ;
    enterCharacterIntoString !?tokenString !* ;
    repeat
    while '0'->'9' :
      enterCharacterIntoString !?tokenString !* ;
    while '_' :
    end repeat ;
    convertStringToDouble !tokenString !?floatValue error floatNumberConversionError ;
    send $literal_double$ ;
  default
    select
    when ".." :
      send $...$ ;
    when "=" :
      send $.=$ ;
    default
      send $.$ ;
    end select ;
  end select ;
end rule ;


# -------------------------- Type Name -----------------------------

$type_name$ ! tokenString error message "a type name (@...)" style typeNameStyle ;

message incorrectTypeNameError : "in a type name, a letter should follow the '@' character" ;

rule '@' :
  select
  when 'a'->'z' | 'A'->'Z' | '_' | '0'->'9' :
    repeat
      enterCharacterIntoString !?tokenString !* ;
    while 'a'->'z' | 'A'->'Z' | '_' | '0'->'9' :
    end repeat ;
  default
    error incorrectTypeNameError ;
  end select ;
  send $type_name$ ; 
end rule ;

#--------------------------------- Character constant ------------------------------------
$literal_char$ ! charValue error message "a character constant" style characterStyle ;

message incorrectCharConstant : "incorrect literal character" ;

message ASCIIcodeTooLargeError : "ASCII code > 255" ;

message obsoleteCharConstruction : "\\x.. is obsolete: now, use \\u.... or \\U........" ;

message invalideUnicodeDefinition4 : "\\u should be followed by exactly four hexadecimal digits" ;

message invalideUnicodeDefinition8 : "\\U should be followed by exactly eight hexadecimal digits" ;

message unassignedUnicodeValue : "this value does not correspond to an assigned Unicode point" ;

message incorrectHTMLescapeSequence : "Invalid HTML sequence, should be '&...;'" ;

message unknownHTMLescapeSequence : "Invalid &...; HTML sequence" ;

rule '\'' :
  select
  when '\\' :
    select
    when 'f' :
      enterCharacterIntoCharacter !?charValue !'\f' ;
    when 'n' :
      enterCharacterIntoCharacter !?charValue !'\n' ;
    when 'r' :
      enterCharacterIntoCharacter !?charValue !'\r' ;
    when 't' :
      enterCharacterIntoCharacter !?charValue !'\t' ;
    when 'v' :
      enterCharacterIntoCharacter !?charValue !'\v' ;
    when '\\' :
      enterCharacterIntoCharacter !?charValue !'\\' ;
    when '0' :
      enterCharacterIntoCharacter !?charValue !'\0' ;
    when '\'' :
      enterCharacterIntoCharacter !?charValue !'\'' ;
    when 'u' :
      select
      when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
        enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
        select
        when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
          enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
          select
          when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
           enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
            select
            when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
              enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
              convertUnsignedNumberToUnicodeChar !?uint32value !?charValue error unassignedUnicodeValue ;
            default
              error invalideUnicodeDefinition4 ;
            end select ;
          default
            error invalideUnicodeDefinition4 ;
          end select ;
        default
          error invalideUnicodeDefinition4 ;
        end select ;
      default
        error invalideUnicodeDefinition4 ;
      end select ;
    when 'U' :
      select
      when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
        enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
        select
        when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
          enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
          select
          when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
            enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
            select
            when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
              enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
              select
              when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
                enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
                select
                when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
                  enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
                  select
                  when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
                   enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
                    select
                    when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
                      enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
                      convertUnsignedNumberToUnicodeChar !?uint32value !?charValue error unassignedUnicodeValue ;
                    default
                      error invalideUnicodeDefinition8 ;
                    end select ;
                  default
                    error invalideUnicodeDefinition8 ;
                  end select ;
                default
                  error invalideUnicodeDefinition8 ;
                end select ;
              default
                error invalideUnicodeDefinition8 ;
              end select ;
            default
              error invalideUnicodeDefinition8 ;
            end select ;
          default
            error invalideUnicodeDefinition8 ;
          end select ;
        default
          error invalideUnicodeDefinition8 ;
        end select ;
      default
        error invalideUnicodeDefinition8 ;
      end select ;
    when 'x' | 'X' :
      warning obsoleteCharConstruction ;
      select
      when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
        repeat
          enterHexDigitIntoASCIIcharacter !?charValue !* error ASCIIcodeTooLargeError, internalError ;
        while '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
        end repeat ;
      default
        error incorrectCharConstant ;
      end select ;
    when '&' :
      repeat
      while ~ ";" error incorrectHTMLescapeSequence :
        enterCharacterIntoString !?tokenString !* ;
      end repeat ;
      convertHTMLSequenceToUnicodeCharacter !?tokenString !?charValue error unknownHTMLescapeSequence ;
    default
      error incorrectCharConstant ;
    end select ;
  when ' ' -> '\uFFFD' :
     enterCharacterIntoCharacter !?charValue !* ;
  default
    error incorrectCharConstant ;
  end select ;
  select
  when '\'' :
    send $literal_char$ ;
  default
    error incorrectCharConstant ;
  end select ;
end rule ;

#----------------------- Grammar terminal symbol ---------------------------
$terminal$ ! tokenString error message "a terminal symbol ($...$)" style terminalStyle ;

message incorrect_terminal_start : "incorrect terminal start" ;

message incorrect_terminal_end : "terminal does not end with '$'" ;

rule '$' :
  select
  when '!' -> '#' | '%' -> '~'  : # All printable characters, but '$'
    repeat
      enterCharacterIntoString !?tokenString !* ;
    while '!' -> '#' | '%' -> '~' :
    end repeat ;
  default
    error incorrect_terminal_start ;
  end select ;
  select
  when '$' :
  default
    error incorrect_terminal_end ;
  end select ;
  send $terminal$ ; 
end rule ;

# ----------------------------- Delimitors ---------------------------------------
list galgasDelimitorsList error message "the '%K' delimitor" style delimitersStyle :
  "*",     "|",    ",",      "+",     "--",
  "::",    ">",    ";",      ":",
  "-",     "(",    ")",     "->",      "?",
  "==",   "??",    "!",     ":=",     "++",
  "[",     "]",   "+=",     "?!",     "!?",
  "/",    "!=",   ">=",      "&",
  "{",     "}",    "`",     "-=",
  "^",    ">>",    "~",     "<-" ;

rule list galgasDelimitorsList ;


# --------- Special delimitors / non terminal symbol --------------------------
$<$ error message "the '<' delimitor" style delimitersStyle ;
$<->$ error message "the '<->' delimitor" style delimitersStyle ;
$<=$ error message "the '<=' delimitor" style delimitersStyle ;
$<<$ error message "the '<<' delimitor" style delimitersStyle ;
$non_terminal_symbol$ ! tokenString error message "a non terminal symbol <...>" style nonTerminalStyle ;

rule '<' :
  tag onlyInfDelimiter ;
  select
  when '=' :
    send $<=$ ;
  when "->" :
    send $<->$ ;
  when '<' :
    send $<<$ ;
  when 'a' -> 'z' | 'A' ->'Z' :
    repeat
      enterCharacterIntoString !?tokenString !* ;
    while 'a' -> 'z' | 'A' ->'Z' | '0' -> '9' | '_' :
    end repeat ;
    select
    when '>' :
      send $non_terminal_symbol$ ;
    default
      rewind onlyInfDelimiter send $<$ ;
    end select ;
  default
    send $<$ ;
  end select ;
end rule ;

# -------------------- Characters string --------------------------------------
$literal_string$ ! tokenString error message "a character string constant \"...\"" style stringStyle ;

message incorrectStringEnd : "string does not end with '\"'" ;

message obsoleteStringConstruction : "\\ followed by digits is obsolete: now, use \\u.... or \\U........" ;

rule '"' :
  repeat
  while '\\' :
    select
    when 'f' :
      enterCharacterIntoString !?tokenString !'\f' ;
    when 'n' :
      enterCharacterIntoString !?tokenString !'\n' ;
    when 'r' :
      enterCharacterIntoString !?tokenString !'\r' ;
    when 't' :
      enterCharacterIntoString !?tokenString !'\t' ;
    when 'v' :
      enterCharacterIntoString !?tokenString !'\v' ;
    when '\\' :
      enterCharacterIntoString !?tokenString !'\\' ;
    when '"' :
      enterCharacterIntoString !?tokenString !'"' ;
    when '\'' :
      enterCharacterIntoString !?tokenString !'\'' ;
    when '&' :
      repeat
      while ~ ";" error incorrectHTMLescapeSequence :
        enterCharacterIntoString !?identifierString !* ;
      end repeat ;
      convertHTMLSequenceToUnicodeCharacter !?identifierString !?charValue error unknownHTMLescapeSequence ;
      enterCharacterIntoString !?tokenString !charValue ;
    when '0' -> '9' :
      warning obsoleteStringConstruction ;
      repeat
        enterHexDigitIntoASCIIcharacter !?charValue !* error ASCIIcodeTooLargeError, internalError ;
      while '0' -> '9' :
      end repeat ;
      enterCharacterIntoString !?tokenString !charValue ;
    when 'u' :
      select
      when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
        enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
        select
        when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
          enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
          select
          when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
           enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
            select
            when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
              enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
              convertUnsignedNumberToUnicodeChar !?uint32value !?charValue error unassignedUnicodeValue ;
              enterCharacterIntoString !?tokenString !charValue ;
            default
              error invalideUnicodeDefinition4 ;
            end select ;
          default
            error invalideUnicodeDefinition4 ;
          end select ;
        default
          error invalideUnicodeDefinition4 ;
        end select ;
      default
        error invalideUnicodeDefinition4 ;
      end select ;
    when 'U' :
      select
      when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
        enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
        select
        when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
          enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
          select
          when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
            enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
            select
            when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
              enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
              select
              when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
                enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
                select
                when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
                  enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
                  select
                  when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
                   enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
                    select
                    when '0' -> '9' | 'a' -> 'f' | 'A' -> 'F' :
                      enterHexDigitIntoUInt !* !?uint32value error internalError, internalError ;
                      convertUnsignedNumberToUnicodeChar !?uint32value !?charValue error unassignedUnicodeValue ;
                      enterCharacterIntoString !?tokenString !charValue ;
                    default
                      error invalideUnicodeDefinition8 ;
                    end select ;
                  default
                    error invalideUnicodeDefinition8 ;
                  end select ;
                default
                  error invalideUnicodeDefinition8 ;
                end select ;
              default
                error invalideUnicodeDefinition8 ;
              end select ;
            default
              error invalideUnicodeDefinition8 ;
            end select ;
          default
            error invalideUnicodeDefinition8 ;
          end select ;
        default
          error invalideUnicodeDefinition8 ;
        end select ;
      default
        error invalideUnicodeDefinition8 ;
      end select ;
    default
      error incorrectCharConstant ;
    end select ;
   while ' ' | '!' | '#'-> '\uFFFD' :
    enterCharacterIntoString !?tokenString !* ;
  end repeat ;
  select
  when '"' :
    send $literal_string$ ;
  default
    error incorrectStringEnd ;
  end select ;
end rule ;

# ------------------------------------ Comment ----------------------------
$comment$ error message "a comment" style commentStyle ;
rule '#' :
  repeat
  while '\u0001' -> '\u0009' | '\u000B' | '\u000C' | '\u000E' -> '\uFFFD' :
  end repeat ;
  drop $comment$ ;
end rule ;

# --------------------- separators -----------------------------------------
rule '\u0001' -> ' ' :
end rule ;

# --------------------- Indexing
#indexing classDefinition : "Class definition" ;
#indexing superClassReference : "Superclass reference" ;

end lexique ;
