#---------------------------------------------------------------------------*
#                                                                           *
#  'syntax' component parser, as model builder                              *
#                                                                           *
#  Copyright (C) 2007, ..., 2007 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax semanticsModelBuilder :
import lexique galgas_scanner in "galgas_scanner.gLexique" ;
import semantics expressionMetamodel in "expressionMetamodel.gSemantics" ;
import semantics parameterArgumentMetamodel in "parameterArgumentMetamodel.gSemantics" ;
import semantics instructionMetamodel in "instructionMetamodel.gSemantics" ;
import semantics semanticsMetamodel in "semanticsMetamodel.gSemantics" ;

#------------------------------------------------------------------------------*
#                                                                              *
#          Rules prototypes                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <semantics_component_start_symbol>
  !@location outEndOfSourceFile
  !@semanticsComponentRoot outSemanticsComponentRoot
;

nonterminal <formal_parameter_list> !@formalParameterList outFormalParameterList ;

nonterminal <actual_parameter_list> !@actualParameterList outActualParameterList ;

nonterminal <semantic_instruction_list> !@semanticInstructionList outInstructionsList ;

nonterminal <semantic_instruction> !@semanticInstruction outInstruction ;

nonterminal <output_expression_list> !@outExpressionList outExpressionList ;

nonterminal <semantic_declaration> !@semanticDeclaration outSemanticDeclaration ;

#---------------------------------------------------------------------------*

nonterminal <obsolete_declaration> ;

rule <semantics_component_start_symbol>
  !@location outEndOfSourceFile
  !@semanticsComponentRoot outSemanticsComponentRoot
:
  $semantics$ ;
#--- Semantics Component Name
  $identifier$ ? @lstring mSemanticsComponentName ;
  $:$ ;
#--- Loop on import and semantics declarations
  @lstringlist mImportedComponentFileNameList [emptyList] ;
  @semanticDeclarationList mSemanticDeclarationList [emptyList] ;
  repeat
  while
    $import$ ;
    select
      $literal_string$ ? @lstring importedComponentFileName ;
      mImportedComponentFileNameList += !importedComponentFileName ;
    or
      $semantics$ ;
      $identifier$ ?* ;
      $in$ ;
      $literal_string$ ?* ;
    or
      $metamodel$ ;
      $identifier$ ?* ;
      $in$ ;
      $literal_string$ ?* ;
    or
      $treewalking$ ;
      $identifier$ ?* ;
      $in$ ;
      $literal_string$ ?* ;
    or
      $grammar$ ;
      $identifier$ ?* ;
      $in$ ;
      $literal_string$ ?* ;
    end select ;
    $;$ ;
  while
    @semanticDeclaration semanticDeclaration ;
    <semantic_declaration> ? semanticDeclaration ;
    mSemanticDeclarationList += !semanticDeclaration ;
  while
    <obsolete_declaration> ;
  end repeat ;
#--- end
  $end$ ;
  outEndOfSourceFile := here ;
  $semantics$ ;
  $;$ ;
  outSemanticsComponentRoot := [@semanticsComponentRoot new
    !mSemanticsComponentName
    !mImportedComponentFileNameList
    !mSemanticDeclarationList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    A T T R I B U T E    I N    C O L L E C T I O N                        *
#                                                                           *
#---------------------------------------------------------------------------*

nonterminal <attribute_declaration> ?!@attributeInCollectionList ioAttributeInCollectionList ;

#---------------------------------------------------------------------------*

rule <attribute_declaration> ?!@attributeInCollectionList ioAttributeInCollectionList :
  $type_name$ ? @lstring mAttributeTypeName ;
  $identifier$ ? @lstring mAttributeName ;
  $;$ ;
  ioAttributeInCollectionList += !mAttributeTypeName !mAttributeName ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M A P    D E C L A R A T I O N                                         *
#                                                                           *
#---------------------------------------------------------------------------*

rule <obsolete_declaration> :
  $map$ ;
  $type_name$ ? * ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

nonterminal <map_method_declaration> ?!@mapMethodList ioMapMethodList ;

#---------------------------------------------------------------------------*

rule <map_method_declaration> ?!@mapMethodList ioMapMethodList :
  $identifier$ ? @lstring mMethodName ;
  @lstring mGetIDMethodName ;
  select
    mGetIDMethodName := [@lstring new !"" !here] ;
  or
    $,$ ;
    $identifier$ ? mGetIDMethodName ;
  end select ;
  $error$ ;
  $message$ ;
  $literal_string$ ? @lstring mErrorMessage ;
  @lstring mShadowErrorMessage ;
  select
    mShadowErrorMessage := [@lstring new !"" !here] ;
  or
    $,$ ;
    $literal_string$ ? mShadowErrorMessage ;
  end select ;
  $;$ ;
  ioMapMethodList +=
    !mMethodName
    !mGetIDMethodName
    !mErrorMessage
    !mShadowErrorMessage
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_declaration> !@semanticDeclaration outSemanticDeclaration :
  $map$ ;
#--- Map type name
  $type_name$ ? @lstring mMapTypeName ;
  ${$ ;
#--- Map properties
  @attributeInCollectionList mAttributeList [emptyList] ;
  @mapMethodList mInsertMethodList [emptyList] ;
  @mapMethodList mSearchMethodList [emptyList] ;
  @mapMethodList mRemoveMethodList [emptyList] ;
  repeat
  while
    <attribute_declaration> !? mAttributeList ;
  while
    $insert$ ;
    <map_method_declaration> !? mInsertMethodList ;
  while
    $search$ ;
    <map_method_declaration> !? mSearchMethodList ;
  while
    $remove$ ;
    <map_method_declaration> !? mRemoveMethodList ;
  end repeat ;
  $}$ ;
  outSemanticDeclaration := [@mapDeclaration new
    !mMapTypeName
    !mAttributeList
    !mInsertMethodList
    !mSearchMethodList
    !mRemoveMethodList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M A P    I N D E X    D E C L A R A T I O N                            *
#                                                                           *
#---------------------------------------------------------------------------*

rule <obsolete_declaration> :
  $mapindex$ ;
  $type_name$ ? * ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_declaration> !@semanticDeclaration outSemanticDeclaration :
  $mapindex$ ;
  $type_name$ ?@lstring mMapIndexName ;
  $($ ;
  $type_name$ ? @lstring mMapTypeName ;
  $)$ ;
  ${$ ;
  $search$ ;
  $identifier$ ? @lstring mSearchMethodName ;
  $error$ ;
  $message$ ;
  $literal_string$ ? @lstring mSearchMethodErrorMessage ;
  $;$ ;
  $}$ ;
  outSemanticDeclaration := [@mapindexDeclaration new
    !mMapIndexName
    !mMapTypeName
    !mSearchMethodName
    !mSearchMethodErrorMessage
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    C L A S S    D E C L A R A T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

rule <obsolete_declaration> :
  $map$ ;
  $type_name$ ?* ;
  $class$ ;
  $identifier$ ?* ;
  $;$ ;
  repeat
  while
    $insert$ ;
    $identifier$ ?* ;
    $error$ ;
    $message$ ;
    $literal_string$ ?* ;
    $;$ ;
  while
    $search$ ;
    $identifier$ ?* ;
    $error$ ;
    $message$ ;
    $literal_string$ ?* ;
    $;$ ;
  while
    $type_name$ ?* ;
    $identifier$ ?* ;
    $;$ ;
  while
    $block$ ;
    $identifier$ ?* ;
    $:$ ;
    $($ ;
    repeat
    while
      $?$ ;
      $type_name$ ?* ;
      $identifier$ ?* ;
    end repeat ;
    $)$ ;
    $:$ ;
    $($ ;
    repeat
    while
      $?$ ;
      $type_name$ ?* ;
      $identifier$ ?* ;
    end repeat ;
    $)$ ;
    $;$ ;
  end repeat ;
  $}$ ;
end rule ;

#---------------------------------------------------------------------------*

nonterminal <class_message_declaration> ?!@classMessageDefinitionList ioClassMessageDefinitionList ;

#---------------------------------------------------------------------------*

rule <class_message_declaration> ?!@classMessageDefinitionList ioClassMessageDefinitionList :
  $message$ ;
  $identifier$ ? @lstring mMessageName ;
  $literal_string$ ? @lstring mMessageValue ;
  $;$ ;
  ioClassMessageDefinitionList += !mMessageName !mMessageValue ;
end rule ;

#---------------------------------------------------------------------------*

nonterminal <class_abstract_method_declaration>
  ?!@abstractMethodDefinitionList ioAbstractMethodDefinitionList
;

#---------------------------------------------------------------------------*

rule <class_abstract_method_declaration>
  ?!@abstractMethodDefinitionList ioAbstractMethodDefinitionList
:
  $abstract$ ;
  $method$ ;
  $identifier$ ? @lstring mMethodName ;
  @formalParameterList mSignature ;
  <formal_parameter_list> ? mSignature ;
  $;$ ;
  ioAbstractMethodDefinitionList += !mMethodName !mSignature ;
end rule ;

#---------------------------------------------------------------------------*

nonterminal <class_overriden_method_declaration>
  ?!@overridenMethodDefinitionList ioOverridenMethodDefinitionList
;

#---------------------------------------------------------------------------*

rule <class_overriden_method_declaration>
  ?!@overridenMethodDefinitionList ioOverridenMethodDefinitionList
:
  $override$ ;
  $method$ ;
  $identifier$ ? @lstring mMethodName ;
  @formalParameterList mSignature ;
  <formal_parameter_list> ? mSignature ;
  $:$ ;
  @semanticInstructionList mInstructionList ;
  <semantic_instruction_list> ? mInstructionList ;
  $end$ ;
  $method$ ;
  $;$ ;
  ioOverridenMethodDefinitionList +=
    !mMethodName
    !mSignature
    !mInstructionList
  ;
end rule ;

#---------------------------------------------------------------------------*

nonterminal <class_method_declaration> ?!@methodDefinitionList ioMethodDefinitionList ;

#---------------------------------------------------------------------------*

rule <class_method_declaration> ?!@methodDefinitionList ioMethodDefinitionList :
  $method$ ;
  $identifier$ ? @lstring mMethodName ;
  @formalParameterList mSignature ;
  <formal_parameter_list> ? mSignature ;
  $:$ ;
  @semanticInstructionList mInstructionList ;
  <semantic_instruction_list> ? mInstructionList ;
  $end$ ;
  $method$ ;
  $;$ ;
  ioMethodDefinitionList +=
    !mMethodName
    !mSignature
    !mInstructionList
  ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_declaration> !@semanticDeclaration outSemanticDeclaration :
  @bool mIsAbstract ;
  select
    mIsAbstract := false ;
  or
    $abstract$ ;
    mIsAbstract := true ;
  end select ;
  $class$ ;
  $type_name$ ?@lstring mClassTypeName ;
  @lstring mSuperClassName ; # Empty if no super class
  select
    mSuperClassName := [@lstring new !"" !here] ;
  or
    $extends$ ;
    $type_name$ ? mSuperClassName ;
  end select ;
  ${$ ;
  @string mClassMessage := "" ;
#--- Class properties
  @attributeInCollectionList mAttributeList [emptyList] ;
  @abstractMethodDefinitionList mAbstractMethodList [emptyList] ;
  @overridenMethodDefinitionList mOverridenMethodList [emptyList] ;
  @methodDefinitionList mMethodList [emptyList] ;
  @classMessageDefinitionList mMessageList [emptyList] ;
  repeat
  while
    <attribute_declaration> !? mAttributeList ;
  while
    <class_abstract_method_declaration> !? mAbstractMethodList ;
  while
    <class_overriden_method_declaration> !? mOverridenMethodList ;
  while
    <class_method_declaration> !? mMethodList ;
  while
    <class_message_declaration> !? mMessageList ;
  while
    $message$ ;
    if [mClassMessage length] > 0 then
      error here: "the class message is already defined" ;
    end if ;
    @lstring classMessage ;
    $literal_string$ ? classMessage ;
    if [classMessage length] == 0 then
      error classMessage: "the class message string should be not empty" ;
    end if ;
    mClassMessage := [classMessage string] ;
    $;$ ;
  end repeat ;
  $}$ ;
  outSemanticDeclaration := [@classDeclaration new
    !mIsAbstract
    !mClassTypeName
    !mSuperClassName # Empty if no super class
    !mAttributeList
    !mAbstractMethodList
    !mOverridenMethodList
    !mMethodList
    !mMessageList
    !mClassMessage
  ] ;
end rule ;


#---------------------------------------------------------------------------*
#                                                                           *
#    L I S T    D E C L A R A T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration> !@semanticDeclaration outSemanticDeclaration :
  $list$ ;
  $type_name$ ?@lstring mListTypeName ;
  ${$ ;
  @attributeInCollectionList mAttributeList [emptyList] ;
  repeat
  while
    <attribute_declaration> !? mAttributeList ;
  end repeat ;
  $}$ ;
  outSemanticDeclaration := [@listDeclaration new
    !mListTypeName
    !mAttributeList
  ] ;
end rule ;

#---------------------------------------------------------------------------*

rule <obsolete_declaration> :
  $list$ ;
  $type_name$ ? * ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    S O R T E D    L I S T    D E C L A R A T I O N                        *
#                                                                           *
#---------------------------------------------------------------------------*

rule <obsolete_declaration> :
  $sortedlist$ ;
  $type_name$ ? * ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <sortedlist_sort_descriptor> -> @sortedListSortDescriptor :
  $identifier$ ? mSortedAttributeName ;
  select
    $<$ ;
    mAscending := true ;
  or
    $>$ ;
    mAscending := false ;
  end select ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_declaration> !@semanticDeclaration outSemanticDeclaration -> @sortedListDeclaration :
  $sortedlist$ ;
  $type_name$ ? mSortedListTypeName ;
  ${$ ;
  repeat
  while
    <attribute_declaration> !? mAttributeList ;
  end repeat ;
  $}$ ;
  ${$ ;
  repeat
    <sortedlist_sort_descriptor> !? mSortDescriptorList ;
  while
    $,$ ;
  end repeat ;
  $}$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E N U M    D E C L A R A T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration> !@semanticDeclaration outSemanticDeclaration -> @enumDeclaration :
  $enum$ ;
  $type_name$ ? mEnumTypeName ;
  ${$ ;
  mConstructorConstantList := [@lstringlist emptyList] ;
  mConstantList := [@lstringlist emptyList] ;
  repeat
    select
      repeat
        @lstring constantName ;
        $identifier$ ? constantName ;
        mConstantList += !constantName ;
      while
       $,$ ;
      end repeat ;
    or
      $constructor$ ;
      repeat
        @lstring constantName ;
        $identifier$ ? constantName ;
        mConstructorConstantList += !constantName ;
      while
       $,$ ;
      end repeat ;
    end select ;
    $;$ ;
  while
  end repeat ;
  $}$ ;
  ${$ ;
  repeat
  while
    <enum_message_declaration> !? mEnumMessageList ;
  while
    <enum_modifier> !? mEnumModifierList ;
  while
    <enum_method> !? mEnumMethodList ;
  end repeat ;
  $}$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <enum_message_declaration> -> @enumMessage :
  $reader$ ;
  $identifier$ ? mEnumMessageName ;
  ${$ ;
  repeat
    <enum_message_association> !? mMessageAssociationList ;
  while
  end repeat ;
  $}$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <enum_message_association> -> @enumMessageAssociation :
  $identifier$ ? mConstantName ;
  $->$ ;
  $literal_string$ ? mMessage ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <enum_modifier> -> @enumModifier :
  $modifier$ ;
  $identifier$ ? mModifierName ;
  $??$ ;
  @lstring typeName ;
  $type_name$ ? typeName ;
  if [typeName string] != "location" then
    error typeName: "only '@location' type is allowed here" ;
  end if ;
  @lstring mFormalParameterName ;
  $identifier$ ? mLocationFormalArgumentName ;
  ${$ ;
  repeat
    <enum_modifier_case> !? mEnumModifierCaseList ;
  while
  end repeat ;
  $}$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <enum_modifier_case> -> @enumModifierCase :
  $identifier$ ? mSourceConstantName ;
  $->$ ;
  $identifier$ ? mTargetConstantName ;
  <enum_case_message> -> mEnumModifierCaseMessage ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <enum_case_message> -> @enumCaseMessage :
end rule ;

#---------------------------------------------------------------------------*

rule <enum_case_message> -> @enumCaseErrorMessage :
  $error$ ;
  $identifier$ ? mLocationParameterName ;
  $:$ ;
  $literal_string$ ? mMessage ;
end rule ;

#---------------------------------------------------------------------------*

rule <enum_case_message> -> @enumCaseWarningMessage :
  $warning$ ;
  $identifier$ ? mLocationParameterName ;
  $:$ ;
  $literal_string$ ? mMessage ;
end rule ;

#---------------------------------------------------------------------------*

rule <enum_method> -> @enumMethod :
  $method$ ;
  $identifier$ ? mMethodName ;
  $??$ ;
  @lstring typeName ;
  $type_name$ ? typeName ;
  if [typeName string] != "location" then
    error typeName: "only '@location' type is allowed here" ;
  end if ;
  @lstring mFormalParameterName ;
  $identifier$ ? mLocationFormalArgumentName ;
  ${$ ;
  repeat
    <enum_method_case> !? mEnumMethodCaseList ;
  while
  end repeat ;
  $}$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <enum_method_case> -> @enumMethodCase :
  $identifier$ ? mConstantName ;
  <enum_case_message> -> mEnumMethodCaseMessage ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    F I L E W R A P P E R    D E C L A R A T I O N                         *
#                                                                           *
#---------------------------------------------------------------------------*

nonterminal <filewrapper_template> -> @filewrapperTemplate ;

rule <semantic_declaration> !@semanticDeclaration outSemanticDeclaration -> @filewrapperDeclaration :
  $filewrapper$ ;
  $identifier$ ? mFilewrapperName ;
  $in$ ;
  $literal_string$ ? mFilewrapperPath ;
  ${$ ;
  mFilewrapperExtensionList := [@lstringlist emptyList] ;
  repeat
    @lstring extension ;
    $literal_string$ ? extension ;
    mFilewrapperExtensionList += !extension ;
  while
    $,$ ;
  end repeat ;
  $}$ ;
  ${$ ;
  repeat
  while
    <filewrapper_template> !? mFilewrapperTemplateList ;
  end repeat ;
  $}$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <filewrapper_template> -> @filewrapperTemplate :
  $template$ ;
  $identifier$ ? mFilewrapperTemplateName ;
  $literal_string$ ? mFilewrapperTemplatePath ;
  <formal_input_parameter_list> -> mFilewrapperTemplateFormalInputParameters ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;