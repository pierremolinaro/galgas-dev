#---------------------------------------------------------------------------*
#                                                                           *
#  Component for compiling a semantics component                            *
#                                                                           *
#  Copyright (C) 2007, ..., 2010 Pierre Molinaro.                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

semantics semanticsCompilation :
  import "allASTTypes.gSemantics" ;
  import "predefinedTypes.gSemantics" ;
  import "grammarCompilation.gSemantics" ;

  import semantics allASTTypes in "allASTTypes.gSemantics" ;
  import semantics predefinedTypes in "predefinedTypes.gSemantics" ;
  import semantics grammarCompilation in "grammarCompilation.gSemantics" ;
  import grammar semanticsSLRgrammar in "semanticsSLRgrammar.gGrammar" ;

#---------------------------------------------------------------------------*
#                                                                           *
#           G E N E R A T I O N    F I L E W R A P P E R S                  *
#                                                                           *
#---------------------------------------------------------------------------*

filewrapper semanticFileGenerationTemplate in "../generation_templates/semantic_generation" {
} {
  template semanticFileHeader "semantic-file.h.gTemplate"
    ?@string COMPONENT_NAME
    ?@stringlist IMPORTED_COMPONENT_LIST
    ?@lstringlist TYPE_PREDECLARATION_LIST
  ;
  template semanticFileImplementation "semantic-file.cpp.gTemplate"
    ?@string COMPONENT_NAME
    ?@stringlist IMPORTED_COMPONENT_LIST
  ;
}

#---------------------------------------------------------------------------*
#                                                                           *
#    C O M P I L E    S E M A N T I C S   C O M P O N E N T                 *
#                                                                           *
#---------------------------------------------------------------------------*

routine parseSemanticComponent
  ??@lstring inSourceFile
  ?!@parsedComponentStruct ioParsedComponentStruct
  !@semanticsComponentAST outSemanticsComponentRoot
:
  @string basename := [[inSourceFile lastPathComponent] stringByDeletingPathExtension] ;
  @lstring basenameKey [new !basename !here] ;
  if [[ioParsedComponentStruct mParsedSemanticsComponentMap] hasKey ! basename] then
    [[ioParsedComponentStruct mParsedSemanticsComponentMap] searchKey !basenameKey ?outSemanticsComponentRoot] ;
  else
    grammar semanticsSLRgrammar in inSourceFile ?outSemanticsComponentRoot ;
    [!?ioParsedComponentStruct->mParsedSemanticsComponentMap insertKey !basenameKey !outSemanticsComponentRoot] ;
  end if ;
end routine ;

#---------------------------------------------------------------------------*

list @grammarComponentASTList {
  @grammarComponentAST mGrammarComponentAST ;
}

#---------------------------------------------------------------------------*

routine recursivelyImportSemanticComponent
  ?!@parsedComponentStruct ioParsedComponentStruct
  ?!@lstringlist ioOptionComponentFileNameList
  ?!@grammarComponentASTList ioGrammarComponentASTList
  ?!@semanticDeclarationListAST ioSemanticDeclarationList
  ?!@stringset ioParsedFileSet
  ?!@stringlist ioHeaderIncludeList
  ?!@stringlist ioImplementationIncludeList
  ??@lstringlist inListOfFilesToParse
  ??@string inDirectory
:
  foreach inListOfFilesToParse do
    const @lstring filePath [new
      ![mValue absolutePathFromPath !inDirectory]
      ![mValue location]
    ] ;
    if not [ioParsedFileSet hasKey ![filePath string]] then
      ioParsedFileSet += ![filePath string] ;
      const @string extension := [[filePath string] pathExtension] ;
      if extension == "gOption" then
        ioOptionComponentFileNameList += !filePath ;
        ioImplementationIncludeList += ![[filePath lastPathComponent] stringByDeletingPathExtension] ;
      elsif extension == "gGrammar" then
        @grammarComponentAST grammarComponentAST ;
        parseGrammarComponent
          !filePath
          !?ioParsedComponentStruct
          ?grammarComponentAST
        ;
        ioGrammarComponentASTList += !grammarComponentAST ;
        ioImplementationIncludeList += ![[grammarComponentAST mGrammarName] string] ;
      elsif extension == "gSemantics" then
      #--- Parse semantic component
        @semanticsComponentAST semanticsComponentRoot ;
        parseSemanticComponent
          !filePath
          !?ioParsedComponentStruct
          ?semanticsComponentRoot
        ;
        ioHeaderIncludeList += ![[semanticsComponentRoot mSemanticsComponentName] string] ;
      #--- Add declaration to declaration list
        ioSemanticDeclarationList .= [semanticsComponentRoot mSemanticDeclarationList] ;
      #--- Import semantic components named in this file
        recursivelyImportSemanticComponent
          !?ioParsedComponentStruct
          !?ioOptionComponentFileNameList
          !?ioGrammarComponentASTList
          !?ioSemanticDeclarationList
          !?ioParsedFileSet
          !?ioHeaderIncludeList
          !?ioImplementationIncludeList
          ![semanticsComponentRoot mImportedComponentFileNameList]
          !inDirectory
        ;
      end if ;
    end if ;
  end foreach ;
end routine ;

#---------------------------------------------------------------------------*

routine buildOptionComponentMapForSemanticAnalysis
  ??@lstringlist inOptionComponentFileNameList
  ?!@parsedComponentStruct ioParsedComponentStruct
  ?!@optionComponentMapForSemanticAnalysis ioOptionComponentMapForSemanticAnalysis
:
  foreach inOptionComponentFileNameList do
    @lstring optionComponentName ;
    @commandLineOptionMap boolOptionMap ;
    @commandLineOptionMap uintOptionMap ;
    @commandLineOptionMap stringOptionMap ;
    parseOptionComponent
      !mValue
      !?ioParsedComponentStruct
      ?optionComponentName
      ?boolOptionMap
      ?uintOptionMap
      ?stringOptionMap
    ;
    [!?ioOptionComponentMapForSemanticAnalysis insertKey
      !optionComponentName
      !boolOptionMap
      !uintOptionMap
      !stringOptionMap
    ] ;
  end foreach ;
#--- Add predefined option component
  @string2list optionNameList := [@system boolOptionNameList] ;
  @commandLineOptionMap boolOptionMap [emptyMap] ;
  foreach optionNameList do
    if (mValue0 == "galgas_cli_options") then
    [!?boolOptionMap insertKey
      ![@lstring new !mValue1 !here]
      ![@system boolOptionInvocationLetter !mValue0 !mValue1]
      ![@system boolOptionInvocationString !mValue0 !mValue1]
      ![@system boolOptionCommentString !mValue0 !mValue1]
      !"" # Not used
      ] ;
    end if ;
  end foreach ;
#--- 
  optionNameList := [@system uintOptionNameList] ;
  @commandLineOptionMap uintOptionMap [emptyMap] ;
  foreach optionNameList do
    if (mValue0 == "galgas_cli_options") then
    [!?uintOptionMap insertKey
      ![@lstring new !mValue1 !here]
      ![@system uintOptionInvocationLetter !mValue0 !mValue1]
      ![@system uintOptionInvocationString !mValue0 !mValue1]
      ![@system uintOptionCommentString !mValue0 !mValue1]
      !"" # Not used
      ] ;
    end if ;
  end foreach ;
#--- 
  optionNameList := [@system stringOptionNameList] ;
  @commandLineOptionMap stringOptionMap [emptyMap] ;
  foreach optionNameList do
    if (mValue0 == "galgas_cli_options") then
    [!?stringOptionMap insertKey
      ![@lstring new !mValue1 !here]
      ![@system stringOptionInvocationLetter !mValue0 !mValue1]
      ![@system stringOptionInvocationString !mValue0 !mValue1]
      ![@system stringOptionCommentString !mValue0 !mValue1]
      !"" # Not used
      ] ;
    end if ;
  end foreach ;
#---
  [!?ioOptionComponentMapForSemanticAnalysis insertKey
    ![@lstring new !"galgas_cli_options" !here]
    !boolOptionMap
    !uintOptionMap
    !stringOptionMap
  ] ;
end routine ;

#---------------------------------------------------------------------------*

routine buildGrammarComponentMapForSemanticAnalysis
  ??@grammarComponentASTList inGrammarComponentASTList
  ?!@semanticContext ioSemanticContext
:
#--- Perform semantic analysis of grammar components
  foreach inGrammarComponentASTList do
  #--- Analyze start symbol labels
    @grammarLabelMap grammarLabelMap [emptyMap] ;
    foreach [mGrammarComponentAST mStartSymbolLabelList] do
    #--- Analyze signature
      @formalParameterSignature formalArgumentList [emptyList] ;
      foreach mFormalArgumentList do
        formalArgumentList +=
          ![@unifiedTypeMapProxy searchKey !ioSemanticContext->mTypeMap !mFormalArgumentTypeName]
          !mFormalArgumentPassingMode
        ;
      end foreach ;
      [!?grammarLabelMap insertKey
        !mLabelName
        !formalArgumentList
      ] ;
    end foreach ;
  #--- Insert grammar in grammar map
    [!?ioSemanticContext->mGrammarMap insertKey
      ![mGrammarComponentAST mGrammarName]
      !grammarLabelMap
    ] ;
  end foreach ;
end routine ;

#---------------------------------------------------------------------------*

routine compileSemanticsComponent
  ??@lstring inSourceFile
  ??@string inOutputDirectory
  ?!@parsedComponentStruct ioParsedComponentStruct
:
#--- Parsing
  @semanticsComponentAST semanticsComponentRoot ;
  parseSemanticComponent
    !inSourceFile
    !?ioParsedComponentStruct
    ?semanticsComponentRoot
  ;
#--- Check component name
  const @string basename := [[inSourceFile lastPathComponent] stringByDeletingPathExtension] ;
  const @lstring componentName := [semanticsComponentRoot mSemanticsComponentName] ;
  if [componentName string] != basename then
    warning componentName: "GALGAS checks "
      "the component name ('" . [componentName string]
    . "') against the source file base name ('" . basename . "'): they should be identical"
    ;
  end if ; 
#--- Recursively parse all imported files
  @semanticDeclarationListAST semanticDeclarationList := [semanticsComponentRoot mSemanticDeclarationList] ;
  @stringset parsedFileSet [setWithString ![inSourceFile string]] ;
  @lstringlist optionComponentFileNameList [emptyList] ;
  @grammarComponentASTList grammarComponentASTList [emptyList] ;
  @stringlist headerIncludeList [emptyList] ;
  @stringlist implementationIncludeList [emptyList] ;
  recursivelyImportSemanticComponent
    !?ioParsedComponentStruct
    !?optionComponentFileNameList
    !?grammarComponentASTList
    !?semanticDeclarationList
    !?parsedFileSet
    !?headerIncludeList
    !?implementationIncludeList
    ![semanticsComponentRoot mImportedComponentFileNameList]
    ![inSourceFile stringByDeletingLastPathComponent]
  ;
#------ Add predefined types to semantics declarations
  @semanticDeclarationListAST semanticDeclarationListWithPredefinedTypes := semanticDeclarationList ;
  appendPredefinedTypesASTs !?semanticDeclarationListWithPredefinedTypes ;
#------ Build semantic context
  @semanticContext semanticContext ;
  buildSemanticContext
    !semanticDeclarationListWithPredefinedTypes
    ![semanticsComponentRoot mEndOfSourceFile]
    ?semanticContext
  ;
#--- Build option component map for analysis
  buildOptionComponentMapForSemanticAnalysis
    !optionComponentFileNameList
    !?ioParsedComponentStruct
    !?semanticContext->mOptionComponentMapForSemanticAnalysis
  ;
#--- Perform semantic analysis of grammar components
  buildGrammarComponentMapForSemanticAnalysis
    !grammarComponentASTList
    !?semanticContext
  ;
#------ Semantic Analysis
  @semanticDeclarationListForGeneration decoratedDeclarationListForGeneration ;
  performSemanticAnalysis
    ![semanticsComponentRoot mSemanticDeclarationList]
    !semanticContext
    ?decoratedDeclarationListForGeneration
  ;
#------ Generate Header File
  if [@uint errorCount] == 0 then
    @string headerString := [filewrapper semanticFileGenerationTemplate.semanticFileHeader
      ![componentName string]
      !headerIncludeList
      ![semanticContext->mTypeMap allKeyList]
    ] ;
    foreach decoratedDeclarationListForGeneration do
      headerString .= [mDeclaration appendAutoPointerClassDeclaration] ;
    end foreach ;
    foreach decoratedDeclarationListForGeneration do
      headerString .= [mDeclaration appendDeclaration] ;
      headerString .= [mDeclaration appendSpecificDeclaration] ;
    end foreach ;
    [@string generateFile
      !inOutputDirectory
      ![componentName string] . ".h"
      !"//"
      !"\n\n" # Defaut user zone1
      !headerString
      !"\n\n" # Defaut user zone2
      !"#endif\n"
    ] ;
  end if ;
#------ Generate Implementation File
  if [@uint errorCount] == 0 then
    @string implementationString := [filewrapper semanticFileGenerationTemplate.semanticFileImplementation
      ![componentName string]
      !implementationIncludeList
    ] ;
    foreach decoratedDeclarationListForGeneration do
      implementationString .= [mDeclaration appendAutoPointerClassImplementation] ;
      implementationString .= [mDeclaration appendSpecificImplementation] ;
      implementationString .= [mDeclaration appendTypeGenericImplementation] ;
    end foreach ;
    [@string generateFile
      !inOutputDirectory
      ![componentName string] . ".cpp"
      !"//"
      !"\n\n" # Defaut user zone1
      !implementationString
      !"\n\n" # Defaut user zone2
      !"\n"
    ] ;
  end if ;
end routine ;

#---------------------------------------------------------------------------*

end semantics ;
