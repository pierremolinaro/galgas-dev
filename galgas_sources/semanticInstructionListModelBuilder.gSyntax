#---------------------------------------------------------------------------*
#                                                                           *
#  'metamodel' component parser, as model builder                           *
#                                                                           *
#  Copyright (C) 2007, ..., 2008 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

syntax semanticInstructionListModelBuilder :
import lexique galgas_scanner in "galgas_scanner.gLexique" ;
import semantics expressionMetamodel in "expressionMetamodel.gSemantics" ;
import semantics parameterArgumentMetamodel in "parameterArgumentMetamodel.gSemantics" ;
import semantics instructionMetamodel in "instructionMetamodel.gSemantics" ;

#------------------------------------------------------------------------------*
#                                                                              *
#          Rules prototypes                                                    *
#                                                                              *
#------------------------------------------------------------------------------*

nonterminal <expression> !@semanticExpression outExpression ;

nonterminal <formal_parameter_list> !@formalParameterList outFormalParameterList ;

nonterminal <actual_parameter_list> !@actualParameterList outActualParameterList ;

nonterminal <semantic_instruction_list> !@semanticInstructionList outInstructionsList ;

nonterminal <semantic_instruction> !@semanticInstruction outInstruction ;


nonterminal <foreach_enumerated_object> -> @foreachInstructionEnumeratedObject ;

nonterminal <match_instruction_branch> -> @matchInstructionBranch ;

nonterminal <cast_instruction_branch> -> @castInstructionBranch ;

nonterminal <cast_else_or_default> -> @elseOrDefaultForCastInstruction ;

nonterminal <output_expression_list> -> @outExpressionList ;

nonterminal <match_entry> -> @matchEntry ;

nonterminal <semantic_declaration> !@semanticDeclaration outSemanticDeclaration ;

#---------------------------------------------------------------------------*
#                                                                           *
#    S E M A N T I C    I N S T R U C T I O N    L I S T                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction_list> !@semanticInstructionList outInstructionsList :
  outInstructionsList := [@semanticInstructionList emptyList] ;
  repeat
  while
    @semanticInstruction instruction ;
    <semantic_instruction> +? instruction ;
    outInstructionsList += !instruction ;
  end repeat ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    R O U T I N E    D E C L A R A T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration> !@semanticDeclaration outSemanticDeclaration :
  $routine$ ;
  $identifier$ ? @lstring mRoutineName ;
  @formalParameterList mFormalParameterList ;
  <formal_parameter_list> ? mFormalParameterList ;
  $:$ ;
  @semanticInstructionList mRoutineInstructionList ;
  <semantic_instruction_list> ? mRoutineInstructionList ;
  $end$ ;
  $routine$ ;
  $;$ ;
  outSemanticDeclaration := [@routineDeclaration new
    !mRoutineName
    !mFormalParameterList
    !mRoutineInstructionList
  ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E X T E R N    R O U T I N E    D E C L A R A T I O N                  *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_declaration> !@semanticDeclaration outSemanticDeclaration :
  $extern$ ;
  $routine$ ;
  $identifier$ ? @lstring mActionName ;
  @formalParameterList mFormalParameterList ;
  <formal_parameter_list> ? mFormalParameterList ;
  $;$ ;
  outSemanticDeclaration := [@externRoutineDeclaration new
    !mActionName
    !mFormalParameterList
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  L O C A L    V A R I A B L E    D E C L A R A T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction :
  @lstring typeName ;
  $type_name$ ? typeName ;
  @lstring variableName ;
  $identifier$ ? variableName ;
  select
    outInstruction := [@localVariableDeclaration new !typeName !variableName] ;
  or
    $:=$ ;
    @semanticExpression sourceExpression ;
    <expression> -> sourceExpression ;
    outInstruction := [@localVariableDeclarationWithAssignment new !typeName !variableName !sourceExpression] ;
  or
    $[$ ;
    @lstring constructorName ;
    $identifier$ ? constructorName ;
    @outExpressionList expressionList ;
    <output_expression_list> -> expressionList ;
    outInstruction := [@localVariableDeclarationWithInitialization new !typeName !variableName !constructorName !expressionList] ;
    $]$ ;
  end select ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    A S S I G N M E N T    I N S T R U C T I O N                           *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction :
  $identifier$ ?@lstring mTargetVariableName ;
  $:=$ ;
  <expression> ?mSourceExpression ;
  $;$ ;
  outInstruction := [@assignmentInstruction new
    !mTargetVariableName
    !mSourceExpression
  ] ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    I N C R E M E N T    I N S T R U C T I O N                             *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @incrementInstruction :
  $identifier$ ? mReceiverName ;
  $++$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    D E C R E M E N T    I N S T R U C T I O N                             *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @decrementInstruction :
  $identifier$ ? mReceiverName ;
  $--$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    F O R E A C H    I N S T R U C T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <foreach_enumerated_object> -> @foreachInstructionEnumeratedObject :
  <expression> -> mEnumeratedExpression ;
  $($ ;
  repeat
  while
    $*$ ;
    mElements += ![@foreachInstructionJoker new] ;
  while
    @luint count ;
    $unsigned_literal_integer$ ?count ;
    $*$ ;
    @uint n := [count uint] ;
    if n == 0 then
      error count: "this value should be > 0" ;
    end if ;
    loop n :
    while n > 0 do
      mElements += ![@foreachInstructionJoker new] ;
      n -- ;
    end loop ;
  while
    @lstring typeName ;
    $type_name$ ?typeName ;
    @lstring constantName ;
    $identifier$ ?constantName ;
    @foreachInstructionEnumeratedConstant c [new !typeName !constantName] ;
    mElements += !c ;
  end repeat ;
  select
    mEndsWithEllipsis := false ;
  or
    $...$ ;
    mEndsWithEllipsis := true ;
  end select ;
  $)$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @foreachInstruction :
  $foreach$ ;
  repeat
    <foreach_enumerated_object> +> mEnumeratedObjects ;
  while
    $,$ ;
  end repeat ;
  select
    mIndexVariableName := [@lstring new !"" !here] ;
  or
    $:$ ;
    $identifier$ ? mIndexVariableName ;
  end select ;
  select
    mWhileExpression := [@trueExpression new] ;
  or
    $while$ ;
    <expression> -> mWhileExpression ;
  end select ;
  select
  or
    $before$ ;
    repeat
    while
      <semantic_instruction> +> mBeforeInstructionList ;
    end repeat ;
  end select ;
  select
  or
    $between$ ;
    repeat
    while
      <semantic_instruction> +> mBetweenInstructionList ;
    end repeat ;
  end select ;
  $do$ ;
  <semantic_instruction_list> -> mDoInstructionList ;
  select
  or
    $after$ ;
    repeat
    while
      <semantic_instruction> +> mAfterInstructionList ;
    end repeat ;
  end select ;
  $end$ ;
  $foreach$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    I F    I N S T R U C T I O N                                           *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @ifInstruction :
  $if$ ;
  repeat
    <expression> +> mIFexpressionList ;
    $then$ ;
    <semantic_instruction_list> +> mIFinstructionListList ;
  while
    $elsif$ ;
  end repeat ;
  select
  or
    $else$ ;
    <semantic_instruction_list> +> mElseInstructionList ;
  end select ;
  $end$ ;
  $if$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M E T H O D    C A L L    I N S T R U C T I O N                        *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @methodCallInstruction :
  $[$ ;
  <expression> -> mReceiverExpression ;
  $identifier$ ?mMethodName ;
  <actual_parameter_list> -> mActualParameterList ;
  $]$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M O D I F I E R    C A L L    I N S T R U C T I O N                    *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @modifierCallInstruction :
  $[$ ;
  $!?$ ;
  $identifier$ ?mReceiverName ;
  $identifier$ ?mModifierName ;
  <actual_parameter_list> -> mActualParameterList ;
  $]$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    E R R O R    I N S T R U C T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @errorInstruction :
  $error$ ;
  <expression> -> mReceiverExpression ;
  $:$ ;
  <expression> -> mErrorExpression ;
  mBuiltVariableList := [@lstringlist emptyList] ;
  select
  or
    $->$ ;
    repeat
     @lstring builtVariableName ;
     $identifier$ ? builtVariableName ;
     mBuiltVariableList += !builtVariableName ;
    while
      $,$ ;
    end repeat ;
  end select ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    W A R N I N G    I N S T R U C T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @warningInstruction :
  $warning$ ;
  <expression> -> mReceiverExpression ;
  $:$ ;
  <expression> -> mErrorExpression ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    R O U T I N E    C A L L    I N S T R U C T I O N                      *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @routineCallInstruction :
  $identifier$ ? mRoutineName ;
  <actual_parameter_list> -> mActualParameterList ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M A T C H    I N S T R U C T I O N                                     *
#                                                                           *
#---------------------------------------------------------------------------*

rule <match_entry> -> @constantMatchEntry :
  $identifier$ ? mConstantName ;
end rule ;

#---------------------------------------------------------------------------*

rule <match_entry> -> @classMatchEntry :
  $type_name$ ? mClassName ;
  $($ ;
  repeat
  while
    $*$ ;
    mElements += ![@foreachInstructionJoker new] ;
  while
    @luint count ;
    $unsigned_literal_integer$ ?count ;
    $*$ ;
    @uint n := [count uint] ;
    if n == 0 then
      error count: "this value should be > 0" ;
    end if ;
    loop n :
    while n > 0 do
      mElements += ![@foreachInstructionJoker new] ;
      n -- ;
    end loop ;
  while
    @lstring typeName ;
    $type_name$ ?typeName ;
    @lstring constantName ;
    $identifier$ ?constantName ;
    @foreachInstructionEnumeratedConstant c [new !typeName !constantName] ;
    mElements += !c ;
  end repeat ;
  select
    mEndsWithEllipsis := false ;
  or
    $...$ ;
    mEndsWithEllipsis := true ;
  end select ;
  $)$ ;
end rule ;

#---------------------------------------------------------------------------*

rule <match_instruction_branch> -> @matchInstructionBranch :
  $when$ ;
  repeat
    <match_entry> +> mMatchedEntryList ;
  while
    $::$ ;
  end repeat ;
  $:$ ;
  <semantic_instruction_list> -> mMatchBranchInstructionList ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @matchInstruction :
  $match$ ;
  mMatchedVariableNameList := [@lstringlist emptyList] ;
  @lstring matchedVariableName ;
  $identifier$ ? matchedVariableName ;
  mMatchedVariableNameList += !matchedVariableName ;
  $::$ ;
  repeat
    $identifier$ ? matchedVariableName ;
    mMatchedVariableNameList += !matchedVariableName ;
  while
    $::$ ;
  end repeat ;
  $:$ ;
  repeat
    <match_instruction_branch> +> mMatchInstructionBranchList ;
  while
  end repeat ;
  select
  or
    $else$ ;
    <semantic_instruction_list> +> mElseInstructionList ;
  end select ;
  $end$ ;
  $match$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    C A S T    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <cast_instruction_branch> -> @castInstructionBranch :
  $when$ ;
  select
    $>=$ ;
    mUseKindOfClass := true ;
  or
    $==$ ;
    mUseKindOfClass := false ;
  end select ;
  $type_name$ ? mTypeName ;
  select
    $identifier$ ?mConstantVarName ;
    mConstantVarNameIsUnused := false ;
  or
    $unused$ ;
    $identifier$ ?mConstantVarName ;
    mConstantVarNameIsUnused := true ;
  or
    mConstantVarName := [@lstring new !"" !here] ;
    mConstantVarNameIsUnused := true ;
  end select ;
  $do$ ;
  <semantic_instruction_list> -> mCastBranchInstructionList ;
end rule ;

#---------------------------------------------------------------------------*

rule <cast_else_or_default> -> @elseForCastInstruction :
  $else$ ;
  <semantic_instruction_list> -> mElseInstructionList ;
end rule ;

#---------------------------------------------------------------------------*

rule <cast_else_or_default> -> @defaultForCastInstruction :
  $default$ ;
  $error$ ;
  <expression> -> mErrorLocationExpression ;
end rule ;

#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @castInstruction :
  $cast$ ;
  <expression> -> mExpression ;
  $:$ ;
  repeat
    <cast_instruction_branch> +> mCastInstructionBranchList ;
  while
  end repeat ;
  <cast_else_or_default> -> mElseOrDefault ;
  $end$ ;
  $cast$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    A D D    I N S T R U C T I O N    + =                                  *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @addInstruction :
  $identifier$ ? mReceiverName ;
  $+=$ ;
  <output_expression_list> -> mExpressionList ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    A P P E N D    I N S T R U C T I O N    . =                            *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @appendInstruction :
  $identifier$ ? mTargetVariableName ;
  $.=$ ;
  <expression> -> mSourceExpression ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    L O O P    I N S T R U C T I O N                                       *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @loopInstruction :
  $loop$ ;
  <expression> -> mVariantExpression ;
  $:$ ;
  <semantic_instruction_list> -> mFirstInstructions ;
  $while$ ;
  <expression> -> mLoopExpression ;
  $do$ ;
  <semantic_instruction_list> -> mSecondInstructions ;
  $end$ ;
  $loop$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    M E S S A G E    I N S T R U C T I O N                                 *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @messageInstruction :
  $message$ ;
  <expression> -> mExpression ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#    L O G    I N S T R U C T I O N                                         *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @logInstruction :
  $log$ ;
  mLoggedVariableList := [@lstringlist emptyList] ;
  repeat
    @lstring loggedVariableName ;
    $identifier$ ? loggedVariableName ;
    mLoggedVariableList += !loggedVariableName ;
  while
    $,$ ;
  end repeat ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  G R A M M A R    I N S T R U C T I O N                                   *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @grammarInstruction :
  $grammar$ ;
  $identifier$ ?mGrammarComponentName ;
  select
    mLabelName := [@lstring new !"" !here] ;
  or
    $label$ ;
    $identifier$ ?mLabelName ;
  end select ;
  $in$ ;
  <expression> -> mSourceFileExpression ;
  <actual_parameter_list> -> mActualParameterList ;
  select
    mReturnedModelName := [@lstring new !"" !here] ;
  or
    $->$ ;
    $identifier$ ? mReturnedModelName ;
  end select ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  T R E E W A L K I N G    I N S T R U C T I O N                           *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @treewalkingInstruction :
  $treewalking$ ;
  $identifier$ ? mTreewalkingComponentName ;
  $on$ ;
  $identifier$ ? mModelName ;
  <actual_parameter_list> -> mActualParameterList ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*
#                                                                           *
#  T Y P E    M E T H O D    C A L L    I N S T R U C T I O N               *
#                                                                           *
#---------------------------------------------------------------------------*

rule <semantic_instruction> !@semanticInstruction outInstruction -> @typeMethodInstruction :
  $[$ ;
  $type_name$ ? mTypeName ;
  $identifier$ ? mMethodName ;
  <actual_parameter_list> -> mActualParameterList ;
  $]$ ;
  $;$ ;
end rule ;

#---------------------------------------------------------------------------*

end syntax ;
