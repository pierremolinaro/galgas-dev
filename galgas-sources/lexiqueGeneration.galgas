#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#                                           
#  'lexique' component generation           
#                                           
#  Copyright (C) 2007, ..., 2015 Pierre Molinaro.                                                                      *
#                                           
#  e-mail : pierre@pcmolinaro.name          
#                                           
#  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public   *
#  License as published by the Free Software Foundation.                                                               *
#                                           
#  This program is distributed in the hope it will be useful, but WITHOUT ANY WARRANTY; without even the implied       *
#  warranty of MERCHANDIBILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more        *
#  details.                                 
#                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   L E X I C A L    E X P R E S S I O N    C O D E    G E N E R A T I O N                                             *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract getter @lexicalExpressionAST generateConditionCode
  ?context:let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalOrExpressionAST generateConditionCode
  ?context:let @lexiqueAnalysisContext inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = [self.mLeftOperand generateConditionCode !context:inLexiqueAnalysisContext]
  outGeneratedCode += " || "
  outGeneratedCode += [self.mRightOperand generateConditionCode !context:inLexiqueAnalysisContext]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalCharacterIntervalMatchAST generateConditionCode
  ?context:let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = ""
  outGeneratedCode = "testForInputUTF32CharRange ("
  outGeneratedCode += [[self.mLowerBound char] utf32CharConstantRepresentation]
  outGeneratedCode += ", "
  outGeneratedCode += [[self.mUpperBound char] utf32CharConstantRepresentation]
  outGeneratedCode += ")"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalStringMatchAST generateConditionCode
  ?context:let @lexiqueAnalysisContext inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "testForInputUTF32String (kUnicodeString_" + [inLexiqueAnalysisContext.mLexiqueName identifierRepresentation] + "_" + [self.mString identifierRepresentation]
  outGeneratedCode += ", "
  outGeneratedCode += [[self.mString length] string]
  outGeneratedCode += ", true)"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalStringNotMatchAST generateConditionCode
  ?context:let @lexiqueAnalysisContext inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "notTestForInputUTF32String (kUnicodeString_" + [inLexiqueAnalysisContext.mLexiqueName identifierRepresentation] + "_"
  outGeneratedCode += [self.mString identifierRepresentation]
  outGeneratedCode += ", "
  outGeneratedCode += [[self.mString length] string]
  outGeneratedCode += ", gLexicalMessage_"
  outGeneratedCode += [inLexiqueAnalysisContext.mLexiqueName identifierRepresentation] + "_"
  outGeneratedCode += [self.mErrorMessage string]
  outGeneratedCode += " COMMA_LINE_AND_SOURCE_FILE)"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalCharacterMatchAST generateConditionCode
  ?context:let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "testForInputUTF32Char ("
  outGeneratedCode += [[self.mCharacter char] utf32CharConstantRepresentation]
  outGeneratedCode += ")"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalCharacterSetMatchAST generateConditionCode
  ?context:let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "testForCharWithFunction ("
  outGeneratedCode += [self.mCharacterSetName string]
  outGeneratedCode += ")"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#     R O U T I N E    O R    F U N C T I O N    A R G U M E N T                                                       *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract getter @lexicalRoutineOrFunctionFormalInputArgumentAST generateRoutineOrFunctionArgument
  -> @string

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalAttributeInputArgumentAST generateRoutineOrFunctionArgument
  -> @string outGeneratedCode {
  outGeneratedCode = "token.mLexicalAttribute_" + [self.mAttributeName identifierRepresentation]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalCharacterInputArgumentAST generateRoutineOrFunctionArgument
  -> @string outGeneratedCode {
  outGeneratedCode = [[self.mCharacter char] utf32CharConstantRepresentation]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalUnsignedInputArgumentAST generateRoutineOrFunctionArgument
  -> @string outGeneratedCode {
  outGeneratedCode = [[self.mUnsigned uint] string]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalCurrentCharacterInputArgumentAST generateRoutineOrFunctionArgument
  -> @string outGeneratedCode {
  outGeneratedCode = "previousChar ()"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalFunctionInputArgumentAST generateRoutineOrFunctionArgument
  -> @string outGeneratedCode {
  outGeneratedCode = "::scanner_function_" + self.mFunctionName + " (*this"
  for (mLexicalActualInputArgument) in self.mFunctionActualArgumentList do
    outGeneratedCode += ", " + [mLexicalActualInputArgument generateRoutineOrFunctionArgument]
  end
  outGeneratedCode += ")"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#                   R O U T I N E    A R G U M E N T                                                                   *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract getter @abstractLexicalRoutineActualArgumentAST generateRoutineArgument
  -> @string

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalAttributeInputOutputArgumentAST generateRoutineArgument
  -> @string outGeneratedCode {
  outGeneratedCode = "token.mLexicalAttribute_" + [self.mAttributeName identifierRepresentation]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalFormalInputArgumentAST generateRoutineArgument
  -> @string outGeneratedCode {
  outGeneratedCode = [self.mRoutineOrFunctionFormalInputArgument generateRoutineOrFunctionArgument]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  L E X I C A L    S E N D  I N S T R U C T I O N    D E F A U L T                                                    *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract getter @lexicalSendDefaultActionAST generateDefaultSendCode
  ?let @string inScannerClassName
  -> @string

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalSendTerminalByDefaultAST generateDefaultSendCode
  ?let @string unused inScannerClassName
  -> @string outGeneratedCode
{
  outGeneratedCode = "token.mTokenCode = kToken_" + self.mDefaultSentTerminal + " ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalErrorByDefaultAST generateDefaultSendCode
  ?let @string inScannerClassName
  -> @string outGeneratedCode {
  outGeneratedCode = "lexicalError (gLexicalMessage_" + inScannerClassName + "_" + self.mDefaultErrorMessageName + " COMMA_LINE_AND_SOURCE_FILE) ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  L E X I C A L    I N S T R U C T I O N    C O D E    G E N E R A T I O N                                            *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract getter @lexicalInstructionAST generateInstructionCode
  ?let @string inScannerClassName
  ?let @lexiqueAnalysisContext inLexiqueAnalysisContext
  -> @string 

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalStructuredSendInstructionAST generateInstructionCode
  ?let @string inScannerClassName
  ?let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = ""
  for (mAttributeName mSearchListName) in self.mLexicalSendSearchList do
    outGeneratedCode += "if (token.mTokenCode == -1) {\n"
#    outGeneratedCode += "  token.mIsOptional = false ;\n"
    outGeneratedCode += "  token.mTokenCode = search_into_" + mSearchListName + " (token.mLexicalAttribute_" + [mAttributeName identifierRepresentation] + ") ;\n"
    outGeneratedCode += "}\n"
  end
  outGeneratedCode += "if (token.mTokenCode == -1) {\n"
  outGeneratedCode += "  " + [self.mLexicalSendDefaultAction generateDefaultSendCode !inScannerClassName]
  outGeneratedCode += "}\n"
  outGeneratedCode += "enterToken (token) ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalDropInstructionAST generateInstructionCode
  ?let @string unused inScannerClassName
  ?let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode
{
  outGeneratedCode = "enterDroppedTerminal (kToken_" + [self.mTerminalName identifierRepresentation] + ") ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalSimpleSendInstructionAST generateInstructionCode
  ?let @string unused inScannerClassName
  ?let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode
{
  outGeneratedCode  = "token.mTokenCode = kToken_" + [self.mSentTerminal identifierRepresentation] + " ;\n"
#  outGeneratedCode += "token.mIsOptional = false ;\n"
  outGeneratedCode += "enterToken (token) ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalRepeatInstructionAST generateInstructionCode
  ?let @string inScannerClassName
  ?let @lexiqueAnalysisContext inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "do {\n"
  [!?outGeneratedCode incIndentation !2]
#--- Repeated instruction
  for (mInstruction) in self.mRepeatedInstructionList do
    outGeneratedCode += [mInstruction generateInstructionCode !inScannerClassName !inLexiqueAnalysisContext]
  end
#---
  for (mWhileExpression mWhileInstructionList) in self.mLexicalWhileBranchList
  do
    outGeneratedCode += "if ("
    outGeneratedCode += [mWhileExpression generateConditionCode !context:inLexiqueAnalysisContext]
    outGeneratedCode += ") {\n"
    [!?outGeneratedCode incIndentation !2]
    for (mInstruction) in mWhileInstructionList do
      outGeneratedCode += [mInstruction generateInstructionCode !inScannerClassName !inLexiqueAnalysisContext]
    end
    [!?outGeneratedCode decIndentation !2]
  between
    outGeneratedCode += "}else "
  end
  outGeneratedCode += "}else{\n"
                      "  mLoop = false ;\n"
                      "}\n"
  [!?outGeneratedCode decIndentation !2]
  outGeneratedCode += "}while (mLoop) ;\n"
                      "mLoop = true ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalSelectInstructionAST generateInstructionCode
  ?let @string inScannerClassName
  ?let @lexiqueAnalysisContext inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = ""
#---
  for (mSelectExpression mSelectInstructionList) in self.mLexicalSelectBranchList
  do
    outGeneratedCode += "if ("
    outGeneratedCode += [mSelectExpression generateConditionCode !context:inLexiqueAnalysisContext]
    outGeneratedCode += ") {\n"
    [!?outGeneratedCode incIndentation !2]
    for (mInstruction) in mSelectInstructionList do
      outGeneratedCode += [mInstruction generateInstructionCode !inScannerClassName !inLexiqueAnalysisContext]
    end
    [!?outGeneratedCode decIndentation !2]
  between
    outGeneratedCode += "}else "
  end
#--- Default branch
  if [self.mDefaultInstructionList length] > 0 then
    outGeneratedCode += "}else{\n"
    [!?outGeneratedCode incIndentation !2]
    for (mInstruction) in self.mDefaultInstructionList do
      outGeneratedCode += [mInstruction generateInstructionCode !inScannerClassName !inLexiqueAnalysisContext]
    end
    [!?outGeneratedCode decIndentation !2]
  end
  outGeneratedCode += "}\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalRoutineInstructionAST generateInstructionCode
  ?let @string inScannerClassName
  ?let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "::scanner_routine_" + self.mRoutineName + " (*this"
  for (mLexicalRoutineActualArgument) in self.mActualArgumentList do
    outGeneratedCode += ", " + [mLexicalRoutineActualArgument generateRoutineArgument]
  end
  for (mValue) in self.mErrorMessageList do
    outGeneratedCode += ", gLexicalMessage_" + inScannerClassName + "_" + mValue
  end
  outGeneratedCode += ") ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalErrorInstructionAST generateInstructionCode
  ?let @string inScannerClassName
  ?let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "lexicalError (gLexicalMessage_" + inScannerClassName + "_" + self.mErrorMessageName + " COMMA_LINE_AND_SOURCE_FILE) ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalWarningInstructionAST generateInstructionCode
  ?let @string inScannerClassName
  ?let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "lexicalWarning (gLexicalMessage_" + inScannerClassName + "_" + self.mWarningMessageName + " COMMA_LINE_AND_SOURCE_FILE) ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalTagInstructionAST generateInstructionCode
  ?let @string unused inScannerClassName
  ?let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "const C_LocationInSource currentLocationForTag_" + self.mLexicalTagName + " = mCurrentLocation ;\n"
                    + "const C_LocationInSource endLocationForTag_" + self.mLexicalTagName + " = mTokenEndLocation ;\n"
                    + "const utf32 currentCharForTag_" + self.mLexicalTagName + " = mCurrentChar ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalRewindInstructionAST generateInstructionCode
  ?let @string unused inScannerClassName
  ?let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "mCurrentLocation = currentLocationForTag_" + self.mLexicalTagName + " ;\n"
                    + "mTokenEndLocation = endLocationForTag_" + self.mLexicalTagName + " ;\n"
                    + "mCurrentChar = currentCharForTag_" + self.mLexicalTagName + " ;\n"
                    + "token.mTokenCode = kToken_" + [self.mTerminalName identifierRepresentation] + " ;\n"
#                    + "token.mIsOptional = false ;\n"
                    + "enterToken (token) ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalLogInstructionAST generateInstructionCode
  ?let @string unused inScannerClassName
  ?let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "lexicalLog (LINE_AND_SOURCE_FILE) ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#        L E X I C A L    R U L E    C O D E    G E N E R A T I O N                                                    *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract getter @abstractLexicalRuleAST generateCode
  ?let @string inScannerClassName
  ?let @lexiqueAnalysisContext unused inLexiqueAnalysisContext
  -> @string 

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalImplicitRuleAST generateCode
  ?let @string inScannerClassName
  ?let @lexiqueAnalysisContext inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  @tokenSortedlist tokenSortedList
  [[inLexiqueAnalysisContext mLexicalTokenListMap] searchKey !self.mListName ?* ?tokenSortedList ?*]
  outGeneratedCode = ""
  for > (mLength mName mTerminalName) in tokenSortedList do
    outGeneratedCode += "if (testForInputUTF32String (kUnicodeString_" + inScannerClassName + "_"
    outGeneratedCode += [mName identifierRepresentation]
    outGeneratedCode += ", "
    outGeneratedCode += [[mName length] string]
    outGeneratedCode += ", true)) {\n"
    outGeneratedCode += "  token.mTokenCode = kToken_" + [mTerminalName identifierRepresentation] + " ;\n"
#    outGeneratedCode += "  token.mIsOptional = false ;\n"
    outGeneratedCode += "  enterToken (token) ;\n"
    outGeneratedCode += "}else "
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @lexicalExplicitRuleAST generateCode
  ?let @string inScannerClassName
  ?let @lexiqueAnalysisContext inLexiqueAnalysisContext
  -> @string outGeneratedCode {
  outGeneratedCode = "if ("
  outGeneratedCode += [self.mLexicalRuleExpression generateConditionCode !context:inLexiqueAnalysisContext]
  outGeneratedCode += ") {\n"
  [!?outGeneratedCode incIndentation !2]
  for (mInstruction) in self.mInstructionList do
    outGeneratedCode += [mInstruction generateInstructionCode !inScannerClassName !inLexiqueAnalysisContext]
  end
  [!?outGeneratedCode decIndentation !2]
  outGeneratedCode += "}else "
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

