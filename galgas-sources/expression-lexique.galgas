#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!   AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @lexiqueIntrospectionExpressionAST : @semanticExpressionAST {
  public var @lstring mLexiqueComponentName
  public var @lstring mLexiqueGetterName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3ExpressionSyntax {

  #·····················································································································
  
  rule <primary> !@semanticExpressionAST outExpression {
    $[$
    $lexique$
    $identifier$ ?let lexiqueName
    $:$
    $identifier$ ?let getterName
    outExpression = @lexiqueIntrospectionExpressionAST.new {!lexiqueName !getterName}
    $]$
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @lexiqueIntrospectionExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap unused ioTypeMap
{
}

#·····················································································································

override method @lexiqueIntrospectionExpressionAST analyzeSemanticExpression
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMap-entry unused inType
  ?let @analysisContext inAnalysisContext
  ?!@localVarManager unused ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
 let usefulnessName = lexiqueNameForUsefulEntitiesGraph (!self.mLexiqueComponentName)
  [!?ioUsefulEntitiesGraph addEdge !inUsefulnessCallerEntityName !usefulnessName]
#--- Check  lexique is declared
  [inAnalysisContext.mSemanticContext.mLexiqueComponentMapForSemanticAnalysis searchKey
    !self.mLexiqueComponentName
    ?3*
  ]
#--- Check getter name
  if self.mLexiqueGetterName.string != "symbols" then
    error self.mLexiqueGetterName:"unknown getter"
  end
#---
  outExpression = @lexiqueIntrospectionForGeneration.new {
    !@unifiedTypeMap-entry. searchKey {!inAnalysisContext.mSemanticContext.mTypeMap !["stringlist" nowhere]}
    !self.mLexiqueComponentName.location
    !self.mLexiqueComponentName.string
    !self.mLexiqueGetterName.string}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @lexiqueIntrospectionForGeneration : @semanticExpressionForGeneration {
  public var @string mLexiqueComponentName
  public var @string mLexiqueGetterName
}

#·····················································································································

override method @lexiqueIntrospectionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@stringset ioInclusionSet
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression {
#---
  ioInclusionSet += !"lexique-" + [self.mLexiqueComponentName fileNameRepresentation]
  outCppExpression = "C_Lexique_" + [self.mLexiqueComponentName identifierRepresentation] + "::"
  + [self.mLexiqueGetterName identifierRepresentation]
  + "(" + [self.mLocation sourceFile] + ")"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
