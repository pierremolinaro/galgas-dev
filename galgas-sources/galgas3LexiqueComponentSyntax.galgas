#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#                                           
#  Copyright (C) 2014, ..., 2014 Pierre Molinaro.                                                                      *
#                                           
#  e-mail : pierre@pcmolinaro.name          
#                                           
#  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public   *
#  License as published by the Free Software Foundation.                                                               *
#                                           
#  This program is distributed in the hope it will be useful, but WITHOUT ANY WARRANTY; without even the implied       *
#  warranty of MERCHANDIBILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more        *
#  details.                                 
#                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax galgas3LexiqueComponentSyntax (galgas3Scanner) {

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations {
  @bool isTemplate
  select
    isTemplate = true
    $template$
  or
    isTemplate = false
  end
  $lexique$
#--- Lexique Component Name
  $identifier$ ?let @lstring lexiqueComponentName
#--- Enable indexing ?
  @lstring indexingDirectory
  select
    indexingDirectory = ["" nowhere]
  or
    $indexing$
    $in$
    $"string"$ ? indexingDirectory
  end
  ${$
#--- Parse lexique body
  @metamodelTemplateDelimitorListAST templateDelimitorList = {}
  @templateReplacementListAST templateReplacementList = {}
  @lexicalAttributeListAST lexicalAttributeList = {}
  @lexicalStyleListAST lexicalStyleList = {}
  @terminalDeclarationListAST terminalDeclarationList = {}
  @lexicalMessageDeclarationListAST lexicalMessageDeclarationList = {}
  @lexicalListDeclarationListAST lexicalListDeclarationList = {}
  @lexicalRuleListAST lexicalRuleList = {}
  @externRoutineListAST externRoutineList = {}
  @externFunctionListAST externFunctionList = {}
  @indexingListAST indexingListAST = {}
  @lexicalFunctionListAST lexicalFunctionListAST = {}
  repeat
  while 
    <extern_routine_declaration> !?externRoutineList
  while 
    <extern_function_declaration> !?externFunctionList
  while 
    <template_delimitor> !? templateDelimitorList
  while 
    <template_replacement> !? templateReplacementList
  while 
    <lexical_attribute_declaration> !? lexicalAttributeList
  while 
    <style_declaration> !? lexicalStyleList
  while 
    <terminal_declaration> !? terminalDeclarationList
  while 
    <lexical_list_declaration> !? lexicalListDeclarationList
  while
    <lexical_explicit_rule> !? lexicalRuleList
  while
    <lexical_implicit_rule> !? lexicalRuleList
  while
    <lexical_message_declaration> !?lexicalMessageDeclarationList
  while
    <lexical_indexing_declaration> !?indexingListAST
  while
    <lexical_function_declaration> !?lexicalFunctionListAST
  end
  $}$
  ioDeclarations.mDeclarationList += !@lexiqueComponentAST.new {
    !false # Is not predefined
    !lexiqueComponentName
    !isTemplate
    !templateDelimitorList
    !templateReplacementList
    !lexicalAttributeList
    !lexicalStyleList
    !terminalDeclarationList
    !lexicalMessageDeclarationList
    !lexicalListDeclarationList
    !lexicalRuleList
    !externRoutineList
    !externFunctionList
    !indexingListAST
    !indexingDirectory
    !lexicalFunctionListAST
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_indexing_declaration> ?!@indexingListAST ioIndexingListAST {
  $indexing$
  $identifier$ ?var @lstring indexName indexing indexingNameDefinition
  $:$
  $"string"$ ?var @lstring indexComment
  ioIndexingListAST += !indexName !indexComment
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <template_delimitor> ?!@metamodelTemplateDelimitorListAST ioTemplateDelimitorList {
  $template$
  $"string"$ ?let @lstring startString
  var optionList =@lstringlist {}
  repeat
  while
    $%attribute$ ?var  @lstring optionName
    optionList += !optionName
  end
  $...$
  $"string"$ ?let @lstring endString
  ioTemplateDelimitorList += !startString !optionList !endString
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <template_replacement> ?!@templateReplacementListAST ioTemplateReplacement {
  $replace$
  $"string"$ ?var @lstring matchString
  @lstring replacementString
  @lstring replacementFunction
  select
    $...$
    $"string"$ ?replacementString
    $:$
    $identifier$ ?replacementFunction
  or
    $->$
    $"string"$ ?replacementString
    replacementFunction = @lstring.new {!"" !@location.here}
  end
  ioTemplateReplacement += !matchString !replacementString !replacementFunction
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_message_declaration>
  ?!@lexicalMessageDeclarationListAST ioLexicalMessageDeclarationList {
  $message$
  $identifier$ ?let @lstring messageName
  $:$
  $"string"$ ?let @lstring messageValue
  ioLexicalMessageDeclarationList += !messageName !messageValue
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_implicit_rule> ?!@lexicalRuleListAST ioLexicalImplicitRuleList {
  $rule$
  $list$
  $identifier$ ?let @lstring listName
  ioLexicalImplicitRuleList += !@lexicalImplicitRuleAST.new {!listName}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_explicit_rule> ?!@lexicalRuleListAST ioLexicalExplicitRuleList {
  $rule$
  <lexical_expression> ?let @lexicalExpressionAST lexicalRuleExpression
  ${$
  @lexicalInstructionListAST instructionList = {}
  repeat
  while
    <lexical_instruction> !?instructionList
  end
  $}$
  ioLexicalExplicitRuleList += !@lexicalExplicitRuleAST.new {!lexicalRuleExpression !instructionList}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
  $send$
  <lexical_send_instruction> ?let @lexicalInstructionAST instruction
  ioInstructionList += !instruction
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_send_instruction> !@lexicalInstructionAST outInstruction {
  $\$terminal\$$ ?let @lstring sentTerminal
  outInstruction = @lexicalSimpleSendInstructionAST.new {!sentTerminal}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_send_instruction> !@lexicalInstructionAST outInstruction {
  @lexicalSendSearchListAST lexicalSendSearchList = {}
  $search$
  repeat
    $identifier$ ?let @lstring attributeName
    $in$
    $identifier$ ?let @lstring searchListName
    lexicalSendSearchList += !attributeName !searchListName
    $default$
  while
    $search$
  end
  @lexicalSendDefaultActionAST lexicalSendDefaultAction
  select
    $\$terminal\$$ ?let @lstring defaultSentTerminal
    lexicalSendDefaultAction = @lexicalSendTerminalByDefaultAST.new {!defaultSentTerminal}
  or
    $error$
    $identifier$ ?let @lstring defaultErrorMessageName
    lexicalSendDefaultAction = @lexicalErrorByDefaultAST.new {!defaultErrorMessageName}
  end
  outInstruction = @lexicalStructuredSendInstructionAST.new {
    !lexicalSendSearchList
    !lexicalSendDefaultAction
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
  $repeat$
  @lexicalInstructionListAST repeatedInstructionList = {}
  repeat
  while
    <lexical_instruction> !?repeatedInstructionList
  end
  @lexicalWhileBranchListAST lexicalWhileBranchList = {}
  repeat
    <repeat_while_branch> !?lexicalWhileBranchList
  while
  end
  $end$
  ioInstructionList += !@lexicalRepeatInstructionAST.new {
    !repeatedInstructionList
    !lexicalWhileBranchList
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <repeat_while_branch> ?!@lexicalWhileBranchListAST ioLexicalWhileBranchList {
  $while$
  <lexical_expression> ?let @lexicalExpressionAST whileExpression
  $:$
  @lexicalInstructionListAST whileInstructionList = {}
  repeat
  while
    <lexical_instruction> !?whileInstructionList
  end
  ioLexicalWhileBranchList += !whileExpression !whileInstructionList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
  $select$
  @lexicalSelectBranchListAST lexicalWhileBranchList = {}
  repeat
    $case$
    <lexical_expression> ?let @lexicalExpressionAST selectExpression
    $:$
    @lexicalInstructionListAST selectInstructionList = {}
    repeat
    while
      <lexical_instruction> !?selectInstructionList
    end
    lexicalWhileBranchList += !selectExpression !selectInstructionList
  while
  end
  $default$
  @lexicalInstructionListAST defaultInstructionList = {}
  repeat
  while
    <lexical_instruction> !?defaultInstructionList
  end
  $end$
  ioInstructionList += !@lexicalSelectInstructionAST.new {
    !lexicalWhileBranchList
    !defaultInstructionList
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
  $identifier$ ?let @lstring actionName
  @lexicalRoutineCallActualArgumentListAST actualArgumentList = {}
  $($
  repeat
  while
    $!$ ?let selector
    if selector.string != "" then
      error selector : "the selector should be '!'"
    end
    let passingModeLocation = @location.here
    <lexical_output_effective_argument> ?let @lexicalRoutineOrFunctionFormalInputArgumentAST arg
    actualArgumentList += !@lexicalFormalInputArgumentAST.new {!passingModeLocation !arg}
  while
    $!?$ ?let selector
    if selector.string != "" then
      error selector : "the selector should be '!?'"
    end
    let passingModeLocation = @location.here
    $identifier$ ?var @lstring attributeName
    var arg = @lexicalAttributeInputOutputArgumentAST.new{!passingModeLocation !attributeName}
    actualArgumentList += !arg
  end
  @lstringlist errorMessageList = {}
  select
  or
    $error$
    repeat
      $identifier$ ?let @lstring errorMessageName
      errorMessageList += !errorMessageName
    while
      $,$
    end
  end
  $)$
  ioInstructionList += !@lexicalRoutineInstructionAST.new {
    !actionName
    !actualArgumentList
    !errorMessageList}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_output_effective_argument> !@lexicalRoutineOrFunctionFormalInputArgumentAST outEffectiveArgument {
  $'char'$ ?var @lchar character
  outEffectiveArgument = @lexicalCharacterInputArgumentAST.new {!character}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_output_effective_argument> !@lexicalRoutineOrFunctionFormalInputArgumentAST outEffectiveArgument {
  $uint32$ ?var @luint unsignedValue
  outEffectiveArgument = @lexicalUnsignedInputArgumentAST.new {!unsignedValue}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_output_effective_argument> !@lexicalRoutineOrFunctionFormalInputArgumentAST outEffectiveArgument {
  let currentLocation = @location.here
  $*$
  outEffectiveArgument = @lexicalCurrentCharacterInputArgumentAST.new {!currentLocation}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_output_effective_argument> !@lexicalRoutineOrFunctionFormalInputArgumentAST outEffectiveArgument {
  $identifier$ ?let idf
  select
    outEffectiveArgument = @lexicalAttributeInputArgumentAST.new {!idf}
  or
    $($
    @lexicalFunctionCallActualArgumentListAST functionActualArgumentList = {}
    repeat
    while
      $!$ ?let selector
      if selector.string != "" then
        error selector : "the selector should be '!'"
      end
      <lexical_output_effective_argument> ?let @lexicalRoutineOrFunctionFormalInputArgumentAST arg
      functionActualArgumentList += !arg
    end
    $)$
    outEffectiveArgument = @lexicalFunctionInputArgumentAST.new {!idf !functionActualArgumentList}
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
  $error$
  $identifier$ ?let errorMessageName
  ioInstructionList += !@lexicalErrorInstructionAST.new {!errorMessageName}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
  $warning$
  $identifier$ ?let warningMessageName
  ioInstructionList += !@lexicalWarningInstructionAST.new {!warningMessageName}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
  $drop$
  $\$terminal\$$ ?let terminalName
  ioInstructionList += !@lexicalDropInstructionAST.new {!terminalName}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

#rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
#  $option$
#  $\$terminal\$$ ?let terminalName
#  ioInstructionList += !@lexicalOptionInstructionAST.new {!terminalName}
#}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
  $tag$
  $identifier$ ?let tagName
  ioInstructionList += !@lexicalTagInstructionAST.new {!tagName}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
  $rewind$
  $identifier$ ?let tagName
  $send$
  $\$terminal\$$ ?let terminalName
  ioInstructionList += !@lexicalRewindInstructionAST.new {!tagName !terminalName}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_instruction> ?!@lexicalInstructionListAST ioInstructionList {
  $log$
  ioInstructionList += !@lexicalLogInstructionAST.new
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_expression> !@lexicalExpressionAST outExpression {
  <lexical_term> ?outExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_term> !@lexicalExpressionAST outExpression {
  <lexical_factor> ?outExpression
  repeat
  while
    $|$
    <lexical_factor> ?let rightExpression
    outExpression = @lexicalOrExpressionAST.new {!outExpression !rightExpression}
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_factor>!@lexicalExpressionAST outExpression {
  $"string"$ ?let string
  outExpression = @lexicalStringMatchAST.new {!string}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_factor> !@lexicalExpressionAST outExpression {
  $~$
  $"string"$ ?let string
  $error$
  $identifier$ ?let errorMessage
  outExpression = @lexicalStringNotMatchAST.new {!string !errorMessage}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_factor> !@lexicalExpressionAST outExpression {
  $'char'$ ?let @lchar character
  select
    outExpression = @lexicalCharacterMatchAST.new {!character}
  or
    $->$
    $'char'$ ?let @lchar upperBound
    outExpression = @lexicalCharacterIntervalMatchAST.new {!character !upperBound}
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_factor> !@lexicalExpressionAST outExpression {
  $identifier$ ?let @lstring characterSet
  outExpression = @lexicalCharacterSetMatchAST.new {!characterSet}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_list_declaration> ?!@lexicalListDeclarationListAST ioLexicalListDeclarationList {
  $list$
  $identifier$ ?let name
  @sentLexicalAttributeListAST sentAttributeList = {}
  repeat
  while
    $!$ ?let selector
    $identifier$ ?let sentAttribute
    sentAttributeList += !selector !sentAttribute
  end
  @lstring theStyle # Empty string if no style
  select
    $style$
    $identifier$ ? theStyle
  or
    theStyle = @lstring.new {!"" !@location.here}
  end
  $error$
  $message$
  $"string"$ ?let @lstring errorMessage
  ${$
  var entryList =@lexicalListEntryListAST {}
  repeat
    <lexical_list_entry> !?entryList
  while
    $,$
  end
  $}$
  ioLexicalListDeclarationList += !name !theStyle !errorMessage !sentAttributeList !entryList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_list_entry> ?!@lexicalListEntryListAST ioLexicalListEntryList {
  $"string"$ ?var @lstring entrySpelling
  @lstringlist optionList = {}
  select
  or
    $%attribute$ ?var  @lstring optionName
    optionList += !optionName
  end
  @lstring terminalSpelling
  select
    $->$
    $\$terminal\$$ ? terminalSpelling
  or
    terminalSpelling = entrySpelling
  end
  ioLexicalListEntryList += !entrySpelling !terminalSpelling !optionList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <lexical_attribute_declaration> ?!@lexicalAttributeListAST ioLexicalAttributeList {
  $@type$ ?let @lstring typeName
  $identifier$ ?let @lstring name
  ioLexicalAttributeList += !typeName !name
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <terminal_declaration> ?!@terminalDeclarationListAST ioTerminalDeclarationList {
  $\$terminal\$$ ?let @lstring name indexing terminalDeclaration
  var sentAttributeList =@sentLexicalAttributeListAST {}
  repeat
  while
    $!$ ?let selector
    $identifier$ ?let sentAttribute
    sentAttributeList += !selector !sentAttribute
  end
  @lstring theStyle = [""nowhere] # Empty string if no style
  @lstringlist optionList = {}
  repeat
  while
    $style$
    if theStyle.string != "" then
      error @location.here : "duplicated style reference"
    end
    $identifier$ ? theStyle
  while
    $%attribute$ ?let @lstring optionName
    optionList += !optionName
  end
  $error$
  $message$
  $"string"$ ?let @lstring errorMessage
  ioTerminalDeclarationList += !name !sentAttributeList !errorMessage !theStyle !optionList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <style_declaration> ?!@lexicalStyleListAST ioLexicalStyleList {
  $style$
  $identifier$ ?let @lstring styleIdentifier
  if [styleIdentifier.string containsCharacter !'_'] then 
    error styleIdentifier : "for compatibility with latex formatting, a style name should not contain '_' character"
  end
  if [styleIdentifier.string containsCharacterInRange !'0' !'9'] then 
    error styleIdentifier : "for compatibility with latex formatting, a style name should not contain any digit"
  end
  $->$
  $"string"$ ?let @lstring comment
  ioLexicalStyleList += !styleIdentifier !comment
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <extern_routine_declaration> ?!@externRoutineListAST ioExternRoutineList {
  $extern$
  $proc$
  $identifier$ ?var @lstring routineName
  var lexicalRoutineFormalArgumentList =@lexicalExternRoutineFormalArgumentListAST {}
  repeat
  while
    @lexicalArgumentModeAST lexicalArgumentMode
    select
      $?!$ ?let selector
      if selector.string != "" then
        error selector : "the selector should be '?!'"
      end
      lexicalArgumentMode = @lexicalArgumentModeAST. lexicalInputOutputMode
    or
      $?$ ?let selector
      if selector.string != "" then
        error selector : "the selector should be '?'"
      end
      lexicalArgumentMode = @lexicalArgumentModeAST. lexicalInputMode
    end
    $@type$ ?var @lstring lexicalTypeName
    $identifier$ ?var @lstring formalArgumentName
    lexicalRoutineFormalArgumentList += !lexicalArgumentMode !lexicalTypeName !formalArgumentName
  end
  @stringlist errorMessageList = {}
  select
  or
    $error$
    repeat
      $identifier$ ?var @lstring errorMessageName
      errorMessageList += ![errorMessageName string]
    while
      $,$
    end
  end
  ioExternRoutineList += !routineName !lexicalRoutineFormalArgumentList !errorMessageList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <extern_function_declaration> ?!@externFunctionListAST ioExternFunctionList {
  $extern$
  $func$
  $identifier$ ?var @lstring functionName
  var lexicalFunctionFormalArgumentList =@lexicalExternFunctionFormalArgumentListAST {}
  repeat
  while
    $?$ ?let selector
    if selector.string != "" then
      error selector : "the selector should be '?'"
    end
    $@type$ ?var @lstring lexicalTypeName
    $identifier$ ?var @lstring formalArgumentName
    lexicalFunctionFormalArgumentList += !lexicalTypeName !formalArgumentName
  end
  $->$
  $@type$ ?var @lstring returnedTypeName
  ioExternFunctionList += !functionName !lexicalFunctionFormalArgumentList !returnedTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

}