//
//  MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software
// and associated documentation files (the "Software"), to deal in the Software without
// restriction, including without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the
// Software is furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all copies or
// substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING
// BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
// NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
// DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//--------------------------------------------------------------------------------------------------

syntax extension galgas3SyntaxComponentSyntax {

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <syntax_instruction_ggs3> ?!@galgasDeclarationAST ioDeclarations
                                 !@syntaxInstructionAST outInstruction {
    let @lstring mLabelName // No label : empty string
    select
      mLabelName = @lstring {!"" !@location.here}
    or
      $parse$
      mLabelName = @lstring {!"parse" !@location.here}
    or
      $parse$
      $identifier$ ? mLabelName
    end
    $<non_terminal>$ ?let @lstring mNonterminalName indexing ruleReference
    <actual_parameter_list_ggs3> !?ioDeclarations ?let @actualParameterListAST mActualParameterList
  //--- Get syntax directed translation result string
    let @abstractGrammarInstructionSyntaxDirectedTranslationResult grammarInstructionSyntaxDirectedTranslationResult
    select
      grammarInstructionSyntaxDirectedTranslationResult = @grammarInstructionSyntaxDirectedTranslationResultNone {}
    or
      $:>$
      <syntax_directed_translation_result> !?ioDeclarations ?grammarInstructionSyntaxDirectedTranslationResult
    end
    outInstruction = @nonterminalCallInstruction {
      !mNonterminalName.location
      !mNonterminalName
      !mLabelName
      !mActualParameterList
      !grammarInstructionSyntaxDirectedTranslationResult
    }
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

}

//--------------------------------------------------------------------------------------------------

syntax extension galgas4SyntaxComponentSyntax {

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  rule <syntax_instruction> ?!@galgasDeclarationAST ioDeclarations
                            !@syntaxInstructionAST outInstruction {
    let @lstring mLabelName // No label : empty string
    select
      mLabelName = @lstring {!"" !@location.here}
    or
      $parse$
      mLabelName = @lstring {!"parse" !@location.here}
    or
      $parse$
      $identifier$ ? mLabelName
    end
    $<non_terminal>$ ?let @lstring mNonterminalName indexing ruleReference
    <actual_parameter_list_within_parenthesis> !?ioDeclarations ?let @actualParameterListAST mActualParameterList
  //--- Get syntax directed translation result string
    let @abstractGrammarInstructionSyntaxDirectedTranslationResult grammarInstructionSyntaxDirectedTranslationResult
    select
      grammarInstructionSyntaxDirectedTranslationResult = @grammarInstructionSyntaxDirectedTranslationResultNone {}
    or
      $:>$
      <syntax_directed_translation_result> !?ioDeclarations ?grammarInstructionSyntaxDirectedTranslationResult
    end
    outInstruction = @nonterminalCallInstruction {
      !mNonterminalName.location
      !mNonterminalName
      !mLabelName
      !mActualParameterList
      !grammarInstructionSyntaxDirectedTranslationResult
    }
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

}

//--------------------------------------------------------------------------------------------------
//! AST
//--------------------------------------------------------------------------------------------------

final class @nonterminalCallInstruction : @syntaxInstructionAST {
  private let @lstring mNonterminalName
  private let @lstring mLabelName // No label : empty string
  private let @actualParameterListAST mActualParameterList
  private let @abstractGrammarInstructionSyntaxDirectedTranslationResult mGrammarInstructionSyntaxDirectedTranslationResult

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  override method transformInstructionForGrammarAnalysis
                    ?!usedTerminalMap:@terminalSymbolsMapForGrammarAnalysis unused ioActuallyUsedTerminalSymbolMap
                    ?nonTerminalSymbolMap:let @nonTerminalSymbolMapForGrammarAnalysis inNonTerminalSymbolMap
                    ?!addedTerminalIndex:@uint unused ioAddedNonTerminalIndex
                    ?!@syntaxInstructionListForGrammarAnalysis ioSyntaxInstructionList {
    [inNonTerminalSymbolMap searchKey !self.mNonterminalName ?let nonTerminalSymbolIndex ?*]
    ioSyntaxInstructionList += !@nonTerminalInstructionForGrammarAnalysis {
      !self.mInstructionLocation
      !self.mNonterminalName
      !nonTerminalSymbolIndex
    }
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

  override method analyzeSyntaxInstruction ?let @lstring inUsefulnessCallerEntityName
                                           ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
                                           ?let @analysisContext inAnalysisContext
                                           ?!@unifiedTypeMap ioTypeMap
                                            ?let @bool inHasTranslateFeature
                                            ?let @terminalMap unused inTerminalMap
                                            ?let @string unused inLexiqueName
                                            ?let @nonterminalMap inNonterminalMap
                                            ?let @string unused inComponentName
                                            ?let @stringset unused inIndexNameSet
                                            ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
                                            ?!@localVarManager ioVariableMap
                                            ?!@uint unused ioSelectMethodCount {
    [inNonterminalMap searchKey !self.mNonterminalName ?let @nonterminalLabelMap labelMap]
    let @formalParameterSignature signature
    if self.mLabelName.string == "parse" then
      signature = @()
    else
      [labelMap searchKey !self.mLabelName ?* ?signature ?*]
    end
  //--- call arguments
    [self.mActualParameterList analyzeRoutineArguments
      !inUsefulnessCallerEntityName
      !?ioUsefulEntitiesGraph
      !inAnalysisContext
      !?ioTypeMap
      !self.mNonterminalName
      !invokedEntityNameForErrorSignaling: "'<" + self.mNonterminalName + ">' nonterminal declaration"
      !signature
      !?ioVariableMap
      !?ioInstructionListForGeneration
      ?let @actualParameterListForGeneration actualParameterListForGeneration
    ]
  //--- Check syntax directed translation destination variable
    [self.mGrammarInstructionSyntaxDirectedTranslationResult analyzeSDT
      !inAnalysisContext
      !?ioTypeMap
      !inHasTranslateFeature
      !?ioVariableMap
    ]
  //--- Generate instructions
    ioInstructionListForGeneration += !@nonterminalInstructionForGeneration {
      !self.mInstructionLocation
      !self.mNonterminalName.string
      !self.mLabelName.string
      !actualParameterListForGeneration
      !self.mGrammarInstructionSyntaxDirectedTranslationResult
    }
  }

  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

}

//--------------------------------------------------------------------------------------------------
//! GENERATION TYPES
//--------------------------------------------------------------------------------------------------

final class @nonterminalInstructionForGeneration : @syntaxInstructionForGeneration {
  private let @string mNonterminalName
  private let @string mLabelName
  private let @actualParameterListForGeneration mActualParameterList
  private let @abstractGrammarInstructionSyntaxDirectedTranslationResult mGrammarInstructionSyntaxDirectedTranslationResult
}

//--------------------------------------------------------------------------------------------------
//! SYNTAX SIGNATURE
//--------------------------------------------------------------------------------------------------

override method @nonterminalInstructionForGeneration appendSyntaxSignature
  ?let @string inPosfix
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
{
  ioInstructionListForGeneration += !@nonterminalInstructionForGeneration {
    !self.mInstructionLocation
    !self.mNonterminalName
    !inPosfix
    !@actualParameterListForGeneration ()
    !@grammarInstructionSyntaxDirectedTranslationResultNone {}
  }
}

//--------------------------------------------------------------------------------------------------
//! COMPARE SYNTAX SIGNATURE
//--------------------------------------------------------------------------------------------------

override getter @nonterminalInstructionForGeneration compareInstructionSyntaxSignature
  ?let @semanticInstructionForGeneration inTestedInstruction
  ->@bool {
  if let si = inTestedInstruction as @nonterminalInstructionForGeneration then
    result = self.mNonterminalName == si.mNonterminalName
    if not result then
      error si.mInstructionLocation
      :"syntax signature error; the expected syntax instruction here is the '<" + self.mNonterminalName + ">' nonterminal invocation"
    end
  elsif let si = inTestedInstruction as @syntaxInstructionForGeneration then
    error si.mInstructionLocation
      :"syntax signature error; the expected syntax instruction here is the '<" + self.mNonterminalName + ">' nonterminal invocation"
    result = false
  else
    error self.mInstructionLocation : "internal error"
    result = false
  end
  if not result then
    error self.mInstructionLocation
    :"reference syntax instruction is here"
  end
}

//--------------------------------------------------------------------------------------------------
//! CODE GENERATION
//--------------------------------------------------------------------------------------------------

override method @nonterminalInstructionForGeneration generateInstruction
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?let @bool inGenerateSyntaxDirectedTranslationString
  ?!@string ioGeneratedCode
{
//--- Parameters
  var parameterCppNameList = @stringlist ()
  var jokerParametersToReleaseList = @stringlist ()
  var inputVariableList = @stringlist ()
  for (mActualParameter) in self.mActualParameterList do
    [mActualParameter generateActualParameter
      !?ioInclusionSet
      !?ioTemporaryVariableIndex
      !?ioGeneratedCode
      !?jokerParametersToReleaseList
      !?inputVariableList
      !?ioUnusedVariableCppNameSet
      ?let @string parameterCppName
    ]
    parameterCppNameList += !parameterCppName
  end
//--- Generate method call
  let sdtResultVarName = "syntaxDirectedTranslationResult_" + [self.mInstructionLocation startLocationIndex]
  if inGenerateSyntaxDirectedTranslationString then
    ioGeneratedCode += "String " + sdtResultVarName + " ;\n"
  end
  ioGeneratedCode += "nt_" + [self.mNonterminalName identifierRepresentation] + "_" + [self.mLabelName identifierRepresentation] + " ("
  for (mValue) in parameterCppNameList do
    ioGeneratedCode += mValue + ", "
  end
  if inGenerateSyntaxDirectedTranslationString then
    ioGeneratedCode += sdtResultVarName + ", "
  end
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
  ioGeneratedCode +=  compilerCppName () + ") ;\n"
  [self.mGrammarInstructionSyntaxDirectedTranslationResult generateCode
    !inGenerateSyntaxDirectedTranslationString
    !sdtResultVarName
    !?ioUnusedVariableCppNameSet
    !?ioGeneratedCode
  ]
//--- Release temporary variables
  for (mValue) in jokerParametersToReleaseList do
    ioGeneratedCode += mValue + ".drop () ; // Release temporary input variables (joker in source)\n"
  end
}

//--------------------------------------------------------------------------------------------------
