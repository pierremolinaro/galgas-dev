#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @setterCallInstructionAST : @semanticInstructionAST {
  @bool mPrefixedBySelf
  @lstring mReceiverName
  @lstringlist mReceiverStructAttributes
  @lstring mTypeNameForCasting
  @lstring mSetterName
  @actualParameterListAST mActualParameterList
}

#·····················································································································*

class @selfSetterCallInstructionAST : @semanticInstructionAST {
  @lstring mSetterName
  @actualParameterListAST mActualParameterList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3InstructionsSyntax {

  #·····················································································································

  rule <semantic_instruction> !@semanticInstructionAST outInstruction {
    $[$
    let instructionLocation = @location.here
    $!?$ ?let selector
    if selector.string != "" then
      error selector : "an empty selector '!?' is required here"
    end
    @bool prefixedBySelf
    select
      prefixedBySelf = false
    or
      $self$
      $.$
      prefixedBySelf = true
    end
    $identifier$ ?let mReceiverName
    var mReceiverStructAttributes =@lstringlist {}
    repeat
    while
      $.$
      $identifier$ ?let structAttribute
      mReceiverStructAttributes += !structAttribute
    end
    @lstring typeNameForCasting
    select
      typeNameForCasting = ["" nowhere]
    or
      $as$
      $@type$ ?typeNameForCasting
    end
    $identifier$ ?let mSetterName
    <actual_parameter_list> ?let @actualParameterListAST actualParameterList
    $]$
    outInstruction = @setterCallInstructionAST.new {
      !instructionLocation
      !prefixedBySelf
      !mReceiverName
      !mReceiverStructAttributes
      !typeNameForCasting
      !mSetterName
      !actualParameterList
    }
  }

  #·····················································································································

  rule <semantic_instruction> !@semanticInstructionAST outInstruction {
    $[$
    let instructionLocation = @location.here
    $!?$ ?let selector
    if selector.string != "" then
      error selector : "an empty selector '!?' is required here"
    end
    $self$
    $identifier$ ?let mSetterName
    <actual_parameter_list> ?let @actualParameterListAST actualParameterList
    $]$
    outInstruction = @selfSetterCallInstructionAST.new {
      !instructionLocation
      !mSetterName
      !actualParameterList
    }
  }  

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CONTEXT
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @setterCallInstructionAST enterInstructionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  for () in self.mActualParameterList do
    [mActualParameter enterParameterInSemanticContext !?ioTypeMap]
  end
}

#·····················································································································*

override method @selfSetterCallInstructionAST enterInstructionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  for () in self.mActualParameterList do
    [mActualParameter enterParameterInSemanticContext !?ioTypeMap]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @setterCallInstructionAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@variableMap ioVariableMap
{
  var @unifiedTypeMap-proxy receiverType
  var @string targetVariableCppName
  var @string nameForCheckingFormalParameterUsing
  if self.mPrefixedBySelf then
    if [inAnalysisContext.mSelfCopyTypeProxy isNull] then
      error self.mReceiverName: "'self' cannot be used in this context"
      : receiverType, nameForCheckingFormalParameterUsing, targetVariableCppName
    elsif [inAnalysisContext.mSelfTypeProxy isNull] then
      error self.mReceiverName: "'self' object is not mutable"
      : receiverType, nameForCheckingFormalParameterUsing, targetVariableCppName
    else
      let propertyMap = [inAnalysisContext.mSelfTypeProxy mPropertyMap]
      [propertyMap searchKey !self.mReceiverName ?* ?receiverType]
      nameForCheckingFormalParameterUsing = inAnalysisContext.mSelfObjectCppName
      targetVariableCppName = inAnalysisContext.mSelfObjectCppPrefixForAccessingProperty + "mProperty_"+ [self.mReceiverName.string identifierRepresentation]
    end
  else
    [!?ioVariableMap searchForReadWriteAccess
       !self.mReceiverName
       ?receiverType
       ?targetVariableCppName
       ?nameForCheckingFormalParameterUsing
    ]  
  end
#--- Get target variable type
  for () in self.mReceiverStructAttributes do
    let propertyMap = [receiverType mPropertyMap]
    [propertyMap searchKey !mValue ?let isPublic ?receiverType]
    if not isPublic then
      error mValue : " inaccessible property (due to its 'private' qualifier)"
    end
  end
#--- Check receiver object
#  [!?ioVariableMap searchForReadWriteAccess
#     !self.mReceiverName
#     ?@unifiedTypeMap-proxy receiverType
#     ?let @string receiverVariableCppName
#     ?let @string receiverVariableBaseName
#  ]
#  for () in self.mReceiverStructAttributes do
#    let propertyMap = [receiverType mPropertyMap]
#    [propertyMap searchKey !mValue ?let isPublic ?receiverType]
#    if not isPublic then
#      error mValue : " inaccessible property (due to its 'private' qualifier)"
#    end
#  end
#--- Type cast ?
  @unifiedTypeMap-proxy castType
  if self.mTypeNameForCasting.string != "" then
    castType = .searchKey {!inAnalysisContext.mSemanticContext.mTypeMap !self.mTypeNameForCasting}
    checkAssignmentTypes (!receiverType !castType !self.mTypeNameForCasting.location)
  else 
    castType = receiverType
  end
#--- Get setter signature
  [[castType mSetterMap] searchKey
    !self.mSetterName
    ?let kind
    ?let routineSignature
    ?let hasCompilerArgument
    ?*
    ?let setterErrorMessage
  ]
  if setterErrorMessage != "" then
    error self.mSetterName : setterErrorMessage
  end
#--- Check effective parameters
  analyzeRoutineArguments (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inAnalysisContext
    !self.mSetterName
    !"'" + self.mSetterName + "' setter header"
    !routineSignature
    !self.mActualParameterList
    !?ioVariableMap
    !?ioInstructionListForGeneration
    ?let @actualParameterListForGeneration actualParameterListForGeneration
  )
#--- Search base type that declares the method
  var baseType = castType
  var searching = true
  if @uint.errorCount == 0 then
    loop (@uint.max) while searching do
      if [[baseType mSuperType] isNull] then
        searching = false
      else
        if [[[baseType mSuperType] mSetterMap] hasKey !self.mSetterName.string] then
          baseType = [baseType mSuperType]
        else
          searching = false
        end
      end  
    end
  end
#--- Generate instructions
  ioInstructionListForGeneration += !@setterCallInstructionForGeneration.new {
    !self.mReceiverName.location
    !targetVariableCppName
    !nameForCheckingFormalParameterUsing
    !self.mReceiverStructAttributes
    !castType
    !receiverType
    !self.mSetterName
    !kind
    !actualParameterListForGeneration
    !hasCompilerArgument
    !baseType
  }
}

#·····················································································································*

override method @selfSetterCallInstructionAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@variableMap ioVariableMap {
#--- Analyze "self", as receiver expression
  @unifiedTypeMap-proxy selfTypeProxy
  @string selfObjectCppName
  if [inAnalysisContext.mSelfTypeProxy isNull] then
    error self.mSetterName: "'self' can be used only in extension setters" : selfObjectCppName, selfTypeProxy
  else
    selfObjectCppName = inAnalysisContext.mSelfObjectCppName
    selfTypeProxy = inAnalysisContext.mSelfTypeProxy
  end
#--- Get setter signature
  @formalParameterSignature routineSignature
  @bool hasCompilerArgument
  @methodKind kind
  [[selfTypeProxy mSetterMap] searchKey !self.mSetterName ?kind ?routineSignature ?hasCompilerArgument ?* ?*]
#--- Check effective parameters
  analyzeRoutineArguments (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inAnalysisContext
    !self.mSetterName
    !"'" + self.mSetterName + "' setter header"
    !routineSignature
    !self.mActualParameterList
    !?ioVariableMap
    !?ioInstructionListForGeneration
    ?let @actualParameterListForGeneration actualParameterListForGeneration
  )
#--- Generate instructions
  ioInstructionListForGeneration += !@selfSetterCallInstructionForGeneration.new {
    !selfObjectCppName
    !selfTypeProxy
    !self.mSetterName
    !kind
    !actualParameterListForGeneration
    !hasCompilerArgument}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @setterCallInstructionForGeneration : @semanticInstructionForGeneration {
  @location mInstructionLocation
  @string mReceiverCppName
  @string mReceiverBaseName
  @lstringlist mReceiverStructAttributes
  @unifiedTypeMap-proxy mCastType
  @unifiedTypeMap-proxy mReceiverType
  @lstring mSetterName
  @methodKind mKind
  @actualParameterListForGeneration mActualParameterList
  @bool mHasCompilerArgument
  @unifiedTypeMap-proxy mMethodBaseType
}

#·····················································································································*

class @selfSetterCallInstructionForGeneration : @semanticInstructionForGeneration {
  @string mSelfCppName
  @unifiedTypeMap-proxy mReceiverType
  @lstring mSetterName
  @methodKind mKind
  @actualParameterListForGeneration mActualParameterList
  @bool mHasCompilerArgument
}

#·····················································································································*

override method @setterCallInstructionForGeneration generateInstruction
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?let @bool unused inGenerateSyntaxDirectedTranslationString
  ?!@string ioGeneratedCode
{
  ioGeneratedCode += "{\n"
  var receiverCppName = self.mReceiverCppName
  for () in self.mReceiverStructAttributes do
    receiverCppName += ".mProperty_" + [[mValue string] identifierRepresentation]
  end
  [!?ioUnusedVariableCppNameSet removeKey !self.mReceiverBaseName]
#--- Parameters
  @stringlist parameterCppNameList = {}
  @stringlist jokerParametersToReleaseList = {}
  @stringlist inputVariableList = {}
  for () in self.mActualParameterList do
    [mActualParameter generateActualParameter
      !?ioInclusionSet
      !?ioTemporaryVariableIndex
      !?ioGeneratedCode
      !?jokerParametersToReleaseList
      !?inputVariableList
      !?ioUnusedVariableCppNameSet
      ?let parameterCppName
    ]
    parameterCppNameList += !parameterCppName
  end
#--- Generate method call
  switch self.mKind
  case definedAsExtension :
    ioInclusionSet += !"setter-" + [[self.mMethodBaseType key] fileNameRepresentation] + "-" + [self.mSetterName fileNameRepresentation]
    if [[self.mReceiverType mTypeKindEnum] isClassType] then
      if ([[self.mReceiverType mAllTypedAttributeList] length] > 0) then #  & [mReceiverType mIsConcrete] then
        ioGeneratedCode += receiverCppName + ".insulate (HERE) ;\n"
      end
      let pointerUniqueName = "ptr_" + [self.mInstructionLocation locationIndex]
      ioGeneratedCode += "cPtr_" + [self.mReceiverType identifierRepresentation] + " * " + pointerUniqueName
      ioGeneratedCode += " = (cPtr_" + [self.mReceiverType identifierRepresentation] + " *) " + receiverCppName + ".ptr () ;\n"
      if [self.mCastType key] != [self.mReceiverType key] then
        ioInclusionSet += !"class-" + [[self.mCastType key] fileNameRepresentation]
        ioGeneratedCode += "if ((NULL != " + pointerUniqueName
                        + ") && (dynamic_cast <cPtr_" + [self.mCastType identifierRepresentation] + " *> ("
                        + pointerUniqueName + ") == NULL)) {\n"
        [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
        ioGeneratedCode += "  " + compilerCppName () + "->onTheFlyRunTimeError (\"cannot cast to '@" 
                        + [self.mCastType key] + "'\"" + [self.mInstructionLocation commaSourceFile] + ") ;\n"
        ioGeneratedCode += "  " + pointerUniqueName + " = NULL ;\n"
        ioGeneratedCode += "}\n"
      end
      ioGeneratedCode += "callExtensionSetter_" + [self.mSetterName identifierRepresentation]
                      +  " ((cPtr_" + [self.mCastType identifierRepresentation] + " *) " + pointerUniqueName + ", "
    else
      ioGeneratedCode += "extensionSetter_" + [self.mSetterName identifierRepresentation] + " (" + receiverCppName + ", "
    end
  case definedAsMember :
    [self.mReceiverType addHeaderFileName !?ioInclusionSet]
    ioGeneratedCode += receiverCppName + ".setter_" + [[self.mSetterName string] identifierRepresentation] + " ("
  end
  for () in parameterCppNameList
    do ioGeneratedCode += mValue
    between ioGeneratedCode += ", "
  end
  if self.mHasCompilerArgument then
    if [parameterCppNameList length] != 0 then
      ioGeneratedCode +=  ", "
    end
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
    ioGeneratedCode +=  compilerCppName ()
    ioGeneratedCode += " COMMA_"
  elsif [parameterCppNameList length] != 0 then
    ioGeneratedCode += " COMMA_"
  end
  ioGeneratedCode += [[self.mSetterName location] sourceFile] + ") ;\n"
  ioGeneratedCode += "}\n"
}

#·····················································································································*

override method @selfSetterCallInstructionForGeneration generateInstruction
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?let @bool unused inGenerateSyntaxDirectedTranslationString
  ?!@string ioGeneratedCode {
  ioGeneratedCode += "{\n"
  [!?ioUnusedVariableCppNameSet removeKey !self.mSelfCppName]
#--- Parameters
  var parameterCppNameList =@stringlist {}
  var jokerParametersToReleaseList =@stringlist {}
  var inputVariableList =@stringlist {}
  for () in self.mActualParameterList do
    [mActualParameter generateActualParameter
      !?ioInclusionSet
      !?ioTemporaryVariableIndex
      !?ioGeneratedCode
      !?jokerParametersToReleaseList
      !?inputVariableList
      !?ioUnusedVariableCppNameSet
      ?let @string parameterCppName]
    parameterCppNameList += !parameterCppName
  end
#--- Generate setter call
  switch self.mKind
  case definedAsExtension :
    ioInclusionSet += !"setter-" + [[self.mReceiverType key] fileNameRepresentation] + "-" + [self.mSetterName fileNameRepresentation]
    if [[self.mReceiverType mTypeKindEnum] isClassType] then
      ioGeneratedCode += "callExtensionSetter_" + [self.mSetterName identifierRepresentation] + " (" + self.mSelfCppName + ", "
    else
      ioGeneratedCode += "extensionSetter_" + [self.mSetterName identifierRepresentation] + " (" + self.mSelfCppName + ", "
    end
  case definedAsMember :
    if [[self.mReceiverType mTypeKindEnum]isClassType] then
      ioGeneratedCode += self.mSelfCppName + "->setter_" + [self.mSetterName identifierRepresentation] + " ("
    else
      ioGeneratedCode += self.mSelfCppName + ".setter_" + [self.mSetterName identifierRepresentation] + " ("
    end
  end
  for () in parameterCppNameList
    do ioGeneratedCode += mValue
    between ioGeneratedCode += ", "
  end
  if self.mHasCompilerArgument then
    if [parameterCppNameList length] != 0 then
      ioGeneratedCode +=  ", "
    end
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
    ioGeneratedCode +=  compilerCppName ()
    ioGeneratedCode += " COMMA_"
  elsif [parameterCppNameList length] != 0 then
    ioGeneratedCode += " COMMA_"
  end
  ioGeneratedCode += [[self.mSetterName location] sourceFile] + ") ;\n"
  ioGeneratedCode += "}\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
