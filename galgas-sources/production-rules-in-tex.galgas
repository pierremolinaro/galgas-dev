#----------------------------------------------------------------------------------------------------------------------*
#                                                                                                                      *
#  Copyright (C) 2016, ..., 2016 Pierre Molinaro.                                                                      *
#                                                                                                                      *
#  e-mail : molinaro@ls2n.fr                                                                                           *
#                                                                                                                      *
#  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public   *
#  License as published by the Free Software Foundation.                                                               *
#                                                                                                                      *
#  This program is distributed in the hope it will be useful, but WITHOUT ANY WARRANTY; without even the implied       *
#  warranty of MERCHANDIBILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more        *
#  details.                                                                                                            *
#                                                                                                                      *
#----------------------------------------------------------------------------------------------------------------------*
#                                                                                                                      *
#    O U T P U T    P R O D U C T I O N    R U L E S    I N    T E X                                                   *
#                                                                                                                      *
#----------------------------------------------------------------------------------------------------------------------*

filewrapper grammarRulesInTexDocumentTemplate in "../generation_templates/grammar_generation" {
}{
}{
  template document "grammar-rules-document.tex.galgasTemplate"
    ?@string DOCUMENT_NAME
    ?@string TEX_ESCAPED_DOCUMENT_NAME
 }

#----------------------------------------------------------------------------------------------------------------------*

private func escapeForTex ?let @string inString -> @string result {
  result = ""
  for idx in 0 ..< [inString length] do
    let c = [inString characterAtIndex !idx]
    if c == '_' then
      result += "\\_"
    elsif c == '{' then
      result += "\\{"
    elsif c == '}' then
      result += "\\}"
    elsif c == '&' then
      result += "\\&"
    elsif c == '$' then
      result += "\\$"
    elsif c == '^' then
      result += "\\verb=^="
    elsif c == '%' then
      result += "\\verb=%="
    elsif c == '~' then
      result += "$\\sim$"
    elsif c == '^' then
      result += "$\\wedge$"
    else
      result += c
    end
  end
}

#----------------------------------------------------------------------------------------------------------------------*

proc emitProductionRulesInTexFile
  ?let @string inProductDirectory
  ?let @string  inGrammarComponentName
  ?let @nonTerminalSymbolMapForGrammarAnalysis inNonTerminalMapForGrammarAnalysis
  ?let @syntaxComponentListForGrammarAnalysis inSyntaxComponentListForGrammarAnalysis
  ?let @lstring inStartSymbol
{
#---------------------------------------------------------- Print an tex file for grammar rule list
  let texDocumentFilePath = inProductDirectory + "/../tex/" + inGrammarComponentName + ".document.tex"
  [[texDocumentFilePath stringByDeletingLastPathComponent] makeDirectory]
  let document = [filewrapper grammarRulesInTexDocumentTemplate.document
    !inGrammarComponentName
    !escapeForTex (!inGrammarComponentName)
  ]
  [document writeToFileWhenDifferentContents !texDocumentFilePath ?*]
  let texFilePath = inProductDirectory + "/../tex/" + inGrammarComponentName + ".tex"
  var s = ""
  [inNonTerminalMapForGrammarAnalysis searchKey !inStartSymbol ?let startSymbolIndex ?*] ;
  s += "\\startSymbol{" + escapeForTex (!inStartSymbol) + "}{" + startSymbolIndex + "}\n\n"
  s += "\\nonTerminalSummaryStart "
  for nonterminal in inNonTerminalMapForGrammarAnalysis
    do s += "\\nonTerminalSummary{" + escapeForTex (!nonterminal.lkey) + "}{" + nonterminal.mNonTerminalIndex + "}"
    between s += "\\nonTerminalSummarySeparator "
  end
  s += "\\nonTerminalSummaryEnd "
  for nonterminal in inNonTerminalMapForGrammarAnalysis do
    s += "\\nonTerminalSection{" + escapeForTex (!nonterminal.lkey) + "}{" + nonterminal.mNonTerminalIndex + "}\n\n"
    for syntaxComponent in inSyntaxComponentListForGrammarAnalysis do
      for productionRule in syntaxComponent.mProductionRulesList do
        if productionRule.mLeftNonterminalSymbol.string == nonterminal.lkey then
          if [productionRule.mInstructionList displayVertically] then
            [productionRule displayRuleVertically !syntaxComponent.mSyntaxComponentName.string !?s]
          else
            [productionRule displayRule !syntaxComponent.mSyntaxComponentName.string !?s]
          end
        end
      end
    end
  end
  [s writeToFileWhenDifferentContents !texFilePath ?*]
}

#----------------------------------------------------------------------------------------------------------------------*

method @productionRuleListForGrammarAnalysis-element displayRule
  ?let @string inSyntaxCompnentName
  ?!@string ioGeneratedCode
{
  @string currentNode = "P0start"
  @rowList rowArray = {!{!currentNode !"[firstPoint] {}"}}
  @string arrows = ""
  @string arrowStyle = "--"
  var column = 1
  var unusedMaxUsedRowIndex = 0 # Unused
  for (instruction) in self.mInstructionList do
    [instruction tikzNodeForSyntaxInstruction
      !?rowArray
      !fromRow:0
      !?column:column
      !?node:currentNode
      !?arrowShape:arrowStyle
      !?arrows:arrows
      !?unusedMaxUsedRowIndex
      !debug:false
    ]
  end
#--- Add a final arrow
  [!?rowArray append !nodeDefinition:"[lastPoint] {}" !row:0 !column:column ?nodeName:let lastNodeName]
  arrows += "  \\draw[->] (" + currentNode + ") -- (" + lastNodeName + ") ;\n"
#--- Write tex file
  let loc = self.mLeftNonterminalSymbol.location
  ioGeneratedCode += "\\ruleSubsection{"
    + escapeForTex (!inSyntaxCompnentName)
    + "}{"
    + escapeForTex (![[[loc file] lastPathComponent] stringByDeletingPathExtension])
    + "}{" + escapeForTex (![loc line])
    + "}\n\n"
    + "\\begin{tikzpicture}\n"
    + "  \\matrix[column sep=\\ruleMatrixColumnSeparation, row sep=\\ruleMatrixRowSeparation] {\n"
  for > (columns) in rowArray do
    ioGeneratedCode += "    "
    for column in columns do
      if column.mValue0 == "" then
        ioGeneratedCode += "& "
      else
        ioGeneratedCode += "\\node (" + column.mValue0 + ") " + column.mValue1 + "; & "
      end
    end
    ioGeneratedCode += "\\\\\n"
  end
  ioGeneratedCode += "  };\n"
#--- Arrows
  ioGeneratedCode += arrows
  ioGeneratedCode += "\\end{tikzpicture}\n\n"
}

#----------------------------------------------------------------------------------------------------------------------*

method @productionRuleListForGrammarAnalysis-element displayRuleVertically
  ?let @string inSyntaxCompnentName
  ?!@string ioGeneratedCode
{
  @string currentNode = "P0start"
  @rowList rowArray = {}
  for i in 1 ..< [self.mInstructionList length] do
    rowArray += !{!"" !""}
  end
  rowArray += !{!currentNode !"[firstPoint] {}"}
  @string arrows = ""
  @string arrowStyle = "--"
  var unusedMaxUsedRowIndex = 0 # Unused
  var row = [self.mInstructionList length]
  for (instruction) in self.mInstructionList do
    row --
    var column = 1
    [instruction tikzNodeForSyntaxInstruction
      !?rowArray
      !fromRow:row
      !?column:column
      !?node:currentNode
      !?arrowShape:arrowStyle
      !?arrows:arrows
      !?unusedMaxUsedRowIndex
      !debug:false
    ]
  end
#--- Add a final arrow
  [!?rowArray append !nodeDefinition:"[lastPoint] {}" !row:row !column:2 ?nodeName:let lastNodeName]
  arrows += "  \\draw[->] (" + currentNode + ") -- (" + lastNodeName + ") ;\n"
#--- Write tex file
  let loc = self.mLeftNonterminalSymbol.location
  ioGeneratedCode += "\\ruleSubsection{"
    + escapeForTex (!inSyntaxCompnentName)
    + "}{"
    + escapeForTex (![[[loc file] lastPathComponent] stringByDeletingPathExtension])
    + "}{" + escapeForTex (![loc line])
    + "}\n\n"
    + "\\begin{tikzpicture}\n"
    + "  \\matrix[column sep=\\ruleMatrixColumnSeparation, row sep=\\ruleMatrixRowSeparation] {\n"
  for > (columns) in rowArray do
    ioGeneratedCode += "    "
    for column in columns do
      if column.mValue0 == "" then
        ioGeneratedCode += "& "
      else
        ioGeneratedCode += "\\node (" + column.mValue0 + ") " + column.mValue1 + "; & "
      end
    end
    ioGeneratedCode += "\\\\\n"
  end
  ioGeneratedCode += "  };\n"
#--- Arrows
  ioGeneratedCode += arrows
  ioGeneratedCode += "\\end{tikzpicture}\n\n"
}

#----------------------------------------------------------------------------------------------------------------------*
#! tikzNodeForSyntaxInstruction
#----------------------------------------------------------------------------------------------------------------------*

method @syntaxInstructionListForGrammarAnalysis tikzNodeForSyntaxInstruction
  ?!@rowList ioRowList
  ?fromRow:let @uint inRow
  ?!column:@uint ioColumn
  ?!node: @string ioCurrentNode
  ?!arrowShape: @string ioArrowShape
  ?!arrows:@string ioArrows
  ?!@uint ioMaxUsedRowIndex
  ?debug:let @bool inDebug
{
  if [self length] == 0 then
    [!?ioRowList append
      !nodeDefinition:"[point] {}"
      !row:inRow
      !column:ioColumn
      ?nodeName:let nodeName
    ]
    ioArrows += "  \\draw (" + ioCurrentNode + ") " + ioArrowShape + " (" + nodeName + ") ;\n"
    ioCurrentNode = nodeName
    ioArrowShape = "--"
    ioColumn ++
  else
    for (instruction) in self do
      [instruction tikzNodeForSyntaxInstruction
        !?ioRowList
        !fromRow:inRow
        !?column:ioColumn
        !?node:ioCurrentNode
        !?arrowShape:ioArrowShape
        !?arrows:ioArrows
        !?ioMaxUsedRowIndex
        !debug:inDebug
      ]
    end
  end
}

#----------------------------------------------------------------------------------------------------------------------*

list @rowList {
  @2stringlist columns %setter # Node name, node definition
}

#----------------------------------------------------------------------------------------------------------------------*

setter @rowList append
  ?nodeDefinition:let @string inNodeDefinition
  ?row:let @uint inRow
  ?column:let @uint inColumn
  !nodeName:@string outNodeName
{
#--- Add rows ?
  let length = [self length]
  if length <= inRow then
    for i in length ... inRow do
      self += !{}
    end
  end
#--- Get row
  var cols = [self columnsAtIndex !inRow]
#--- add column pads in row
  if [cols length] <= inColumn then
    for i in [cols length] ... inColumn do
      cols += !"" !""
    end
  end
#--- Add node
  outNodeName = "p" + inRow + "-" + [cols length]
  cols += !outNodeName !inNodeDefinition
#--- Set new row
  [!?self setColumnsAtIndex !cols !inRow]
}

#----------------------------------------------------------------------------------------------------------------------*

abstract method @abstractSyntaxInstructionForGrammarAnalysis tikzNodeForSyntaxInstruction
  ?!@rowList ioRowList
  ?fromRow:let @uint inRow
  ?!column:@uint ioColumn
  ?!node: @string ioCurrentNode
  ?!arrowShape: @string ioArrowShape
  ?!arrows:@string ioArrows
  ?!@uint ioMaxUsedRowIndex
  ?debug:let @bool inDebug

#----------------------------------------------------------------------------------------------------------------------*

override method @terminalInstructionForGrammarAnalysis tikzNodeForSyntaxInstruction
  ?!@rowList ioRowList
  ?fromRow:let @uint inRow
  ?!column:@uint ioColumn
  ?!node: @string ioCurrentNode
  ?!arrowShape: @string ioArrowShape
  ?!arrows:@string ioArrows
  ?!@uint unused ioMaxUsedRowIndex
  ?debug:let @bool unused inDebug
{
  [!?ioRowList append
    !nodeDefinition:"[terminal] {" + escapeForTex (!mTerminalSymbolName) + "}"
    !row:inRow
    !column:ioColumn
    ?nodeName:let nodeName
  ]
  ioArrows += "  \\draw[->] (" + ioCurrentNode + ") " + ioArrowShape + " (" + nodeName + ") ;\n"
  ioCurrentNode = nodeName
  ioArrowShape = "--"
  ioColumn ++
}

#----------------------------------------------------------------------------------------------------------------------*

override method @nonTerminalInstructionForGrammarAnalysis tikzNodeForSyntaxInstruction
  ?!@rowList ioRowList
  ?fromRow:let @uint inRow
  ?!column:@uint ioColumn
  ?!node: @string ioCurrentNode
  ?!arrowShape: @string ioArrowShape
  ?!arrows:@string ioArrows
  ?!@uint unused ioMaxUsedRowIndex
  ?debug:let @bool unused inDebug
{
  let nodeDefinition = "[nonterminal] {\\nonTerminalSymbol{" + escapeForTex (!mNonterminalSymbolName)
    + "}{" + mNonterminalSymbolIndex + "}}"
  [!?ioRowList append
    !nodeDefinition:nodeDefinition
    !row:inRow
    !column:ioColumn
    ?nodeName:let nodeName
  ]
  ioArrows += "  \\draw[->] (" + ioCurrentNode + ") " + ioArrowShape + " (" + nodeName + ") ;\n"
  ioCurrentNode = nodeName
  ioArrowShape = "--"
  ioColumn ++
}

#----------------------------------------------------------------------------------------------------------------------*

override method @selectInstructionForGrammarAnalysis tikzNodeForSyntaxInstruction
  ?!@rowList ioRowList
  ?fromRow:let @uint inRow
  ?!column:@uint ioColumn
  ?!node: @string ioCurrentNode
  ?!arrowShape: @string ioArrowShape
  ?!arrows:@string ioArrows
  ?!@uint ioMaxUsedRowIndex
  ?debug:let @bool inDebug
{
#--- Trace ?
  if inDebug then
    [!?ioRowList append
      !nodeDefinition:"[nonterminal] {SS" + [mSelectBranchList length] + "}"
      !row:inRow
      !column:ioColumn
      ?nodeName:let markerStartNodeName
    ]
    ioArrows += "  \\draw[->] (" + ioCurrentNode + ") " + ioArrowShape + " (" + markerStartNodeName + ") ;\n"
    ioCurrentNode = markerStartNodeName
    ioArrowShape = "--"
    ioColumn ++
  end
#--- Enter start point
  [!?ioRowList append !nodeDefinition:"[point] {}" !row:inRow !column:ioColumn ?nodeName:let startNodeName]
  ioColumn ++
#--- Enter branch zero
  var branches = mSelectBranchList
  [!?branches popFirst ?let @syntaxInstructionListForGrammarAnalysis brancheZero]
  var branchZeroColumn = ioColumn
  var maxUsedRowIndex = inRow
  [brancheZero tikzNodeForSyntaxInstruction
    !?ioRowList
    !fromRow:inRow
    !?column:branchZeroColumn
    !?node:ioCurrentNode
    !?arrowShape:ioArrowShape
    !?arrows:ioArrows
    !?maxUsedRowIndex
    !debug:inDebug
  ]
  var endColumn = branchZeroColumn
#--- Enter following branches
  @stringlist endingNodes = {}
  for branch in branches do
    maxUsedRowIndex ++
    var branchColumn = ioColumn
    var currentNode = startNodeName
    var arrowStyle = "|-"
    [branch.mSyntaxInstructionList tikzNodeForSyntaxInstruction
      !?ioRowList
      !fromRow:maxUsedRowIndex
      !?column:branchColumn
      !?node:currentNode
      !?arrowShape:arrowStyle
      !?arrows:ioArrows
      !?maxUsedRowIndex
      !debug:inDebug
    ]
    endingNodes += !currentNode
    if endColumn < branchColumn then
      endColumn = branchColumn
    end
  end
  ioColumn = endColumn
  if ioMaxUsedRowIndex < maxUsedRowIndex then
    ioMaxUsedRowIndex = maxUsedRowIndex
  end
#--- Enter end point
  [!?ioRowList append !nodeDefinition:"[point] {}" !row:inRow !column:ioColumn ?nodeName:let endNodeName]
  ioColumn ++
  ioArrowShape = "--"
  ioArrows += "  \\draw (" + ioCurrentNode + ") " + ioArrowShape + " (" + endNodeName + ") ;\n"
  ioCurrentNode = endNodeName
#--- Add ending nodes
  for (node) in endingNodes do
    ioArrows += "  \\draw[->] (" + node + ") -| (" + endNodeName + ") ;\n"
  end
#--- Trace ?
  if inDebug then
    [!?ioRowList append
      !nodeDefinition:"[nonterminal] {SE" + [mSelectBranchList length] + "}"
      !row:inRow
      !column:ioColumn
      ?nodeName:let markerEndNodeName
    ]
    ioColumn ++
    ioArrows += "  \\draw[->] (" + ioCurrentNode + ") " + ioArrowShape + " (" + markerEndNodeName + ") ;\n"
    ioCurrentNode = markerEndNodeName
  end
}

#----------------------------------------------------------------------------------------------------------------------*

override method @repeatInstructionForGrammarAnalysis tikzNodeForSyntaxInstruction
  ?!@rowList ioRowList
  ?fromRow:let @uint inRow
  ?!column:@uint ioColumn
  ?!node: @string ioCurrentNode
  ?!arrowShape: @string ioArrowShape
  ?!arrows:@string ioArrows
  ?!@uint ioMaxUsedRowIndex
  ?debug:let @bool inDebug
{
#--- Trace ?
  if inDebug then
    [!?ioRowList append
      !nodeDefinition:"[nonterminal] {RS" + [mRepeatBranchList length] + "}"
      !row:inRow
      !column:ioColumn
      ?nodeName:let markerStartNodeName
    ]
    ioArrows += "  \\draw[->] (" + ioCurrentNode + ") " + ioArrowShape + " (" + markerStartNodeName + ") ;\n"
    ioCurrentNode = markerStartNodeName
    ioArrowShape = "--"
    ioColumn ++
  end
#--- Enter return point
  [!?ioRowList append !nodeDefinition:"[point] {}" !row:inRow !column:ioColumn ?nodeName:let returnNodeName]
  ioColumn ++
#--- Enter branch zero
  var branches = mRepeatBranchList
  [!?branches popFirst ?let @syntaxInstructionListForGrammarAnalysis brancheZero]
  var maxUsedRowIndex = inRow
  [brancheZero tikzNodeForSyntaxInstruction
    !?ioRowList
    !fromRow:inRow
    !?column:ioColumn
    !?node:ioCurrentNode
    !?arrowShape:ioArrowShape
    !?arrows:ioArrows
    !?maxUsedRowIndex
    !debug:inDebug
  ]
  var endColumn = ioColumn
#--- Enter start point
  [!?ioRowList append !nodeDefinition:"[point] {}" !row:inRow !column:ioColumn ?nodeName:let startNodeName]
  ioColumn ++
#--- Enter following branches; particular case: one empty branch
  var oneEmptyBranch = [branches length] == 1
  if oneEmptyBranch then
    [branches first ?let firstBranch]
    oneEmptyBranch = [firstBranch length] == 0
  end
#--- Enter branches
  @stringlist endingNodes = {}
  if oneEmptyBranch then
    endingNodes += !startNodeName
  else
    var nextBranchUsedRowIndex = inRow
    for branch in branches do
      nextBranchUsedRowIndex ++
      var branchColumn = ioColumn
      var currentNode = startNodeName
      var arrowStyle = "|-"
      [branch.mSyntaxInstructionList tikzNodeForSyntaxInstruction
        !?ioRowList
        !fromRow:nextBranchUsedRowIndex
        !?column:branchColumn
        !?node:currentNode
        !?arrowShape:arrowStyle
        !?arrows:ioArrows
        !?nextBranchUsedRowIndex
        !debug:inDebug
      ]
      endingNodes += !currentNode
      if endColumn < branchColumn then
        endColumn = branchColumn
      end
    end
    ioColumn = endColumn
  #--- Compute row of return node
    ioMaxUsedRowIndex = nextBranchUsedRowIndex
  end
  if ioMaxUsedRowIndex < maxUsedRowIndex then
    ioMaxUsedRowIndex = maxUsedRowIndex
  end
  ioMaxUsedRowIndex ++
#--- Enter upper return node, and draw back arrow
  [!?ioRowList append !nodeDefinition:"[point] {}" !row:ioMaxUsedRowIndex !column:ioColumn ?nodeName:let returnUpperNodeName]
  ioColumn ++
  ioArrows += "  \\draw[->] (" + returnUpperNodeName + ") -| (" + returnNodeName + ") ;\n"
#--- Add ending arrows
  for (node) in endingNodes do
    ioArrows += "  \\draw[->] (" + node + ") -| (" + returnUpperNodeName + ") ;\n"
  end
#--- Trace ?
  if inDebug then
    [!?ioRowList append
      !nodeDefinition:"[nonterminal] {RE" + [mRepeatBranchList length] + "}"
      !row:inRow
      !column:ioColumn
      ?nodeName:let markerEndNodeName
    ]
    ioColumn ++
    ioArrows += "  \\draw[->] (" + ioCurrentNode + ") " + ioArrowShape + " (" + markerEndNodeName + ") ;\n"
    ioCurrentNode = markerEndNodeName
  end
}

#----------------------------------------------------------------------------------------------------------------------*
#! isLinear
#----------------------------------------------------------------------------------------------------------------------*

getter @syntaxInstructionListForGrammarAnalysis displayVertically -> @bool {
  result = [self length] > 4
  for (instruction) in self while result do
    result = [instruction isLinear]
  end
}

#----------------------------------------------------------------------------------------------------------------------*

abstract getter @abstractSyntaxInstructionForGrammarAnalysis isLinear -> @bool

#----------------------------------------------------------------------------------------------------------------------*

override getter @terminalInstructionForGrammarAnalysis isLinear -> @bool {
  result = true
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @nonTerminalInstructionForGrammarAnalysis isLinear -> @bool {
  result = true
}


#----------------------------------------------------------------------------------------------------------------------*

override getter @selectInstructionForGrammarAnalysis isLinear -> @bool {
  result = false
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @repeatInstructionForGrammarAnalysis isLinear -> @bool {
  result = false
}

#----------------------------------------------------------------------------------------------------------------------*

