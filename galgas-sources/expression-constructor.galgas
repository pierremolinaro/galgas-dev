#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!   AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @constructorExpressionAST : @semanticExpressionAST {
  @lstring mTypeName
  @lstring mConstructorName
  @actualOutputExpressionList mExpressions
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @defaultConstructorExpressionAST : @semanticExpressionAST {
  @lstring mTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3ExpressionSyntax {

  #·····················································································································

  rule <optional_type> !@lstring outTypeName {
    select
      outTypeName = .new {!"" !.here}
    or
      $@type$ ?outTypeName indexing typeReferenceInConstructor
    end
  }
  
  #·····················································································································
  
  rule <primary> !@semanticExpressionAST outExpression {
  #--- Type name
    <optional_type> ?let @lstring typeName
    $.$
  #--- Constructor name
    $identifier$ ?let @lstring constructorName
  #--- Actual arguments
    @actualOutputExpressionList expressionList
    select
      expressionList = {}
    or
      ${$
      <output_expression_list> ?expressionList
      $}$
    end
    outExpression = @constructorExpressionAST.new {
      !typeName
      !constructorName
      !expressionList}
  }
  
  #·····················································································································
  
  rule <primary> !@semanticExpressionAST outExpression {
    <optional_type> ?let @lstring typeName
    $.$
    $default$
    outExpression = @defaultConstructorExpressionAST.new {!typeName}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @constructorExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [@unifiedTypeMap-entry makeOptionalEntry !?ioTypeMap !self.mTypeName ?*]
  [self.mExpressions enterInSemanticContext !?ioTypeMap]
}

#·····················································································································

private proc analyzeConstructorInvocation
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @unifiedTypeMap-entry inType
  ?let @analysisContext inAnalysisContext
  ?!@localVarManager ioVariableMap
  ?let @lstring inTypeName
  ?let @lstring inConstructorName
  ?let @actualOutputExpressionList inConstuctorEffectiveParameterExpressions
  !@unifiedTypeMap-entry outReturnedType
  !@unifiedTypeMap-entry outConstructorType
  !@semanticExpressionListForGeneration outConstructorEffectiveParameterList
  !@bool outHasCompilerArgument
{
  outConstructorType = inType
  if inTypeName.string != "" then
    outConstructorType = @unifiedTypeMap-entry.searchKey {!inAnalysisContext.mSemanticContext.mTypeMap !inTypeName}
  end
  if [outConstructorType isNull] then
    error inTypeName
    : "cannot infer type"
    : outReturnedType, outConstructorType, outConstructorEffectiveParameterList, outHasCompilerArgument
  else
  #--- Get constructor map from type map
    let @constructorMap constructorMap = [outConstructorType mConstructorMap]
  #--- Search contructor
    @functionSignature constructorFormalArgumentTypeList
    with [inConstructorName string] in constructorMap  do
      constructorFormalArgumentTypeList = mArgumentTypeList
      outHasCompilerArgument = mHasCompilerArgument
      outReturnedType = mReturnedType
    else
      if [constructorMap count] == 0 then
        error inConstructorName
        : "the '@"+ [outConstructorType key] + "' type does not define any constructor"
        : constructorFormalArgumentTypeList, outHasCompilerArgument, outReturnedType
      else
        error inConstructorName
        : "the '@"+ [outConstructorType key] + "' type does not define the '" + inConstructorName + "' constructor"
        : constructorFormalArgumentTypeList, outHasCompilerArgument, outReturnedType
        fixit { replace [constructorMap keyList] }
      end
    end
  #--- Analysis arguments
    if [inConstuctorEffectiveParameterExpressions length] != [constructorFormalArgumentTypeList length] then
      error inConstructorName
      : "calling the '" + inConstructorName + "' constructor of '@" + [outConstructorType key] + "' requires "
      + [[constructorFormalArgumentTypeList length] string] + " parameter(s), while this call has "
      + [[inConstuctorEffectiveParameterExpressions length] string] + " parameter(s)"
    end
    outConstructorEffectiveParameterList = {}
    for (mActualSelector mExpression mEndOfExpressionLocation) in inConstuctorEffectiveParameterExpressions, (mFormalSelector mFormalArgumentType mFormalArgumentName) in constructorFormalArgumentTypeList do
      [mExpression analyzeSemanticExpression
        !inUsefulnessCallerEntityName
        !?ioUsefulEntitiesGraph
        !mFormalArgumentType
        !inAnalysisContext
        !?ioVariableMap
        ?@semanticExpressionForGeneration exp
      ]
      checkAssignmentTypeWithImplicitGetterCall (!mFormalArgumentType ![exp mResultType] !mEndOfExpressionLocation !?exp)
      outConstructorEffectiveParameterList += !exp
      if mFormalSelector.string != mActualSelector.string then
        error mActualSelector 
         : "the selector should be '!" + if mFormalSelector.string != "" then mFormalSelector.string + ":" else "" end + "'"
      end
    end
  end
}

#·····················································································································

override method @constructorExpressionAST analyzeSemanticExpression
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @unifiedTypeMap-entry inType
  ?let @analysisContext inAnalysisContext
  ?!@localVarManager ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
  analyzeConstructorInvocation (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inType
    !inAnalysisContext
    !?ioVariableMap
    !self.mTypeName
    !self.mConstructorName
    !self.mExpressions
    ?let @unifiedTypeMap-entry returnedType
    ?let @unifiedTypeMap-entry constructorType
    ?let @semanticExpressionListForGeneration constructorEffectiveParameterList
    ?let @bool hasCompilerArgument
  )
  let constructorTypeUsefulnessName = typeNameForUsefulEntitiesGraph (!.new{![constructorType key] !self.mTypeName})
  [!?ioUsefulEntitiesGraph addEdge !inUsefulnessCallerEntityName !constructorTypeUsefulnessName]
  let returnedTypeUsefulnessName = typeNameForUsefulEntitiesGraph (!.new{![returnedType key] !self.mTypeName})
  [!?ioUsefulEntitiesGraph addEdge !inUsefulnessCallerEntityName !returnedTypeUsefulnessName]
#---
  outExpression = @constructorExpressionForGeneration.new {
    !returnedType
    ![self.mConstructorName location]
    !constructorType
    ![self.mConstructorName string]
    !constructorEffectiveParameterList
    !hasCompilerArgument
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @defaultConstructorExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  if self.mTypeName.string != "" then
    [@unifiedTypeMap-entry makeEntry !?ioTypeMap !self.mTypeName ?*]
  end
}

#·····················································································································

override method @defaultConstructorExpressionAST analyzeSemanticExpression
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @unifiedTypeMap-entry inType
  ?let @analysisContext inAnalysisContext
  ?!@localVarManager unused ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
  var type = inType
  if self.mTypeName.string != "" then
    type = @unifiedTypeMap-entry.searchKey {!inAnalysisContext.mSemanticContext.mTypeMap !self.mTypeName}
  end
  if [type isNull] then
    error self.mTypeName : "cannot infer type" : outExpression
  else
  #--- Check type accepts default constructor
    if not [type mIsConcrete] then
      error self.mTypeName: "an abstract class does not support the default constructor"
    elsif [type mDefaultConstructorName] == "" then
      error self.mTypeName: "this class does not support the default constructor"
    end
  #---
    let usefulnessName = typeNameForUsefulEntitiesGraph (!.new {![type key] !self.mTypeName})
    [!?ioUsefulEntitiesGraph addEdge !inUsefulnessCallerEntityName !usefulnessName]
  #---
    outExpression = @defaultConstructorExpressionForGeneration.new {
      !type
      !self.mTypeName.location
    }
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @constructorExpressionForGeneration : @semanticExpressionForGeneration {
  @unifiedTypeMap-entry mConstructorType
  @string mConstructorName
  @semanticExpressionListForGeneration mEffectiveParameterList
  @bool mHasCompilerArgument
}

#·····················································································································

override method @constructorExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
{
  [self.mResultType addHeaderFileName !?ioInclusionSet]
  @stringlist parameterList = {}
  for (mExpression) in self.mEffectiveParameterList do
    [mExpression generateExpression
      !?ioGeneratedCode
      !?ioInclusionSet
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet 
      ?let @string parameter
    ]
    parameterList += !parameter  
  end
  outCppExpression = "GALGAS_"  + [self.mConstructorType identifierRepresentation]
                   + "::constructor_" + [self.mConstructorName identifierRepresentation]
                   + " ("
  for (mValue) in parameterList
    do outCppExpression += mValue
    between outCppExpression += ", "
  end
  @bool needsComma = [parameterList length] > 0
  if self.mHasCompilerArgument then
    if needsComma then
      outCppExpression += ", "
    end
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
    outCppExpression += compilerCppName ()
    needsComma = true
  end
  if needsComma then
    outCppExpression += " " + [self.mLocation commaSourceFile] + ")"
  else
    outCppExpression += [self.mLocation sourceFile] + ")"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @defaultConstructorExpressionForGeneration : @semanticExpressionForGeneration {
}

#·····················································································································

override method @defaultConstructorExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@stringset ioInclusionSet
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression
{
  [self.mResultType addHeaderFileName !?ioInclusionSet]
  outCppExpression =
    "GALGAS_"  + [self.mResultType identifierRepresentation]
  + "::constructor_"
  + [[self.mResultType mDefaultConstructorName] identifierRepresentation]
  + " (" + [self.mLocation sourceFile] + ")"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
