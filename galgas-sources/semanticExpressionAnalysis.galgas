#—————————————————————————————————————————————————————————————————————————————————————————————————
#
#  Semantic Analysis of expression
#
#  Copyright (C) 2010, ..., 2016 Pierre Molinaro.
#
#  MIT License
#
# Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
# documentation files (the "Software"), to deal in the Software without restriction, including without limitation
# the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software,
# and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all copies or substantial portions
# of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED
# TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
# THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
# CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
# DEALINGS IN THE SOFTWARE.
#
#—————————————————————————————————————————————————————————————————————————————————————————————————
#
#    A N A L Y S I S    C O N T E X T
#
#—————————————————————————————————————————————————————————————————————————————————————————————————

enum @selfMutability %equatable {
  case none
  case propertiesAreMutableSelfIsNot
  case selfAndPropertiesAreMutable
  case initializer
  
  #·······························································································

  getter propertiesAreMutable -> @bool {
    switch self
    case none : result = false
    case propertiesAreMutableSelfIsNot, selfAndPropertiesAreMutable, initializer : result = true
    end
  }

  #·······························································································
  
  getter selfIsMutable -> @bool {
    switch self
    case none, propertiesAreMutableSelfIsNot : result = false
    case selfAndPropertiesAreMutable, initializer : result = true
    end
  }

  #·······························································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————

enum @selfAvailability %equatable {
  case none
  case available (@unifiedTypeMapEntry type @selfMutability selfMutability)
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

struct @analysisContext {
  public let @semanticContext semanticContext
  public let @predefinedTypes predefinedTypes
  public let @string selfObjectCppName %initArgLabel
  public let @selfAvailability selfAvailability %initArgLabel
  public let @string selfObjectCppPrefixForAccessingProperty %initArgLabel # i.e. "ioObject->", ...
  public let @bool requiresSelfForAccessingProperty %initArgLabel
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

proc checkAssignmentTypes ?let @unifiedTypeMapEntry inTargetType
                          ?let @unifiedTypeMapEntry inSourceType
                          ?let @location inErrorLocation {
  var @unifiedTypeMapEntry sourceType = inSourceType
  var @bool ok = inTargetType == sourceType
  loop (@uint.max) while (not ok) & (not [[sourceType definition].superType isNull]) do
    sourceType = [sourceType definition].superType
    ok = inTargetType == sourceType
  end
  if not ok, let weakRefType = [[inTargetType definition].typeKind getWeakReferenceType] then
    sourceType = inSourceType
    ok = weakRefType.referenceType == sourceType
    loop (@uint.max) while (not ok) & (not [[sourceType definition].superType isNull]) do
      sourceType = [sourceType definition].superType
      ok = weakRefType.referenceType == sourceType
    end
  end
  if not ok then
    error inErrorLocation
     : "target object type is '@" + [inTargetType definition].typeName.string + "' and cannot be assigned from an '@" + [inSourceType definition].typeName.string + "' object"
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

proc checkAssignmentTypeWithImplicitGetterCall ?let @unifiedTypeMapEntry inTargetType
                                                ?let @unifiedTypeMapEntry inSourceType
                                                ?let @location inErrorLocation
                                                ?!@semanticExpressionForGeneration ioExpression {
#--- Optional ?
  let targetTypeDef = [inTargetType definition]
  let targetTypeForBigIntAssignment = if not [targetTypeDef.unwrappedType isNull]
    then targetTypeDef.unwrappedType
    else inTargetType
  end
#--- Assignment from a @bigint ?
  var handled = false
  if let bigIntSource = ioExpression as @literalBigIntExpressionForGeneration then
    if "uint" == targetTypeForBigIntAssignment.typeName then
      let bigIntValue = bigIntSource.mValue
      if (bigIntValue < 0) | (bigIntValue >= (1 << 32)) then
        error ioExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @uint"
      end
      ioExpression = @literalUIntExpressionForGeneration {
        !targetTypeForBigIntAssignment
        !ioExpression.mLocation
        ![bigIntValue uint]
      }
      handled = true
    elsif "sint" == targetTypeForBigIntAssignment.typeName then
      let bigIntValue = bigIntSource.mValue
      if (bigIntValue < -(1 << 31)) | (bigIntValue >= (1 << 31)) then
        error ioExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @sint"
      end
      ioExpression = @literalSIntExpressionForGeneration {
        !targetTypeForBigIntAssignment
        !ioExpression.mLocation
        ![bigIntValue sint]
      }
      handled = true
    elsif "uint64" == targetTypeForBigIntAssignment.typeName then
      let bigIntValue = bigIntSource.mValue
      if (bigIntValue < 0) | (bigIntValue >= (1 << 64)) then
        error ioExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @uint64"
      end
      ioExpression = @literalUInt64ExpressionForGeneration {
        !targetTypeForBigIntAssignment
        !ioExpression.mLocation
        ![bigIntValue uint64]
      }
      handled = true
    elsif "sint64" == targetTypeForBigIntAssignment.typeName then
      let bigIntValue = bigIntSource.mValue
      if (bigIntValue < -(1 << 63)) | (bigIntValue >= (1 << 63)) then
        error ioExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @sint64"
      end
      ioExpression = @literalSInt64ExpressionForGeneration {
        !targetTypeForBigIntAssignment
        !ioExpression.mLocation
        ![bigIntValue sint64]
      }
      handled = true
    end
  end
#---
  if not handled,
        not [[inTargetType definition].unwrappedType isNull],
        [[inTargetType definition].unwrappedType typeName] == [inSourceType typeName] then
    handled = true
  end
#---
  if not handled then
    var sourceType = inSourceType
    var ok = [inTargetType typeName] == [inSourceType typeName]
    loop (@uint.max) while (not ok) & (not [[sourceType definition].superType isNull]) do
      sourceType = [sourceType definition].superType
      ok = inTargetType == sourceType
    end
    if not ok, let targetWeakRef = [targetTypeDef.typeKind getWeakReferenceType] then
      sourceType = inSourceType
      ok = [targetWeakRef.referenceType typeName] == [sourceType typeName]
      loop (@uint.max) while (not ok) & (not [[sourceType definition].superType isNull]) do
        sourceType = [sourceType definition].superType
        ok = [targetWeakRef.referenceType typeName] == [sourceType typeName]
      end
    end
  #--- Check for a direct property access or for a conversion getter
    if not ok, let prop = [inSourceType definition].propertyMap §[targetTypeDef.typeName.string] then
      [prop.mAccessControl checkSetAccess !.none !inErrorLocation]
      let conversionExpression = @propertyAccessExpressionForGeneration {
        !prop.mPropertyType
        !inErrorLocation
        !ioExpression
        !targetTypeDef.typeName.string
      }
      ioExpression = conversionExpression
      ok = prop.mPropertyType == inTargetType
    elsif (not ok) && [[inSourceType definition].getterMap hasKey !targetTypeDef.typeName.string] then
      [[inSourceType definition].getterMap searchKey
        !targetTypeDef.typeName
        ?let kind
        ?let @functionSignature argumentTypeList
        ?*
        ?let hasCompilerArgument
        ?let returnedType
        ?2*
      ]
      ok = ([argumentTypeList count] == 0) & (returnedType == inTargetType)
   #--- Add conversion expression
      let conversionExpression = @getterCallExpressionForGeneration {
        !inTargetType
        !inErrorLocation
        !kind
        !ioExpression
        !()
        !targetTypeDef.typeName.string
        !()
        !hasCompilerArgument
      }
      ioExpression = conversionExpression
    end
    if not ok then
      error inErrorLocation
       : "target object type is '@" + targetTypeDef.typeName.string
       + "' and cannot be assigned from an '@" + [inSourceType definition].typeName.string
       + "' object; this type have no '"
       + targetTypeDef.typeName.string
       + "' getter without argument that returns a '@"
       + targetTypeDef.typeName.string + "' object"
    end
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

proc checkDiadicOperator ?operatorIsHandled: let @bool inOperandIsHandled
                          ?let @string inOperatorNameForErrorMessage
                          ?let @location inErrorLocation
                          ?!@semanticExpressionForGeneration ioLeftExpression
                          ?!@semanticExpressionForGeneration ioRightExpression {
  let leftResultType  = ioLeftExpression.mResultType
  let rightResultType = ioRightExpression.mResultType
  if not inOperandIsHandled then
    error inErrorLocation
    : "left operand type is '@" + [leftResultType definition].typeName.string + "' and does not accept '"
      + inOperatorNameForErrorMessage
      + "' operator (right operand type is '@" + [rightResultType definition].typeName.string + ")"
  else
    var @unifiedTypeMapEntry rightType = rightResultType
    var @bool ok = leftResultType == rightType
    loop (@uint.max) while (not ok) & (not [[rightType definition].superType isNull]) do
      rightType = [rightType definition].superType
      ok = leftResultType == rightType
    end
  #---- convert right bigint ?
    if not ok, let bigIntSource = ioRightExpression as @literalBigIntExpressionForGeneration then
      if "uint" == [leftResultType definition].typeName then
        let bigIntValue = bigIntSource.mValue
        if (bigIntValue < 0) | (bigIntValue >= (1 << 32)) then
          error ioRightExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @uint"
        end
        ioRightExpression = @literalUIntExpressionForGeneration {!leftResultType !ioRightExpression.mLocation ![bigIntValue uint]}
        ok = true
      elsif "sint" == [leftResultType definition].typeName then
        let bigIntValue = bigIntSource.mValue
        if (bigIntValue < -(1 << 31)) | (bigIntValue >= (1 << 31)) then
          error ioRightExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @sint"
        end
        ioRightExpression = @literalSIntExpressionForGeneration {!leftResultType !ioRightExpression.mLocation ![bigIntValue sint]}
        ok = true
      elsif "uint64" == [leftResultType definition].typeName then
        let bigIntValue = bigIntSource.mValue
        if (bigIntValue < 0) | (bigIntValue >= (1 << 64)) then
          error ioRightExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @uint64"
        end
        ioRightExpression = @literalUInt64ExpressionForGeneration {!leftResultType !ioRightExpression.mLocation ![bigIntValue uint64]}
        ok = true
      elsif "sint64" == [leftResultType definition].typeName then
        let bigIntValue = bigIntSource.mValue
        if (bigIntValue < -(1 << 63)) | (bigIntValue >= (1 << 63)) then
          error ioRightExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @sint64"
        end
        ioRightExpression = @literalSInt64ExpressionForGeneration {!leftResultType !ioRightExpression.mLocation ![bigIntValue sint64]}
        ok = true
      end
    end
  #---- convert left bigint ?
    if not ok, let bigIntSource = ioLeftExpression as @literalBigIntExpressionForGeneration then
      if "uint" == [rightType definition].typeName then
        let bigIntValue = bigIntSource.mValue
        if (bigIntValue < 0) | (bigIntValue >= (1 << 32)) then
          error ioLeftExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @uint"
        end
        ioLeftExpression = @literalUIntExpressionForGeneration {!rightType !ioLeftExpression.mLocation ![bigIntValue uint]}
        ok = true
      elsif "sint" == [rightType definition].typeName then
        let bigIntValue = bigIntSource.mValue
        if (bigIntValue < -(1 << 31)) | (bigIntValue >= (1 << 31)) then
          error ioLeftExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @sint"
        end
        ioLeftExpression = @literalSIntExpressionForGeneration {!rightType !ioLeftExpression.mLocation ![bigIntValue sint]}
        ok = true
      elsif "uint64" == [rightType definition].typeName then
        let bigIntValue = bigIntSource.mValue
        if (bigIntValue < 0) | (bigIntValue >= (1 << 64)) then
          error ioLeftExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @uint64"
        end
        ioLeftExpression = @literalUInt64ExpressionForGeneration {!rightType !ioLeftExpression.mLocation ![bigIntValue uint64]}
        ok = true
      elsif "sint64" == [rightType definition].typeName then
        let bigIntValue = bigIntSource.mValue
        if (bigIntValue < -(1 << 63)) | (bigIntValue >= (1 << 63)) then
          error ioLeftExpression.mLocation : "literal value (" + bigIntValue + ") does not fit in an @sint64"
        end
        ioLeftExpression = @literalSInt64ExpressionForGeneration {!rightType !ioLeftExpression.mLocation ![bigIntValue sint64]}
        ok = true
      end
    end
  #--- Check for a direct property access or for a conversion getter
    if not ok, let prop = [rightType definition].propertyMap §[[leftResultType definition].typeName] then
      [prop.mAccessControl checkSetAccess !.none !inErrorLocation]
      let conversionExpression = @propertyAccessExpressionForGeneration {
        !prop.mPropertyType
        !inErrorLocation
        !ioRightExpression
        ![leftResultType definition].typeName.string
      }
      ioRightExpression = conversionExpression
      ok = prop.mPropertyType == leftResultType
    elsif (not ok) && [[rightType definition].getterMap hasKey ![leftResultType definition].typeName.string] then
      [[rightType definition].getterMap searchKey
        ![leftResultType definition].typeName
        ?let kind
        ?let @functionSignature argumentTypeList
        ?*
        ?let hasCompilerArgument
        ?let returnedType
        ?2*
      ]
      ok = ([argumentTypeList count] == 0) & (returnedType == leftResultType)
   #--- Add conversion expression
      let conversionExpression = @getterCallExpressionForGeneration {
        !leftResultType
        !inErrorLocation
        !kind
        !ioRightExpression
        !()
        ![leftResultType definition].typeName.string
        !()
        !hasCompilerArgument
      }
      ioRightExpression = conversionExpression
    end
    if not ok then
      error inErrorLocation
      : "left operand type of '" + inOperatorNameForErrorMessage + "' operator is '@" + [leftResultType definition].typeName.string
      + "', right operand type is '@" + [rightType definition].typeName.string
      + "', it should be either the same, either inherits from left operand type, either have a '"
      + [leftResultType definition].typeName.string + "' getter without argument that returns a '@" + [leftResultType definition].typeName.string + "' object"
    end
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#
#    S E M A N T I C    C O N T E X T    F O R    A N A L Y S I S
#
#—————————————————————————————————————————————————————————————————————————————————————————————————

struct @predefinedTypes {
  public let @unifiedTypeMapEntry mLocationType
  public let @unifiedTypeMapEntry mBoolType
  public let @unifiedTypeMapEntry mCharType
  public let @unifiedTypeMapEntry mStringType
  public let @unifiedTypeMapEntry mUIntType
  public let @unifiedTypeMapEntry mSIntType
  public let @unifiedTypeMapEntry mUInt64Type
  public let @unifiedTypeMapEntry mSInt64Type
  public let @unifiedTypeMapEntry mDoubleType
  public let @unifiedTypeMapEntry mLBoolType
  public let @unifiedTypeMapEntry mLCharType
  public let @unifiedTypeMapEntry mLStringType
  public let @unifiedTypeMapEntry mLUIntType
  public let @unifiedTypeMapEntry mLSIntType
  public let @unifiedTypeMapEntry mLUInt64Type
  public let @unifiedTypeMapEntry mLSInt64Type
  public let @unifiedTypeMapEntry mLDoubleType
  public let @unifiedTypeMapEntry mStringListType
  public let @unifiedTypeMapEntry mLBigIntType
  public let @unifiedTypeMapEntry mBigIntType
  public let @unifiedTypeMapEntry mStringSetType
  public let @unifiedTypeMapEntry mLStringListType
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#
#    E X P R E S S I O N    A N A L Y S I S
#
#—————————————————————————————————————————————————————————————————————————————————————————————————

abstract method @semanticExpressionAST analyzeSemanticExpression
  ?usefulnessCallerEntityName: let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry inType
  ?let @analysisContext inAnalysisContext
  ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager ioVariableMap
  !@semanticExpressionForGeneration outExpression

#—————————————————————————————————————————————————————————————————————————————————————————————————
