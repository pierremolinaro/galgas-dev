//--------------------------------------------------------------------------------------------------
//
//  'lexique' component
//
//  Copyright (C) 2007, ..., 2015 Pierre Molinaro.
//
//  MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software
// and associated documentation files (the "Software"), to deal in the Software without
// restriction, including without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the
// Software is furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all copies or
// substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING
// BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
// NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
// DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//
//--------------------------------------------------------------------------------------------------

abstract class @lexicalExpressionAST {
}

//--------------------------------------------------------------------------------------------------

abstract class @lexicalInstructionAST {
}

//--------------------------------------------------------------------------------------------------

list @lexicalInstructionListAST {
  public let @lexicalInstructionAST mInstruction
}

//--------------------------------------------------------------------------------------------------

list @templateDelimitorListAST {
  public let @lstring mStartString
  public let @bool nonAtomicSelection %initArgLabel
  public let @bool mTemplateEndMark
  public let @bool mPreserved
  public let @lstring mEndString
}

//--------------------------------------------------------------------------------------------------

list @templateReplacementListAST {
  public let @lstring mMatchString
  public let @lstring mReplacementString
  public let @lstring mReplacementFunction
}

//--------------------------------------------------------------------------------------------------

abstract class @abstractLexicalRuleAST {
}

//--------------------------------------------------------------------------------------------------

list @lexicalRuleListAST {
  public let @abstractLexicalRuleAST mLexicalRule
}

//--------------------------------------------------------------------------------------------------

getter @lexicalRuleListAST useLoopLocalVar -> @bool {
  result = false
  for (lexicalRule) in self while not result do
    result = [lexicalRule lexicalRuleUsesLoopLocalVar]
  end
}

//--------------------------------------------------------------------------------------------------

final class @lexicalImplicitRuleAST : @abstractLexicalRuleAST {
  private let @lstring mListName
}

//--------------------------------------------------------------------------------------------------

final class @lexicalExplicitRuleAST : @abstractLexicalRuleAST {
  private let @lexicalExpressionAST mLexicalRuleExpression
  private let @lexicalInstructionListAST mInstructionList
}

//--------------------------------------------------------------------------------------------------

list @lexicalMessageDeclarationListAST {
  public let @lstring mMessageName
  public let @lstring mMessageValue
}

//--------------------------------------------------------------------------------------------------

list @lexicalAttributeListAST {
  public let @lstring mTypeName
  public let @lstring mName
}

//--------------------------------------------------------------------------------------------------

list @lexicalStyleListAST {
  public let @lstring mName
  public let @lstring mComment
}

//--------------------------------------------------------------------------------------------------

list @terminalDeclarationListAST {
  public let @lstring mName
  public let @sentLexicalAttributeListAST mSentAttributeList
  public let @lstring mSyntaxErrorMessage
  public let @lstring mStyle // Empty string if no style
  public let @bool nonAtomicSelection %initArgLabel
  public let @bool isEndOfTemplateMark %initArgLabel
}

//--------------------------------------------------------------------------------------------------

list @lexicalListEntryListAST {
  public let @lstring mEntrySpelling
  public let @lstring mTerminalSpelling
  public let @bool nonAtomicSelection %initArgLabel
  public let @bool isEndOfTemplateMark %initArgLabel
}

//--------------------------------------------------------------------------------------------------

list @sentLexicalAttributeListAST {
  public let @lstring mFormalSelector
  public let @lstring mAttributeName
}

//--------------------------------------------------------------------------------------------------

list @lexicalListDeclarationListAST {
  public let @lstring mName
  public let @lstring mStyle // Empty string if no style
  public let @lstring mSyntaxErrorMessage
  public let @sentLexicalAttributeListAST mSentAttributeList
  public let @lexicalListEntryListAST mEntryList
}

//--------------------------------------------------------------------------------------------------

final class @lexicalOrExpressionAST : @lexicalExpressionAST {
  private let @lexicalExpressionAST mLeftOperand
  private let @lexicalExpressionAST mRightOperand
}

//--------------------------------------------------------------------------------------------------

final class @lexicalCharacterSetMatchAST : @lexicalExpressionAST {
  private let @lstring mCharacterSetName
}

//--------------------------------------------------------------------------------------------------

final class @lexicalCharacterMatchAST : @lexicalExpressionAST {
  private let @lchar mCharacter
}

//--------------------------------------------------------------------------------------------------

final class @lexicalCharacterIntervalMatchAST : @lexicalExpressionAST {
  private let @lchar mLowerBound
  private let @lchar mUpperBound
}

//--------------------------------------------------------------------------------------------------

final class @lexicalStringMatchAST : @lexicalExpressionAST {
  private let @lstring mString
}

//--------------------------------------------------------------------------------------------------

final class @lexicalStringNotMatchAST : @lexicalExpressionAST {
  private let @lstring mString
  private let @lstring mErrorMessage
}

//--------------------------------------------------------------------------------------------------

list @lexicalSendSearchListAST {
  private let @lstring mAttributeName
  private let @lstring mSearchListName
}

//--------------------------------------------------------------------------------------------------

abstract class @lexicalSendDefaultActionAST {
}

//--------------------------------------------------------------------------------------------------

final class @lexicalSendTerminalByDefaultAST : @lexicalSendDefaultActionAST {
  private let @lstring mDefaultSentTerminal
}

//--------------------------------------------------------------------------------------------------

final class @lexicalErrorByDefaultAST : @lexicalSendDefaultActionAST {
  private let @lstring mDefaultErrorMessageName
}

//--------------------------------------------------------------------------------------------------

list @lexicalWhileBranchListAST {
  public let @lexicalExpressionAST mWhileExpression
  public let @lexicalInstructionListAST mWhileInstructionList
}

//--------------------------------------------------------------------------------------------------

list @lexicalSelectBranchListAST {
  public let @lexicalExpressionAST mSelectExpression
  public let @lexicalInstructionListAST mSelectInstructionList
}

//--------------------------------------------------------------------------------------------------

abstract class @lexicalRoutineOrFunctionFormalInputArgumentAST {
}

//--------------------------------------------------------------------------------------------------

list @lexicalFunctionCallActualArgumentListAST {
  public let @lexicalRoutineOrFunctionFormalInputArgumentAST mLexicalActualInputArgument
}

//--------------------------------------------------------------------------------------------------

final class @lexicalAttributeInputArgumentAST : @lexicalRoutineOrFunctionFormalInputArgumentAST {
  private let @lstring mAttributeName
}

//--------------------------------------------------------------------------------------------------

final class @lexicalCharacterInputArgumentAST : @lexicalRoutineOrFunctionFormalInputArgumentAST {
  private let @lchar mCharacter
}

//--------------------------------------------------------------------------------------------------

final class @lexicalUnsignedInputArgumentAST : @lexicalRoutineOrFunctionFormalInputArgumentAST {
  private let @lbigint mUnsigned
}

//--------------------------------------------------------------------------------------------------

final class @lexicalCurrentCharacterInputArgumentAST : @lexicalRoutineOrFunctionFormalInputArgumentAST {
  private let @location mLocation
}

//--------------------------------------------------------------------------------------------------

final class @lexicalFunctionInputArgumentAST : @lexicalRoutineOrFunctionFormalInputArgumentAST {
  private let @lstring mFunctionName
  private let @lexicalFunctionCallActualArgumentListAST mFunctionActualArgumentList
}

//--------------------------------------------------------------------------------------------------

enum @lexicalArgumentModeAST %equatable {
  case lexicalInputMode
  case lexicalInputOutputMode
}

getter @lexicalArgumentModeAST lexicalFormalModeName -> @string {
  switch self
  case lexicalInputMode       : result = "!"
  case lexicalInputOutputMode : result = "?!"
  end
}

getter @lexicalArgumentModeAST cppConstInFormalArgument -> @string {
  switch self
  case lexicalInputMode       : result = "const "
  case lexicalInputOutputMode : result = ""
  end
}

getter @lexicalArgumentModeAST cppReferenceInFormalArgument -> @string {
  switch self
  case lexicalInputMode       : result = ""
  case lexicalInputOutputMode : result = " &"
  end
}

getter @lexicalArgumentModeAST cocoaPointerInFormalArgument -> @string {
  switch self
  case lexicalInputMode       : result = ""
  case lexicalInputOutputMode : result = " *"
  end
}

//--------------------------------------------------------------------------------------------------

list @indexingListAST {
  public let @lstring mIndexName
  public let @lstring mIndexComment
}

//--------------------------------------------------------------------------------------------------
//
//           B U I L D    T E R M I N A L    M A P
//
//--------------------------------------------------------------------------------------------------

enum @lexicalTypeEnum %equatable {
  case lexicalType_string
  case lexicalType_char
  case lexicalType_uint
  case lexicalType_uint64
  case lexicalType_sint
  case lexicalType_sint64
  case lexicalType_double
  case lexicalType_bigint
}

//--------------------------------------------------------------------------------------------------

list @lexicalSentValueList {
  public let @lstring mLexicalFormalSelector
  public let @string mLexicalAttributeName
  public let @lexicalTypeEnum mLexicalType
}

//--------------------------------------------------------------------------------------------------

map @terminalMap {
  public let @lexicalSentValueList mSentAttributeList
  insert insertKey error message "the '%K' terminal has been already declared in %L"
  search searchKey error message "the '%K' terminal is not declared"
}

//--------------------------------------------------------------------------------------------------
//          B U I L D    L E X I C A L    T Y P E    M A P
//--------------------------------------------------------------------------------------------------

map @lexicalTypeMap {
  public let @lexicalTypeEnum mLexicalType
  insert insertKey error message "the '@%K' lexical type is already declared in %L"
  search searchKey error message "there is no '@%K' lexical type"
}

//--------------------------------------------------------------------------------------------------

func %once buildLexicalTypeMap () -> @lexicalTypeMap {
  result = @()
  [!?result insertKey !@lstring {!"string" !@location.here} !.lexicalType_string]
  [!?result insertKey !@lstring {!"char"   !@location.here} !.lexicalType_char]
  [!?result insertKey !@lstring {!"uint"   !@location.here} !.lexicalType_uint]
  [!?result insertKey !@lstring {!"uint64" !@location.here} !.lexicalType_uint64]
  [!?result insertKey !@lstring {!"sint"   !@location.here} !.lexicalType_sint]
  [!?result insertKey !@lstring {!"sint64" !@location.here} !.lexicalType_sint64]
  [!?result insertKey !@lstring {!"double" !@location.here} !.lexicalType_double]
  [!?result insertKey !@lstring {!"bigint" !@location.here} !.lexicalType_bigint]
}

//--------------------------------------------------------------------------------------------------

map @lexicalAttributeMap {
  public let @lexicalTypeEnum mLexicalType
  insert insertKey error message "the '%K' lexical attribute has been already declared in %L"
  search searchKey error message "the '%K' lexical attribute is not declared"
}

//--------------------------------------------------------------------------------------------------
//   B U I L D    L E X I C A L    A T T R I B U T E    M A P
//--------------------------------------------------------------------------------------------------

proc buildLexicalAttributeMap
  ?let @lexicalTypeMap inLexicalTypeMap
  ?let @lexicalAttributeListAST inLexicalAttributeList
  ?!@lexicalAttributeMap ioLexicalAttributeMap {
  for (mTypeName mName) in inLexicalAttributeList do
    if [inLexicalTypeMap hasKey !mTypeName.string] then
      [inLexicalTypeMap searchKey !mTypeName ?let @lexicalTypeEnum lexicalType]
      [!?ioLexicalAttributeMap insertKey !mName !lexicalType]
    else
      var @string m = ""
      m += "the @" + mTypeName + " type is not a valid lexical attribute type; are allowed:"
      for (lkey *) in inLexicalTypeMap do
        m += " @" + lkey
      between
        m += ","
      end
      error mTypeName: m
    end
  end
}

//--------------------------------------------------------------------------------------------------
