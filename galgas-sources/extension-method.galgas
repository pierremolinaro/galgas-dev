#----------------------------------------------------------------------------------------------------------------------*
#   AST                                                                                                                *
#----------------------------------------------------------------------------------------------------------------------*

class @extensionMethodAST : @semanticDeclarationAST {
  @lstring mTypeName
  @lstring mExtensionMethodName
  @formalParameterListAST mExtensionMethodFormalParameterList
  @semanticInstructionListAST mExtensionMethodInstructionList
  @location mEndOfMethodLocation
}

#----------------------------------------------------------------------------------------------------------------------*
#   SYNTAX                                                                                                             *
#----------------------------------------------------------------------------------------------------------------------*

syntax extension galgas3DeclarationsSyntax {

  #·····················································································································

  rule <declaration> ?!@galgas3DeclarationAST ioDeclarations {
    $method$
    $type_name$ ?var  @lstring className indexing typeReferenceExtensionMethod
    $identifier$ ?var  @lstring mMethodName indexing extensionMethodDefinition
    <formal_parameter_list> ?let @formalParameterListAST formalParameterList
    ${$
    <semantic_instruction_list> ?let @semanticInstructionListAST routineInstructionList
    let endOfMethodLocation = @location.here
    $}$
    ioDeclarations.mDeclarationList += !@extensionMethodAST.new {
      !false # Is not predefined
      !className
      !mMethodName
      !formalParameterList
      !routineInstructionList
      !endOfMethodLocation
    }
  }

  #·····················································································································

}

#----------------------------------------------------------------------------------------------------------------------*
#   KEY REPRESENTATION                                                                                                 *
#----------------------------------------------------------------------------------------------------------------------*

override getter @extensionMethodAST keyRepresentation
  ->@string outString {
  outString = "extension method @" + mTypeName + " " + mExtensionMethodName
}

#----------------------------------------------------------------------------------------------------------------------*
#   ENTER IN GRAPH                                                                                                     *
#----------------------------------------------------------------------------------------------------------------------*

override method @extensionMethodAST enterDeclarationInGraph
  ?!@semanticTypePrecedenceGraph ioSemanticTypePrecedenceGraph
  ?!@extensionMethodMapForBuildingContext ioExtensionMethodMapForBuildingContext
  ?!@extensionGetterMapForBuildingContext unused ioExtensionGetterMapForBuildingContext
  ?!@extensionModifierMapForBuildingContext unused ioExtensionModifierMapForBuildingContext
  ?!@semanticDeclarationListAST unused ioExtensionOverrideDefinitionList
{
  let key = @lstring.new {!"extension method @" + mTypeName + " " + mExtensionMethodName ![mExtensionMethodName location]}
  [!?ioSemanticTypePrecedenceGraph addNode !key !self]
  [!?ioSemanticTypePrecedenceGraph addEdge !key !@lstring.new {!"@" + mTypeName ![mTypeName location]}]
#---
  if not [ioExtensionMethodMapForBuildingContext hasKey ![mTypeName string]] then
    [!?ioExtensionMethodMapForBuildingContext insertKey
      !mTypeName
      !@extensionMethodMapForType. emptyMap
    ]
  end
  with mTypeName in !?ioExtensionMethodMapForBuildingContext error message searchKey  do
    [!?mExtensionMethodMapForType insertKey
      !mExtensionMethodName
      !mExtensionMethodFormalParameterList
    ]
  end
}

#----------------------------------------------------------------------------------------------------------------------*
#   SEMANTIC CONTEXT                                                                                                   *
#----------------------------------------------------------------------------------------------------------------------*

override method @extensionMethodAST enterInSemanticContext
  ?let @extensionMethodMapForBuildingContext unused inExtensionMethodMapForBuildingContext
  ?let @extensionGetterMapForBuildingContext unused inExtensionGetterMapForBuildingContext
  ?let @extensionModifierMapForBuildingContext unused inExtensionModifierMapForBuildingContext
  ?!@semanticContext unused ioSemanticContext
{
}

#----------------------------------------------------------------------------------------------------------------------*
#   SEMANTIC ANALYSIS                                                                                                  *
#----------------------------------------------------------------------------------------------------------------------*

override method @extensionMethodAST semanticAnalysis
  ?let @string unused inProductDirectory
  ?let @semanticContext inSemanticContext
  ?let @predefinedTypes inPredefinedTypes
  ?!@semanticDeclarationSortedListForGeneration ioSemanticDeclarationListForGeneration {
#--- Self type
  let @unifiedTypeMap-proxy selfType = @unifiedTypeMap-proxy.searchKey {!inSemanticContext.mTypeMap !mTypeName}
  @string selfObjectName
  @string selfObjectAccessor
  @bool implementedAsFunction
#--- Check self type is a class
  if [selfType mTypeKindEnum] == @typeKindEnum. classType then
    selfObjectName = "object"
    selfObjectAccessor = "object->"
    implementedAsFunction = false
  else
    selfObjectName = "inObject"
    selfObjectAccessor = "inObject."
    implementedAsFunction = true
  end
#--- Analyze instruction list
  var formalParameterListForGeneration =@formalParameterListForGeneration.emptyList{}
  @semanticInstructionListForGeneration semanticInstructionListForGeneration
  let analysisContext = @analysisContext.new {
    !inSemanticContext
    !inPredefinedTypes
    !selfType
    !selfObjectName
    !@unifiedTypeMap-proxy. null}
  analyzeRoutineBody (
    !analysisContext
    !mExtensionMethodFormalParameterList
    !@localConstantList.emptyList
    !if ([selfType mTypeKindEnum] == @typeKindEnum. classType) | ([selfType mTypeKindEnum] == @typeKindEnum. structType)
       then [selfType mAllTypedAttributeList]
       else @typedPropertyList.emptyList
     end
    !@typedPropertyList.emptyList
    !selfObjectAccessor
    !mExtensionMethodInstructionList
    !mEndOfMethodLocation
    ?semanticInstructionListForGeneration
    ?formalParameterListForGeneration
  )
#---
  ioSemanticDeclarationListForGeneration +=
    !"extension method " + mExtensionMethodName
    !@extensionMethodForGeneration.new {
      !true # Generate Cpp header file
      !"method-" + [selfType key] + "-" + mExtensionMethodName
      !selfType
      ![mExtensionMethodName string]
      !implementedAsFunction
      !formalParameterListForGeneration
      ![selfType mAllTypedAttributeList]
      !semanticInstructionListForGeneration
    }
    !"~"
}

#----------------------------------------------------------------------------------------------------------------------*
#   EXTENSION LIST MAP                                                                                                 *
#----------------------------------------------------------------------------------------------------------------------*

override method @extensionMethodAST buildExtensionListMaps
  ?!@genericExtensionMethodListMap unused ioAbstractExtensionModifierListMapAST
  ?!@genericExtensionMethodListMap unused ioExtensionModifierListMap
  ?!@genericExtensionMethodListMap unused ioOverridingExtensionModifierListMap
  ?!@genericExtensionMethodListMap unused ioOverridingAbstractExtensionModifierListMap
  ?!@genericExtensionMethodListMap unused ioAbstractExtensionMethodListMapAST
  ?!@genericExtensionMethodListMap ioExtensionMethodListMap
  ?!@genericExtensionMethodListMap unused ioOverridingExtensionMethodListMap
  ?!@genericExtensionMethodListMap unused ioOverridingAbstractExtensionMethodListMap
  ?!@genericExtensionMethodListMap unused ioAbstractExtensionGetterListMap
  ?!@genericExtensionMethodListMap unused ioExtensionGetterListMap
  ?!@genericExtensionMethodListMap unused ioOverridingExtensionGetterListMap
  ?!@genericExtensionMethodListMap unused ioOverridingAbstractExtensionGetterListMap
{
  ioExtensionMethodListMap += !mTypeName.string !mExtensionMethodName
}

#----------------------------------------------------------------------------------------------------------------------*
#   CODE GENERATION                                                                                                    *
#----------------------------------------------------------------------------------------------------------------------*

class @extensionMethodForGeneration : @semanticDeclarationWithHeaderForGeneration {
  @unifiedTypeMap-proxy mReceiverType
  @string mExtensionMethodName
  @bool mImplementedAsFunction
  @formalParameterListForGeneration mExtensionMethodFormalParameterList
  @typedPropertyList mTypedAttributeList
  @semanticInstructionListForGeneration mSemanticInstructionListForGeneration
}

#·······················································································································

override getter @extensionMethodForGeneration headerKind ->@headerKind outResult {
  outResult = @headerKind.oneHeader
}

#·······················································································································

override method @extensionMethodForGeneration appendDeclaration1
  ?!@stringset unused ioInclusionSet
  !@string outHeader {
  if mImplementedAsFunction then
    outHeader = [filewrapper semanticComponentGenerationTemplate.extensionMethodAsFunctionDeclaration
      ![mReceiverType key]
      !mExtensionMethodName
      !mExtensionMethodFormalParameterList
    ] 
  else
    outHeader = [filewrapper semanticComponentGenerationTemplate.extensionMethodDeclaration
      ![mReceiverType key]
      !mExtensionMethodName
      !mExtensionMethodFormalParameterList
    ] 
  end
}

#·······················································································································

override method @extensionMethodForGeneration appendSpecificImplementation
  ?let @unifiedTypeMap unused inTypeMap
  ?!@stringset ioInclusionSet
  !@string outImplementation {
  if mImplementedAsFunction then
    @formalParameterListForGeneration extensionMethodFormalParameterList = mExtensionMethodFormalParameterList
    [!?extensionMethodFormalParameterList insertAtIndex
      !["" nowhere]
      !@formalArgumentPassingModeAST. argumentConstantIn
      !mReceiverType
      !@lstring.new {!"self" !@location.here}
      !"inObject"
      !0
    ]
    ioInclusionSet += !"method-" + [mReceiverType key]+ "-" + mExtensionMethodName
    generateProcedure (
      !static:false
      !"extensionMethod_" + [mExtensionMethodName identifierRepresentation]
      !?ioInclusionSet
      !extensionMethodFormalParameterList
      !mSemanticInstructionListForGeneration
      !false # is not const
      !"C_Compiler"
      !true # Generate location Argument
      !false # Do not generate 'resetTemplateString'
      !false # Do not generate syntaxDirectedTranslationString argument
      ?let @string code
    )
    outImplementation = [filewrapper semanticComponentGenerationTemplate.extensionMethodAsFunctionImplementation
      ![mReceiverType key]
      !mExtensionMethodName
      !code
    ] 
  else
    [mReceiverType addHeaderFileName !?ioInclusionSet]
    for () in mExtensionMethodFormalParameterList do
      [mFormalArgumentType addHeaderFileName !?ioInclusionSet]
    end
    @unifiedTypeMap-proxy baseType = mReceiverType
    @bool searching = true
    loop( @uint. max) while searching do
      if not [[baseType mSuperType] isNull] then
        if [[[baseType mSuperType] mInstanceMethodMap] hasKey !mExtensionMethodName] then
          baseType = [baseType mSuperType]
        else
          searching = false
        end
      else
        searching = false
      end  
    end
    ioInclusionSet += !"method-" + [baseType key]+ "-" + mExtensionMethodName
    generateExtensionMethod (
      !mReceiverType
      !""
      !?ioInclusionSet
      !mExtensionMethodName
      !mExtensionMethodFormalParameterList
      !mSemanticInstructionListForGeneration
      ?let @string methodImplementation
    )
    outImplementation = [filewrapper semanticComponentGenerationTemplate.extensionMethodImplementation
      ![mReceiverType key]
      !mExtensionMethodName
      !mExtensionMethodFormalParameterList
      !methodImplementation
    ]
  end
}

#----------------------------------------------------------------------------------------------------------------------*

