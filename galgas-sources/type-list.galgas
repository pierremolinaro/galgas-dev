#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   AST                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @listDeclarationAST : @semanticDeclarationAST {
  private let @lstring mListTypeName
  private let @propertyInCollectionListAST mPropertyList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SYNTAX                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3DeclarationsSyntax {

  #·····················································································································

  rule <declaration> ?!@galgas3DeclarationAST ioDeclarations {
    $list$
    $@type$ ?let @lstring listTypeName indexing listDefinition, structDefinition "-element"
    ${$
    var @propertyInCollectionListAST propertyList = {}
    repeat
      <property_declaration> !?propertyList
      select
      or
        $;$
      end
    while
    end
    $}$
    ioDeclarations.mDeclarationList += !@listDeclarationAST.new {
      !false # Is not predefined
      !listTypeName
      !propertyList
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas4DeclarationsSyntax {

  #·····················································································································

  rule <declaration> ?!@galgas3DeclarationAST ioDeclarations {
    $list$
    $@type$ ?let @lstring listTypeName indexing listDefinition, structDefinition "-element"
    ${$
    var @propertyInCollectionListAST propertyList = {}
    repeat
      <property_declaration> !?propertyList
      select
      or
        $;$
      end
    while
    end
    $}$
    ioDeclarations.mDeclarationList += !@listDeclarationAST.new {
      !false # Is not predefined
      !listTypeName
      !propertyList
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   ENTER IN GRAPH                          
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @listDeclarationAST enterDeclarationInGraph
  ?!@semanticTypePrecedenceGraph ioSemanticTypePrecedenceGraph
  ?!@extensionMethodMapForBuildingContext unused ioExtensionMethodMapForBuildingContext
  ?!@extensionGetterMapForBuildingContext unused ioExtensionGetterMapForBuildingContext
  ?!@extensionSetterMapForBuildingContext unused ioExtensionSetterMapForBuildingContext
  ?!@semanticDeclarationListAST unused ioExtensionOverrideDefinitionList {
  let key = @lstring.new {!"@" + self.mListTypeName !self.mListTypeName.location}
  [!?ioSemanticTypePrecedenceGraph addNode !key !self]
  for (* mPropertyTypeName * * *) in self.mPropertyList do
    [!?ioSemanticTypePrecedenceGraph noteNode !@lstring.new {!"@" + mPropertyTypeName !mPropertyTypeName.location}]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC ANALYSIS                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @listDeclarationAST enterDeclarationInSemanticContext
  ?let @extensionMethodMapForBuildingContext inExtensionMethodMapForBuildingContext
  ?let @extensionGetterMapForBuildingContext inExtensionGetterMapForBuildingContext
  ?let @extensionSetterMapForBuildingContext inExtensionSetterMapForBuildingContext
  ?!@semanticContext ioSemanticContext
{
#--- Handle attributes
  var @typedPropertyList typedAttributeList = {}
  for (* mPropertyTypeName mPropertyName isPublic mAttributeList) in self.mPropertyList do
    [!?ioSemanticContext.mTypeMap makeEntry  !mPropertyTypeName ?let attributeTypeIndex]
    var hasSelector = false
    for (mValue) in mAttributeList do
      if mValue.string == "setter" then
        warning mValue : "'%setter' is obsolete and has no effect"
      elsif mValue.string == "nogetter" then
        warning mValue : "'%nogetter' is obsolete and has no effect"
      elsif mValue.string == "selector" then
        if hasSelector then
          error mValue : "the '%selector' attribute is already named"
        end
        hasSelector = true
      else
        error mValue
        : "only '%selector' attribute is allowed here"
        fixit { replace {!"%selector"} }
      end
    end
    typedAttributeList += !attributeTypeIndex !mPropertyName !isPublic !hasSelector
  end
#--- uint type index
  [!?ioSemanticContext.mTypeMap makeEntryFromString !"uint" ?let uintType]
#--- List type index
  [!?ioSemanticContext.mTypeMap makeEntry  !self.mListTypeName ?let listTypeIndex]
#--- Element type index
  [!?ioSemanticContext.mTypeMap makeEntry
    !@lstring.new {!self.mListTypeName.string + elementTypeNameSuffix () !self.mListTypeName.location}
    ?let @unifiedTypeMapEntry listElementTypeIndex
  ]
#--- Property description
  var @enumerationDescriptorList enumerationDescriptor = {}
  var @functionSignature constructorAttributeTypeList = {}
  var @formalParameterSignature setterOutputFormalArgumentList = {}
  var @formalParameterSignature setterInputFormalArgumentList = {}
  for (mPropertyTypeEntry @lstring mPropertyName * mHasSelector) in typedAttributeList do
    let selector = if mHasSelector then mPropertyName.string else "" end
    enumerationDescriptor += !mPropertyTypeEntry !mPropertyName.string
    constructorAttributeTypeList +=
      ![selector nowhere]
      !mPropertyTypeEntry
      !mPropertyName.string
    setterOutputFormalArgumentList +=
      ![selector nowhere]
      !mPropertyTypeEntry
      !.argumentOut
      !mPropertyName.string
    setterInputFormalArgumentList +=
      ![selector nowhere]
      !mPropertyTypeEntry
      !.argumentConstantIn
      !mPropertyName.string
  end
#--- Constructors
  var @constructorMap constructorMap = {}
  enterConstructorWithoutArgument (
    !?constructorMap
    !?ioSemanticContext.mTypeMap 
    !name:"emptyList"
    !resultTypeName:self.mListTypeName.string
    !hasLexiqueArg:false
  )
  [!?constructorMap insertKey
    !@lstring.new {!"listWithValue" !@location.here}
    !constructorAttributeTypeList
    !false
    !listTypeIndex
  ]
#--- Readers
  commonGetterMapForAllTypes (!?ioSemanticContext.mTypeMap ?var @getterMap getterMap )
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !getterName:"length"
    !returnedTypeName:"uint"
    !hasCompilerArgument:false
  )
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !getterName:"range"
    !returnedTypeName:"range"
    !hasCompilerArgument:false
  )
  enterBaseGetterWithArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !getterName: "subListFromIndex" !"uint" !"inIndex"
    !returnedTypeName:self.mListTypeName.string
    !hasCompilerArgument:true
  )
  enterBaseGetterWithArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !getterName: "subListToIndex" !"uint" !"inIndex"
    !returnedTypeName:self.mListTypeName.string
    !hasCompilerArgument:true
  )
  enterBaseGetterWithArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !getterName: "subListWithRange" !"range" !"inRange"
    !returnedTypeName:self.mListTypeName.string
    !hasCompilerArgument:true
  )
  for (mPropertyTypeEntry mPropertyName * *) in typedAttributeList do
    [!?getterMap insertOrReplace
      !@lstring.new {!mPropertyName.string + "AtIndex" !mPropertyName.location}
      !@methodKind.definedAsMember
      !@functionSignature {!["" nowhere] !uintType !"inIndex"}
      !@location.here
      !true
      !mPropertyTypeEntry
      !@methodQualifier.isBasic
      !"" # No Error message
    ]
  end
#------------------------- Instance methods
  var @instanceMethodMap instanceMethodMap = {}
  [!?instanceMethodMap insertKey
    !@lstring.new {!"first" !@location.here}
    !@methodKind.definedAsMember
    !setterOutputFormalArgumentList
    !@location.here
    !true
    !@methodQualifier.isBasic
    !""
  ]
  [!?instanceMethodMap insertKey
    !@lstring.new {!"last" !@location.here}
    !@methodKind.definedAsMember
    !setterOutputFormalArgumentList
    !@location.here
    !true
    !@methodQualifier.isBasic
    !""
  ]
#----------------------------------------- Modifiers
  var @setterMap setterMap = {}
  [!?setterMap insertOrReplace
    !@lstring.new {!"append" !@location.here}
    !@methodKind.definedAsMember
    !{!["" nowhere] !listElementTypeIndex !@formalArgumentPassingModeAST.argumentIn !"inElement"}
    !true
    !@methodQualifier.isBasic
    !""
  ]
  [!?setterMap insertOrReplace
    !@lstring.new {!"popFirst" !@location.here}
    !@methodKind.definedAsMember
    !setterOutputFormalArgumentList
    !true
    !@methodQualifier.isBasic
    !""
  ]
  [!?setterMap insertOrReplace
    !@lstring.new {!"popLast" !@location.here}
    !@methodKind.definedAsMember
    !setterOutputFormalArgumentList
    !true
    !@methodQualifier.isBasic
    !""
  ]
  setterOutputFormalArgumentList += !["" nowhere] !uintType !@formalArgumentPassingModeAST.argumentConstantIn !"inIndex"
  [!?setterMap insertOrReplace
    !@lstring.new {!"removeAtIndex" !@location.here}
    !@methodKind.definedAsMember
    !setterOutputFormalArgumentList
    !true
    !@methodQualifier.isBasic
    !""
  ]
#  drop setterOutputFormalArgumentList
  for (mPropertyTypeEntry mPropertyName mHasSetter *) in typedAttributeList do
    if mHasSetter then
      var setterFormalArgumentList = @formalParameterSignature {}
      setterFormalArgumentList += !["" nowhere] !mPropertyTypeEntry !@formalArgumentPassingModeAST.argumentConstantIn !mPropertyName.string
      setterFormalArgumentList += !["" nowhere] !uintType !@formalArgumentPassingModeAST.argumentConstantIn !mPropertyName.string
      [!?setterMap insertOrReplace
        !@lstring.new {!"set" + [mPropertyName.string stringByCapitalizingFirstCharacter] + "AtIndex" !mPropertyName.location}
        !@methodKind.definedAsMember
        !setterFormalArgumentList
        !true
        !@methodQualifier.isBasic
        !"" # No error message
      ]
    end
  end
#--- setter 'insertAtIndex'
  setterInputFormalArgumentList += !["" nowhere] !uintType !@formalArgumentPassingModeAST.argumentConstantIn !"inInsertionIndex"
  [!?setterMap insertOrReplace
    !@lstring.new {!"insertAtIndex" !@location.here}
    !@methodKind.definedAsMember
    !setterInputFormalArgumentList
    !true
    !@methodQualifier.isBasic
    !"" # No error message
  ]
#  drop setterInputFormalArgumentList
#--- Add extensions
  addExtensions (
    !inExtensionMethodMapForBuildingContext
    !inExtensionGetterMapForBuildingContext
    !inExtensionSetterMapForBuildingContext
    !?ioSemanticContext
    !self.mListTypeName
    !isClass: false
    !?getterMap
    !?setterMap
    !?instanceMethodMap
    !acceptSetters: true
  )
#--- Enter in type map
  let typeDefinition = @unifiedTypeDefinition.new {
    !self.mListTypeName
    !self.mIsPredefined
    !true # Concrete Type
    !@unifiedTypeMapEntry.null
    !@typeKindEnum. listType
    !true # Support collection value
    !typedAttributeList
    !{}
    !{}
    !constructorMap
    !getterMap
    !setterMap
    !instanceMethodMap
    !{}
    !@optionalMethodMap {}
    !enumerationDescriptor
    !.doNotGenererateObjectCompare | .plusEqualOperatorWithExpression | .infixAddOperator | .isComparable
    !constructorAttributeTypeList
    !{}
    !{}
    !@mapSearchMethodListAST {}
    !@mapSearchMethodListAST {}
    !false # Do not generate header in separate file
    !listElementTypeIndex # Type for enumerated element for "for" instruction
    !"emptyList" # Default constructor
    !"list-" + [self.mListTypeName fileNameRepresentation]
    !@headerKind.oneHeader
  }
  [!?ioSemanticContext.mTypeMap insertType !typeDefinition]
}

#·······················································································································

override method @listDeclarationAST addAssociatedElement
  ?!@semanticDeclarationListAST ioSemanticDeclarationList
{
  var @propertyInCollectionListAST structAttributeList = {}
  for (isConstant mPropertyTypeName mPropertyName * *) in self.mPropertyList do
    structAttributeList +=
      !isConstant: isConstant
      !mPropertyTypeName
      !mPropertyName
      !true
      !{}
  end
  ioSemanticDeclarationList += !@structDeclarationAST.new {
    !self.mIsPredefined
    !.new {!self.mListTypeName.string + elementTypeNameSuffix () !self.mListTypeName.location}
    !structAttributeList
    !""
  }
}


#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   ENTER IN SEMANTIC CONTEXT               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @listDeclarationAST semanticAnalysis
  ?!@lstringlist unused ioUsefulnessRootEntities 
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @string unused inProductDirectory
  ?let @semanticContext inSemanticContext
  ?let @predefinedTypes unused inPredefinedTypes
  ?!@semanticDeclarationListForGeneration ioSemanticDeclarationListForGeneration
{
#--- Useful entities graph
  let nameForUsefulness = typeNameForUsefulEntitiesGraph (!self.mListTypeName)
  [!?ioUsefulEntitiesGraph addNode !nameForUsefulness !nameForUsefulness]
  let elementTypeNameForUsefulness = typeNameForUsefulEntitiesGraph (!.new{!self.mListTypeName.string + "-element" !self.mListTypeName})
  [!?ioUsefulEntitiesGraph addEdge !nameForUsefulness !elementTypeNameForUsefulness]
#---
  var @typedPropertyList typedAttributeList = {}
  var @propertyIndexMap attributeMap = {}
  for (* mPropertyTypeName mPropertyName mIsPublic mAttributeList) in self.mPropertyList do
    let propertyTypeNameForUsefulness = typeNameForUsefulEntitiesGraph (!mPropertyTypeName)
    [!?ioUsefulEntitiesGraph addEdge !nameForUsefulness !propertyTypeNameForUsefulness]
    let t = [inSemanticContext.mTypeMap searchKey !mPropertyTypeName]
    let hasSetter = mIsPublic
    var hasSelector = false
    for (mValue) in mAttributeList do
      if mValue.string == "setter" then
        warning mValue : "obsolete attribute: setter is implicitly generated for a public property"
      elsif mValue.string == "nogetter" then
        warning mValue : "obsolete attribute: use 'private' qualifier"
      elsif mValue.string == "selector" then
        if hasSelector then
          error mValue : "the '%selector' attribute is already named"
        end
        hasSelector = true
      else
        error mValue
        : "only '%selector' attribut is allowed here"
        fixit { replace {!"%selector"} }
      end
    end
    typedAttributeList +=
      !t
      !mPropertyName
      !hasSetter
      !hasSelector
    [!?attributeMap insertKey !mPropertyName !t]
  end
#---
  let selfType = [inSemanticContext.mTypeMap searchKey !self.mListTypeName]
  ioSemanticDeclarationListForGeneration +=
    !"list type " + self.mListTypeName
    !@listTypeForGeneration.new {
      !selfType
      ![inSemanticContext.mTypeMap searchKey !.new {!self.mListTypeName.string + elementTypeNameSuffix () !self.mListTypeName.location}]
      !typedAttributeList
    }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION                         
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @listTypeForGeneration : @semanticTypeForGeneration {
  private let @unifiedTypeMapEntry mListElementTypeIndex
  private let @typedPropertyList mTypedAttributeList
}

#·······················································································································

filewrapper listGenerationTemplate in "+generation-templates/type_generation" {
}{
}{
  template listTypeHeader1 "GALGAS_list.h1.galgasTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedPropertyList ATTRIBUTE_LIST

  template listTypeSpecificImplementation "GALGAS_list.cpp.galgasTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedPropertyList ATTRIBUTE_LIST
    ?@string ELEMENT_TYPE_IDENTIFIER
}

#·······················································································································

override method @listTypeForGeneration appendDeclaration1
  ?!@stringset unused ioInclusionSet
  !@string outHeader
{
  let selfTypedefinition = [self.mSelfTypeEntry definition]
  outHeader = [filewrapper listGenerationTemplate.listTypeHeader1
    !selfTypedefinition.mTypeName.string
    ![self.mSelfTypeEntry identifierRepresentation]
    !self.mTypedAttributeList
  ]
  outHeader += [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    !selfTypedefinition.mTypeName.string
    ![self.mSelfTypeEntry identifierRepresentation]
    !selfTypedefinition.mIsConcrete
    !selfTypedefinition.mConstructorMap
    !selfTypedefinition.mGetterMap
    !selfTypedefinition.mSetterMap
    !selfTypedefinition.mInstanceMethodMap
    !selfTypedefinition.mClassMethodMap
    !selfTypedefinition.mOptionalMethodMap
    !selfTypedefinition.mEnumerationDescriptor
    !selfTypedefinition.mHandledOperatorFlags
    !selfTypedefinition.mAddAssignOperatorArguments
    !selfTypedefinition.mTypeForEnumeratedElement
  ]
}

#·······················································································································

override method @listTypeForGeneration appendSpecificImplementation
                                        ?let @unifiedTypeMap unused inTypeMap
                                        ?!@stringset ioInclusionSet
                                        !@string outImplementation {
  for (t 3*) in self.mTypedAttributeList do
    [t addHeaderFileName !?ioInclusionSet]
  end
  [self.mListElementTypeIndex addHeaderFileName !?ioInclusionSet]
  [self.mSelfTypeEntry addHeaderFileName !?ioInclusionSet]
  let selfTypedefinition = [self.mSelfTypeEntry definition]
  outImplementation = [filewrapper listGenerationTemplate.listTypeSpecificImplementation
    !selfTypedefinition.mTypeName.string
    ![self.mSelfTypeEntry identifierRepresentation]
    !self.mTypedAttributeList
    ![[self.mSelfTypeEntry definition].mTypeForEnumeratedElement identifierRepresentation]
  ]
 }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

