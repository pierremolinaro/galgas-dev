#----------------------------------------------------------------------------------------------------------------------*
#   AST                                                                                                                *
#----------------------------------------------------------------------------------------------------------------------*

class @listDeclarationAST : @semanticDeclarationAST {
  @lstring mListTypeName %nogetter
  @propertyInCollectionListAST mAttributeList %nogetter
}

#----------------------------------------------------------------------------------------------------------------------*
#   SYNTAX                                                                                                             *
#----------------------------------------------------------------------------------------------------------------------*

syntax extension galgas3DeclarationsSyntax {

  rule <declaration> ?!@galgas3DeclarationAST ioDeclarations {
    $list$
    $@type$ ?var @lstring listTypeName indexing listDefinition, structDefinition "-element"
    ${$
    @propertyInCollectionListAST attributeList = {}
    repeat
      <property_declaration> !? attributeList
      select
      or
        $;$
      end
    while
    end
    $}$
    ioDeclarations.mDeclarationList += !@listDeclarationAST.new {
      !false # Is not predefined
      !listTypeName
      !attributeList
    }
  }

}

#----------------------------------------------------------------------------------------------------------------------*
#   ENTER IN GRAPH                                                                                                     *
#----------------------------------------------------------------------------------------------------------------------*

override method @listDeclarationAST enterDeclarationInGraph
  ?!@semanticTypePrecedenceGraph ioSemanticTypePrecedenceGraph
  ?!@extensionMethodMapForBuildingContext unused ioExtensionMethodMapForBuildingContext
  ?!@extensionGetterMapForBuildingContext unused ioExtensionGetterMapForBuildingContext
  ?!@extensionSetterMapForBuildingContext unused ioExtensionSetterMapForBuildingContext
  ?!@semanticDeclarationListAST unused ioExtensionOverrideDefinitionList {
  let key = @lstring.new {!"@" + mListTypeName ![mListTypeName location]}
  [!?ioSemanticTypePrecedenceGraph addNode !key !self]
  for () in mAttributeList do
    [!?ioSemanticTypePrecedenceGraph noteNode !@lstring.new {!"@" + mPropertyTypeName ![mPropertyTypeName location]}]
  end
}

#----------------------------------------------------------------------------------------------------------------------*
#   SEMANTIC ANALYSIS                                                                                                  *
#----------------------------------------------------------------------------------------------------------------------*

override method @listDeclarationAST enterInSemanticContext
  ?let @extensionMethodMapForBuildingContext inExtensionMethodMapForBuildingContext
  ?let @extensionGetterMapForBuildingContext inExtensionGetterMapForBuildingContext
  ?let @extensionSetterMapForBuildingContext inExtensionSetterMapForBuildingContext
  ?!@semanticContext ioSemanticContext {
#--- Handle attributes
  var typedAttributeList =@typedPropertyList {}
  for () in mAttributeList do
    [@unifiedTypeMap-proxy makeProxy !?ioSemanticContext.mTypeMap !mPropertyTypeName ?let @unifiedTypeMap-proxy attributeTypeIndex]
    @bool hasGetter = true
    @bool hasSetter = false
    for () in mFeatureList do
      if mValue.string == "nogetter" then
        hasGetter = false
      elsif mValue.string == "setter" then
        hasSetter = true
      end
    end
    typedAttributeList += !attributeTypeIndex !mPropertyName !hasSetter !hasGetter
  end
#--- uint type index
  [@unifiedTypeMap-proxy makeProxyFromString !?ioSemanticContext.mTypeMap !"uint" ?let @unifiedTypeMap-proxy uintType]
#--- List type index
  [@unifiedTypeMap-proxy makeProxy !?ioSemanticContext.mTypeMap !mListTypeName ?let @unifiedTypeMap-proxy listTypeIndex]
#--- Element type index
  [@unifiedTypeMap-proxy makeProxy
    !?ioSemanticContext.mTypeMap
    !@lstring.new {!mListTypeName.string + elementTypeNameSuffix () !mListTypeName.location}
    ?let @unifiedTypeMap-proxy listElementTypeIndex
  ]
#--- Property description
  var enumerationDescriptor =@enumerationDescriptorList {}
  var constructorAttributeTypeList =@functionSignature {}
  var setterOutputFormalArgumentList =@formalParameterSignature {}
  var setterInputFormalArgumentList =@formalParameterSignature {}
  for () in typedAttributeList do
    enumerationDescriptor += !mAttributeTypeProxy ![mAttributeName string]
    constructorAttributeTypeList += !["" nowhere] !mAttributeTypeProxy !mAttributeName.string
    setterOutputFormalArgumentList += !["" nowhere] !mAttributeTypeProxy !@formalArgumentPassingModeAST.argumentOut !mAttributeName.string
    setterInputFormalArgumentList += !["" nowhere] !mAttributeTypeProxy !@formalArgumentPassingModeAST.argumentConstantIn !mAttributeName.string
  end
#--- Constructors
  var constructorMap =@constructorMap.emptyMap{}
  enterConstructorWithoutArgument (
    !?constructorMap
    !?ioSemanticContext.mTypeMap 
    !name:"emptyList"
    !resultTypeName:[mListTypeName string]
    !hasLexiqueAndLocationArgs:false
  )
  [!?constructorMap insertKey
    !@lstring.new {!"listWithValue" !@location.here}
    !constructorAttributeTypeList
    !false
    !listTypeIndex
  ]
#--- Readers
  commonGetterMapForAllTypes ( !?ioSemanticContext.mTypeMap ?var @getterMap getterMap )
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !getterName:"length"
    !returnedTypeName:"uint"
    !false
  )
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !getterName:"range"
    !returnedTypeName:"range"
    !false
  )
  enterBaseGetterWithArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !"subListFromIndex" !"uint" !"inIndex"
    !returnedTypeName:[mListTypeName string]
    !true
  )
  enterBaseGetterWithArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !"subListToIndex" !"uint" !"inIndex"
    !returnedTypeName:[mListTypeName string]
    !true
  )
  enterBaseGetterWithArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !"subListWithRange" !"range" !"inRange"
    !returnedTypeName:[mListTypeName string]
    !true
  )
  for () in typedAttributeList do
    if mHasGetter then
      [!?getterMap insertOrReplace
        !@lstring.new {![mAttributeName string] + "AtIndex" ![mAttributeName location]}
        !@methodKind.definedAsMember
        !@functionSignature {!["" nowhere] !uintType !"inIndex"}
        !@location.here
        !true
        !mAttributeTypeProxy
        !@methodQualifier.isBasic
        !"" # No Error message
      ]
    end
  end
#------------------------- Instance methods
  var instanceMethodMap =@instanceMethodMap.emptyMap{}
  [!?instanceMethodMap insertKey
    !@lstring.new {!"first" !@location.here}
    !@methodKind.definedAsMember
    !setterOutputFormalArgumentList
    !@location.here
    !true
    !@methodQualifier.isBasic
    !""
  ]
  [!?instanceMethodMap insertKey
    !@lstring.new {!"last" !@location.here}
    !@methodKind.definedAsMember
    !setterOutputFormalArgumentList
    !@location.here
    !true
    !@methodQualifier.isBasic
    !""
  ]
#----------------------------------------- Modifiers
  var setterMap =@setterMap.emptyMap{}
  [!?setterMap insertOrReplace
    !@lstring.new {!"popFirst" !@location.here}
    !@methodKind.definedAsMember
    !setterOutputFormalArgumentList
    !true
    !@methodQualifier.isBasic
    !""
  ]
  [!?setterMap insertOrReplace
    !@lstring.new {!"popLast" !@location.here}
    !@methodKind.definedAsMember
    !setterOutputFormalArgumentList
    !true
    !@methodQualifier.isBasic
    !""
  ]
  setterOutputFormalArgumentList += !["" nowhere] !uintType !@formalArgumentPassingModeAST.argumentConstantIn !"inIndex"
  [!?setterMap insertOrReplace
    !@lstring.new {!"removeAtIndex" !@location.here}
    !@methodKind.definedAsMember
    !setterOutputFormalArgumentList
    !true
    !@methodQualifier.isBasic
    !""
  ]
  drop setterOutputFormalArgumentList
  for () in typedAttributeList do
    if mHasSetter then
      var setterFormalArgumentList =@formalParameterSignature {}
      setterFormalArgumentList += !["" nowhere] !mAttributeTypeProxy !@formalArgumentPassingModeAST.argumentConstantIn !mAttributeName.string
      setterFormalArgumentList += !["" nowhere] !uintType !@formalArgumentPassingModeAST.argumentConstantIn !mAttributeName.string
      [!?setterMap insertOrReplace
        !@lstring.new {!"set" + [[mAttributeName string] stringByCapitalizingFirstCharacter] + "AtIndex" ![mAttributeName location]}
        !@methodKind.definedAsMember
        !setterFormalArgumentList
        !true
        !@methodQualifier.isBasic
        !"" # No error message
      ]
    end
  end
#--- setter 'insertAtIndex'
  setterInputFormalArgumentList += !["" nowhere] !uintType !@formalArgumentPassingModeAST.argumentConstantIn !"inInsertionIndex"
  [!?setterMap insertOrReplace
    !@lstring.new {!"insertAtIndex" !@location.here}
    !@methodKind.definedAsMember
    !setterInputFormalArgumentList
    !true
    !@methodQualifier.isBasic
    !"" # No error message
  ]
  drop setterInputFormalArgumentList
#--- Enumeration variants
  var enumerationVariants =@stringlist {}
  enumerationVariants += !"up"
  enumerationVariants += !"down"
#--- Add Categories
  addExtensions (
    !inExtensionMethodMapForBuildingContext
    !inExtensionGetterMapForBuildingContext
    !inExtensionSetterMapForBuildingContext
    !?ioSemanticContext
    !mListTypeName
    !?getterMap
    !?setterMap
    !?instanceMethodMap
  )
#--- Enter in type map
  [!?ioSemanticContext.mTypeMap insertKey
    !mListTypeName
    !mIsPredefined
    !true # Concrete Type
    !@unifiedTypeMap-proxy. null
    !@typeKindEnum. listType
    !true # Support collection value
    !typedAttributeList
    !@attributeMap. emptyMap
    !@typedPropertyList.emptyList
    !constructorMap
    !getterMap
    !setterMap
    !instanceMethodMap
    !@classMethodMap. emptyMap
    !enumerationDescriptor
    !enumerationVariants
    !doNotGenererateObjectCompare () | plusEqualOperatorWithExpression () | infixAddOperator ()
    !constructorAttributeTypeList
    !{}
    !{}
    !@mapSearchMethodListAST.emptyList
    !@mapSearchMethodListAST.emptyList
    !false # Do not generate header in separate file
    !listElementTypeIndex # Type for enumerated element for "for" instruction
    !"emptyList" # Default constructor
    !"list-" + mListTypeName
    !@headerKind.oneHeader
  ]
}

#••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••*

override method @listDeclarationAST addAssociatedElement
  ?!@semanticDeclarationListAST ioSemanticDeclarationList
{
  @propertyInCollectionListAST structAttributeList = {}
  for () in mAttributeList do
    structAttributeList +=
      !mPropertyTypeName
      !mPropertyName
      !{}
  end
  ioSemanticDeclarationList += !@structDeclarationAST.new {
    !mIsPredefined
    !.new {!mListTypeName.string + elementTypeNameSuffix () !mListTypeName.location}
    !structAttributeList
    !""
  }
}


#----------------------------------------------------------------------------------------------------------------------*
#   ENTER IN SEMANTIC CONTEXT                                                                                          *
#----------------------------------------------------------------------------------------------------------------------*

override method @listDeclarationAST semanticAnalysis
  ?!@lstringlist unused ioUsefulnessRootEntities 
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @string unused inProductDirectory
  ?let @semanticContext inSemanticContext
  ?let @predefinedTypes unused inPredefinedTypes
  ?!@semanticDeclarationSortedListForGeneration ioSemanticDeclarationListForGeneration
{
#--- Useful entities graph
  let nameForUsefulness = typeNameForUsefulEntitiesGraph (!mListTypeName)
  [!?ioUsefulEntitiesGraph addNode !nameForUsefulness !nameForUsefulness]
  let elementTypeNameForUsefulness = typeNameForUsefulEntitiesGraph (!.new{!mListTypeName.string + "-element" !mListTypeName})
  [!?ioUsefulEntitiesGraph addEdge !nameForUsefulness !elementTypeNameForUsefulness]
#---
  @typedPropertyList typedAttributeList = {}
  @attributeIndexMap attributeMap = {}
  for () in mAttributeList do
    let propertyTypeNameForUsefulness = typeNameForUsefulEntitiesGraph (!mPropertyTypeName)
    [!?ioUsefulEntitiesGraph addEdge !nameForUsefulness !propertyTypeNameForUsefulness]
    let t = @unifiedTypeMap-proxy.searchKey {!inSemanticContext.mTypeMap !mPropertyTypeName}
    var hasGetter = true
    var hasSetter = false
    for () in mFeatureList do
      if mValue.string == "nogetter" then
        if not hasGetter then
          error mValue:"duplicate feature"
        end
        hasGetter = false
      elsif mValue.string == "setter" then
        if hasSetter then
          error mValue:"duplicate feature"
        end
        hasSetter = true
      else
        error mValue:"unknown feature; the features available here are: 'nogetter', 'setter'"
      end
    end
    typedAttributeList +=
      !t
      !mPropertyName
      !hasSetter
      !hasGetter
    [!?attributeMap insertKey !mPropertyName !t]
  end
#---
  let selfType = @unifiedTypeMap-proxy.searchKey {!inSemanticContext.mTypeMap !mListTypeName}
  ioSemanticDeclarationListForGeneration +=
    !"list type " + mListTypeName
    !@listTypeForGeneration.new {
      !selfType
      !.searchKey {!inSemanticContext.mTypeMap !.new {!mListTypeName.string + elementTypeNameSuffix () !mListTypeName.location}}
      !typedAttributeList
    }
  !""
}

#----------------------------------------------------------------------------------------------------------------------*
#   CODE GENERATION                                                                                                    *
#----------------------------------------------------------------------------------------------------------------------*

class @listTypeForGeneration : @semanticTypeForGeneration {
  @unifiedTypeMap-proxy mListElementTypeIndex
  @typedPropertyList mTypedAttributeList
}

#••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••*

filewrapper listGenerationTemplate in "../generation_templates/type_generation" {
}{
}{
  template listTypeHeader1 "GALGAS_list.h1.galgasTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedPropertyList ATTRIBUTE_LIST

  template listTypeSpecificImplementation "GALGAS_list.cpp.galgasTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedPropertyList ATTRIBUTE_LIST
}

#••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••*

override method @listTypeForGeneration appendDeclaration1
  ?!@stringset unused ioInclusionSet
  !@string outHeader {
  outHeader = [filewrapper listGenerationTemplate.listTypeHeader1
    ![mTypeProxy key]
    ![mTypeProxy identifierRepresentation]
    !mTypedAttributeList
  ]
  outHeader += [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    ![mTypeProxy key]
    ![mTypeProxy identifierRepresentation]
    ![mTypeProxy mIsConcrete]
    ![mTypeProxy mConstructorMap]
    ![mTypeProxy mGetterMap]
    ![mTypeProxy mSetterMap]
    ![mTypeProxy mInstanceMethodMap]
    ![mTypeProxy mClassMethodMap]
    ![mTypeProxy mEnumerationDescriptor]
    ![mTypeProxy mHandledOperatorFlags]
    ![mTypeProxy mAddAssignOperatorArguments]
    ![mTypeProxy mTypeForEnumeratedElement]
  ]
}

#••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••*

override method @listTypeForGeneration appendSpecificImplementation
  ?let @unifiedTypeMap unused inTypeMap
  ?!@stringset ioInclusionSet
  !@string outImplementation {
  for (t ...) in mTypedAttributeList do
    [t addHeaderFileName !?ioInclusionSet]
  end
  [mListElementTypeIndex addHeaderFileName !?ioInclusionSet]
  [mTypeProxy addHeaderFileName !?ioInclusionSet]
  outImplementation = [filewrapper listGenerationTemplate.listTypeSpecificImplementation
    ![mTypeProxy key]
    ![mTypeProxy identifierRepresentation]
    !mTypedAttributeList
  ]
 }

#----------------------------------------------------------------------------------------------------------------------*

