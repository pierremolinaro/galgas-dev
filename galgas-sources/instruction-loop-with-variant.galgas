#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @loopInstructionWithVariantAST : @semanticInstructionAST {
  public let @semanticExpressionAST mVariantExpression
  public let @location mEndOfVariantExpression
  public let @semanticInstructionListAST mFirstInstructions
  public let @location mEndOfFirstInstructions
  public let @semanticExpressionAST mLoopExpression
  public let @location mEndOfLoopExpression
  public let @semanticInstructionListAST mSecondInstructions
  public let @location mEndOfSecondInstructions
  public let @location mEndOfLoopInstruction
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3InstructionsSyntax {

  #·····················································································································

  rule <semantic_instruction> !@semanticInstructionAST outInstruction {
    $loop$
    let instructionLocation = @location.here
  #--- Variant
    $($
    <expression> ?let @semanticExpressionAST variantExpression
    let endOfVariantExpression = @location.here
    $)$
  #---
    <semantic_instruction_list> ?let @semanticInstructionListAST firstInstructions
  #---
    let endOfFirstInstructions = @location.here
    $while$
    <expression> ?let @semanticExpressionAST loopExpression
    let endOfLoopExpression = @location.here
    $do$
    <semantic_instruction_list> ?let @semanticInstructionListAST secondInstructions
    let endOfSecondInstructions = @location.here
    $end$
    select
    or
      $%attribute$ ?let @lstring terminator
      if terminator.string != "loop" then
        error terminator : "the terminator attribute should be '%loop'" fixit { replace "%loop" }
      end
    end
    let endOfLoopInstruction = @location.here
    outInstruction = @loopInstructionWithVariantAST.new {
      !instructionLocation
      !variantExpression
      !endOfVariantExpression
      !firstInstructions
      !endOfFirstInstructions
      !loopExpression
      !endOfLoopExpression
      !secondInstructions
      !endOfSecondInstructions
      !endOfLoopInstruction
    }
  }  

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas4InstructionsSyntax {

  #·····················································································································

  rule <semantic_instruction> !@semanticInstructionAST outInstruction {
    $loop$
    let instructionLocation = @location.here
  #--- Variant
    $($
    <expression> ?let @semanticExpressionAST variantExpression
    let endOfVariantExpression = @location.here
    $)$
  #---
    <semantic_instruction_list> ?let @semanticInstructionListAST firstInstructions
  #---
    let endOfFirstInstructions = @location.here
    $while$
    <expression> ?let @semanticExpressionAST loopExpression
    let endOfLoopExpression = @location.here
    $do$
    <semantic_instruction_list> ?let @semanticInstructionListAST secondInstructions
    let endOfSecondInstructions = @location.here
    $end$
    select
    or
      $%attribute$ ?let @lstring terminator
      if terminator.string != "loop" then
        error terminator : "the terminator attribute should be '%loop'" fixit { replace "%loop" }
      end
    end
    let endOfLoopInstruction = @location.here
    outInstruction = @loopInstructionWithVariantAST.new {
      !instructionLocation
      !variantExpression
      !endOfVariantExpression
      !firstInstructions
      !endOfFirstInstructions
      !loopExpression
      !endOfLoopExpression
      !secondInstructions
      !endOfSecondInstructions
      !endOfLoopInstruction
    }
  }  

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CONTEXT
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @loopInstructionWithVariantAST enterInstructionInSemanticContext ?!@unifiedTypeMap ioTypeMap {
  [self.mVariantExpression enterExpressionInSemanticContext !?ioTypeMap]
  [self.mLoopExpression enterExpressionInSemanticContext !?ioTypeMap]
  [self.mFirstInstructions enterInstructionListInSemanticContext !?ioTypeMap]
  [self.mSecondInstructions enterInstructionListInSemanticContext !?ioTypeMap]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @loopInstructionWithVariantAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap {
#--- Variant expression
  [self.mVariantExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: @unifiedTypeMapEntry.null
    !inAnalysisContext
    !?ioVariableMap
    ?var variantExpression
  ]
#--- Check variant expression type is @uint
  checkAssignmentTypeWithImplicitGetterCall (
    !inAnalysisContext.mPredefinedTypes.mUIntType
    !variantExpression.mResultType
    !variantExpression.mLocation
    !?variantExpression
  )
#  if variantExpression.mResultType != inAnalysisContext.mPredefinedTypes.mUIntType then
#    error self.mEndOfVariantExpression
#    : "the variant expression type should be '@" + [inAnalysisContext.mPredefinedTypes.mUIntType definition].mTypeName.string + "', but it has '@" + [variantExpression.mResultType definition].mTypeName.string + "' type"
#  end
#--- Loop expression
  [self.mLoopExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: @unifiedTypeMapEntry.null
    !inAnalysisContext
    !?ioVariableMap
    ?let loopExpression
  ]
#--- Check loop expression type is @bool
  if loopExpression.mResultType != inAnalysisContext.mPredefinedTypes.mBoolType then
    error self.mEndOfLoopExpression
    : "the loop expression type should be '@" + [inAnalysisContext.mPredefinedTypes.mBoolType definition].mTypeName.string + "', but it has '@" + [loopExpression.mResultType definition].mTypeName.string + "' type"
  end
#--- Begin override for analyze loop instruction list
  [!?ioVariableMap openOverrideForRepeatBlock]
#--- First branch
  analyzeSemanticInstructionList (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inAnalysisContext
    !?ioTypeMap
    !localConstantList: {}
    !localInitializedVariableListWarnsIfNotMutated: {}
    !localInitializedVariableListNoWarnsIfNotMutated: {}
    !self.mFirstInstructions
    !self.mEndOfFirstInstructions
    !?ioVariableMap
    ?let first_instructionList
  )
#--- Second branch
  analyzeSemanticInstructionList (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inAnalysisContext
    !?ioTypeMap
    !localConstantList: {}
    !localInitializedVariableListWarnsIfNotMutated: {}
    !localInitializedVariableListNoWarnsIfNotMutated: {}
    !self.mSecondInstructions
    !self.mEndOfSecondInstructions
    !?ioVariableMap
    ?let second_instructionList
  )
#--- End override
  [!?ioVariableMap closeOverride !self.mEndOfLoopInstruction]
#--- Generate instruction
  ioInstructionListForGeneration += ! @loopInstructionWithVariantForGeneration.new {
    !self.mInstructionLocation
    !variantExpression
    !first_instructionList
    !loopExpression
    !second_instructionList}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @loopInstructionWithVariantForGeneration : @semanticInstructionForGeneration {
  public let @location mInstructionLocation
  public let @semanticExpressionForGeneration mVariantExpression
  public let @semanticInstructionListForGeneration mFirstInstructions
  public let @semanticExpressionForGeneration mLoopExpression
  public let @semanticInstructionListForGeneration mSecondInstructions
}

#······················································································································

override method @loopInstructionWithVariantForGeneration generateInstruction
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?let @bool inGenerateSyntaxDirectedTranslationString
  ?!@string ioGeneratedCode
{
#--- Variant expression
  [self.mVariantExpression generateExpression
    !?ioGeneratedCode
    !?ioInclusionSet
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    ?let @string variantCppVarName
  ]
  ioGeneratedCode += "if (" + variantCppVarName + ".isValid ()) {\n"
  [!?ioGeneratedCode incIndentation !2]
  let @string variantVar = "variant_" + [[self.mInstructionLocation locationIndex] string]
  ioGeneratedCode += "uint32_t " + variantVar + " = " + variantCppVarName + ".uintValue () ;\n"
  let @string loopVar = "loop_" + [[self.mInstructionLocation locationIndex] string]
  ioGeneratedCode += "bool " + loopVar + " = true ;\n"
                  + "while (" + loopVar + ") {\n"
  [!?ioGeneratedCode incIndentation !2]
  generateInstructionList (
    !?ioInclusionSet
    !self.mFirstInstructions
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !inGenerateSyntaxDirectedTranslationString
    !?ioGeneratedCode
  )
  [self.mLoopExpression generateExpression !?ioGeneratedCode !?ioInclusionSet !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?let loopExpressionVar]
  ioGeneratedCode += loopVar + " = " + loopExpressionVar + ".isValid () ;\n"
                   + "if (" + loopVar + ") {\n"
                   + "  " + loopVar + " = " + loopExpressionVar + ".boolValue () ;\n"
                   + "}\n"
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
  ioGeneratedCode += "if (" + loopVar + " && (0 == " + variantVar + ")) {\n"
                     "  " + loopVar + " = false ;\n"
                     "  " + compilerCppName () + "->loopRunTimeVariantError (" + [self.mInstructionLocation sourceFile] + ") ;\n"
                     "}\n"
                   + "if (" + loopVar + ") {\n"
  ioGeneratedCode += "  " + variantVar + " -- ;\n"
  generateInstructionList (
    !?ioInclusionSet
    !self.mSecondInstructions
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !inGenerateSyntaxDirectedTranslationString
    !?ioGeneratedCode
  )
  ioGeneratedCode += "}\n"
  [!?ioGeneratedCode decIndentation !2]
  ioGeneratedCode += "}\n"
  [!?ioGeneratedCode decIndentation !2]
  ioGeneratedCode += "}\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
