#
#  MIT License
#
# Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
# documentation files (the "Software"), to deal in the Software without restriction, including without limitation
# the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software,
# and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all copies or substantial portions
# of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED
# TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
# THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
# CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
# DEALINGS IN THE SOFTWARE.
#
#—————————————————————————————————————————————————————————————————————————————————————————————————
#!   AST
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @filewrapperObjectInstanciationInExpressionAST : @semanticExpressionAST {
  public let @lstring mFilewrapperName
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @filewrapperInExpressionAST : @semanticExpressionAST {
  public let @lstring mFilewrapperName
  public let @lstring mFilewrapperPath
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @filewrapperTemplateInExpressionAST : @semanticExpressionAST {
  public let @lstring mFilewrapperName
  public let @lstring mFilewrapperTemplateName
  public let @actualOutputExpressionList mExpressions
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3ExpressionSyntax {

  #·······························································································

  rule <primary_ggs3> ?!@galgasDeclarationAST ioDeclarations
                      !@semanticExpressionAST outExpression {
    $[$
    $filewrapper$
    $identifier$ ?let filewrapperName indexing filewrapperReference
    select
      outExpression = @filewrapperObjectInstanciationInExpressionAST.new {
        !filewrapperName
      }
    or
      $.$
      select
        $"string"$ ?let @lstring filePath
        outExpression = @filewrapperInExpressionAST.new {
          !filewrapperName
          !filePath
        }
      or
        $identifier$ ?let @lstring filewrapperTemplateName
        <output_expression_list_ggs3> !?ioDeclarations ?let @actualOutputExpressionList expressionList
        outExpression = @filewrapperTemplateInExpressionAST.new {
          !filewrapperName
          !filewrapperTemplateName
          !expressionList
        }
      end
    end
    $]$
  }

  #·······························································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas4ExpressionSyntax {

  #·······························································································

  rule <primary> ?!@galgasDeclarationAST ioDeclarations
                 !@semanticExpressionAST outExpression {
    $filewrapper$
    $[$
    $identifier$ ?let filewrapperName indexing filewrapperReference
    select
      outExpression = @filewrapperObjectInstanciationInExpressionAST.new {!filewrapperName}
    or
      $.$
      select
        $"string"$ ?let @lstring filePath
        outExpression = @filewrapperInExpressionAST.new {
          !filewrapperName
          !filePath
        }
      or
        $identifier$ ?let @lstring filewrapperTemplateName
        $:$
        <output_expression_list> !?ioDeclarations ?let @actualOutputExpressionList expressionList
        outExpression = @filewrapperTemplateInExpressionAST.new {
          !filewrapperName
          !filewrapperTemplateName
          !expressionList
        }
      end
    end
    $]$
  }

  #·······························································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @filewrapperObjectInstanciationInExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap unused ioTypeMap
{
}

#·····················································································································

override method @filewrapperObjectInstanciationInExpressionAST analyzeSemanticExpression
  ?usefulnessCallerEntityName: let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry unused inType
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager unused ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
  let usefulnessName = filewrapperNameForUsefulEntitiesGraph (!self.mFilewrapperName)
  [!?ioUsefulEntitiesGraph addEdge !inUsefulnessCallerEntityName !usefulnessName]
#--- Search for filewrapper name in filewrapper map
  [inAnalysisContext.mSemanticContext.mFilewrapperMap searchKey
    !self.mFilewrapperName
    ?let @lstring filewrapperPath
    ?4*
    ?let @bool isInternal
  ]
#--- Check internal
  if isInternal then
    let filewrapperDeclarationLocation = [inAnalysisContext.mSemanticContext.mFilewrapperMap locationForKey !self.mFilewrapperName.string]
    if [self.mFilewrapperName.location file] != [filewrapperDeclarationLocation file] then
      error self.mFilewrapperName : "this filewrapper is internal to '" + [filewrapperDeclarationLocation file] + "' file"
    end
  end
#---
  outExpression = @filewrapperInExpressionForGeneration.new {
    ![ioTypeMap typeMapEntryForLKey !@lstring.new {!"filewrapper" !self.mFilewrapperName.location}]
    !self.mFilewrapperName.location
    !self.mFilewrapperName.string
    !filewrapperPath.string}
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @filewrapperTemplateInExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  for (* mExpression *) in self.mExpressions do
    [mExpression enterExpressionInSemanticContext !?ioTypeMap]
  end
}

#·····················································································································

override method @filewrapperTemplateInExpressionAST analyzeSemanticExpression
  ?usefulnessCallerEntityName: let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry unused inType
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
  let usefulnessName = filewrapperTemplateNameForUsefulEntitiesGraph (!self.mFilewrapperName !self.mFilewrapperTemplateName)
  [!?ioUsefulEntitiesGraph addEdge !inUsefulnessCallerEntityName !usefulnessName]
#---
  [inAnalysisContext.mSemanticContext.mFilewrapperMap searchKey
     !self.mFilewrapperName
     ?4*
     ?let @filewrapperTemplateMap filewrapperTemplateMap
     ?let @bool isInternal
   ]
#--- Check internal
  if isInternal then
    let filewrapperDeclarationLocation = [inAnalysisContext.mSemanticContext.mFilewrapperMap locationForKey !self.mFilewrapperName.string]
    if [self.mFilewrapperName.location file] != [filewrapperDeclarationLocation file] then
      error self.mFilewrapperName : "this filewrapper is internal to '" + [filewrapperDeclarationLocation file] + "' file"
    end
  end
#---
  [filewrapperTemplateMap searchKey !self.mFilewrapperTemplateName ?let @functionSignature templateSignature ?*]
#--- Analysis arguments
  var @semanticExpressionListForGeneration effectiveParameterList
  if [self.mExpressions count] != [templateSignature count] then
    error self.mFilewrapperTemplateName
    : "calling the '" + self.mFilewrapperTemplateName + "' template of '" + self.mFilewrapperName + "' filewrapper requires "
    + [[templateSignature count] string] + " parameter(s), while this call has "
    + [[self.mExpressions count] string] + " parameter(s)"
    : effectiveParameterList
  else
    effectiveParameterList = {}
    for (mActualSelector mExpression mEndOfExpressionLocation) in self.mExpressions, (mFormalSelector mFormalArgumentType *) in templateSignature do
      if mFormalSelector.string != mActualSelector.string then
        let @string s = "!" + if mFormalSelector.string != "" then mFormalSelector.string + ":" else "" end
        error mActualSelector : "the selector should be '" + s + "'" fixit { replace s }
      end
      [mExpression analyzeSemanticExpression
        !usefulnessCallerEntityName: inUsefulnessCallerEntityName
        !?ioUsefulEntitiesGraph
        !inferenceType: mFormalArgumentType
        !inAnalysisContext
        !?ioTypeMap
        !?ioVariableMap
        ?@semanticExpressionForGeneration exp
      ]
      checkAssignmentTypeWithImplicitGetterCall (!mFormalArgumentType !exp.mResultType !mEndOfExpressionLocation !?exp)
      effectiveParameterList += !exp
    end
  end
#---
  outExpression = @filewrapperTemplateInExpressionForGeneration.new {
    !inAnalysisContext.mPredefinedTypes.mStringType
    !self.mFilewrapperName.location
    !self.mFilewrapperName.string
    !self.mFilewrapperTemplateName.string
    !effectiveParameterList}
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @filewrapperInExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap unused ioTypeMap
{
}

#·····················································································································

override method @filewrapperInExpressionAST analyzeSemanticExpression
  ?usefulnessCallerEntityName: let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry unused inType
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager unused ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
  let usefulnessName = filewrapperNameForUsefulEntitiesGraph (!self.mFilewrapperName)
  [!?ioUsefulEntitiesGraph addEdge !inUsefulnessCallerEntityName !usefulnessName]
#--- Parse filewrapper path
  var @stringlist directoryList
  let @string fileName
  if [self.mFilewrapperPath count] == 0 then
    error self.mFilewrapperPath : "path is empty" : directoryList, fileName
  elsif [self.mFilewrapperPath firstCharacterOrNul] == '/' then
    error self.mFilewrapperPath : "the path should not begin with '/'" : directoryList, fileName
  else
    directoryList = [self.mFilewrapperPath componentsSeparatedByString !"/"]
    [!?directoryList popLast ?fileName]
  end
#--- Get filewrapper features
  [inAnalysisContext.mSemanticContext.mFilewrapperMap searchKey
    !self.mFilewrapperName
    ?*
    ?*
    ?var @wrapperFileMap filewrapperFileMap
    ?var @wrapperDirectoryMap filewrapperDirectoryMap
    ?*
    ?let @bool isInternal
  ]
#--- Check internal
  if isInternal then
    let filewrapperDeclarationLocation = [inAnalysisContext.mSemanticContext.mFilewrapperMap locationForKey !self.mFilewrapperName.string]
    if [self.mFilewrapperName.location file] != [filewrapperDeclarationLocation file] then
      error self.mFilewrapperName : "this filewrapper is internal to '" + [filewrapperDeclarationLocation file] + "' file"
    end
  end
#--- Search for file
  var @string builtPath = ""
  loop ([filewrapperDirectoryMap count] + 1)
  while [directoryList count] > 0 do
    [!?directoryList popFirst ?let directoryName]
    builtPath += directoryName + "/"
    if [filewrapperDirectoryMap hasKey !directoryName] then
      [filewrapperDirectoryMap searchKey
        !@lstring.new {!directoryName !@location.here}
        ?filewrapperFileMap
        ?filewrapperDirectoryMap
        ?*
      ]
    else
      error self.mFilewrapperPath : "the '" + builtPath + "' path does not exists for the '" + self.mFilewrapperName + "' filewrapper" : filewrapperFileMap
      directoryList = @stringlist {} # For exiting loop
    end
  end
  let @uint fileIndex
  let @bool isTextFile
  if [filewrapperFileMap hasKey !fileName] then
    [filewrapperFileMap searchKey !@lstring.new {!fileName !@location.here} ?* ?isTextFile ?* ?fileIndex]
  else
    error self.mFilewrapperPath
    : "the '" + fileName + "' file does not exist in the '" + builtPath + "' directory of '" + self.mFilewrapperName + "' filewrapper"
    : fileIndex, isTextFile
  end
#---
  outExpression = @filewrapperStaticPathInExpressionForGeneration.new {
    !if isTextFile
      then inAnalysisContext.mPredefinedTypes.mStringType
      else [ioTypeMap typeMapEntryForLKey !@lstring.new {!"data" !@location.here}]
     end
    !self.mFilewrapperName.location
    !self.mFilewrapperName.string
    !fileIndex
    !isTextFile}
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @filewrapperInExpressionForGeneration : @semanticExpressionForGeneration {
  public let @string mFilewrapperName
  public let @string mFilewrapperPath
}

#·····················································································································

override method @filewrapperInExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@stringset ioInclusionSet
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression {
  ioInclusionSet += !"filewrapper-" + self.mFilewrapperName
  outCppExpression = "GALGAS_filewrapper (gWrapperDirectory_0_" + [self.mFilewrapperName identifierRepresentation] + ")"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @filewrapperTemplateInExpressionForGeneration : @semanticExpressionForGeneration {
  public let @string mFilewrapperName
  public let @string mFilewrapperTemplateName
  public let @semanticExpressionListForGeneration mActualOutputParameterList
}

#·····················································································································

override method @filewrapperTemplateInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression {
#---
  ioInclusionSet += !"filewrapper-" + self.mFilewrapperName
  var parameterList = @stringlist {}
  for (mExpression) in self.mActualOutputParameterList do
    [mExpression generateExpression
      !?ioGeneratedCode
      !?ioInclusionSet
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      ?let parameter
    ]
    parameterList += !parameter
  end
  outCppExpression = "GALGAS_string (filewrapperTemplate_" + [self.mFilewrapperName identifierRepresentation] + "_" + [self.mFilewrapperTemplateName identifierRepresentation]
                  + " (" + compilerCppName ()
  for (mValue) in parameterList do
    outCppExpression += ", " + mValue
  end
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
  outCppExpression += [self.mLocation commaSourceFile] + "))"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @filewrapperStaticPathInExpressionForGeneration : @semanticExpressionForGeneration {
  public let @string mFilewrapperName
  public let @uint mFilewrapperFileIndex
  public let @bool mIsTextFile
}

#·····················································································································

override method @filewrapperStaticPathInExpressionForGeneration generateExpression
  ?!@string unused ioGeneratedCode
  ?!@stringset ioInclusionSet
  ?!@uint unused ioTemporaryVariableIndex
  ?!@stringset unused ioUnusedVariableCppNameSet
  !@string outCppExpression {
#---
  ioInclusionSet += !"filewrapper-" + self.mFilewrapperName
  outCppExpression = if self.mIsTextFile then "GALGAS_string" else "GALGAS_data" end
  outCppExpression += " (gWrapperFileContent_" + [self.mFilewrapperFileIndex string] + "_" + [self.mFilewrapperName identifierRepresentation] + ")"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
