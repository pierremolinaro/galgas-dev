#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

  abstract refclass @abstractGrammarInstructionSyntaxDirectedTranslationResult {
  }

  #·····················································································································

  refclass @grammarInstructionSyntaxDirectedTranslationResultNone :
    @abstractGrammarInstructionSyntaxDirectedTranslationResult {
  }

  #·····················································································································

  refclass @grammarInstructionSyntaxDirectedTranslationDropResult :
    @abstractGrammarInstructionSyntaxDirectedTranslationResult {
    @location mLocation
  }

  #·····················································································································

  refclass @grammarInstructionSyntaxDirectedTranslationResultInVar :
    @abstractGrammarInstructionSyntaxDirectedTranslationResult {
    @lstring mActualParameterName
  }

  #·····················································································································

  refclass @grammarInstructionSyntaxDirectedTranslationResultInDeclaredVar :
    @abstractGrammarInstructionSyntaxDirectedTranslationResult {
    @lstring mActualParameterTypeName
    @lstring mActualParameterName
  }

  #·····················································································································

  refclass @grammarInstructionSyntaxDirectedTranslationResultInDeclaredConst :
    @abstractGrammarInstructionSyntaxDirectedTranslationResult {
    @lstring mActualParameterTypeName
    @lstring mActualParameterName
  }

  #·····················································································································

  refclass @grammarInFileInstructionAST : @semanticInstructionAST {
    @lstring mGrammarComponentName
    @lstring mLabelName
    @semanticExpressionAST mSourceExpression
    @location mEndOfSourceExpression
    @actualParameterListAST mActualParameterList
    @abstractGrammarInstructionSyntaxDirectedTranslationResult mAbstractGrammarInstructionSyntaxDirectedTranslationResult
  }

  #·····················································································································

  refclass @grammarInStringInstructionAST : @semanticInstructionAST {
    @lstring mGrammarComponentName
    @lstring mLabelName
    @semanticExpressionAST mSourceExpression
    @location mEndOfSourceExpression
    @semanticExpressionAST mNameExpression
    @location mEndOfNameExpression
    @actualParameterListAST mActualParameterList
    @abstractGrammarInstructionSyntaxDirectedTranslationResult mAbstractGrammarInstructionSyntaxDirectedTranslationResult
  }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3InstructionsSyntax {

  #·····················································································································

  rule <semantic_instruction> !@semanticInstructionAST outInstruction {
    $grammar$
    let instructionLocation = @location.here
    $identifier$ ?let @lstring grammarComponentName indexing grammarComponentReference
    @lstring labelName
    select
      labelName = @lstring.new {!"" !@location.here}
    or
      $label$
      $identifier$ ?labelName
    end
    <grammar_instruction_core> !instructionLocation !grammarComponentName !labelName ?outInstruction
  }
  
  #·····················································································································

  rule <grammar_instruction_core>
    ?let @location inInstructionLocation
    ?let @lstring inGrammarComponentName
    ?let @lstring inLabelName
    !@semanticInstructionAST outInstruction
  {
    $in$
    <expression> ?let @semanticExpressionAST mSourceExpression
    let endOfSourceExpression = @location.here
    <actual_parameter_list> ?let @actualParameterListAST actualParameterList
  #--- Get syntax directed translation result string
    @abstractGrammarInstructionSyntaxDirectedTranslationResult grammarInstructionSyntaxDirectedTranslationResult
    select
      grammarInstructionSyntaxDirectedTranslationResult = @grammarInstructionSyntaxDirectedTranslationResultNone.new
    or
      $:>$
      <syntax_directed_translation_result> ?grammarInstructionSyntaxDirectedTranslationResult
    end
    outInstruction = @grammarInFileInstructionAST.new {
      !inInstructionLocation
      !inGrammarComponentName
      !inLabelName
      !mSourceExpression
      !endOfSourceExpression
      !actualParameterList
      !grammarInstructionSyntaxDirectedTranslationResult
    }
  }

  #·····················································································································

  rule <grammar_instruction_core>
    ?let @location inInstructionLocation
    ?let @lstring inGrammarComponentName
    ?let @lstring inLabelName
    !@semanticInstructionAST outInstruction
  {
    $on$
    @semanticExpressionAST sourceExpression
    @location endOfSourceExpression
    @semanticExpressionAST nameExpression
    @location endOfNameExpression
    select
      <expression> ?sourceExpression
      endOfSourceExpression = .here
      nameExpression = @literalStringExpressionAST.new {!.here !{!""}}
      endOfNameExpression = .here
      warning .here : "obsolete syntax, use '... on (source_string, name_string) ...'"
        + " instead of '... on source_string ...' ; you can use empty string \"\" for name_string"
    or
      $($
      <expression> ?sourceExpression
      endOfSourceExpression = .here
      $,$
      <expression> ?nameExpression
      endOfNameExpression = .here
      $)$
    end
    <actual_parameter_list> ?let @actualParameterListAST actualParameterList
  #--- Get syntax directed translation result string
    @abstractGrammarInstructionSyntaxDirectedTranslationResult grammarInstructionSyntaxDirectedTranslationResult
    select
      grammarInstructionSyntaxDirectedTranslationResult = @grammarInstructionSyntaxDirectedTranslationResultNone.new
    or
      $:>$
      <syntax_directed_translation_result> ?grammarInstructionSyntaxDirectedTranslationResult
    end
    outInstruction = @grammarInStringInstructionAST.new {
      !inInstructionLocation
      !inGrammarComponentName
      !inLabelName
      !sourceExpression
      !endOfSourceExpression
      !nameExpression
      !endOfNameExpression
      !actualParameterList
      !grammarInstructionSyntaxDirectedTranslationResult
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CONTEXT
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

  override method @grammarInFileInstructionAST enterInstructionInSemanticContext
    ?!@unifiedTypeMap ioTypeMap
  {
    [self.mSourceExpression enterExpressionInSemanticContext !?ioTypeMap]
    for (mActualParameter) in self.mActualParameterList do
      [mActualParameter enterParameterInSemanticContext !?ioTypeMap]
    end
  }

  #·····················································································································

  override method @grammarInStringInstructionAST enterInstructionInSemanticContext
    ?!@unifiedTypeMap ioTypeMap
  {
    [self.mSourceExpression enterExpressionInSemanticContext !?ioTypeMap]
    for (mActualParameter) in self.mActualParameterList do
      [mActualParameter enterParameterInSemanticContext !?ioTypeMap]
    end
  }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @grammarInFileInstructionAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap
{
  let usefulnessName = grammarNameForUsefulEntitiesGraph (!self.mGrammarComponentName)
  [!?ioUsefulEntitiesGraph addEdge !inUsefulnessCallerEntityName !usefulnessName]
#--- Search grammar in grammar map
  [inAnalysisContext.mSemanticContext.mGrammarMap searchKey
    !self.mGrammarComponentName
    ?let @grammarLabelMap grammarLabelMap
    ?*
    ?let @bool hasTranslateFeature
  ]
#--- Search label
  [grammarLabelMap searchKey
    !self.mLabelName
    ?let @formalParameterSignature labelSignature
  ]
#--- Check actual parameters
  analyzeRoutineArguments (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inAnalysisContext
    !self.mLabelName
    !"label of the " + self.mGrammarComponentName + " grammar"
    !labelSignature
    !self.mActualParameterList
    !?ioVariableMap
    !?ioInstructionListForGeneration
    ?let @actualParameterListForGeneration actualParameterListForGeneration
  )
#--- Parse source expression
  [self.mSourceExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !@unifiedTypeMap-entry.null
    !inAnalysisContext
    !?ioVariableMap
    ?let @semanticExpressionForGeneration sourceExpression
  ]
#--- Check source expression type: @lstring for file, @string for source string itself
  if [sourceExpression mResultType] != inAnalysisContext.mPredefinedTypes.mLStringType then
    error self.mEndOfSourceExpression
    :"this expression is the source file path and its type should be '@lstring', but it is '@"
     + [[sourceExpression mResultType] key] + "'"
  end
#--- Check syntax directed translation result parameter
  let syntaxDirectedTranslationResultVarName = "syntaxDirectedTranslationResult_" + [self.mGrammarComponentName.location locationIndex]
  var assignementList =@stringlist {}
  [self.mAbstractGrammarInstructionSyntaxDirectedTranslationResult analyzeGrammarInstructionSDT
    !inAnalysisContext
    !hasTranslateFeature
    !syntaxDirectedTranslationResultVarName
    !?assignementList
    !?ioVariableMap
  ]
#--- Generate instructions
  ioInstructionListForGeneration += !@grammarInstructionWithSourceFileForGeneration.new {
    !self.mInstructionLocation
    ![self.mGrammarComponentName string]
    ![self.mLabelName string]
    !sourceExpression
    !actualParameterListForGeneration
    !hasTranslateFeature
    !assignementList
    !syntaxDirectedTranslationResultVarName
  }
}

#······················································································································*

override method @grammarInStringInstructionAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap
{
  let grammarUsefulnessName = grammarNameForUsefulEntitiesGraph (!self.mGrammarComponentName)
  [!?ioUsefulEntitiesGraph addEdge !inUsefulnessCallerEntityName !grammarUsefulnessName]
#--- Search grammar in grammar map
  [inAnalysisContext.mSemanticContext.mGrammarMap searchKey
    !self.mGrammarComponentName
    ?let @grammarLabelMap grammarLabelMap
    ?*
    ?let @bool hasTranslateFeature
  ]
#--- Search label
  [grammarLabelMap searchKey
    !self.mLabelName
    ?let @formalParameterSignature labelSignature
  ]
#--- Check actual parameters
  analyzeRoutineArguments (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inAnalysisContext
    !self.mLabelName
    !"label of the " + self.mGrammarComponentName + " grammar"
    !labelSignature
    !self.mActualParameterList
    !?ioVariableMap
    !?ioInstructionListForGeneration
    ?let @actualParameterListForGeneration actualParameterListForGeneration
  )
#--- Parse source expression
  [self.mSourceExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !@unifiedTypeMap-entry.null
    !inAnalysisContext
    !?ioVariableMap
    ?let @semanticExpressionForGeneration sourceExpression
  ]
#--- Check source expression type: @string
  if [sourceExpression mResultType] != inAnalysisContext.mPredefinedTypes.mStringType then
    error self.mEndOfSourceExpression
    :"this expression is the source string and its type should be '@string', but it is '@"
     + [[sourceExpression mResultType] key] + "'"
  end
#--- Parse name expression
  [self.mNameExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !@unifiedTypeMap-entry.null
    !inAnalysisContext
    !?ioVariableMap
    ?let @semanticExpressionForGeneration nameExpression
  ]
#--- Check name expression type: @string
  if [nameExpression mResultType] != inAnalysisContext.mPredefinedTypes.mStringType then
    error self.mEndOfNameExpression
    :"this expression is the name string and its type should be '@string', but it is '@"
     + [[sourceExpression mResultType] key] + "'"
  end
#--- Check syntax directed translation result parameter
  let syntaxDirectedTranslationResultVarName = "syntaxDirectedTranslationResult_" + [self.mGrammarComponentName.location locationIndex]
  var assignementList =@stringlist {}
  [self.mAbstractGrammarInstructionSyntaxDirectedTranslationResult analyzeGrammarInstructionSDT
    !inAnalysisContext
    !hasTranslateFeature
    !syntaxDirectedTranslationResultVarName
    !?assignementList
    !?ioVariableMap
  ]
#--- Generate instructions
  ioInstructionListForGeneration += !@grammarInstructionWithSourceExpressionForGeneration.new {
    !self.mInstructionLocation
    ![self.mGrammarComponentName string]
    ![self.mLabelName string]
    !sourceExpression
    !nameExpression
    !actualParameterListForGeneration
    !hasTranslateFeature
    !assignementList
    !syntaxDirectedTranslationResultVarName
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! GENERATION (source file)
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

  refclass @grammarInstructionWithSourceFileForGeneration : @semanticInstructionForGeneration {
    @location mInstructionLocation
    @string mGrammarName
    @string mLabelName
    @semanticExpressionForGeneration mSourceFileExpression
    @actualParameterListForGeneration mActualParameterList
    @bool mGrammarHasTranslateFeature
    @stringlist mAssignementList
    @string mSyntaxDirectedTranslationResultVarName
  }

  #·····················································································································

  override method @grammarInstructionWithSourceFileForGeneration generateInstruction
    ?!@stringset ioInclusionSet
    ?!@uint ioTemporaryVariableIndex
    ?!@stringset ioUnusedVariableCppNameSet
    ?let @bool unused inGenerateSyntaxDirectedTranslationString
    ?!@string ioGeneratedCode
  {
    ioInclusionSet += !"grammar-" + [self.mGrammarName fileNameRepresentation]
  #--- Source string
    [self.mSourceFileExpression generateExpression
      !?ioGeneratedCode
      !?ioInclusionSet
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      ?let @string sourceVar
    ]
  #--- Parameters
    var parameterCppNameList =@stringlist {}
    var jokerParametersToReleaseList =@stringlist {}
    var inputVariableList =@stringlist {}
    for (mActualParameter) in self.mActualParameterList do
      [mActualParameter generateActualParameter
        !?ioInclusionSet
        !?ioTemporaryVariableIndex
        !?ioGeneratedCode
        !?jokerParametersToReleaseList
        !?inputVariableList
        !?ioUnusedVariableCppNameSet
        ?let @string parameterCppName
      ]
      parameterCppNameList += !parameterCppName
      if (mActualParameter is >= @inputActualParameterForGeneration) then
        ioGeneratedCode += parameterCppName + ".drop () ;\n"
      end
    end
    if self.mGrammarHasTranslateFeature then
      ioGeneratedCode += "C_String " + self.mSyntaxDirectedTranslationResultVarName + " ;\n"
    end
    ioGeneratedCode += "cGrammar_" + [self.mGrammarName identifierRepresentation]
                    +  "::_performSourceFileParsing_" + [self.mLabelName identifierRepresentation] 
                    +  " ("
                    + compilerCppName () + ", "
                    + if self.mGrammarHasTranslateFeature then self.mSyntaxDirectedTranslationResultVarName + ", " else "" end
                    + sourceVar
    for (mValue) in parameterCppNameList do
      ioGeneratedCode += ", " + mValue
    end
    ioGeneratedCode += " " + [self.mInstructionLocation commaSourceFile] + ") ;\n"
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
    for s in self.mAssignementList do
      ioGeneratedCode += s.mValue + "\n"
    end
  }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! GENERATION (source string)
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

  refclass @grammarInstructionWithSourceExpressionForGeneration : @semanticInstructionForGeneration {
    @location mInstructionLocation
    @string mGrammarName
    @string mLabelName
    @semanticExpressionForGeneration mSourceStringExpression
    @semanticExpressionForGeneration mNameStringExpression
    @actualParameterListForGeneration mActualParameterList
    @bool mGrammarHasTranslateFeature
    @stringlist mAssignementList
    @string mSyntaxDirectedTranslationResultVarName
  }

  #·····················································································································

  override method @grammarInstructionWithSourceExpressionForGeneration generateInstruction
    ?!@stringset ioInclusionSet
    ?!@uint ioTemporaryVariableIndex
    ?!@stringset ioUnusedVariableCppNameSet
    ?let @bool unused inGenerateSyntaxDirectedTranslationString
    ?!@string ioGeneratedCode {
    ioInclusionSet += !"grammar-" + [self.mGrammarName fileNameRepresentation]
  #--- Source string
    [self.mSourceStringExpression generateExpression
      !?ioGeneratedCode
      !?ioInclusionSet
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      ?let @string sourceVar
    ]
  #--- Name string
    [self.mNameStringExpression generateExpression
      !?ioGeneratedCode
      !?ioInclusionSet
      !?ioTemporaryVariableIndex
      !?ioUnusedVariableCppNameSet
      ?let @string nameVar
    ]
  #--- Parameters
    var parameterCppNameList =@stringlist {}
    var jokerParametersToReleaseList =@stringlist {}
    var inputVariableList =@stringlist {}
    for (mActualParameter) in self.mActualParameterList do
      [mActualParameter generateActualParameter
         !?ioInclusionSet
         !?ioTemporaryVariableIndex
         !?ioGeneratedCode
         !?jokerParametersToReleaseList
         !?inputVariableList
         !?ioUnusedVariableCppNameSet
         ?let @string parameterCppName
      ]
      parameterCppNameList += !parameterCppName
      if (mActualParameter is >= @inputActualParameterForGeneration) then
        ioGeneratedCode += parameterCppName + ".drop () ;\n"
      end
    end
    if self.mGrammarHasTranslateFeature then
      ioGeneratedCode += "C_String " + self.mSyntaxDirectedTranslationResultVarName + " ;\n"
    end
    ioGeneratedCode += "cGrammar_" + [self.mGrammarName identifierRepresentation]
                    +  "::_performSourceStringParsing_" + [self.mLabelName identifierRepresentation] 
                    +  " (" + compilerCppName () + ", "
                    + if self.mGrammarHasTranslateFeature then self.mSyntaxDirectedTranslationResultVarName + ", " else "" end
                    + sourceVar
                    + ", " + nameVar
    for (mValue) in parameterCppNameList do
      ioGeneratedCode += ", " + mValue
    end
    ioGeneratedCode += " " + [self.mInstructionLocation commaSourceFile] + ") ;\n"
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
    for s in self.mAssignementList do
      ioGeneratedCode += s.mValue + "\n"
    end
  }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
