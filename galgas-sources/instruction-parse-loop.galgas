#
#  MIT License
#
# Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
# documentation files (the "Software"), to deal in the Software without restriction, including without limitation
# the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software,
# and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all copies or substantial portions
# of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED
# TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
# THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
# CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
# DEALINGS IN THE SOFTWARE.
#
#—————————————————————————————————————————————————————————————————————————————————————————————————
#! AST
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @parseLoopInstruction : @syntaxInstructionAST {
  private let @semanticExpressionAST mVariantExpression
  private let @location mEndOfVariantExpression
  private let @semanticExpressionAST mWhileExpression
  private let @location mEndOfWhileExpression
  private let @syntaxInstructionList mDoInstructionList
  private let @location mEndOfInstructionList
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3SyntaxComponentSyntax {

  #·······························································································

  rule <syntax_instruction_ggs3> ?!@galgasDeclarationAST ioDeclarations
                                 !@syntaxInstructionAST outInstruction {
    $parse$
    let instructionLocation = @location.here
    $loop$
    <expression_ggs3> !?ioDeclarations ?let @semanticExpressionAST variantExpression
    let endOfVariantExpression = @location.here
    $while$
    <expression_ggs3> !?ioDeclarations ?let @semanticExpressionAST whileExpression
    let endOfWhileExpression = @location.here
    $do$
    <syntax_instruction_list_ggs3> !?ioDeclarations ?let @syntaxInstructionList instructionList
    $end$
    let endOfInstructionList = @location.here
    outInstruction = @parseLoopInstruction {
      !instructionLocation
      !variantExpression
      !endOfVariantExpression
      !whileExpression
      !endOfWhileExpression
      !instructionList
      !endOfInstructionList
    }
  }

  #·······························································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas4SyntaxComponentSyntax {

  #·······························································································

  rule <syntax_instruction> ?!@galgasDeclarationAST ioDeclarations
                            !@syntaxInstructionAST outInstruction {
    $parse$
    let instructionLocation = @location.here
    $loop$
    <expression> !?ioDeclarations ?let @semanticExpressionAST variantExpression
    let endOfVariantExpression = @location.here
    $while$
    <expression> !?ioDeclarations ?let @semanticExpressionAST whileExpression
    let endOfWhileExpression = @location.here
    $do$
    <syntax_instruction_list> !?ioDeclarations ?let @syntaxInstructionList instructionList
    $end$
    let endOfInstructionList = @location.here
    outInstruction = @parseLoopInstruction {
      !instructionLocation
      !variantExpression
      !endOfVariantExpression
      !whileExpression
      !endOfWhileExpression
      !instructionList
      !endOfInstructionList}
  }

  #·······························································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! TRANSFORM
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @parseLoopInstruction transformInstruction
  ?!usedTerminalMap:@terminalSymbolsMapForGrammarAnalysis ioActuallyUsedTerminalSymbolMap
  ?nonTerminalSymbolMap:let @nonTerminalSymbolMapForGrammarAnalysis inNonTerminalSymbolMap
  ?!addedTerminalIndex:@uint ioAddedNonTerminalIndex
  ?!@syntaxInstructionListForGrammarAnalysis ioSyntaxInstructionList {
  transformInstructionList (
    !self.mDoInstructionList
    !?usedTerminalMap:ioActuallyUsedTerminalSymbolMap
    !nonTerminalSymbolMap:inNonTerminalSymbolMap
    !?addedTerminalIndex:ioAddedNonTerminalIndex
    !?ioSyntaxInstructionList
  )
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @parseLoopInstruction analyzeSyntaxInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?let @bool inHasTranslateFeature
  ?let @terminalMap inTerminalMap
  ?let @string inLexiqueName
  ?let @nonterminalMap inNonterminalMap
  ?let @string inComponentName
  ?let @stringset inIndexNameSet
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap
  ?!@uint ioSelectMethodCount {
#--- Variant expression
  [self.mVariantExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: @unifiedTypeMapEntry.null
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?let variant_expression
  ]
  if variant_expression.mResultType != inAnalysisContext.predefinedTypes.mUIntType then
    error self.mEndOfVariantExpression
    :"the variant expression type is '@" + [variant_expression.mResultType definition].typeName.string
    + "', but it should be '@" + [inAnalysisContext.predefinedTypes.mUIntType definition].typeName.string + "' type"
  end
#--- While expression
  [self.mWhileExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: @unifiedTypeMapEntry.null
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?let while_expression
  ]
  if while_expression.mResultType != inAnalysisContext.predefinedTypes.mBoolType then
    error self.mEndOfWhileExpression
    :"the variant expression type is '@" + [variant_expression.mResultType definition].typeName.string
    + "', but it should be '@" + [inAnalysisContext.predefinedTypes.mBoolType definition].typeName.string + "' type"
  end
#--- 'do' instructions
#--- Begin block
  [!?ioVariableMap openOverrideForRepeatBlock]
#--- 'or' blocks
  analyzeSyntaxInstructionList (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inAnalysisContext
    !?ioTypeMap
    !inHasTranslateFeature
    !inTerminalMap
    !inLexiqueName
    !inNonterminalMap
    !inComponentName
    !self.mDoInstructionList
    !self.mEndOfInstructionList
    !inIndexNameSet
    !?ioVariableMap
    !?ioSelectMethodCount
    ?let instructionList
  )
#--- End block
  [!?ioVariableMap closeOverride !self.mEndOfInstructionList]
#--- Generate instruction
  ioInstructionListForGeneration += !@parseLoopInstructionForGeneration {
    !self.mInstructionLocation
    !variant_expression
    !while_expression
    !instructionList
  }
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! GENERATION TYPES
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @parseLoopInstructionForGeneration : @syntaxInstructionForGeneration {
  private let @semanticExpressionForGeneration mVariantExpression
  private let @semanticExpressionForGeneration mWhileExpression
  private let @semanticInstructionListForGeneration mInstructionList
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX SIGNATURE
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @parseLoopInstructionForGeneration appendSyntaxSignature
  ?let @string inPosfix
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration {
  ioInstructionListForGeneration += syntaxSignatureOfInstructionList (!inPosfix !self.mInstructionList)
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @parseLoopInstructionForGeneration generateInstruction
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?let @bool inGenerateSyntaxDirectedTranslationString
  ?!@string ioGeneratedCode
{
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
#--- Variant
  [self.mVariantExpression generateExpression !?ioGeneratedCode !?ioInclusionSet !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?let variantVarCppName]
#--- Loop variable
  ioGeneratedCode += "if (" + variantVarCppName + ".isValid ()) {\n"
  [!?ioGeneratedCode incIndentation !2]
  let @string variantVar = "variant_" + [ioTemporaryVariableIndex string]
  ioTemporaryVariableIndex += 1
  ioGeneratedCode += "uint32_t " + variantVar + " = " + variantVarCppName + ".uintValue () ;\n"
#--- loop
  let @string loopVar = "loop_" + [ioTemporaryVariableIndex string]
  ioTemporaryVariableIndex += 1
  ioGeneratedCode += "bool " + loopVar + " = true ;\n"
  ioGeneratedCode += "while (" + loopVar + ") {\n"
  [!?ioGeneratedCode incIndentation !2]
  ioGeneratedCode += loopVar + " = false ;\n"
  [self.mWhileExpression generateExpression !?ioGeneratedCode !?ioInclusionSet !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?let whileVarCppName]
  ioGeneratedCode += "if (" + whileVarCppName + ".isValidAndTrue ()) {\n"
  [!?ioGeneratedCode incIndentation !2]
  ioGeneratedCode += loopVar + " = true ;\n"
  ioGeneratedCode += variantVar + " -- ; // Decrement variant\n"
  ioGeneratedCode += "const C_parsingContext parsingContext = " + compilerCppName () + "->parsingContext () ;\n"
  generateInstructionList (
    !?ioInclusionSet
    !self.mInstructionList
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !inGenerateSyntaxDirectedTranslationString
    !?ioGeneratedCode
  )
  ioGeneratedCode += compilerCppName () + "->setParsingContext (parsingContext) ;\n"
  [!?ioGeneratedCode decIndentation !2]
  ioGeneratedCode += "}\n"
  [!?ioGeneratedCode decIndentation !2]
  ioGeneratedCode += "}\n"
  [!?ioGeneratedCode decIndentation !2]
  ioGeneratedCode += "}\n"
#--- Execute 'parse' part
  ioGeneratedCode += "//--- 'parse'\n"
  generateInstructionList (
    !?ioInclusionSet
    !syntaxSignatureOfInstructionList (!"parse" !self.mInstructionList)
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !inGenerateSyntaxDirectedTranslationString
    !?ioGeneratedCode
  )
  ioGeneratedCode += compilerCppName () + "->resetTemplateString () ;\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
