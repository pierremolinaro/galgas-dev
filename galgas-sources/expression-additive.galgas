#
#  MIT License
#
# Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
# documentation files (the "Software"), to deal in the Software without restriction, including without limitation
# the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software,
# and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all copies or substantial portions
# of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED
# TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
# THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
# CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
# DEALINGS IN THE SOFTWARE.
#
#—————————————————————————————————————————————————————————————————————————————————————————————————
#!   AST
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @rightShiftExpressionAST : @semanticExpressionAST {
  private let @location mOperatorLocation
  private let @semanticExpressionAST mLeftExpression
  private let @semanticExpressionAST mRightExpression
}

#·······················································································································

refclass @leftShiftExpressionAST : @semanticExpressionAST {
  private let @location mOperatorLocation
  private let @semanticExpressionAST mLeftExpression
  private let @semanticExpressionAST mRightExpression
}

#·······················································································································

refclass @addExpressionAST : @semanticExpressionAST {
  private let @location mOperatorLocation
  private let @semanticExpressionAST mLeftExpression
  private let @semanticExpressionAST mRightExpression
}

#·······················································································································

refclass @addExpressionNoOverflowAST : @semanticExpressionAST {
  private let @location mOperatorLocation
  private let @semanticExpressionAST mLeftExpression
  private let @semanticExpressionAST mRightExpression
}

#·······················································································································

refclass @subExpressionAST : @semanticExpressionAST {
  private let @location mOperatorLocation
  private let @semanticExpressionAST mLeftExpression
  private let @semanticExpressionAST mRightExpression
}

#·······················································································································

refclass @subExpressionNoOverflowAST : @semanticExpressionAST {
  private let @location mOperatorLocation
  private let @semanticExpressionAST mLeftExpression
  private let @semanticExpressionAST mRightExpression
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3ExpressionSyntax {

  #·······························································································

  rule <simple_expression_ggs3> ?!@galgasDeclarationAST ioDeclarations
                                !@semanticExpressionAST outExpression {
    <term_ggs3> !?ioDeclarations ? outExpression
    repeat
    while
      let operatorLocation = @location.here
      $<<$
      let leftOperand = outExpression
      <term_ggs3> !?ioDeclarations ?let rightOperand
      outExpression = @leftShiftExpressionAST.new {!operatorLocation !leftOperand !rightOperand}
    while
      let operatorLocation = @location.here
      $>>$
      let leftOperand = outExpression
      <term_ggs3> !?ioDeclarations ?let rightOperand
      outExpression = @rightShiftExpressionAST.new {!operatorLocation !leftOperand !rightOperand}
    while
      let operatorLocation = @location.here
      $+$
      let leftOperand = outExpression
      <term_ggs3> !?ioDeclarations ?let rightOperand
      outExpression = @addExpressionAST.new {!operatorLocation !leftOperand !rightOperand}
    while
      let operatorLocation = @location.here
      $&+$
      let leftOperand = outExpression
      <term_ggs3> !?ioDeclarations ?let rightOperand
      outExpression = @addExpressionNoOverflowAST.new {!operatorLocation !leftOperand !rightOperand}
    while
      let operatorLocation = @location.here
      $-$
      let leftOperand = outExpression
      <term_ggs3> !?ioDeclarations ?let rightOperand
      outExpression = @subExpressionAST.new {!operatorLocation !leftOperand !rightOperand}
    while
      let operatorLocation = @location.here
      $&-$
      let leftOperand = outExpression
      <term_ggs3> !?ioDeclarations ?let rightOperand
      outExpression = @subExpressionNoOverflowAST.new {!operatorLocation !leftOperand !rightOperand}
    end
  }

  #·······························································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas4ExpressionSyntax {

  #·······························································································

  rule <simple_expression> ?!@galgasDeclarationAST ioDeclarations
                           !@semanticExpressionAST outExpression {
    <term> !?ioDeclarations ?outExpression
    repeat
    while
      let operatorLocation = @location.here
      $<<$
      let leftOperand = outExpression
      <term> !?ioDeclarations ?let rightOperand
      outExpression = @leftShiftExpressionAST.new {!operatorLocation !leftOperand !rightOperand}
    while
      let operatorLocation = @location.here
      $>>$
      let leftOperand = outExpression
      <term> !?ioDeclarations ?let rightOperand
      outExpression = @rightShiftExpressionAST.new {!operatorLocation !leftOperand !rightOperand}
    while
      let operatorLocation = @location.here
      $+$
      let leftOperand = outExpression
      <term> !?ioDeclarations ?let rightOperand
      outExpression = @addExpressionAST.new {!operatorLocation !leftOperand !rightOperand}
    while
      let operatorLocation = @location.here
      $&+$
      let leftOperand = outExpression
      <term> !?ioDeclarations ?let rightOperand
      outExpression = @addExpressionNoOverflowAST.new {!operatorLocation !leftOperand !rightOperand}
    while
      let operatorLocation = @location.here
      $-$
      let leftOperand = outExpression
      <term> !?ioDeclarations ?let rightOperand
      outExpression = @subExpressionAST.new {!operatorLocation !leftOperand !rightOperand}
    while
      let operatorLocation = @location.here
      $&-$
      let leftOperand = outExpression
      <term> !?ioDeclarations ?let rightOperand
      outExpression = @subExpressionNoOverflowAST.new {!operatorLocation !leftOperand !rightOperand}
    end
  }

  #·······························································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @rightShiftExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [self.mLeftExpression enterExpressionInSemanticContext !?ioTypeMap]
  [self.mRightExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#·····················································································································

override method @rightShiftExpressionAST analyzeSemanticExpression
  ?usefulnessCallerEntityName: let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry inInferenceType
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
#--- Left expression analysis
  [self.mLeftExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: inInferenceType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?let @semanticExpressionForGeneration leftExpression
  ]
#--- Right expression analysis
  [self.mRightExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: inAnalysisContext.predefinedTypes.mBigIntType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?let @semanticExpressionForGeneration rightExpression
  ]
#--- Check operator availability
  let @unifiedTypeMapEntry leftType = leftExpression.mResultType
  if let left = leftExpression as @literalBigIntExpressionForGeneration,
     let right = rightExpression as @literalUIntExpressionForGeneration then
     outExpression = @literalBigIntExpressionForGeneration.new {
       !inAnalysisContext.predefinedTypes.mBigIntType
       !self.mOperatorLocation
       !left.mValue >> right.mValue
     }
  elsif let left = leftExpression as @literalBigIntExpressionForGeneration,
     let right = rightExpression as @literalBigIntExpressionForGeneration then
     outExpression = @literalBigIntExpressionForGeneration.new {
       !inAnalysisContext.predefinedTypes.mBigIntType
       !self.mOperatorLocation
       !left.mValue >> [right.mValue uint]
     }
  elsif not [[leftType definition].mHandledOperatorFlags infixShiftOperator] then
    error self.mOperatorLocation
    : "left operand type is '@" + [leftType definition].typeName.string + "' and does not support a shift operator"
    :outExpression
  else
  #--- Generate expression
    outExpression = @binaryOperatorExpressionForGeneration.new {
      !leftType
      !self.mOperatorLocation
      !leftExpression
      !.rightShift
      !rightExpression
    }
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @leftShiftExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [self.mLeftExpression enterExpressionInSemanticContext !?ioTypeMap]
  [self.mRightExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#·····················································································································

override method @leftShiftExpressionAST analyzeSemanticExpression
  ?usefulnessCallerEntityName: let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry inInferenceType
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
#--- Left expression analysis
  [self.mLeftExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: inInferenceType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?let @semanticExpressionForGeneration leftExpression
  ]
#--- Right expression analysis
  [self.mRightExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: inAnalysisContext.predefinedTypes.mBigIntType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?let @semanticExpressionForGeneration rightExpression
  ]
#--- Check operator availability
  let @unifiedTypeMapEntry leftType = leftExpression.mResultType
  if let left = leftExpression as @literalBigIntExpressionForGeneration,
     let right = rightExpression as @literalUIntExpressionForGeneration then
     outExpression = @literalBigIntExpressionForGeneration.new {
       !inAnalysisContext.predefinedTypes.mBigIntType
       !self.mOperatorLocation
       !left.mValue << right.mValue
     }
  elsif let left = leftExpression as @literalBigIntExpressionForGeneration,
     let right = rightExpression as @literalBigIntExpressionForGeneration then
     outExpression = @literalBigIntExpressionForGeneration.new {
       !inAnalysisContext.predefinedTypes.mBigIntType
       !self.mOperatorLocation
       !left.mValue << [right.mValue uint]
     }
  elsif not [[leftType definition].mHandledOperatorFlags infixShiftOperator] then
    error self.mOperatorLocation
    : "left operand type is '@" + [leftType definition].typeName.string + "' and does not support a shift operator"
    :outExpression
  else
  #--- Generate expression
    outExpression = @binaryOperatorExpressionForGeneration.new {
      !leftType
      !self.mOperatorLocation
      !leftExpression
      !.leftShift
      !rightExpression
    }
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @addExpressionNoOverflowAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [self.mLeftExpression enterExpressionInSemanticContext !?ioTypeMap]
  [self.mRightExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#·····················································································································

override method @addExpressionNoOverflowAST analyzeSemanticExpression
  ?usefulnessCallerEntityName: let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry inInferenceType
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
#--- Left expression analysis
  [self.mLeftExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: inInferenceType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?var @semanticExpressionForGeneration leftExpression
  ]
#--- Right expression analysis
  [self.mRightExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: leftExpression.mResultType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?@semanticExpressionForGeneration rightExpression
  ]
#--- Check operator availability
  checkDiadicOperator (
    ![[leftExpression.mResultType definition].mHandledOperatorFlags infixAddOperatorNoOVF]
    !"&+"
    !self.mOperatorLocation
    !?leftExpression
    !?rightExpression
  )
#--- Generate expression
  if let leftBigint  = leftExpression as @literalBigIntExpressionForGeneration,
     let rightBigint = rightExpression as @literalBigIntExpressionForGeneration then
    outExpression = @literalBigIntExpressionForGeneration.new {
      !inAnalysisContext.predefinedTypes.mBigIntType
      !self.mOperatorLocation
      !leftBigint.mValue + rightBigint.mValue
    }
  else
    outExpression = @binaryOperatorExpressionForGeneration.new {
      !leftExpression.mResultType
      !self.mOperatorLocation
      !leftExpression
      !.addNoOverflow
      !rightExpression
    }
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @addExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [self.mLeftExpression enterExpressionInSemanticContext !?ioTypeMap]
  [self.mRightExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#·····················································································································

override method @addExpressionAST analyzeSemanticExpression
  ?usefulnessCallerEntityName: let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry inInferenceType
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager ioVariableMap
  !@semanticExpressionForGeneration outExpression {
#--- Left expression analysis
  [self.mLeftExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: inInferenceType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?var @semanticExpressionForGeneration leftExpression
  ]
#--- Right expression analysis
  [self.mRightExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: leftExpression.mResultType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?@semanticExpressionForGeneration rightExpression
  ]
#--- Check operator availability
  checkDiadicOperator (
    ![[leftExpression.mResultType definition].mHandledOperatorFlags infixAddOperator]
    !"+"
    !self.mOperatorLocation
    !?leftExpression
    !?rightExpression
  )
#--- Generate expression
  if let leftBigint  = leftExpression as @literalBigIntExpressionForGeneration,
     let rightBigint = rightExpression as @literalBigIntExpressionForGeneration then
    outExpression = @literalBigIntExpressionForGeneration.new {
      !inAnalysisContext.predefinedTypes.mBigIntType
      !self.mOperatorLocation
      !leftBigint.mValue + rightBigint.mValue
    }
  else
    outExpression = @binaryOperatorExpressionForGeneration.new {
      !leftExpression.mResultType
      !self.mOperatorLocation
      !leftExpression
      !.add
      !rightExpression
    }
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @subExpressionNoOverflowAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [self.mLeftExpression enterExpressionInSemanticContext !?ioTypeMap]
  [self.mRightExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#·····················································································································

override method @subExpressionNoOverflowAST analyzeSemanticExpression
  ?usefulnessCallerEntityName: let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry inInferenceType
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
#--- Left expression analysis
  [self.mLeftExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: inInferenceType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?var @semanticExpressionForGeneration leftExpression
  ]
#--- Right expression analysis
  [self.mRightExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: leftExpression.mResultType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?@semanticExpressionForGeneration rightExpression
  ]
#--- Check operator availability
  checkDiadicOperator (
    ![[leftExpression.mResultType definition].mHandledOperatorFlags infixSubOperatorNoOVF]
    !"&-"
    !self.mOperatorLocation
    !?leftExpression
    !?rightExpression
  )
#--- Generate expression
  if let leftBigint  = leftExpression as @literalBigIntExpressionForGeneration,
     let rightBigint = rightExpression as @literalBigIntExpressionForGeneration then
    outExpression = @literalBigIntExpressionForGeneration.new {
      !inAnalysisContext.predefinedTypes.mBigIntType
      !self.mOperatorLocation
      !leftBigint.mValue - rightBigint.mValue
    }
  else
    outExpression = @binaryOperatorExpressionForGeneration.new {
      !leftExpression.mResultType
      !self.mOperatorLocation
      !leftExpression
      !.subNoOverflow
      !rightExpression
    }
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @subExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [self.mLeftExpression enterExpressionInSemanticContext !?ioTypeMap]
  [self.mRightExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#·····················································································································

override method @subExpressionAST analyzeSemanticExpression
  ?usefulnessCallerEntityName: let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry inInferenceType
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager ioVariableMap
  !@semanticExpressionForGeneration outExpression {
#--- Left expression analysis
  [self.mLeftExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: inInferenceType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?var @semanticExpressionForGeneration leftExpression
  ]
#--- Right expression analysis
  [self.mRightExpression analyzeSemanticExpression
    !usefulnessCallerEntityName: inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: leftExpression.mResultType
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?@semanticExpressionForGeneration rightExpression
  ]
#--- Check operator availability
  checkDiadicOperator (
    ![[leftExpression.mResultType definition].mHandledOperatorFlags infixSubOperator]
    !"-"
    !self.mOperatorLocation
    !?leftExpression
    !?rightExpression
  )
#--- Generate expression
  if let leftBigint  = leftExpression as @literalBigIntExpressionForGeneration,
     let rightBigint = rightExpression as @literalBigIntExpressionForGeneration then
    outExpression = @literalBigIntExpressionForGeneration.new {
      !inAnalysisContext.predefinedTypes.mBigIntType
      !self.mOperatorLocation
      !leftBigint.mValue - rightBigint.mValue
    }
  else
    outExpression = @binaryOperatorExpressionForGeneration.new {
      !leftExpression.mResultType
      !self.mOperatorLocation
      !leftExpression
      !.sub
      !rightExpression
    }
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION
#—————————————————————————————————————————————————————————————————————————————————————————————————

enum @binaryOperator {
  case rightShift
  case leftShift
  case add
  case addNoOverflow
  case sub
  case subNoOverflow
  case operator_and
  case operator_or
  case operator_xor
  case multiply_operation_no_ovf
  case multiply_operation
  case divide_operation
  case divide_operation_no_ovf
  case modulo_operation
}

#·····················································································································

refclass @binaryOperatorExpressionForGeneration : @semanticExpressionForGeneration {
  public let @semanticExpressionForGeneration mLeftExpression
  public let @binaryOperator mOperator
  public let @semanticExpressionForGeneration mRightExpression
}

#·····················································································································

override method @binaryOperatorExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression {
  [self.mLeftExpression.mResultType addHeaderFileName !?ioInclusionSet]
#--- Left operand
  [self.mLeftExpression generateExpression !?ioGeneratedCode !?ioInclusionSet !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?let leftTemporaryOperand]
#--- Right operand
  [self.mRightExpression generateExpression !?ioGeneratedCode !?ioInclusionSet !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?let rightTemporaryOperand]
#--- Generate Comparison
  let @string operatorString
  let @bool hasCompilerOption
  switch self.mOperator
  case rightShift    :
    operatorString = "right_shift_operation"
    hasCompilerOption = true
  case leftShift     :
    operatorString = "left_shift_operation"
    hasCompilerOption = true
  case add           :
    operatorString = "add_operation"
    hasCompilerOption = true
  case addNoOverflow :
    operatorString = "add_operation_no_ovf"
    hasCompilerOption = true
  case sub           :
    operatorString = "substract_operation"
    hasCompilerOption = true
  case subNoOverflow :
    operatorString = "substract_operation_no_ovf"
    hasCompilerOption = true
  case operator_and :
    operatorString = "operator_and"
    hasCompilerOption = false
  case operator_or :
    operatorString = "operator_or"
    hasCompilerOption = false
  case operator_xor :
    operatorString = "operator_xor"
    hasCompilerOption = false
  case multiply_operation_no_ovf :
    operatorString = "multiply_operation_no_ovf"
    hasCompilerOption = true
  case multiply_operation :
    operatorString = "multiply_operation"
    hasCompilerOption = true
  case divide_operation :
    operatorString = "divide_operation"
    hasCompilerOption = true
  case divide_operation_no_ovf :
    operatorString = "divide_operation_no_ovf"
    hasCompilerOption = true
  case modulo_operation :
    operatorString = "modulo_operation"
    hasCompilerOption = true
  end
  outCppExpression = leftTemporaryOperand + "." + operatorString + " ("
  outCppExpression += rightTemporaryOperand
  if hasCompilerOption then
    outCppExpression += ", " + compilerCppName ()
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
  end
  outCppExpression += [self.mLocation commaSourceFile] + ")"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
