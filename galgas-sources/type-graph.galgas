#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   AST                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @graphInsertModifierList {
  public let @lstring mInsertModifierName
  public let @lstring mInsertErrorMessage
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @graphDeclarationAST : @semanticDeclarationAST {
  public let @lstring mGraphTypeName
  public let @lstring mAssociatedListTypeName
  public let @graphInsertModifierList mInsertModifierList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SYNTAX                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3DeclarationsSyntax {

  #·····················································································································

  rule <declaration_ggs3> ?!@galgasDeclarationAST ioDeclarations {
    $graph$
    $@type$ ?let @lstring graphTypeName indexing graphDefinition
    $($
    $@type$ ?let @lstring associatedListTypeName
    $)$
    ${$
    var graphInsertModifierList = @graphInsertModifierList {}
    repeat
    while
      $%insertSetter$
      $identifier$ ?let @lstring insertName
      $%errorMessage$
      $"string"$ ?let @lstring errorMessage
      graphInsertModifierList += !insertName !errorMessage
    end
    $}$
    ioDeclarations.mDeclarationList += !@graphDeclarationAST.new {
      !false # Is not predefined
      !graphTypeName
      !associatedListTypeName
      !graphInsertModifierList
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas4DeclarationsSyntax {

  #·····················································································································

  rule <declaration> ?!@galgasDeclarationAST ioDeclarations
                     ?!@stringset unused ioUserImplicitDeclaredTypeSet {
    $graph$
    $@type$ ?let @lstring graphTypeName indexing graphDefinition
    $($
    $@type$ ?let @lstring associatedListTypeName
    $)$
    ${$
    var graphInsertModifierList = @graphInsertModifierList {}
    repeat
    while
      $%insertSetter$
      $identifier$ ?let @lstring insertName
      $%errorMessage$
      $"string"$ ?let @lstring errorMessage
      graphInsertModifierList += !insertName !errorMessage
    end
    $}$
    ioDeclarations.mDeclarationList += !@graphDeclarationAST.new {
      !false # Is not predefined
      !graphTypeName
      !associatedListTypeName
      !graphInsertModifierList
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   ENTER IN GRAPH                          
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @graphDeclarationAST enterDeclarationInGraph
  ?!@semanticTypePrecedenceGraph ioSemanticTypePrecedenceGraph
  ?!@extensionMethodMapForBuildingContext unused ioExtensionMethodMapForBuildingContext
  ?!@extensionGetterMapForBuildingContext unused ioExtensionGetterMapForBuildingContext
  ?!@extensionSetterMapForBuildingContext unused ioExtensionSetterMapForBuildingContext
  ?!@semanticDeclarationListAST unused ioExtensionOverrideDefinitionList
{
  let key = @lstring.new {!"@" + self.mGraphTypeName !self.mGraphTypeName.location}
  [!?ioSemanticTypePrecedenceGraph addNode !key !self]
  [!?ioSemanticTypePrecedenceGraph addEdge !key !.new {!"@" + self.mAssociatedListTypeName !self.mAssociatedListTypeName.location}]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC CONTEXT                        
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @graphDeclarationAST enterDeclarationInSemanticContext
  ?let @extensionMethodMapForBuildingContext inExtensionMethodMapForBuildingContext
  ?let @extensionGetterMapForBuildingContext inExtensionGetterMapForBuildingContext
  ?let @extensionSetterMapForBuildingContext inExtensionSetterMapForBuildingContext
  ?!@unifiedTypeMap ioTypeMap
  ?!@semanticContext ioSemanticContext {
#--- string type Entry
  [!?ioTypeMap makeEntryFromString
    !"string"
    ?let @unifiedTypeMapEntry stringTypeEntry
  ]
#--- lstring type Entry
  [!?ioTypeMap makeEntryFromString
    !"lstring"
    ?let @unifiedTypeMapEntry lstringTypeEntry
  ]
#--- Graph type Entry
  [!?ioTypeMap makeEntry
    !self.mGraphTypeName
    ?let @unifiedTypeMapEntry graphTypeEntry
  ]
#--- Associated list type Entry
  [!?ioTypeMap makeEntry
    !self.mAssociatedListTypeName
    ?let @unifiedTypeMapEntry associatedListTypeEntry
  ]
#--- @lstringlist type Entry
  [!?ioTypeMap makeEntryFromString
    !"lstringlist"
    ?let @unifiedTypeMapEntry lstringListTypeEntry
  ]
#---
  commonGetterMapForAllTypes (!?ioTypeMap ?var @getterMap getterMap )
  var @constructorMap constructorMap = {}
  var @setterMap setterMap = {}
  var @instanceMethodMap instanceMethodMap = {}
#--- Methods 'topologicalSort', 'depthFirstTopologicalSort'
  var formalParameterList = @formalParameterSignature {}
  formalParameterList += 
    !["" nowhere]
    !associatedListTypeEntry
    !@formalArgumentPassingModeAST.argumentOut
    !"outSortedInformationList"
  formalParameterList +=
    !["" nowhere]
    !lstringListTypeEntry
    !@formalArgumentPassingModeAST.argumentOut
    !"outSortedLKeyList"
  formalParameterList +=
    !["" nowhere]
    !associatedListTypeEntry
    !@formalArgumentPassingModeAST.argumentOut
    !"outUnsortedInformationList"
  formalParameterList +=
    !["" nowhere]
    !lstringListTypeEntry
    !@formalArgumentPassingModeAST.argumentOut 
    !"outUnsortedLKeyList"
  [!?instanceMethodMap insertKey
    !@lstring.new {!"topologicalSort" !@location.here}
    !@methodKind.definedAsMember
    !formalParameterList
    !@location.here
    !true # Has Compiler Argument
    !@methodQualifier.isBasic
    !"" # No error message
  ]
  [!?instanceMethodMap insertKey
    !@lstring.new {!"depthFirstTopologicalSort" !@location.here}
    !@methodKind.definedAsMember
    !formalParameterList
    !@location.here
    !true # Has Compiler Argument
    !@methodQualifier.isBasic
    !"" # No error message
  ]
#  drop formalParameterList
#--- Methods 'nodesWithNoSuccessor', 'nodesWithNoPredecessor'
  formalParameterList = {}
  formalParameterList +=
    !["" nowhere]
    !associatedListTypeEntry
    !@formalArgumentPassingModeAST.argumentOut
    !"outInformationList"
  formalParameterList +=
    !["" nowhere]
    !lstringListTypeEntry
    !@formalArgumentPassingModeAST.argumentOut
    !"outLKeyList"
  [!?instanceMethodMap insertKey
    !@lstring.new {!"circularities" !@location.here}
    !@methodKind.definedAsMember
    !formalParameterList
    !@location.here
    !false # No Compiler Argument
    !@methodQualifier.isBasic
    !"" # No error message
  ]
  [!?instanceMethodMap insertKey
    !@lstring.new {!"nodesWithNoSuccessor" !@location.here}
    !@methodKind.definedAsMember
    !formalParameterList
    !@location.here
    !false # No Compiler Argument
    !@methodQualifier.isBasic
    !"" # No error message
  ]
  [!?instanceMethodMap insertKey
    !@lstring.new {!"nodesWithNoPredecessor" !@location.here}
    !@methodKind.definedAsMember
    !formalParameterList
    !@location.here
    !false # No Compiler Argument
    !@methodQualifier.isBasic
    !"" # No error message
  ]
#  drop formalParameterList
#--- Getter 'nodeList'
  enterBaseFinalGetterWithoutArgument (
    !?getterMap
    !?ioTypeMap
    !getterName: "nodeList"
    !obsoleteName: ""
    !returnedTypeName: self.mAssociatedListTypeName
    !hasCompilerArgument: false # No compiler argument
  )
#--- Getter 'reversedGraph'
  enterBaseFinalGetterWithoutArgument (
    !?getterMap
    !?ioTypeMap
    !getterName:"reversedGraph"
    !obsoleteName: ""
    !returnedTypeName: self.mGraphTypeName.string
    !hasCompilerArgument:false # No compiler argument
  )
#--- Getter 'undefinedNodeCount'
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioTypeMap
    !getterName:"undefinedNodeCount"
    !obsoleteName: ""
    !returnedTypeName:"uint"
    !hasCompilerArgument:false # No compiler argument
  )
#--- Getter 'undefinedNodeKeyList'
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioTypeMap
    !getterName:"undefinedNodeKeyList"
    !obsoleteName: ""
    !returnedTypeName:"stringlist"
    !hasCompilerArgument:false # No compiler argument
  )
#--- Getter 'undefinedNodeReferenceList'
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioTypeMap
    !getterName:"undefinedNodeReferenceList"
    !obsoleteName: ""
    !returnedTypeName:"lstringlist"
    !hasCompilerArgument:false # No compiler argument
  )
#--- Getter 'graphviz'
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioTypeMap
    !getterName:"graphviz"
    !obsoleteName: ""
    !returnedTypeName:"string"
    !hasCompilerArgument:false # No compiler argument
  )
#--- Getter 'isNodeDefined'
  enterInheritedGetterWithArgument (
    !?getterMap
    !?ioTypeMap
    !getterName:"isNodeDefined"
    !argumentTypeName:"string"
    !argumentName:"inKey"
    !returnedTypeName:"bool"
    !hasCompilerArgument:false # No compiler argument
  )
#--- Getter 'locationForKey'
  enterInheritedGetterWithArgument (
    !?getterMap
    !?ioTypeMap
    !getterName:"locationForKey"
    !argumentTypeName:"string"
    !argumentName:"inKey"
    !returnedTypeName:"location"
    !hasCompilerArgument:true # Compiler argument
  )
#--- Getter 'keyList'
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioTypeMap
    !getterName:"keyList"
    !obsoleteName: ""
    !returnedTypeName:"stringlist"
    !hasCompilerArgument:false # No compiler argument
  )
#--- Getter 'lkeyList'
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioTypeMap
    !getterName:"lkeyList"
    !obsoleteName: ""
    !returnedTypeName:"lstringlist"
    !hasCompilerArgument:false # No compiler argument
  )
#--- Getter 'egdes'
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioTypeMap
    !getterName:"edges"
    !obsoleteName: ""
    !returnedTypeName:"2stringlist"
    !hasCompilerArgument:false # No compiler argument
  )
#--- Getter 'subgraphFromNodes'
  enterBaseGetterWith2Arguments (
    !?getterMap
    !?ioTypeMap
    !getterName: "subgraphFromNodes"
    !arg1Selector: ""
    !arg1TypeName: "lstringlist"
    !arg1Name: "inStartNodeLStringList"
    !arg2Selector: "" 
    !arg2TypeName: "stringset"
    !arg2Name: "inNodesToExclude"
    !returnedTypeName: self.mGraphTypeName.string
    !hasCompilerArgument:true # has compiler argument
  )
#--- Getter 'accessibleNodesFrom'
  enterBaseGetterWith2Arguments (
    !?getterMap
    !?ioTypeMap
    !getterName: "accessibleNodesFrom"
    !arg1Selector: "" !arg1TypeName: "lstringlist" !arg1Name: "inStartNodeStringList"
    !arg2Selector: "" !arg2TypeName: "stringset" !arg2Name: "inNodesToExclude"
    !returnedTypeName:"lstringlist"
    !hasCompilerArgument:true # has compiler argument
  )
#--- Constructor 'emptyGraph'
  [!?constructorMap insertKey
    !@lstring.new {!"emptyGraph" !@location.here}
    !{}
    !false # No compiler argument
    !graphTypeEntry
  ]
#--- setter 'enterNode'
  formalParameterList = {}
  formalParameterList +=
    !["" nowhere]
    !lstringTypeEntry
    !@formalArgumentPassingModeAST.argumentIn
    !"inLKey"
  for (* mFormalArgumentType mFormalArgumentName) in [associatedListTypeEntry definition].mAddAssignOperatorArguments do
    formalParameterList +=
      !["" nowhere]
      !mFormalArgumentType
      !@formalArgumentPassingModeAST.argumentIn
      !mFormalArgumentName
  end
  for (mInsertModifierName *) in self.mInsertModifierList do
    [!?setterMap insertOrReplace
      !mInsertModifierName
      !@methodKind.definedAsMember
      !formalParameterList
      !true # has compiler argument
      !@methodQualifier.isBasic
      !"" # No error message
    ]
  end
#  drop formalParameterList
#--- setter 'removeEdgeToDominators'
  [!?setterMap insertKey
    !@lstring.new {!"removeEdgesToDominators" !@location.here}
    !@methodKind.definedAsMember
    !{}
    !false # No compiler argument
    !@methodQualifier. isInherited
    !"" # No error message
  ]
#--- setter 'removeEdgesToNode'
  formalParameterList = @formalParameterSignature {}
  formalParameterList += !["" nowhere] !stringTypeEntry !@formalArgumentPassingModeAST.argumentIn !"inNodeName"
  [!?setterMap insertKey
    !@lstring.new {!"removeEdgesToNode" !@location.here}
    !@methodKind.definedAsMember
    !formalParameterList
    !true # Compiler argument
    !@methodQualifier. isInherited
    !"" # No error message
  ]
#  drop formalParameterList
#--- setter 'addEdge'
  formalParameterList = @formalParameterSignature {}
  formalParameterList += !["" nowhere] !lstringTypeEntry !@formalArgumentPassingModeAST.argumentIn !"inSourceNodeLKey"
  formalParameterList += !["" nowhere] !lstringTypeEntry !@formalArgumentPassingModeAST.argumentIn !"inTargetNodeLKey"
  [!?setterMap insertKey
    !@lstring.new {!"addEdge" !@location.here}
    !@methodKind.definedAsMember
    !formalParameterList
    !false # No compiler argument
    !@methodQualifier. isInherited
    !"" # No error message
  ]
#  drop formalParameterList
#--- setter 'noteNode'
  formalParameterList = @formalParameterSignature {}
  formalParameterList += !["" nowhere] !lstringTypeEntry !@formalArgumentPassingModeAST.argumentIn !"inNodeLKey"
  [!?setterMap insertKey
    !@lstring.new {!"noteNode" !@location.here}
    !@methodKind.definedAsMember
    !formalParameterList
    !false # No compiler argument
    !@methodQualifier. isInherited
    !"" # No error message
  ]
#  drop formalParameterList
#--- Add Categories
  addExtensions (
    !inExtensionMethodMapForBuildingContext
    !inExtensionGetterMapForBuildingContext
    !inExtensionSetterMapForBuildingContext
    !?ioSemanticContext
    !?ioTypeMap
    !self.mGraphTypeName
    !isClass: false
    !?getterMap
    !?setterMap
    !?instanceMethodMap
    !acceptSetters: true
  )
#--- Enter type in type map
  let typeDefinition = @unifiedTypeDefinition.new {
    !self.mGraphTypeName
    !self.mIsPredefined
    !true # Concrete Type
    !@unifiedTypeMapEntry.null
    !@typeKindEnum. graphType
    !false # Does not support collection value
    !@typedPropertyList {}
    !{}
    !@typedPropertyList {}
    !constructorMap
    !getterMap
    !setterMap
    !instanceMethodMap
    !@classMethodMap {}
    !@optionalMethodMap {}
    !@enumerationDescriptorList {}
    !.doNotGenererateObjectCompare
    !@functionSignature {}
    !{}
    !{}
    !@mapSearchMethodListAST {}
    !@mapSearchMethodListAST {}
    !false # Do not generate header in separate file
    !.null # Type for enumerated element for "for" instruction
    !"emptyGraph" # Default constructor
    !"graph-" + [self.mGraphTypeName fileNameRepresentation]
    !@headerKind.oneHeader
  }
  [!?ioTypeMap insertType !typeDefinition.mTypeName !typeDefinition]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC ANALYSIS                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @graphDeclarationAST semanticAnalysis
  ?!@lstringlist unused ioUsefulnessRootEntities 
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @string unused inProductDirectory
  ?let @semanticContext unused inSemanticContext
                                ?!@unifiedTypeMap ioTypeMap
  ?let @predefinedTypes unused inPredefinedTypes
  ?!@semanticDeclarationListForGeneration ioSemanticDeclarationListForGeneration
{
#--- Useful entities graph
  let nameForUsefulness = typeNameForUsefulEntitiesGraph (!self.mGraphTypeName)
  [!?ioUsefulEntitiesGraph addNode !nameForUsefulness !nameForUsefulness]
  let associatedTypeNameForUsefulness = typeNameForUsefulEntitiesGraph (!self.mAssociatedListTypeName)
  [!?ioUsefulEntitiesGraph addEdge !nameForUsefulness !associatedTypeNameForUsefulness]
#---
  let graphTypeEntry = [ioTypeMap searchKey !self.mGraphTypeName]
  let associatedListTypeEntry = [ioTypeMap searchKey !self.mAssociatedListTypeName]
  let associatedListElementTypeEntry = [ioTypeMap searchKey
    !@lstring.new {!self.mAssociatedListTypeName.string +elementTypeNameSuffix () !self.mAssociatedListTypeName.location}
  ]
  if [associatedListTypeEntry definition].mTypeKindEnum != @typeKindEnum. listType then
    error self.mAssociatedListTypeName: "the '@" + self.mAssociatedListTypeName + "' type should be a list"
  end
#--- Analyze insert methods
  let @stringset reservedModifierNames = {!"noteNode", !"addArc"}
  for (mInsertModifierName mInsertErrorMessage) in self.mInsertModifierList do
    if [reservedModifierNames hasKey !mInsertModifierName.string] then
      error mInsertModifierName : "the '" + mInsertModifierName + "' setter name is reserved for a graph type"
    end
    check_K_L_escapeCharacters (!mInsertErrorMessage )
  end
#---
  ioSemanticDeclarationListForGeneration +=
    !"graph " + self.mGraphTypeName
    !@graphDeclarationForGeneration.new {
      !graphTypeEntry
      !associatedListTypeEntry
      !associatedListElementTypeEntry
      !self.mInsertModifierList
    }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION                         
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @graphDeclarationForGeneration : @semanticTypeForGeneration {
  public let @unifiedTypeMapEntry mAssociatedListTypeEntry
  public let @unifiedTypeMapEntry mAssociatedListElementTypeEntry
  public let @graphInsertModifierList mInsertModifierList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @graphDeclarationForGeneration appendDeclaration1
  ?!@stringset unused ioInclusionSet
  !@string outHeader {
  let selfTypedefinition = [self.mSelfTypeEntry definition]
  outHeader = [filewrapper typeGenerationTemplate.graphTypeHeader1
    !selfTypedefinition.mTypeName.string
    ![self.mSelfTypeEntry identifierRepresentation]
  ]
  outHeader += [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    !selfTypedefinition.mTypeName.string
    ![self.mSelfTypeEntry identifierRepresentation]
    !selfTypedefinition.mIsConcrete
    !selfTypedefinition.mConstructorMap
    !selfTypedefinition.mGetterMap
    !selfTypedefinition.mSetterMap
    !selfTypedefinition.mInstanceMethodMap
    !selfTypedefinition.mClassMethodMap
    !selfTypedefinition.mOptionalMethodMap
    !selfTypedefinition.mEnumerationDescriptor
    !selfTypedefinition.mHandledOperatorFlags
    !selfTypedefinition.mAddAssignOperatorArguments
    !selfTypedefinition.mTypeForEnumeratedElement
  ]
 }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @graphDeclarationForGeneration appendSpecificImplementation
  ?let @unifiedTypeMap unused inTypeMap
  ?!@stringset ioInclusionSet
  !@string outImplementation {
  [self.mSelfTypeEntry addHeaderFileName !?ioInclusionSet]
  [self.mAssociatedListTypeEntry addHeaderFileName !?ioInclusionSet]
  [self.mAssociatedListElementTypeEntry addHeaderFileName !?ioInclusionSet]
  outImplementation = [filewrapper typeGenerationTemplate.graphTypeSpecificImplementation
    !self.mSelfTypeEntry
    ![self.mSelfTypeEntry identifierRepresentation]
    !self.mAssociatedListTypeEntry
    !self.mInsertModifierList
  ]
 }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

