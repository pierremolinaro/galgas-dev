#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!   AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @bangExpressionAST : @semanticExpressionAST {
  public let @location mOperatorLocation
  public let @semanticExpressionAST mExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3ExpressionSyntax {

  #·····················································································································
  
  rule <primary_ggs3> !@semanticExpressionAST outExpression {
    let operatorLocation = @location.here
    <primary_ggs3> ? outExpression
#    $°$
    $.$
    $bang$
    outExpression = @bangExpressionAST.new {!operatorLocation !outExpression}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas4ExpressionSyntax {

  #·····················································································································
  
  rule <primary> !@semanticExpressionAST outExpression {
    let operatorLocation = @location.here
    <primary> ? outExpression
#    $°$
    $.$
    $bang$
    outExpression = @bangExpressionAST.new {!operatorLocation !outExpression}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @bangExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [self.mExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#·····················································································································

override method @bangExpressionAST analyzeSemanticExpression
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?inferenceType: let @unifiedTypeMapEntry unused inType
  ?let @analysisContext inAnalysisContext
                                ?!@unifiedTypeMap ioTypeMap
  ?!@localVarManager ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
#--- Expression analysis
  [self.mExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inferenceType: @unifiedTypeMapEntry.null
    !inAnalysisContext
    !?ioTypeMap
    !?ioVariableMap
    ?let @semanticExpressionForGeneration expression
  ]
#--- Check operator availability
  let @unifiedTypeMapEntry type = expression.mResultType
  if let [[type definition].mTypeKindEnum weakReferenceType ?referenceType: let classIndex] then
    outExpression = @bangExpressionForGeneration.new {
      !classIndex
      !self.mOperatorLocation
      !expression
      ![type definition].mTypeName.string
    }
  else
    error self.mOperatorLocation
    : "operand type is '@" + [type definition].mTypeName.string + "' and does not support the '°' operator"
    : outExpression
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @bangExpressionForGeneration : @semanticExpressionForGeneration {
  public let @semanticExpressionForGeneration mExpression
  public let @string mReceiverTypeName
}

#·····················································································································

override method @bangExpressionForGeneration generateExpression ?!@string ioGeneratedCode
                                                                ?!@stringset ioInclusionSet
                                                                ?!@uint ioTemporaryVariableIndex
                                                                ?!@stringset ioUnusedVariableCppNameSet
                                                                !@string outCppExpression {
  [self.mExpression.mResultType addHeaderFileName !?ioInclusionSet]
#--- Operand
  [self.mExpression generateExpression
    !?ioGeneratedCode
    !?ioInclusionSet
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    ?let operand
  ]
#--- Generate
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
  outCppExpression = operand + ".bang_" + [self.mReceiverTypeName identifierRepresentation]
  outCppExpression += " (" + compilerCppName () + [self.mLocation commaSourceFile] + ")"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
