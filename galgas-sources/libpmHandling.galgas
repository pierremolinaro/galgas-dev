#  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public
#  License as published by the Free Software Foundation.
#                                           
#  This program is distributed in the hope it will be useful, but WITHOUT ANY WARRANTY; without even the implied
#  warranty of MERCHANDIBILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more
#  details.                                 
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————

filewrapper libpmFileWrapper in "../libpm" {
 "h", "cpp", "c", "m", "mak", "py" # Text files
}{
 "tiff", "png", "xib", "gif" # Binary files
}{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc updateLIBPMatPath ?let @string inLIBPMPath {
#  message "LIBPM path: '" . inLIBPMPath . "'\n" ;
  let @filewrapper fw = [filewrapper libpmFileWrapper]
#--- Create directories
  for (path) in [fw allDirectoryPathes] do
    let @string actualDirectory = inLIBPMPath + path
    [actualDirectory makeDirectory]
  end
#--- Update text files
  var @stringset allNeededFileSet = {}
  for (path) in [fw allTextFilePathes] do
    let contents = [fw textFileContentsAtPath !path]
    let actualFilePath = inLIBPMPath + path
    if [actualFilePath pathExtension] == "py" then
      [contents writeToExecutableFileWhenDifferentContents !actualFilePath ?*]
    else
      [contents writeToFileWhenDifferentContents !actualFilePath ?*]
    end
    allNeededFileSet += !path
  end
#--- Update binary files
  for (mValue) in [fw allBinaryFilePathes] do
    let contents = [fw binaryFileContentsAtPath !mValue]
    let actualFilePath = inLIBPMPath + mValue
    [contents writeToFileWhenDifferentContents !actualFilePath ?*]
    allNeededFileSet += !mValue
  end
#--- Remove useless files
  var @stringset allActualFileSet = {}
  for (mValue) in [inLIBPMPath regularFiles !true] do
    allActualFileSet += !"/" + mValue
  end
  let uselessFileSet = allActualFileSet - allNeededFileSet
  for (key) in uselessFileSet do
    [@string deleteFile !inLIBPMPath + key]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————

