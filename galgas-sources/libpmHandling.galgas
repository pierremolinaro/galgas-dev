#  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public   *
#  License as published by the Free Software Foundation.                                                               *
#                                                                                                                      *
#  This program is distributed in the hope it will be useful, but WITHOUT ANY WARRANTY; without even the implied       *
#  warranty of MERCHANDIBILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more        *
#  details.                                                                                                            *
#----------------------------------------------------------------------------------------------------------------------*

filewrapper libpmFileWrapper in "../libpm" {
 "h", "cpp", "c", "m", "mak", "pch", "py"
}{
 "tiff", "png", "xib", "gif"
}{
}

#----------------------------------------------------------------------------------------------------------------------*

proc updateLIBPMatPath ?let @string inLIBPMPath {
#  message "LIBPM path: '" . inLIBPMPath . "'\n" ;
  let @filewrapper fw = [filewrapper libpmFileWrapper]
#--- Create directories
  for () in [fw allDirectoryPathes] do
    let @string actualDirectory = inLIBPMPath + mValue
    [actualDirectory makeDirectory]
  end
#--- Update text files
  var allNeededFileSet =@stringset.emptySet{}
  for () in [fw allTextFilePathes] do
    let @string contents = [fw textFileContentsAtPath !mValue]
    let @string actualFilePath = inLIBPMPath + mValue
    if [actualFilePath pathExtension] == "py" then
      [contents writeToExecutableFileWhenDifferentContents !actualFilePath ?*]
    else
      [contents writeToFileWhenDifferentContents !actualFilePath ?*]
    end
    allNeededFileSet += !mValue
  end
#--- Update binary files
  for () in [fw allBinaryFilePathes] do
    let @data contents = [fw binaryFileContentsAtPath !mValue]
    let @string actualFilePath = inLIBPMPath + mValue
    [contents writeToFileWhenDifferentContents !actualFilePath ?*]
    allNeededFileSet += !mValue
  end
#--- Remove useless files
  var allActualFileSet =@stringset.emptySet{}
  for () in [inLIBPMPath regularFiles !true] do
    allActualFileSet += !"/" + mValue
  end
  let uselessFileSet = allActualFileSet - allNeededFileSet
  for () in uselessFileSet do
    [@string deleteFile !inLIBPMPath + key]
  end
}

#----------------------------------------------------------------------------------------------------------------------*

