#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#                                           
#  Galgas 3 'project' syntax                
#                                           
#  Copyright (C) 2014, ..., 2014 Pierre Molinaro.
#                                           
#  e-mail : pierre@pcmolinaro.name          
#                                           
#  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public
#  License as published by the Free Software Foundation.
#                                           
#  This program is distributed in the hope it will be useful, but WITHOUT ANY WARRANTY; without even the implied
#  warranty of MERCHANDIBILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more
#  details.                                 
#                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @galgas3QualifiedFeatureList {
  public let @lstring mFeatureName
  public let @lstring mFeatureValue
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct @galgas3ProjectComponentAST {
  public let @lstringlist mProjectSourceList
  public let @lbigint mMajorVersion
  public let @lbigint mMinorVersion
  public let @lbigint mRevisionVersion
  public let @lstringlist mGenerationFeatureList
  public let @galgas3QualifiedFeatureList mQualifiedFeatureList
  public let @lstring mTargetName
  public let @location mEndOfSourceFile
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

indexing grammar galgas3ProjectGrammar "LL1" {
  syntax galgas3ProjectSyntax

  <project_component_start_symbol> !@galgas3ProjectComponentAST outComponent

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax galgas3ProjectSyntax (galgasScanner) {

rule <project_header> !@lbigint majorVersion !@lbigint minorVersion !@lbigint revisionVersion !@lstring targetName {
  $project$
  $($
  $literalInt$ ?majorVersion
  $:$
  $literalInt$ ?minorVersion
  $:$
  $literalInt$ ?revisionVersion
  $)$
  $->$
  $"string"$ ?targetName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

rule <project_component_start_symbol> !@galgas3ProjectComponentAST outProjectComponentAST {
  <project_header>
    ?let @lbigint majorVersion
    ?let @lbigint minorVersion
    ?let @lbigint revisionVersion
    ?let @lstring targetName
  ${$
  var @lstringlist projectSourceList = {}
  var @lstringlist generateFeatureList = {}
  var @galgas3QualifiedFeatureList qualifiedFeatureList = {}
  repeat
  while
    $%attribute$ ?let @lstring featureName
    select
      $:$
      $"string"$ ?let @lstring featureValue
      qualifiedFeatureList += !featureName !featureValue
    or
      generateFeatureList += !featureName
    end
  while
    $"string"$ ?let @lstring filePath
    projectSourceList += !filePath
  end
  $}$
  outProjectComponentAST = @galgas3ProjectComponentAST.new {
    !projectSourceList
    !majorVersion
    !minorVersion
    !revisionVersion
    !generateFeatureList
    !qualifiedFeatureList
    !targetName
    !@location.here
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

}