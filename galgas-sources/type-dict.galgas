#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   AST                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @dictDeclarationAST : @semanticDeclarationAST {
  public var @lstring mDictTypeName
  public var @lstring mKeyTypeName
  public var @lstringlist mAttributeList
  public var @propertyInCollectionListAST mPropertyList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @dictDeclarationAST keyRepresentation ->@string outString {
  outString = "dict @" + self.mDictTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SYNTAX                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3DeclarationsSyntax {

  #·····················································································································
  
  rule <declaration> ?!@galgas3DeclarationAST ioDeclarations {
    $dict$
  #--- Dict type name
    $@type$ ?let mDictTypeName indexing mapDefinition, structDefinition "-element"
  #--- Dict key type name
    $:$
    $@type$ ?let keyTypeName
  #--- Map attributes
    var @lstringlist mapAttributeList = {}
    repeat
    while
      $%attribute$ ?let attribute
      mapAttributeList += !attribute
    end
  #--- Map properties
    ${$
    var propertyList =@propertyInCollectionListAST {}
    repeat
    while
      <property_declaration> !? propertyList
    while
      $;$
    end
    $}$
    ioDeclarations.mDeclarationList += !@dictDeclarationAST.new {
      !false # Is not predefined
      !mDictTypeName
      !keyTypeName
      !mapAttributeList
      !propertyList
    }
  }
  
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   ASSOCIATED ELEMENT                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @dictDeclarationAST addAssociatedElement ?!@semanticDeclarationListAST ioSemanticDeclarationList {
  var @propertyInCollectionListAST structAttributeList = {}
  structAttributeList += !isConstant: false !self.mKeyTypeName !["key" nowhere] !true !{}
  for (* mPropertyTypeName mPropertyName * *) in self.mPropertyList do
    structAttributeList += !isConstant: false !mPropertyTypeName !mPropertyName !true !{}
  end
  ioSemanticDeclarationList += !@structDeclarationAST.new {
    !self.mIsPredefined
    !.new {!self.mDictTypeName.string + elementTypeNameSuffix () !self.mDictTypeName.location}
    !structAttributeList
    !""
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   ENTER IN GRAPH                          
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @dictDeclarationAST enterDeclarationInGraph
  ?!@semanticTypePrecedenceGraph ioSemanticTypePrecedenceGraph
  ?!@extensionMethodMapForBuildingContext unused ioExtensionMethodMapForBuildingContext
  ?!@extensionGetterMapForBuildingContext unused ioExtensionGetterMapForBuildingContext
  ?!@extensionSetterMapForBuildingContext unused ioExtensionSetterMapForBuildingContext
  ?!@semanticDeclarationListAST unused ioExtensionOverrideDefinitionList
 {
  let dictionary = @lstring.new {!"@" + self.mDictTypeName !self.mDictTypeName.location}
  [!?ioSemanticTypePrecedenceGraph addNode !dictionary !self]
  [!?ioSemanticTypePrecedenceGraph noteNode !@lstring.new {!"@" + self.mKeyTypeName !self.mKeyTypeName}]
  for (* mPropertyTypeName * * *) in self.mPropertyList do
    [!?ioSemanticTypePrecedenceGraph noteNode !@lstring.new {!"@" + mPropertyTypeName !mPropertyTypeName}]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   ENTER IN SEMANTIC CONTEXT               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @dictDeclarationAST enterInSemanticContext
  ?let @extensionMethodMapForBuildingContext inExtensionMethodMapForBuildingContext
  ?let @extensionGetterMapForBuildingContext inExtensionGetterMapForBuildingContext
  ?let @extensionSetterMapForBuildingContext inExtensionSetterMapForBuildingContext
  ?!@semanticContext ioSemanticContext {
#--- key type index
  [@unifiedTypeMap-entry makeEntryFromString !?ioSemanticContext.mTypeMap !self.mKeyTypeName ?let keyTypeIndex]
#--- Enumeration description
  var enumerationDescriptor = @enumerationDescriptorList.listWithValue {!keyTypeIndex !"key"}
#--- Attributes
  var @bool usesSelectorsInInsertAndSearch = false
  for (s) in self.mAttributeList do
    if s.string == "selectors" then
      if usesSelectorsInInsertAndSearch then
        error s : "duplicated attribute"
      end
      usesSelectorsInInsertAndSearch = true
    else
      error s : "unknown attribute"
    end
  end
#---
  var @constructorMap constructorMap = {}
  commonGetterMapForAllTypes ( !?ioSemanticContext.mTypeMap ?var @getterMap getterMap )
  var @setterMap setterMap = {}
  var @instanceMethodMap instanceMethodMap = {}
#--- Constructors
  enterConstructorWithoutArgument (
    !?constructorMap
    !?ioSemanticContext.mTypeMap
    !name:"emptyDict"
    !resultTypeName:self.mDictTypeName.string
    !hasLexiqueArg:false
  )
#--- Getters
  enterBaseGetterWithArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !getterName: "hasKey" !self.mKeyTypeName.string !"inKey"
    !returnedTypeName:"bool"
    !hasCompilerArgument:false
  )
  enterInheritedGetterWithoutArgument (
    !?getterMap
    !?ioSemanticContext.mTypeMap
    !getterName:"count"
    !returnedTypeName:"uint"
    !hasCompilerArgument:false
  )
#--- Handle properties
  var @formalParameterSignature insertMethodFormalArgumentList = {}
  var @typedPropertyList typedPropertyList = {}
  var @functionSignature argumentTypeListForAddAssignWithFieldExpressionList = {}
  argumentTypeListForAddAssignWithFieldExpressionList += !["" nowhere] !keyTypeIndex !"key"
  insertMethodFormalArgumentList +=
    ![if usesSelectorsInInsertAndSearch then "key" else "" end nowhere]
    !keyTypeIndex
    !@formalArgumentPassingModeAST.argumentConstantIn
    !"inKey"
  var @formalParameterSignature removeMethodFormalArgumentList = {}
  removeMethodFormalArgumentList +=
    ![if usesSelectorsInInsertAndSearch then "key" else "" end nowhere]
    !keyTypeIndex
    !@formalArgumentPassingModeAST.argumentConstantIn
    !"inKey"
  var @optionalMethodSignature optionalMethodSignature = {}
  optionalMethodSignature +=
    !true
    ![if usesSelectorsInInsertAndSearch then "key" else "" end nowhere]
    !keyTypeIndex
    !"inKey"
  var typesToIncludeInHeaderCompilation = @unifiedTypeMapEntryList.emptyList
  for (* mPropertyTypeName mPropertyName * *) in self.mPropertyList do
    [@unifiedTypeMap-entry makeEntry !?ioSemanticContext.mTypeMap !mPropertyTypeName ?let attributeTypeIndex]
    let hasSetter = true
    let hasSelector = false
    argumentTypeListForAddAssignWithFieldExpressionList += !["" nowhere] !attributeTypeIndex !mPropertyName.string
    typedPropertyList += !attributeTypeIndex !mPropertyName !hasSetter !hasSelector
    typesToIncludeInHeaderCompilation += !attributeTypeIndex
    enumerationDescriptor += !attributeTypeIndex !mPropertyName.string
    insertMethodFormalArgumentList +=
      !if usesSelectorsInInsertAndSearch then mPropertyName else ["" nowhere] end
      !attributeTypeIndex
      !@formalArgumentPassingModeAST.argumentConstantIn
      !mPropertyName.string
    removeMethodFormalArgumentList +=
      !if usesSelectorsInInsertAndSearch then mPropertyName else ["" nowhere] end
      !attributeTypeIndex
      !@formalArgumentPassingModeAST.argumentOut
      !mPropertyName.string
    optionalMethodSignature +=
      !false
      !if usesSelectorsInInsertAndSearch || hasSelector then mPropertyName else ["" nowhere] end
      !attributeTypeIndex
      !mPropertyName.string
  end
#--- Handle remove setter
  [!?setterMap insertOrReplace
    !.new {!"removeKey" !self.mDictTypeName.location}
    !@methodKind.definedAsMember
    !removeMethodFormalArgumentList
    !true
    !@methodQualifier.isBasic
    !""
  ]
#--- Handle search methods (methods if no state, setters if states)
  [!?instanceMethodMap insertKey
    !.new {!"searchKey" !self.mDictTypeName}
    !@methodKind.definedAsMember
    !removeMethodFormalArgumentList
    !self.mDictTypeName.location
    !true
    !@methodQualifier.isBasic
    !"" # No error message
  ]
#--- Enter "attributeForKey" getters
  for (* mPropertyTypeName mPropertyName * *) in self.mPropertyList do
    let accessorName = @lstring.new {!mPropertyName.string + "ForKey" !mPropertyName.location}
    [@unifiedTypeMap-entry makeEntry !?ioSemanticContext.mTypeMap !mPropertyTypeName ?let attributeTypeIndex]
    [!?getterMap insertOrReplace
      !accessorName
      !@methodKind.definedAsMember
      !@functionSignature {!["" nowhere] !keyTypeIndex !"inKey"}
      !@location.here
      !true
      !attributeTypeIndex
      !@methodQualifier.isBasic
      !"" # No Error message
    ]
  end
#---------- Enter "set...Attribute...forKey" setters, and 'with' accessor
  for (* mPropertyTypeName mPropertyName * *) in self.mPropertyList do
    let accessorName = @lstring.new {
      !"set" + [mPropertyName.string stringByCapitalizingFirstCharacter] + "ForKey"
      !mPropertyName.location
    }
    [@unifiedTypeMap-entry makeEntry
      !?ioSemanticContext.mTypeMap
      !mPropertyTypeName
      ?let @unifiedTypeMap-entry attributeTypeIndex
    ]
    var accessorFormalArgumentList =@formalParameterSignature {}
    accessorFormalArgumentList += !["" nowhere] !attributeTypeIndex !.argumentConstantIn !mPropertyName.string
    accessorFormalArgumentList += !["" nowhere] !keyTypeIndex !.argumentConstantIn !"key"
    [!?setterMap insertOrReplace
      !accessorName
      !@methodKind.definedAsMember
      !accessorFormalArgumentList
      !true
      !@methodQualifier.isBasic
      !"" # No error message
    ]
  end
#--- Add Categories
  addExtensions (
    !inExtensionMethodMapForBuildingContext
    !inExtensionGetterMapForBuildingContext
    !inExtensionSetterMapForBuildingContext
    !?ioSemanticContext
    !self.mDictTypeName
    !?getterMap
    !?setterMap
    !?instanceMethodMap
  )
#--- Element type index
  [@unifiedTypeMap-entry makeEntry
    !?ioSemanticContext.mTypeMap
    !@lstring.new {!self.mDictTypeName.string + elementTypeNameSuffix () !self.mDictTypeName.location}
    ?let @unifiedTypeMap-entry elementTypeEntry
  ]
#--- Add Optional method
  var optionalMethodMap = @optionalMethodMap {}
  [!?optionalMethodMap insertKey !["searchKey" nowhere] !optionalMethodSignature]
#--- Enter in type map
  [!?ioSemanticContext.mTypeMap insertKey
    !self.mDictTypeName
    !self.mIsPredefined
    !true # Concrete Type
    !@unifiedTypeMap-entry.null
    !@typeKindEnum.mapType
    !true # Support collection value
    !@typedPropertyList.emptyList
    !{}
    !typedPropertyList
    !constructorMap
    !getterMap
    !setterMap
    !instanceMethodMap
    !{}
    !optionalMethodMap
    !enumerationDescriptor
    !.doNotGenererateObjectCompare | .plusEqualOperatorWithFieldListNeedsCompilerArg
    !argumentTypeListForAddAssignWithFieldExpressionList
    !{}
    !{}
    !{}
    !{}
    !false # Do not generate header in separate file
    !elementTypeEntry # Type for enumerated element for "for" instruction
    !"emptyDict"
    !"dict-" + [self.mDictTypeName fileNameRepresentation]
    !@headerKind.oneHeader
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC ANALYSIS                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc check_K_escapeCharacters ?@lstring inString {  #--- Check escape sequences : only %K or %%
  for components in [inString.string componentsSeparatedByString !"%%"] do
    var @stringlist explodedArray = [components.mValue componentsSeparatedByString !"%"]
    [!?explodedArray popFirst ?*]
    for (mValue) in explodedArray do
      if [mValue length] > 0 then
        let @char c = [mValue characterAtIndex !0]
        if c != 'K' then
          error inString: "only '%K' and '%%' escape sequences are allowed in this error message"
        end
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc check_K_L_escapeCharacters ?@lstring inString {  #--- Check escape sequences : only %K, %L or %%
  for components in [inString.string componentsSeparatedByString !"%%"] do
    var @stringlist explodedArray = [components.mValue componentsSeparatedByString !"%"]
    [!?explodedArray popFirst ?*]
    for (mValue) in explodedArray do
      if [mValue length] > 0 then
        let @char c = [mValue characterAtIndex !0]
        if (c != 'K') & (c != 'L') then
          error inString: "only '%K', '%L' and '%%' escape sequences are allowed in this error message"
        end
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func %once forbiddenKeysForMapAndDict -> @stringset {
  result = {}
  result += !"key"
  result += !"description"
  result += !"object"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @dictDeclarationAST semanticAnalysis
  ?!@lstringlist unused ioUsefulnessRootEntities 
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @string unused inProductDirectory
  ?let @semanticContext inSemanticContext
  ?let @predefinedTypes unused inPredefinedTypes
  ?!@semanticDeclarationListForGeneration ioSemanticDeclarationListForGeneration
{
#--- Useful entities graph
  let nameForUsefulness = typeNameForUsefulEntitiesGraph (!self.mDictTypeName)
  [!?ioUsefulEntitiesGraph addNode !nameForUsefulness !nameForUsefulness]
  let elementTypeNameForUsefulness = typeNameForUsefulEntitiesGraph (!.new{!self.mDictTypeName.string + "-element" !self.mDictTypeName})
  [!?ioUsefulEntitiesGraph addEdge !nameForUsefulness !elementTypeNameForUsefulness]
#------ Enter attributes
  var @typedPropertyList typedAttributeList = {}
  var @propertyIndexMap attributeMap = {}
  for (* mPropertyTypeName mPropertyName * *) in self.mPropertyList do
    let propertyTypeNameForUsefulness = typeNameForUsefulEntitiesGraph (!mPropertyTypeName)
    [!?ioUsefulEntitiesGraph addEdge !nameForUsefulness !propertyTypeNameForUsefulness]
    var @unifiedTypeMap-entry t = .searchKey{!inSemanticContext.mTypeMap !mPropertyTypeName}
    let hasSetter = true
    let hasSelector = false
    typedAttributeList +=
      !t
      !mPropertyName
      !hasSetter
      !hasSelector
    if [forbiddenKeysForMapAndDict () hasKey !mPropertyName.string] then
      var @string m = "a property cannot be named:"
      for s in forbiddenKeysForMapAndDict () do
        m+= " " + s
      end
      m += "; theses names are reserved"
      error mPropertyName: m
    end
    [!?attributeMap insertKey !mPropertyName !t]
  end
#--- Enter map declaration
  ioSemanticDeclarationListForGeneration +=
    !"dict " + self.mDictTypeName
    !@dictTypeForGeneration.new {
      !@unifiedTypeMap-entry.searchKey {!inSemanticContext.mTypeMap !self.mDictTypeName}
      !@unifiedTypeMap-entry.searchKey {!inSemanticContext.mTypeMap !@lstring.new {!self.mDictTypeName.string + elementTypeNameSuffix () !self.mDictTypeName.location}}
      !self.mDictTypeName
      !typedAttributeList
      !self.mKeyTypeName
    }
}
  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION                         
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @dictTypeForGeneration : @semanticTypeForGeneration {
  public var @unifiedTypeMap-entry mElementTypeEntry
  public var @lstring mDictTypeName
  public var @typedPropertyList mTypedAttributeList
  public var @lstring mKeyTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @dictTypeForGeneration appendDeclaration1
  ?!@stringset unused ioInclusionSet
  !@string outHeader
{
  outHeader = [filewrapper typeGenerationTemplate.dictTypeHeader1
    ![self.mSelfTypeEntry  key]
    ![self.mSelfTypeEntry  identifierRepresentation]
    !self.mTypedAttributeList
  ]
  outHeader += [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    ![self.mSelfTypeEntry  key]
    ![self.mSelfTypeEntry  identifierRepresentation]
    ![self.mSelfTypeEntry  mIsConcrete]
    ![self.mSelfTypeEntry  mConstructorMap]
    ![self.mSelfTypeEntry  mGetterMap]
    ![self.mSelfTypeEntry  mSetterMap]
    ![self.mSelfTypeEntry  mInstanceMethodMap]
    ![self.mSelfTypeEntry  mClassMethodMap]
    ![self.mSelfTypeEntry  mOptionalMethodMap]
    ![self.mSelfTypeEntry  mEnumerationDescriptor]
    ![self.mSelfTypeEntry  mHandledOperatorFlags]
    ![self.mSelfTypeEntry  mAddAssignOperatorArguments]
    ![self.mSelfTypeEntry  mTypeForEnumeratedElement]
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @dictTypeForGeneration appendSpecificImplementation
  ?let @unifiedTypeMap unused inTypeMap
  ?!@stringset ioInclusionSet
  !@string outImplementation {
  [self.mElementTypeEntry addHeaderFileName !?ioInclusionSet]
  [self.mSelfTypeEntry  addHeaderFileName !?ioInclusionSet]
  outImplementation = [filewrapper typeGenerationTemplate.dictTypeSpecificImplementation
    ![self.mSelfTypeEntry  identifierRepresentation]
    !self.mTypedAttributeList
    ![self.mKeyTypeName identifierRepresentation]
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
