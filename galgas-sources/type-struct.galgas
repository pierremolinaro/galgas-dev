#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   AST                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @structDeclarationAST : @semanticDeclarationAST {
  private let @lstring mStructTypeName
  private let @propertyInCollectionListAST mStructurePropertyList
  private let @string mEnumeratedElementTypeName # Empty string if not enumerable
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SYNTAX                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3DeclarationsSyntax {

  #·····················································································································

  rule <declaration> ?!@galgas3DeclarationAST ioDeclarations {
    $struct$
    $@type$ ?let @lstring structTypeName indexing structDefinition
    ${$
    var @propertyInCollectionListAST propertyList = {}
    repeat
    while
      <property_declaration> !?propertyList
    while
      $;$
    end
    $}$
    ioDeclarations.mDeclarationList += !@structDeclarationAST.new {
      !false # Is not predefined
      !structTypeName
      !propertyList
      !""
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas4DeclarationsSyntax {

  #·····················································································································

  rule <declaration> ?!@galgas3DeclarationAST ioDeclarations {
    $struct$
    $@type$ ?let @lstring structTypeName indexing structDefinition
    ${$
    var @propertyInCollectionListAST propertyList = {}
    repeat
    while
      <property_declaration> !?propertyList
    while
      $;$
    end
    $}$
    ioDeclarations.mDeclarationList += !@structDeclarationAST.new {
      !false # Is not predefined
      !structTypeName
      !propertyList
      !""
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   ENTER IN GRAPH                          
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @structDeclarationAST enterDeclarationInGraph
  ?!@semanticTypePrecedenceGraph ioSemanticTypePrecedenceGraph
  ?!@extensionMethodMapForBuildingContext unused ioExtensionMethodMapForBuildingContext
  ?!@extensionGetterMapForBuildingContext unused ioExtensionGetterMapForBuildingContext
  ?!@extensionSetterMapForBuildingContext unused ioExtensionSetterMapForBuildingContext
  ?!@semanticDeclarationListAST unused ioExtensionOverrideDefinitionList
{
  let key = @lstring.new {!"@" + self.mStructTypeName !self.mStructTypeName.location}
  [!?ioSemanticTypePrecedenceGraph addNode !key !self]
  for (* mPropertyTypeName * * *) in self.mStructurePropertyList do
    let propertyKey = @lstring.new {!"@" + mPropertyTypeName !mPropertyTypeName.location}
    [!?ioSemanticTypePrecedenceGraph addEdge !key !propertyKey]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   KEY REPRESENTATION                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @structDeclarationAST keyRepresentation ->@string result {
  result = "struct @" + self.mStructTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC CONTEXT                        
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private func %once selectorFeatureForNewConstructor -> @string {
  result = "selector"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @structDeclarationAST enterInSemanticContext
  ?let @extensionMethodMapForBuildingContext inExtensionMethodMapForBuildingContext
  ?let @extensionGetterMapForBuildingContext inExtensionGetterMapForBuildingContext
  ?let @extensionSetterMapForBuildingContext inExtensionSetterMapForBuildingContext
  ?!@semanticContext ioSemanticContext
{
  let optionalMethodMap = @optionalMethodMap {}
#--- Struct type index
  [!?ioSemanticContext.mTypeMap makeEntry  !self.mStructTypeName ?let structTypeIndex]
#--- Constructors and property map
  var @propertyMap propertyMap = {}
  var @constructorMap constructorMap = {}
  var @functionSignature constructorPropertyTypeList = {}
  var @typedPropertyList typedPropertyList = {}
  var @unifiedTypeMap-entryList typesToIncludeInHeaderCompilation = {}
  for (isConstant mPropertyTypeName mPropertyName isPublic mAttributeList) in self.mStructurePropertyList do
    [!?ioSemanticContext.mTypeMap makeEntry  !mPropertyTypeName ?let propertyTypeEntry]
    var @lstring selector = ["" nowhere]
    for (mValue) in mAttributeList do
      if mValue.string == selectorFeatureForNewConstructor () then
        selector = mPropertyName
      end
    end
    constructorPropertyTypeList += !selector !propertyTypeEntry !mPropertyName.string
    typesToIncludeInHeaderCompilation += !propertyTypeEntry
    let hasSetter = isPublic && not isConstant
    let hasSelector = false
    typedPropertyList += !propertyTypeEntry !mPropertyName !hasSetter !hasSelector
    [!?propertyMap insertKey !mPropertyName !isPublic !isConstant !propertyTypeEntry]
  end
  [!?constructorMap insertKey
    !@lstring.new {!"new" !.here}
    !constructorPropertyTypeList
    !false
    !structTypeIndex
  ]
#--- Getters
  commonGetterMapForAllTypes (!?ioSemanticContext.mTypeMap ?var @getterMap getterMap)
#--- Setters
  var @setterMap setterMap = {}
#--- Instance methods
  var @instanceMethodMap instanceMethodMap = {}
#--- Add Categories
  addExtensions (
    !inExtensionMethodMapForBuildingContext
    !inExtensionGetterMapForBuildingContext
    !inExtensionSetterMapForBuildingContext
    !?ioSemanticContext
    !self.mStructTypeName
    !?getterMap
    !?setterMap
    !?instanceMethodMap
    !acceptSetters: true
  )
#--- Enumeration by "for" instruction
  let @unifiedTypeMap-entry enumeratedType
  if self.mEnumeratedElementTypeName == "" then
    enumeratedType = @unifiedTypeMap-entry.null
  else
   [!?ioSemanticContext.mTypeMap makeEntryFromString
     !self.mEnumeratedElementTypeName
     ?enumeratedType
    ]
  end
#--- Generate a "default" constructor ?
  var @string defaultConstructorName = "default"
  for (mPropertyTypeEntry * * *) in typedPropertyList while defaultConstructorName != "" do
    if ([mPropertyTypeEntry mDefaultConstructorName] == "") | not [mPropertyTypeEntry mIsConcrete] then
      defaultConstructorName = ""
    end
  end  
#--- Enter in type map
  [!?ioSemanticContext.mTypeMap insertKey
    !self.mStructTypeName
    !self.mIsPredefined
    !true # Concrete Type
    !@unifiedTypeMap-entry.null
    !@typeKindEnum.structType
    !false # Does not support collection value
    !typedPropertyList
    !propertyMap
    !typedPropertyList
    !constructorMap
    !getterMap
    !setterMap
    !instanceMethodMap
    !{}
    !optionalMethodMap
    !@enumerationDescriptorList {}
    !.generateDescriptionGetterUtilityMethod | .isComparable
    !@functionSignature {}
    !{}
    !{}
    !@mapSearchMethodListAST {}
    !@mapSearchMethodListAST {}
    !false # Do not generate header in separate file
    !enumeratedType # Type for enumerated element for "for" instruction
    !defaultConstructorName # Default constructor
    !"struct-" + [self.mStructTypeName fileNameRepresentation]
    !@headerKind.oneHeader
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC ANALYSIS                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private func %once forbiddenKeysForStruct -> @stringset {
  result = {}
  result += !"description"
  result += !"object"
  result += !"dynamicType"
}

#·······················································································································

override method @structDeclarationAST semanticAnalysis
  ?!@lstringlist unused ioUsefulnessRootEntities 
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @string unused inProductDirectory
  ?let @semanticContext inSemanticContext
  ?let @predefinedTypes unused inPredefinedTypes
  ?!@semanticDeclarationListForGeneration ioSemanticDeclarationListForGeneration
{
#--- Useful entities graph
  let structNameForUsefulness = typeNameForUsefulEntitiesGraph (!self.mStructTypeName)
  [!?ioUsefulEntitiesGraph addNode !structNameForUsefulness !structNameForUsefulness]
#---
  if [self.mStructurePropertyList length] == 0 then
    error self.mStructTypeName: "a structure cannot be empty: it must have at least one property" 
  end
  var @typedPropertyList typedPropertyList = {}
  var @propertyIndexMap propertyMap = {}
  for (* mPropertyTypeName mPropertyName * mAttributeList) in self.mStructurePropertyList do
    let propertyTypeNameForUsefulness = typeNameForUsefulEntitiesGraph (!mPropertyTypeName)
    [!?ioUsefulEntitiesGraph addEdge !structNameForUsefulness !propertyTypeNameForUsefulness]
    let t = [inSemanticContext.mTypeMap searchKey !mPropertyTypeName]
    var hasSelector = false
    for (mValue) in mAttributeList do
      if mValue.string != selectorFeatureForNewConstructor () then
        error mValue : "a struct property accepts only the %" + selectorFeatureForNewConstructor () + " feature"
      end if hasSelector then
        error mValue : "duplicated %" + selectorFeatureForNewConstructor () + " feature"
      else
        hasSelector = true
      end
    end
    let hasSetter = true # mIsPublic
    typedPropertyList +=
      !t
      !mPropertyName
      !hasSetter
      !hasSelector
    if [forbiddenKeysForStruct () hasKey !mPropertyName.string] then
      var @string m = "an property cannot be named:"
      for s in forbiddenKeysForStruct () do
        m+= " " + s
      end
      m += "; theses names are reserved"
      error mPropertyName: m
    end
    [!?propertyMap insertKey !mPropertyName !t]
  end
  ioSemanticDeclarationListForGeneration +=
    !"struct " + self.mStructTypeName
    !@structTypeForGeneration.new {
      ![inSemanticContext.mTypeMap searchKey !self.mStructTypeName]
      !typedPropertyList
    }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION                         
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @structTypeForGeneration : @semanticTypeForGeneration {
  private let @typedPropertyList mTypedPropertyList
}

#·······················································································································

filewrapper boolsetGenerationTemplate in "+generation-templates/type_generation" {
}{
}{
  template structTypeHeader1 "GALGAS_struct.h1.galgasTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedPropertyList PROPERTY_LIST
    ?@bool HAS_DEFAULT_CONSTRUCTOR

  template structTypeSpecificImplementation "GALGAS_struct.cpp.galgasTemplate"
    ?@string TYPE_NAME
    ?@string TYPE_IDENTIFIER
    ?@typedPropertyList PROPERTY_LIST
    ?@bool HAS_DEFAULT_CONSTRUCTOR
}

#·······················································································································

override method @structTypeForGeneration appendDeclaration1
  ?!@stringset ioInclusionSet
  !@string outHeader
{
  for (t 3*) in self.mTypedPropertyList do
    [t addHeaderFileName1 !?ioInclusionSet]
  end

  outHeader = [filewrapper boolsetGenerationTemplate.structTypeHeader1
    ![self.mSelfTypeEntry  key]
    ![self.mSelfTypeEntry  identifierRepresentation]
    !self.mTypedPropertyList
    ![self.mSelfTypeEntry  mDefaultConstructorName] != ""
  ]
  outHeader += [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    ![self.mSelfTypeEntry  key]
    ![self.mSelfTypeEntry  identifierRepresentation]
    ![self.mSelfTypeEntry  mIsConcrete]
    ![self.mSelfTypeEntry  mConstructorMap]
    ![self.mSelfTypeEntry  mGetterMap]
    ![self.mSelfTypeEntry  mSetterMap]
    ![self.mSelfTypeEntry  mInstanceMethodMap]
    ![self.mSelfTypeEntry  mClassMethodMap]
    ![self.mSelfTypeEntry  mOptionalMethodMap]
    ![self.mSelfTypeEntry  mEnumerationDescriptor]
    ![self.mSelfTypeEntry  mHandledOperatorFlags]
    ![self.mSelfTypeEntry  mAddAssignOperatorArguments]
    ![self.mSelfTypeEntry  mTypeForEnumeratedElement]
  ]
 }

#·······················································································································

override method @structTypeForGeneration appendSpecificImplementation
  ?let @unifiedTypeMap unused inTypeMap
  ?!@stringset ioInclusionSet
  !@string outImplementation
{
  [self.mSelfTypeEntry  addHeaderFileName !?ioInclusionSet]
  outImplementation = [filewrapper boolsetGenerationTemplate.structTypeSpecificImplementation
    ![self.mSelfTypeEntry  key]
    ![self.mSelfTypeEntry  identifierRepresentation]
    !self.mTypedPropertyList
    ![self.mSelfTypeEntry  mDefaultConstructorName] != ""
  ]
 }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
