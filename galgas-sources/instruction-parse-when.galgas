#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @parseWhenInstruction : @syntaxInstructionAST {
  public var @semanticExpressionAST mWhenExpression
  public var @location mEndOfWhenExpression
  public var @syntaxInstructionList mWhenInstructionList
  public var @location mEndOfWhenInstructionList
  public var @syntaxInstructionList mElseInstructionList
  public var @location mEndOfElseInstructionList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3SyntaxComponentSyntax {

  #·····················································································································
  
  rule <branchOfParseWhithInstruction> !@syntaxInstructionList outElseInstructionList {
    $else$
    <syntax_instruction_list> ? outElseInstructionList
  }
  
  #·····················································································································
  
  rule <branchOfParseWhithInstruction> !@syntaxInstructionList outElseInstructionList {
    $case$
    let instructionLocation = @location.here
    <expression> ?let whenExpression
    let endOfWhenExpression = @location.here
    $:$
    <syntax_instruction_list> ?let whenInstructionList
    let endOfWhenInstructions = @location.here
  #--- Else part
    <branchOfParseWhithInstruction> ?let elseInstructionList
    let endOfElseInstructions = @location.here
    outElseInstructionList = {}
    outElseInstructionList += !@parseWhenInstruction.new {
      !instructionLocation
      !whenExpression
      !endOfWhenExpression
      !whenInstructionList
      !endOfWhenInstructions
      !elseInstructionList
      !endOfElseInstructions
    }
  }
  
  #·····················································································································
  
  rule <syntax_instruction> !@syntaxInstructionAST outInstruction {
    $parse$
    let instructionLocation = @location.here
    $with$
    <expression> ?let @semanticExpressionAST whenExpression
    let endOfWhenExpression = @location.here
    $:$
    <syntax_instruction_list> ?let @syntaxInstructionList whenInstructionList
    let endOfWhenInstructions = @location.here
  #--- Else part
    <branchOfParseWhithInstruction> ?let @syntaxInstructionList elseInstructionList
    let endOfElseInstructions = @location.here
    $end$
    select
    or
      $%attribute$ ?let @lstring terminator
      if terminator.string != "parse" then
        error terminator : "the terminator attribute should be '%parse'" fixit { replace "%parse" }
      end
    end
    outInstruction = @parseWhenInstruction.new {
      !instructionLocation
      !whenExpression
      !endOfWhenExpression
      !whenInstructionList
      !endOfWhenInstructions
      !elseInstructionList
      !endOfElseInstructions
    }
  }
  
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! TRANSFORM
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @parseWhenInstruction transformInstruction
  ?!usedTerminalMap:@terminalSymbolsMapForGrammarAnalysis ioActuallyUsedTerminalSymbolMap
  ?nonTerminalSymbolMap:let @nonTerminalSymbolMapForGrammarAnalysis inNonTerminalSymbolMap
  ?!addedTerminalIndex:@uint ioAddedNonTerminalIndex
  ?!@syntaxInstructionListForGrammarAnalysis ioSyntaxInstructionList
{
  transformInstructionList (
    !self.mElseInstructionList
    !?usedTerminalMap:ioActuallyUsedTerminalSymbolMap
    !nonTerminalSymbolMap:inNonTerminalSymbolMap
    !?addedTerminalIndex:ioAddedNonTerminalIndex
    !?ioSyntaxInstructionList
  )
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
  
override method @parseWhenInstruction analyzeSyntaxInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?let @bool inHasTranslateFeature
  ?let @terminalMap inTerminalMap
  ?let @string inLexiqueName
  ?let @nonterminalMap inNonterminalMap
  ?let @string inComponentName
  ?let @stringset inIndexNameSet
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap
  ?!@uint ioSelectMethodCount {
#--- When expression
  @semanticExpressionForGeneration when_expression
  [self.mWhenExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !@unifiedTypeMap-entry.null
    !inAnalysisContext
    !?ioVariableMap
    ?when_expression
  ]
#--- Begin block
  [!?ioVariableMap openOverrideForRepeatBlock]
#--- 'when' branch
  @uint localSelectMethodCount = ioSelectMethodCount
  @semanticInstructionListForGeneration when_instructionListForGeneration
  analyzeSyntaxInstructionList (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inAnalysisContext
    !inHasTranslateFeature
    !inTerminalMap
    !inLexiqueName
    !inNonterminalMap
    !inComponentName
    !self.mWhenInstructionList
    !self.mEndOfWhenInstructionList
    !inIndexNameSet
    !?ioVariableMap
    !?localSelectMethodCount
    ?when_instructionListForGeneration
  )
#--- 'else' branch
  @semanticInstructionListForGeneration else_instructionListForGeneration
  analyzeSyntaxInstructionList (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inAnalysisContext
    !inHasTranslateFeature
    !inTerminalMap
    !inLexiqueName
    !inNonterminalMap
    !inComponentName
    !self.mElseInstructionList
    !self.mEndOfElseInstructionList
    !inIndexNameSet
    !?ioVariableMap
    !?ioSelectMethodCount
    ?else_instructionListForGeneration
  )
#--- End block
  [!?ioVariableMap closeOverride !self.mEndOfElseInstructionList]
#--- Check syntax signature
  let @bool ok = compareSyntaxSignature (
    !syntaxSignatureOfInstructionList (!"parse" !when_instructionListForGeneration)
    !syntaxSignatureOfInstructionList (!"parse" !else_instructionListForGeneration)
    !self.mEndOfElseInstructionList
  )
#--- Generate instruction
  if ok then
    ioInstructionListForGeneration += !@parseWhenInstructionForGeneration.new {
      !self.mInstructionLocation
      !when_expression
      !when_instructionListForGeneration
      !else_instructionListForGeneration}
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! GENERATION TYPES
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @parseWhenInstructionForGeneration : @syntaxInstructionForGeneration {
  public var @semanticExpressionForGeneration mWhenExpression
  public var @semanticInstructionListForGeneration mInstructionList
  public var @semanticInstructionListForGeneration mElseInstructionList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX SIGNATURE
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @parseWhenInstructionForGeneration appendSyntaxSignature
  ?let @string inPosfix
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration {
  ioInstructionListForGeneration += syntaxSignatureOfInstructionList (!inPosfix !self.mInstructionList)
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @parseWhenInstructionForGeneration generateInstruction
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?let @bool inGenerateSyntaxDirectedTranslationString
  ?!@string ioGeneratedCode
{
  @string sourceVar
  [self.mWhenExpression generateExpression !?ioGeneratedCode !?ioInclusionSet !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?sourceVar]
  ioGeneratedCode += "if (" + sourceVar + ".isValidAndTrue ()) {\n"
  generateInstructionList (
    !?ioInclusionSet
    !self.mInstructionList
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !inGenerateSyntaxDirectedTranslationString
    !?ioGeneratedCode
  )
  ioGeneratedCode += "}else{\n"
  generateInstructionList (
    !?ioInclusionSet
    !self.mElseInstructionList
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    !inGenerateSyntaxDirectedTranslationString
    !?ioGeneratedCode
  )
  ioGeneratedCode += "}\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
