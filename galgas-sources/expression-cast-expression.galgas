#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!   AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

shared class @testDynamicClassInExpressionAST : @semanticExpressionAST {
  @semanticExpressionAST mReceiverExpression
  @location mEndOfReceiverExpression
  @dynamicTypeComparisonKind mTypeComparisonKind
  @lstring mTypeName
}

#·····················································································································

enum @dynamicTypeComparisonKind {
  case equal  # ==
  case inherited  # >=
  case strictlyInherited # >
}


#·····················································································································

shared class @castInExpressionAST : @semanticExpressionAST {
  @semanticExpressionAST mReceiverExpression
  @location mEndOfReceiverExpression
  @lstring mTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3ExpressionSyntax {

  #·····················································································································
  
  rule <expression> !@semanticExpressionAST outExpression {
    <expression_or> ?outExpression
    select
    or
      let endOfReceiverExpression = @location.here
      $is$
      @dynamicTypeComparisonKind kind
      select
        $==$
        kind = .equal
      or
        $>=$
        kind = .inherited
      or
        $>$
        kind = .strictlyInherited
      end
      $@type$ ?let typeName
      outExpression = @testDynamicClassInExpressionAST.new {
        !outExpression
        !endOfReceiverExpression
        !kind
        !typeName
      }
    or
      let endOfReceiverExpression = @location.here
      $as$
      $@type$ ?let typeName
      outExpression = @castInExpressionAST.new {
        !outExpression
        !endOfReceiverExpression
        !typeName
      }
    end
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @testDynamicClassInExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [self.mReceiverExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#·····················································································································

override method @testDynamicClassInExpressionAST analyzeSemanticExpression
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @unifiedTypeMap-proxy unused inType
  ?let @analysisContext inAnalysisContext
  ?!@variableMap ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
#--- Receiver expression analysis
  @semanticExpressionForGeneration expression
  [self.mReceiverExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !@unifiedTypeMap-proxy.null
    !inAnalysisContext
    !?ioVariableMap
    ?expression
  ]
#--- Get type information
   let castType = @unifiedTypeMap-proxy. searchKey {!inAnalysisContext.mSemanticContext.mTypeMap !self.mTypeName}
#--- This type is either a class
  if [[[expression mResultType] mTypeKindEnum] isClassType] then
  #--- Check expression type is a descendant of named type
    @unifiedTypeMap-proxy t = castType
    @bool found = t == [expression mResultType]
    loop( [inAnalysisContext.mSemanticContext.mTypeMap count] + 1)
    while (not found) & not [[t mSuperType] isNull] do
      t = [t mSuperType]
      found = t == [expression mResultType]
    end
    if not found then
      error self.mTypeName
      :"the '@" + self.mTypeName + "' is not a descendant of the '@" + [[expression mResultType] key] + "' type of the cast expression"
    end
  else
    error [expression mLocation]: "expression should be a class instance: it is an @"
     +  [[expression mResultType] key] + " instance"
  end
#--- Generate expression
  outExpression = @testDynamicClassInExpressionForGeneration.new {
    !inAnalysisContext.mPredefinedTypes.mBoolType
    ![self.mTypeName location]
    !expression
    !self.mTypeComparisonKind
    !castType}
}

#·····················································································································

override method @castInExpressionAST enterExpressionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [self.mReceiverExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @castInExpressionAST analyzeSemanticExpression
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @unifiedTypeMap-proxy unused inType
  ?let @analysisContext inAnalysisContext
  ?!@variableMap ioVariableMap
  !@semanticExpressionForGeneration outExpression
{
#--- Receiver expression analysis
  [self.mReceiverExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !@unifiedTypeMap-proxy.null
    !inAnalysisContext 
    !?ioVariableMap
    ?var @semanticExpressionForGeneration expression
   ]
#--- Get type information
   let type = @unifiedTypeMap-proxy. searchKey {
     !inAnalysisContext.mSemanticContext.mTypeMap
     !self.mTypeName
   }
#--- This type is an @object type
  if [[[expression mResultType] mTypeKindEnum] isClassType]then
  #--- Check expression type is a descendant of named type
    @unifiedTypeMap-proxy t = type
    @bool found = t == [expression mResultType]
    loop( [inAnalysisContext.mSemanticContext.mTypeMap count] + 1)
    while (not found) & not [[t mSuperType] isNull] do
      t = [t mSuperType]
      found = t == [expression mResultType]
    end
    if not found then
      error self.mTypeName
      :"the '@" + self.mTypeName + "' is not a descendant of the '@"
      + [[expression mResultType] key] + "' type of the cast expression"
    end
  elsif [[expression mResultType] key] != "object" then
    error [expression mLocation]:
      "cast expression should be a class instance or an @object instance: it is an @"
     +  [[expression mResultType] key] + " instance"
  end
#--- Generate expression
  if [[expression mResultType] key] == "object" then
    outExpression = @extractObjectInExpressionForGeneration.new {
      !type
      !self.mEndOfReceiverExpression
      !expression
      ![self.mTypeName string]}
  else
    outExpression = @castInExpressionForGeneration.new {
      !type
      ![self.mTypeName location]
      !expression
      ![self.mTypeName string]
      !type
    }
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

shared class @testDynamicClassInExpressionForGeneration : @semanticExpressionForGeneration {
  @semanticExpressionForGeneration mReceiverExpression
  @dynamicTypeComparisonKind mTypeComparisonKind
  @unifiedTypeMap-proxy mCastType
}

#·····················································································································

override method @testDynamicClassInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression {
  let typeNameRepresentation = [[self.mCastType key] identifierRepresentation]
#--- Receiver expression
  [self.mReceiverExpression generateExpression
    !?ioGeneratedCode
    !?ioInclusionSet
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    ?var @string receiverExpression
  ]
#--- Generated test
  [self.mCastType addHeaderFileName !?ioInclusionSet]
  switch self.mTypeComparisonKind
  case equal :
    outCppExpression = "GALGAS_bool (" + receiverExpression
      + ".dynamicTypeDescriptor () == & kTypeDescriptor_GALGAS_"
      + typeNameRepresentation + ")"
  case inherited :
    outCppExpression = "GALGAS_bool (NULL != dynamic_cast <const cPtr_"
    + typeNameRepresentation
    + " *> (" + receiverExpression + ".ptr ()))"
  case strictlyInherited :
    outCppExpression = "GALGAS_bool ((" + receiverExpression
      + ".dynamicTypeDescriptor () != & kTypeDescriptor_GALGAS_"
      + typeNameRepresentation + ") && (NULL != dynamic_cast <const cPtr_"
    + typeNameRepresentation
    + " *> (" + receiverExpression + ".ptr ())))"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @castInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression
{
  [self.mCastType addHeaderFileName !?ioInclusionSet] 
  outCppExpression = "temp_" + [ioTemporaryVariableIndex string]
  ioTemporaryVariableIndex ++
#--- Receiver expression
  [self.mReceiverExpression generateExpression
    !?ioGeneratedCode
    !?ioInclusionSet
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    ?var @string receiverExpression
  ]
#---
  ioGeneratedCode += "GALGAS_" + [self.mTypeName identifierRepresentation]
  + " " + outCppExpression + " ;\n"
  + "if (" + receiverExpression + ".isValid ()) {\n"
  [!?ioGeneratedCode incIndentation !2]
#--- 
  ioGeneratedCode +=
    "if (NULL != dynamic_cast <const cPtr_"
  + [self.mTypeName identifierRepresentation]
  + " *> (" + receiverExpression + ".ptr ())) {\n"
  + "  " + outCppExpression + " = (cPtr_" + [self.mTypeName identifierRepresentation] + " *) "
  + receiverExpression + ".ptr () ;\n"
  + "}else{\n"
  [!?ioGeneratedCode incIndentation !2]
  ioGeneratedCode +=
    compilerCppName () + "->castError (\""
  + [self.mTypeName identifierRepresentation] 
  + "\", " + receiverExpression + ".ptr ()->classDescriptor ()"
  + [self.mLocation commaSourceFile] + ") ;\n"
  [!?ioGeneratedCode decIndentation !2]
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
  ioGeneratedCode += "}\n"
  [!?ioGeneratedCode decIndentation !2]
  ioGeneratedCode += "}\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

shared class @extractObjectInExpressionForGeneration : @semanticExpressionForGeneration {
  @semanticExpressionForGeneration mReceiverExpression
  @string mTypeName
}

#·····················································································································

override method @extractObjectInExpressionForGeneration generateExpression
  ?!@string ioGeneratedCode
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  !@string outCppExpression {
  outCppExpression = "temp_" + [ioTemporaryVariableIndex string] ioTemporaryVariableIndex ++
#--- Receiver expression
  [self.mReceiverExpression generateExpression
    !?ioGeneratedCode
    !?ioInclusionSet
    !?ioTemporaryVariableIndex
    !?ioUnusedVariableCppNameSet
    ?var @string receiverExpression
  ]
#--- Generate code
  outCppExpression = "GALGAS_" + [self.mResultType identifierRepresentation]
  + "::extractObject (" + receiverExpression
  + ", " + compilerCppName ()
  + [self.mLocation commaSourceFile] + ")"
  [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
