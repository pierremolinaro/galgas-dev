#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @selfPlusEqualElementsInstructionAST : @semanticInstructionAST {
  public var @actualOutputExpressionList mExpressions
}

#·····················································································································*

refclass @selfPlusEqualExpressionInstructionAST : @semanticInstructionAST {
  public var @semanticExpressionAST mExpression
}

#·····················································································································*

refclass @selfMinusEqualExpressionInstructionAST : @semanticInstructionAST {
  public var @semanticExpressionAST mExpression
}

#·····················································································································*

refclass @selfMulEqualExpressionInstructionAST : @semanticInstructionAST {
  public var @semanticExpressionAST mExpression
}

#·····················································································································*

refclass @selfDivEqualExpressionInstructionAST : @semanticInstructionAST {
  public var @semanticExpressionAST mExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3InstructionsSyntax {

  #·····················································································································

  rule <semantic_instruction> !@semanticInstructionAST outInstruction {
    $self$
    let instructionLocation = @location.here
    $+=$
    <non_empty_output_expression_list> ?let @actualOutputExpressionList expressionList
    outInstruction = @selfPlusEqualElementsInstructionAST.new {
      !instructionLocation
      !expressionList
    }
  }
  
  #·····················································································································
  
  rule <semantic_instruction> !@semanticInstructionAST outInstruction {
    $self$
    let instructionLocation = @location.here
    $+=$
    <expression> ?let @semanticExpressionAST expression
    outInstruction = @selfPlusEqualExpressionInstructionAST.new {
      !instructionLocation
      !expression
    }
  }  

  #·····················································································································
  
  rule <semantic_instruction> !@semanticInstructionAST outInstruction {
    $self$
    let instructionLocation = @location.here
    $-=$
    <expression> ?let @semanticExpressionAST expression
    outInstruction = @selfMinusEqualExpressionInstructionAST.new {
      !instructionLocation
      !expression
    }
  }  

  #·····················································································································
  
  rule <semantic_instruction> !@semanticInstructionAST outInstruction {
    $self$
    let instructionLocation = @location.here
    $*=$
    <expression> ?let @semanticExpressionAST expression
    outInstruction = @selfMulEqualExpressionInstructionAST.new {
      !instructionLocation
      !expression
    }
  }  

  #·····················································································································
  
  rule <semantic_instruction> !@semanticInstructionAST outInstruction {
    $self$
    let instructionLocation = @location.here
    $/=$
    <expression> ?let @semanticExpressionAST expression
    outInstruction = @selfDivEqualExpressionInstructionAST.new {
      !instructionLocation
      !expression
    }
  }  

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CONTEXT
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @selfPlusEqualExpressionInstructionAST enterInstructionInSemanticContext ?!@unifiedTypeMap ioTypeMap {
  [self.mExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @selfMinusEqualExpressionInstructionAST enterInstructionInSemanticContext ?!@unifiedTypeMap ioTypeMap {
  [self.mExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @selfMulEqualExpressionInstructionAST enterInstructionInSemanticContext ?!@unifiedTypeMap ioTypeMap {
  [self.mExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @selfDivEqualExpressionInstructionAST enterInstructionInSemanticContext ?!@unifiedTypeMap ioTypeMap {
  [self.mExpression enterExpressionInSemanticContext !?ioTypeMap]
}

#·····················································································································*

override method @selfPlusEqualElementsInstructionAST enterInstructionInSemanticContext ?!@unifiedTypeMap ioTypeMap {
  [self.mExpressions enterInSemanticContext !?ioTypeMap]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @selfPlusEqualElementsInstructionAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap {
#--- Analyze "self", as receiver expression
  var @unifiedTypeMap-entry selfTypeEntry
  var @string selfObjectCppName
  if [inAnalysisContext.mutableSelfType isNull] then
    error self.mInstructionLocation: "'self' can be used only in extension setters" : selfObjectCppName, selfTypeEntry
  else
    selfObjectCppName = inAnalysisContext.mSelfObjectCppName
    selfTypeEntry = inAnalysisContext.mutableSelfType
  end
#--- Check target variable type support '+=' operator
  let addAssignOperatorArguments= [selfTypeEntry mAddAssignOperatorArguments] # Empty list if += operator is not supported
  if [addAssignOperatorArguments length] == 0 then
    error self.mInstructionLocation
    :"the target object has the '@" + [selfTypeEntry key] + "' type, but this type does not support the '+=' operator"
#--- Analyze source expressions
  elsif [self.mExpressions length] != [addAssignOperatorArguments length] then
    error self.mInstructionLocation
    : "calling the '+=' operator on an '@" + [selfTypeEntry key] + "' object requires "
    + [[addAssignOperatorArguments length] string] + " parameter(s), while this invocation has "
    + [[self.mExpressions length] string] + " parameter(s)"
  else
    var effectiveParameterList =@semanticExpressionListForGeneration {}
    for (mActualSelector mExpression mEndOfExpressionLocation) in self.mExpressions, (mFormalSelector mFormalArgumentType *) in addAssignOperatorArguments do
      [mExpression analyzeSemanticExpression
        !inUsefulnessCallerEntityName
        !?ioUsefulEntitiesGraph
        !mFormalArgumentType
        !inAnalysisContext
        !?ioVariableMap
        ?@semanticExpressionForGeneration expression
      ]
      if mFormalSelector.string != mActualSelector.string then
        let s = "!" + if mFormalSelector.string != "" then mFormalSelector.string + ":" else "" end
        error mActualSelector : "the selector should be '" + s + "'" fixit { replace s }
#        error mActualSelector
#          : "the selector should be '!" + if mFormalSelector.string != "" then mFormalSelector.string + ":" else "" end + "'"
      end
      checkAssignmentTypeWithImplicitGetterCall (!mFormalArgumentType !expression.mResultType !mEndOfExpressionLocation !?expression)
      effectiveParameterList += !expression
    end
  #--- Generate instruction
    ioInstructionListForGeneration += !@plusEqualnstructionForGeneration.new {
      !selfObjectCppName
      !selfTypeEntry
      !""
      !self.mInstructionLocation
      !@lstringlist {}
      !effectiveParameterList}
  end
}

#·····················································································································*

override method @selfPlusEqualExpressionInstructionAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap {
#--- Analyze "self", as receiver expression
  var @unifiedTypeMap-entry selfTypeEntry
  var @string selfObjectCppName
  if [inAnalysisContext.mutableSelfType isNull] then
    error self.mInstructionLocation: "'self' can be used only in extension setters" : selfObjectCppName, selfTypeEntry
  else
    selfObjectCppName = inAnalysisContext.mSelfObjectCppName
    selfTypeEntry = inAnalysisContext.mutableSelfType
  end
#--- Check target variable type support '+= exp' operator
  if not [[selfTypeEntry mHandledOperatorFlags] plusEqualOperatorWithExpression] then
    error self.mInstructionLocation
    :"the target object has the '@" + [selfTypeEntry key] + "' type, but this type does not support the '+=' operator"
  end
#--- Analyze source expression
  [self.mExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !selfTypeEntry
    !inAnalysisContext
    !?ioVariableMap
    ?@semanticExpressionForGeneration expression
  ]
#--- Check assignment type compatibility
  checkAssignmentTypeWithImplicitGetterCall (!selfTypeEntry !expression.mResultType !self.mInstructionLocation !?expression)
#--- Generate instruction
  ioInstructionListForGeneration += !@opEqualInstructionForGeneration.new {
    !self.mInstructionLocation
    !selfTypeEntry
    !selfObjectCppName
    !""
    !{}
    !expression
    !"plusAssign_operation"
  }
}

#·····················································································································*

override method @selfMinusEqualExpressionInstructionAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap {
#--- Analyze "self", as receiver expression
  var @unifiedTypeMap-entry selfTypeEntry
  var @string selfObjectCppName
  if [inAnalysisContext.mutableSelfType isNull] then
    error self.mInstructionLocation: "'self' can be used only in extension setters" : selfObjectCppName, selfTypeEntry
  else
    selfObjectCppName = inAnalysisContext.mSelfObjectCppName
    selfTypeEntry = inAnalysisContext.mutableSelfType
  end
#--- Check target variable type support '-= exp' operator
  if not [[selfTypeEntry mHandledOperatorFlags] minusEqualOperatorWithExpression] then
    error self.mInstructionLocation
    :"the target object has the '@" + [selfTypeEntry key] + "' type, but this type does not support the '-=' operator"
  end
#--- Analyze source expression
  [self.mExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !selfTypeEntry
    !inAnalysisContext
    !?ioVariableMap
    ?@semanticExpressionForGeneration expression
  ]
#--- Check assignment type compatibility
  checkAssignmentTypeWithImplicitGetterCall (!selfTypeEntry !expression.mResultType !self.mInstructionLocation !?expression)
#--- Generate instruction
  ioInstructionListForGeneration += !@opEqualInstructionForGeneration.new {
    !self.mInstructionLocation
    !selfTypeEntry
    !selfObjectCppName
    !""
    !{}
    !expression
    !"minusAssign_operation"
  }
}

#·····················································································································*

override method @selfMulEqualExpressionInstructionAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap {
#--- Analyze "self", as receiver expression
  var @unifiedTypeMap-entry selfTypeEntry
  var @string selfObjectCppName
  if [inAnalysisContext.mutableSelfType isNull] then
    error self.mInstructionLocation: "'self' can be used only in extension setters" : selfObjectCppName, selfTypeEntry
  else
    selfObjectCppName = inAnalysisContext.mSelfObjectCppName
    selfTypeEntry = inAnalysisContext.mutableSelfType
  end
#--- Check target variable type support '*= exp' operator
  if [[selfTypeEntry mHandledOperatorFlags] mulEqualOperatorWithExpression] then
    error self.mInstructionLocation
    :"the target object has the '@" + [selfTypeEntry key] + "' type, but this type does not support the '*=' operator"
  end
#--- Analyze source expression
  [self.mExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !selfTypeEntry
    !inAnalysisContext
    !?ioVariableMap
    ?@semanticExpressionForGeneration expression
  ]
#--- Check assignment type compatibility
  checkAssignmentTypeWithImplicitGetterCall (!selfTypeEntry !expression.mResultType !self.mInstructionLocation !?expression)
#--- Generate instruction
  ioInstructionListForGeneration += !@opEqualInstructionForGeneration.new {
    !self.mInstructionLocation
    !selfTypeEntry
    !selfObjectCppName
    !""
    !{}
    !expression
    !"mulAssign_operation"
  }
}

#·····················································································································*

override method @selfDivEqualExpressionInstructionAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap {
#--- Analyze "self", as receiver expression
  var @unifiedTypeMap-entry selfTypeEntry
  var @string selfObjectCppName
  if [inAnalysisContext.mutableSelfType isNull] then
    error self.mInstructionLocation: "'self' can be used only in extension setters" : selfObjectCppName, selfTypeEntry
  else
    selfObjectCppName = inAnalysisContext.mSelfObjectCppName
    selfTypeEntry = inAnalysisContext.mutableSelfType
  end
#--- Check target variable type support '/= exp' operator
  if [[selfTypeEntry mHandledOperatorFlags] divEqualOperatorWithExpression] then
    error self.mInstructionLocation
    :"the target object has the '@" + [selfTypeEntry key] + "' type, but this type does not support the '/=' operator"
  end
#--- Analyze source expression
  [self.mExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !selfTypeEntry
    !inAnalysisContext
    !?ioVariableMap
    ?@semanticExpressionForGeneration expression
  ]
#--- Check assignment type compatibility
  checkAssignmentTypeWithImplicitGetterCall (!selfTypeEntry !expression.mResultType !self.mInstructionLocation !?expression)
#--- Generate instruction
  ioInstructionListForGeneration += !@opEqualInstructionForGeneration.new {
    !self.mInstructionLocation
    !selfTypeEntry
    !selfObjectCppName
    !""
    !{}
    !expression
    !"divAssign_operation"
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
