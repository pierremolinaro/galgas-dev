#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   AST                                                                                                                *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @classDeclarationAST : @semanticDeclarationAST {
  @bool mIsAbstract
  @bool mIsShared
  @lstring mClassTypeName
  @lstring mSuperClassName # Empty if no super class
  @lstringlist mClassFeatureList
  @propertyInCollectionListAST mPropertyList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SYNTAX                                                                                                             *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3DeclarationsSyntax {

  rule <declaration>
   ?!@galgas3DeclarationAST ioDeclarations {
    @bool isAbstract
    @bool isShared
    select
      isShared = false
      isAbstract = false
    or
      $shared$
      isShared = true
      isAbstract = false
    or
      $abstract$
      isAbstract = true
      select
        isShared = false
      or
        $shared$
        isShared = true
      end
    end
    $class$
    var classNameList =@lstringlist {}
    repeat
      $@type$ ?var @lstring className indexing classDefinition
      classNameList += !className
    while
      $,$
    end
    @lstring mSuperClassName # Empty if no super class
    select
      mSuperClassName = ["" nowhere]
    or
      $:$
      $@type$ ? mSuperClassName indexing classReferencedAsSuperClass
    end
    var classFeatureList =@lstringlist {}
    repeat
    while
      $%attribute$ ?var  @lstring classFeatureName
      classFeatureList += !classFeatureName
    end
    ${$
    @string mClassMessage = ""
  #--- Class properties
    @propertyInCollectionListAST propertyList = {}
    repeat
    while
      <property_declaration> !? propertyList
    while
      $;$
    end
    $}$
    for () in classNameList do
      ioDeclarations.mDeclarationList += !@classDeclarationAST.new {
        !false # Is not predefined
        !isAbstract
        !isShared
        !mValue
        !mSuperClassName # Empty if no super class
        !classFeatureList
        !propertyList
      }
    end
  }

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   ENTER IN GRAPH                                                                                                     *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @classDeclarationAST enterDeclarationInGraph
  ?!@semanticTypePrecedenceGraph ioSemanticTypePrecedenceGraph
  ?!@extensionMethodMapForBuildingContext unused ioExtensionMethodMapForBuildingContext
  ?!@extensionGetterMapForBuildingContext unused ioExtensionGetterMapForBuildingContext
  ?!@extensionSetterMapForBuildingContext unused ioExtensionSetterMapForBuildingContext
  ?!@semanticDeclarationListAST unused ioExtensionOverrideDefinitionList
{
  let key = @lstring.new {!"@" + self.mClassTypeName ![self.mClassTypeName location]}
  [!?ioSemanticTypePrecedenceGraph addNode !key !self]
  if self.mSuperClassName.string != "" then
    [!?ioSemanticTypePrecedenceGraph addEdge !key !@lstring.new {!"@" + self.mSuperClassName !self.mSuperClassName.location}]
  end
  for () in self.mPropertyList do
    [!?ioSemanticTypePrecedenceGraph addEdge !key !@lstring.new {!"@" + mPropertyTypeName !self.mSuperClassName.location}]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC CONTEXT                                                                                                   *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @classDeclarationAST enterInSemanticContext
  ?let @extensionMethodMapForBuildingContext inExtensionMethodMapForBuildingContext
  ?let @extensionGetterMapForBuildingContext inExtensionGetterMapForBuildingContext
  ?let @extensionSetterMapForBuildingContext inExtensionSetterMapForBuildingContext
  ?!@semanticContext ioSemanticContext
{
  @getterMap getterMap
  @setterMap setterMap
  @instanceMethodMap instanceMethodMap
  @typedPropertyList inheritedTypedAttributeList
  @propertyMap attributeMap = {}
#--- Handle super class
  @unifiedTypeMap-proxy superClassIndex
  @bool generateHeaderInSeparateFile
  if [self.mSuperClassName string] == "" then
    superClassIndex = @unifiedTypeMap-proxy.null
    commonGetterMapForAllTypes (!?ioSemanticContext.mTypeMap ?getterMap )
    setterMap = {}
    instanceMethodMap = {}
    inheritedTypedAttributeList = {}
    attributeMap = {}
    generateHeaderInSeparateFile = false
    for () in self.mClassFeatureList do
      if [mValue string] == "generatedInSeparateFile" then
        generateHeaderInSeparateFile = true
      end
    end
  else
    [@unifiedTypeMap-proxy makeProxy !?ioSemanticContext.mTypeMap !self.mSuperClassName ?superClassIndex]
    @typeKindEnum typeKindEnum
    @getterMap inheritedGetterMap
    @setterMap inheritedSetterMap
    @instanceMethodMap inheritedMethodMap
    [ioSemanticContext.mTypeMap searchKey
      !self.mSuperClassName
      ?3*
      ?typeKindEnum
      ?*
      ?inheritedTypedAttributeList
      ?attributeMap
      ?2*
      ?inheritedGetterMap
      ?inheritedSetterMap
      ?inheritedMethodMap
      ?9*
      ?generateHeaderInSeparateFile
      ?4*
    ]
  #---
    if not [typeKindEnum isClassType] then
      error self.mSuperClassName : "the @" + self.mSuperClassName + " should be a class type"
    end
  #--- Mark all setters as inherited
    setterMap = {}
    for () in inheritedSetterMap do
      [!?setterMap insertKey
        !lkey
        !mKind
        !mParameterList
        !mHasCompilerArgument
        !@methodQualifier. isInherited
        !"" # No error message
      ] 
    end
  #--- Mark all getters as inherited
    getterMap = {}
    for () in inheritedGetterMap do
      [!?getterMap insertKey
        !lkey
        !mKind
        !mArgumentTypeList
        !mDeclarationLocation
        !mHasCompilerArgument
        !mReturnedType
        !.isInherited
        !mErrorMessage
      ] 
    end
  #--- Mark all methods as inherited
    instanceMethodMap = {}
    for () in inheritedMethodMap do
      [!?instanceMethodMap insertKey
        !lkey
        !mKind
        !mParameterList
        !mDeclarationLocation
        !mHasCompilerArgument
        !.isInherited
        !mErrorMessage
      ] 
    end
  end
#--- Build attribute Map and constructor argument list
  @functionSignature constructorAttributeTypeList = {}
  for () in inheritedTypedAttributeList do
    constructorAttributeTypeList +=
      !if mHasSelector then mPropertyName else ["" nowhere] end
      !mPropertyTypeProxy
      !mPropertyName.string
  end
  for () in self.mPropertyList do
    [@unifiedTypeMap-proxy makeProxy !?ioSemanticContext.mTypeMap !mPropertyTypeName ?let attributeTypeIndex]
    var hasSelector = false
    for (attribute) in mAttributeList do
      if attribute.string == "selector" then
        if hasSelector then
          error attribute : "duplicated attribute" fixit { remove }
        end
        hasSelector = true
      end
    end
    constructorAttributeTypeList += 
      !if hasSelector then mPropertyName else ["" nowhere] end
      !attributeTypeIndex
      !mPropertyName.string
    [!?attributeMap insertKey
      !mPropertyName
      !mIsPublic
      !attributeTypeIndex
    ]
    if [getterMap hasKey ![mPropertyName string]] then
      error mPropertyName: "'" + mPropertyName + "' is the name of a prefefined or a user defined getter"
    end
  end
#--- Current class Index
  [@unifiedTypeMap-proxy makeProxy
     !?ioSemanticContext.mTypeMap
     !self.mClassTypeName
     ?var @unifiedTypeMap-proxy classIndex
  ]
#--- Constructors
  @constructorMap constructorMap = {}
  if not self.mIsAbstract then
    [!?constructorMap insertKey
      !@lstring.new {!"new" !@location.here}
      !constructorAttributeTypeList
      !false
      !classIndex
    ]
  end
#--- Typed attribute list
  @typedPropertyList currentClassTypedAttributeList = {}
  @typedPropertyList allTypedAttributeList = inheritedTypedAttributeList
  for () in self.mPropertyList do
    [@unifiedTypeMap-proxy makeProxy !?ioSemanticContext.mTypeMap !mPropertyTypeName ?let attributeTypeIndex]
    let hasSetter = true # mIsPublic
    let hasGetter = true # mIsPublic
    var hasSelector = false
    for () in mAttributeList do
      if [mValue string] == "setter" then
        warning mValue : "obsolete attribute: setter is implicitly generated for a public property"
#        hasSetter = true
      elsif [mValue string] == "nogetter" then
        warning mValue : "obsolete attribute: use 'private' qualifier"
#        hasGetter = false
      elsif [mValue string] == "selector" then
        hasSelector = true
      end
    end
    currentClassTypedAttributeList += !attributeTypeIndex !mPropertyName !hasSetter !hasGetter !hasSelector
    allTypedAttributeList += !attributeTypeIndex !mPropertyName !hasSetter !hasGetter !hasSelector
    if hasGetter then
      [!?getterMap insertKey
        !mPropertyName
        !@methodKind.definedAsMember
        !@functionSignature {} # No argument
        !@location.here
        !false # No Compiler argument
        !attributeTypeIndex # Returned type
        !@methodQualifier.isBasic
        !"" # No Error message
      ]
    end
  end
#--- set...attribute setter
  for () in currentClassTypedAttributeList do
    if mHasSetter then
      [!?setterMap insertKey
        !@lstring.new {!"set"+ [[mPropertyName string] stringByCapitalizingFirstCharacter] !@location.here}
        !@methodKind.definedAsMember
        !{!["" nowhere] !mPropertyTypeProxy !@formalArgumentPassingModeAST.argumentIn !mPropertyName.string}
        !false # No compiler argument
        !@methodQualifier.isBasicFinal
        !"" # No error message
      ]
    end
  end
#--- Add Categories
  addExtensions (
    !inExtensionMethodMapForBuildingContext
    !inExtensionGetterMapForBuildingContext
    !inExtensionSetterMapForBuildingContext
    !?ioSemanticContext
    !self.mClassTypeName
    !?getterMap
    !?setterMap
    !?instanceMethodMap
  )
#--- Generate a "default" constructor ?
  @string defaultConstructorName
  if [superClassIndex isNull] then
    defaultConstructorName = "default"
  else
    defaultConstructorName = [superClassIndex mDefaultConstructorName]
  end
  for () in currentClassTypedAttributeList while defaultConstructorName != "" do
    if ([mPropertyTypeProxy mDefaultConstructorName] == "") | not [mPropertyTypeProxy mIsConcrete] then
      defaultConstructorName = ""
    end
  end
#--- Enter in type map
  [!?ioSemanticContext.mTypeMap insertKey
    !self.mClassTypeName
    !self.mIsPredefined
    !not self.mIsAbstract # Concrete Type
    !superClassIndex
    !@typeKindEnum.classType {!isShared: self.mIsShared}
    !false # Does not support collection value
    !allTypedAttributeList
    !attributeMap
    !currentClassTypedAttributeList
    !constructorMap
    !getterMap
    !setterMap
    !instanceMethodMap
    !{}
    !@optionalMethodMap {}
    !{}
    !.none
    !{}
    !{}
    !{}
    !{}
    !{}
    !generateHeaderInSeparateFile
    !@unifiedTypeMap-proxy.null # Type for enumerated element for "for" instruction
    !defaultConstructorName # Default constructor
    !"class-" + [self.mClassTypeName fileNameRepresentation]
    !@headerKind.twoHeaders
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTIC ANALYSIS                                                                                                  *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @classDeclarationAST semanticAnalysis
  ?!@lstringlist unused ioUsefulnessRootEntities 
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @string unused inProductDirectory
  ?let @semanticContext inSemanticContext
  ?let @predefinedTypes unused inPredefinedTypes
  ?!@semanticDeclarationListForGeneration ioSemanticDeclarationListForGeneration
{
#--- Useful entities graph
  let classTypeNameForUsefulness = typeNameForUsefulEntitiesGraph (!self.mClassTypeName)
  [!?ioUsefulEntitiesGraph addNode !classTypeNameForUsefulness !classTypeNameForUsefulness]
  if [self.mSuperClassName string] != "" then
    let superClassNameForUsefulness = typeNameForUsefulEntitiesGraph (!self.mSuperClassName)
    [!?ioUsefulEntitiesGraph addEdge !classTypeNameForUsefulness !superClassNameForUsefulness]
  end
#------ Self type
  let @unifiedTypeMap-proxy selfType = @unifiedTypeMap-proxy.searchKey {!inSemanticContext.mTypeMap !self.mClassTypeName}
#--- Super class type
  let superClassProxy = if self.mSuperClassName.string == ""
    then @unifiedTypeMap-proxy.null
    else @unifiedTypeMap-proxy.searchKey {!inSemanticContext.mTypeMap !self.mSuperClassName}
  end
#------ check "shared" status:
# a base class can be freely declared as shared or not
# a class should the same 'shared' shared status as its super class
  if superClassProxy != .null then
    [[superClassProxy mTypeKindEnum] classType ?isShared:let superClassIsShared]
    if superClassIsShared & not self.mIsShared then
      error self.mClassTypeName : "this class should be declared with 'shared' qualifier, as its super class"
    elsif not superClassIsShared & self.mIsShared then
      error self.mClassTypeName : "this class should be declared with no 'shared' qualifier, as its super class"
    end
  end
#------ Super type, and class features
# A class that has a super may not have any feature
# A class without a super class admits the 'generatedInSeparateFile' feature
  @typedPropertyList allAttributeList
  if superClassProxy == .null then
    allAttributeList = {}
    @bool generatedInSeparateFileFeature = false
    if [self.mClassFeatureList length] > 0 then
      for () in self.mClassFeatureList do
        if [mValue string] == "generatedInSeparateFile" then
          if generatedInSeparateFileFeature then
            error mValue:"the 'generatedInSeparateFile' is already named"
          end
          generatedInSeparateFileFeature = true
        else
          error mValue:"only the 'generatedInSeparateFile' attribute is allowed here"
        end
      end
    end
  else
    allAttributeList = [superClassProxy mAllTypedAttributeList]
    if [self.mClassFeatureList length] > 0 then
      for () in self.mClassFeatureList do
        error mValue:"a class that has a super class does not accept any feature"
      end
    end
  end
#------ Enter attributes
  @typedPropertyList typedAttributeList = {}
  @propertyIndexMap attributeMap = {}
  for () in self.mPropertyList do
    let propertyTypeNameForUsefulness = typeNameForUsefulEntitiesGraph (!mPropertyTypeName)
    [!?ioUsefulEntitiesGraph addEdge !classTypeNameForUsefulness !propertyTypeNameForUsefulness]
    let t = @unifiedTypeMap-proxy.searchKey {!inSemanticContext.mTypeMap !mPropertyTypeName}
    let hasSetter = true # mIsPublic
    let hasGetter = true # mIsPublic
    var hasSelector = false
    for () in mAttributeList do
      if [mValue string] == "setter" then
        warning mValue : "obsolete attribute: setter is implicitly generated for a public property"
#        if hasSetter then
#          error mValue : "the '%setter' attribute is already named"
#        end
#        hasSetter = true
      elsif [mValue string] == "nogetter" then
        warning mValue : "obsolete attribute: use 'private' qualifier"
#        if not hasGetter then
#          error mValue : "the '%nogetter' attribute is already named"
#        end
#        hasGetter = false
      elsif [mValue string] == "selector" then
        if hasSelector then
          error mValue : "the '%selector' attribute is already named"
        end
        hasSelector = true
      else
        error mValue
        : "only '%selector' is allowed here"
        fixit { replace {!"%selector"} }

      end
    end
    typedAttributeList +=
      !t
      !mPropertyName
      !hasSetter
      !hasGetter
      !hasSelector
    allAttributeList +=
      !t
      !mPropertyName
      !hasSetter
      !hasGetter
      !hasSelector
    [!?attributeMap insertKey !mPropertyName !t]
  end
#------
  ioSemanticDeclarationListForGeneration +=
    !"class " + self.mClassTypeName
    !@classTypeForGeneration.new {
      !selfType
      !self.mIsAbstract
      !self.mIsShared
      !superClassProxy
      !allAttributeList
      !typedAttributeList
      ![selfType mGenerateHeaderInSeparateFile]
    }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION                                                                                                    *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @classTypeForGeneration : @semanticTypeForGeneration {
  @bool mIsAbstract
  @bool mIsShared
  @unifiedTypeMap-proxy mSuperClass # Null if no super class
  @typedPropertyList mAllTypedAttributeList # Inherited followed by current attributes
  @typedPropertyList mTypedAttributeList
  @bool mGenerateHeaderInSeparateFile # Significant only for classes
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @classTypeForGeneration appendDeclaration1
  ?!@stringset ioInclusionSet
  !@string outHeader
{
  if not [self.mSuperClass isNull] then
    [self.mSuperClass addHeaderFileName !?ioInclusionSet]
  end

  outHeader = [filewrapper typeGenerationTemplate.classTypeHeader1
    ![self.mTypeProxy key]
    ![self.mTypeProxy identifierRepresentation]
    !if [self.mSuperClass isNull] then "" else [self.mSuperClass identifierRepresentation] end
    !self.mIsAbstract
    ![self.mTypeProxy mDefaultConstructorName] != ""
  ]
  outHeader += [filewrapper typeGenerationTemplate.unifiedClassBodyForType
    ![self.mTypeProxy key]
    ![self.mTypeProxy identifierRepresentation]
    ![self.mTypeProxy mIsConcrete]
    ![self.mTypeProxy mConstructorMap]
    ![self.mTypeProxy mGetterMap]
    ![self.mTypeProxy mSetterMap]
    ![self.mTypeProxy mInstanceMethodMap]
    ![self.mTypeProxy mClassMethodMap]
    ![self.mTypeProxy mOptionalMethodMap]
    ![self.mTypeProxy mEnumerationDescriptor]
    ![self.mTypeProxy mHandledOperatorFlags]
    ![self.mTypeProxy mAddAssignOperatorArguments]
    ![self.mTypeProxy mTypeForEnumeratedElement]
  ]
 }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @classTypeForGeneration appendDeclaration2
  ?let @string inOutputDirectory
  ?!@stringset ioInclusionSet
  !@string outHeader
{
  for (t ...) in self.mTypedAttributeList do
    [t addHeaderFileName !?ioInclusionSet]
  end

  if self.mGenerateHeaderInSeparateFile then
    let part1 = [filewrapper typeGenerationTemplate.classTypeHeader2part1
      ![self.mTypeProxy key]
      ![self.mTypeProxy identifierRepresentation]
      !if [self.mSuperClass isNull] then "" else [self.mSuperClass identifierRepresentation] end
      !self.mTypedAttributeList
      !self.mAllTypedAttributeList
      !self.mIsAbstract
    ]
    let part2 = [filewrapper typeGenerationTemplate.classTypeHeader2part2
      ![self.mTypeProxy key]
      ![self.mTypeProxy identifierRepresentation]
      !if [self.mSuperClass isNull] then "" else [self.mSuperClass identifierRepresentation] end
      !self.mTypedAttributeList
      !self.mAllTypedAttributeList
      !self.mIsAbstract
    ]
    let headerFileName = "separateHeaderFor_" + [self.mTypeProxy identifierRepresentation] + ".h"
    [@string generateFileWithPattern
      !startPath:inOutputDirectory + "/../user-headers"
      !fileName:headerFileName
      !lineComment:"//"
      !header:"" # Header
      !defaultUserZone1:"\n\n" # Defaut user zone1
      !generatedZone2:part1
      !defaultUserZone2:"\n\n" # Defaut user zone2
      !generatedZone3:part2
      !makeExecutable:false
    ]
    outHeader = "#include \"" + headerFileName + "\"\n\n"
  else
    outHeader = [filewrapper typeGenerationTemplate.classTypeHeader2part1
      ![self.mTypeProxy key]
      ![self.mTypeProxy identifierRepresentation]
      !if [self.mSuperClass isNull] then "" else [self.mSuperClass identifierRepresentation] end
      !self.mTypedAttributeList
      !self.mAllTypedAttributeList
      !self.mIsAbstract
    ]
    outHeader += [filewrapper typeGenerationTemplate.classTypeHeader2part2
      ![self.mTypeProxy key]
      ![self.mTypeProxy identifierRepresentation]
      !if [self.mSuperClass isNull] then "" else [self.mSuperClass identifierRepresentation] end
      !self.mTypedAttributeList
      !self.mAllTypedAttributeList
      !self.mIsAbstract
    ]
  end
 }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @classTypeForGeneration appendSpecificImplementation
  ?let @unifiedTypeMap unused inTypeMap
  ?!@stringset ioInclusionSet
  !@string outImplementation
{
  [self.mTypeProxy addHeaderFileName !?ioInclusionSet]
  outImplementation = [filewrapper typeGenerationTemplate.classTypeSpecificImplementation
    ![self.mTypeProxy key]
    ![self.mTypeProxy identifierRepresentation]
    !if [self.mSuperClass isNull] then "" else [self.mSuperClass identifierRepresentation] end
    !self.mTypedAttributeList
    !self.mAllTypedAttributeList
    ![self.mAllTypedAttributeList subListWithRange !@range.new {!0 ![self.mAllTypedAttributeList length] - [self.mTypedAttributeList length]}]
    !self.mIsAbstract
    !self.mIsShared
    ![self.mTypeProxy mDefaultConstructorName] != ""
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

