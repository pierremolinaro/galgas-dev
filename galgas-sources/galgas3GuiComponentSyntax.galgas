#----------------------------------------------------------------------------------------------------------------------*
#                                                                                                                      *
#  Copyright (C) 2014, ..., 2014 Pierre Molinaro.                                                                      *
#                                                                                                                      *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                                                               *
#                                                                                                                      *
#  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public   *
#  License as published by the Free Software Foundation.                                                               *
#                                                                                                                      *
#  This program is distributed in the hope it will be useful, but WITHOUT ANY WARRANTY; without even the implied       *
#  warranty of MERCHANDIBILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more        *
#  details.                                                                                                            *
#                                                                                                                      *
#----------------------------------------------------------------------------------------------------------------------*

syntax galgas3GuiComponentSyntax (galgas3Scanner) {

#----------------------------------------------------------------------------------------------------------------------*
#                                                                                                                      *
#   G U I    C O M P O N E N T                                                                                         *
#                                                                                                                      *
#----------------------------------------------------------------------------------------------------------------------*

rule <declaration>
 ?!@galgas3DeclarationAST ioDeclarations {
  $gui$
  $identifier$ ?var @lstring mGUIName
  ${$
  var importedOptionList =@lstringlist {}
  var simpleGlobalAttributes =@guiSimpleAttributeListAST {}
  var withLexiqueList =@withLexiqueListAST {}
  repeat
  while
    $identifier$ ?var @lstring mKey
    $:$
    var mValue =@lstring.new{!"" !@location.here}
    repeat
      $literal_string$ ?var @lstring value
      mValue = @lstring.new {![mValue string] + [value string] ![value location]}
    while
    end
    simpleGlobalAttributes += !mKey !mValue
  while
    $with$
    select
      $option$
      $identifier$ ?var @lstring optionReference
      importedOptionList += !optionReference
    or
      $lexique$
      $identifier$ ?var @lstring lexiqueReference
      var labels =@guiLabelListAST {}
      var simpleAttributes =@guiSimpleAttributeListAST {}
      var compoundAttributes =@guiCompoundAttributeListAST {}
      ${$
      repeat
      while
        $label$
        @uint displayStyle
        select
          displayStyle = 0
        or
          $*$
          displayStyle = 1
        end
        var terminalList =@terminalLabelListAST {}
        repeat
          $\$terminal\$$ ?var  @lstring terminal
          @uint displayFlags
          select
            displayFlags = 0 # Default
          or
            $-$
            displayFlags = 0xFFFF # Do not diplay
          or
            $($
            $literal_uint32$ ?var @luint leadingStrip
            if leadingStrip.uint >= 15 then
              error leadingStrip:"leading strip should be < 15"
            end
            $,$
            $literal_uint32$ ?var @luint endingStrip
            if endingStrip.uint >= 15 then
              error endingStrip:"tail strip should be < 15"
            end
            displayFlags = (leadingStrip.uint << 4) | endingStrip
           $)$
          end
          terminalList += !terminal !displayFlags
        while
          $,$
        end
        labels += !displayStyle !terminalList !@location.here
      while
        $identifier$ ?var @lstring key
        $literal_string$ ?var @lstring name
        $:$
        var value =@lstring.new{!"" !@location.here}
        repeat
          $literal_string$ ?var @lstring v
          value = @lstring.new {![value string] + [v string] ![v location]}
        while
        end
        compoundAttributes += !key !name !value
      while
        $identifier$ ?var @lstring name
        $:$
        $literal_string$ ?var @lstring value
        simpleAttributes += !name !value
      end
      $}$
      withLexiqueList += 
        !lexiqueReference
        !labels
        !simpleAttributes
        !compoundAttributes
    end
  end
#---
  $}$
  ioDeclarations.mGUIComponentList +=
    !mGUIName
    !importedOptionList
    !simpleGlobalAttributes
    !withLexiqueList
}

#----------------------------------------------------------------------------------------------------------------------*

}