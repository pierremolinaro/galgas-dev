#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @guiSimpleAttributeListAST {
  public let @lstring mKey
  public let @lstring mValue
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @guiCompoundAttributeListAST {
  public let @lstring mKey
  public let @lstring mAttributeName
  public let @lstring mValue
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @terminalLabelListAST {
  public let @lstring mTerminal
  public let @uint mDisplayFlags
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @guiLabelListAST {
  public let @uint mLeadingCharacterStrippedCount
  public let @terminalLabelListAST mTerminalList
  public let @location mLocation
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @withLexiqueListAST {
  public let @lstring mLexiqueFileName
  public let @guiLabelListAST mLabels
  public let @guiSimpleAttributeListAST mSimpleAttributes
  public let @guiCompoundAttributeListAST mCompoundAttributes
}
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @guiCommandLineOptionList {
  public let @string mOptionComponent
  public let @string mOptionIdentifier
  public let @char mOptionChar
  public let @string mOptionString
  public let @string mComment
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @extensionMap {
  public let @string mLexiqueName
  public let @uint mIndex
  insert insertKey error message"the '%K' extension is already declared in %L"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @textMacroList {
  public let @string mKey
  public let @string mContents
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @importedLexiqueList {
  public let @string mLexiqueClassName
  public let @uint mIndex
  public let @string mBlockComment
  public let @string mTitle
  public let @textMacroList mTextMacroList
  public let @guiLabelListAST mLabels
  public let @lexicalStyleListAST mLexicalStyleList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @projectIndexingDescriptorList {
  let @lstring mProjectFileExtension
  let @lstring indexingPathSuffix
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @galgasGUIComponentListAST {
  public let @lstring mGUIComponentName
  public let @lstringlist mImportedOptionList
  public let @guiSimpleAttributeListAST mGlobalSimpleAttributeList
  public let @withLexiqueListAST mWithLexiqueList
  public let @projectIndexingDescriptorList mProjectIndexingDescriptorList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax galgas3GuiComponentSyntax (galgasScanner3) {

  #·····················································································································

  rule <declaration_ggs3> ?!@galgasDeclarationAST ioDeclarations {
    $gui$
    $identifier$ ?let @lstring mGUIName
    ${$
    var @lstringlist importedOptionList = {}
    var @guiSimpleAttributeListAST simpleGlobalAttributes = {}
    var @withLexiqueListAST withLexiqueList = {}
    var @projectIndexingDescriptorList projectExtensionList = {}
    repeat
    while
      $project$
      $"string"$ ?let fileExtension
      $indexing$
      $in$
      $"string"$ ?let indexingPathSuffix
      projectExtensionList += !fileExtension !indexingPathSuffix
    while
      <gui_attributes> !?simpleGlobalAttributes
    while
      <gui_with_option_declaration> !?importedOptionList
    while
      <gui_with_lexique_declaration> !?withLexiqueList
    end
  #---
    $}$
    ioDeclarations.mGUIComponentList +=
      !mGUIName
      !importedOptionList
      !simpleGlobalAttributes
      !withLexiqueList
      !projectExtensionList
  }
  
  #·····················································································································
  
  rule <gui_with_lexique_declaration> ?!@withLexiqueListAST ioWithLexiqueList {
    $with$
    $lexique$
    $identifier$ ?let @lstring lexiqueReference
    var @guiLabelListAST labels = {}
    var @guiSimpleAttributeListAST simpleAttributes = {}
    var @guiCompoundAttributeListAST compoundAttributes = {}
    ${$
    repeat
    while
      $label$
      let @uint displayStyle
      select
        displayStyle = 0
      or
        $*$
        displayStyle = 1
      end
      var @terminalLabelListAST terminalList = {}
      repeat
        $\$terminal\$$ ?let  @lstring terminal
        let @uint displayFlags
        select
          displayFlags = 0 # Default
        or
          $-$
          displayFlags = 0xFFFF # Do not diplay
        or
          $($
          $literalInt$ ?let leadingStrip
          if leadingStrip.bigint >= 15 then
            error leadingStrip:"leading strip should be < 15"
          end
          $,$
          $literalInt$ ?let endingStrip
          if endingStrip.bigint >= 15 then
            error endingStrip:"tail strip should be < 15"
          end
          displayFlags = (leadingStrip.bigint << 4) | endingStrip
         $)$
        end
        terminalList += !terminal !displayFlags
      while
        $,$
      end
      labels += !displayStyle !terminalList !@location.here
    while
      $identifier$ ?let @lstring key
      $"string"$ ?let @lstring name
      $:$
      var value = @lstring.new{!"" !@location.here}
      repeat
        $"string"$ ?let @lstring v
        value = @lstring.new {!value.string + v.string !v.location}
      while
      end
      compoundAttributes += !key !name !value
    while
      $identifier$ ?let @lstring name
      $:$
      $"string"$ ?let @lstring value
      simpleAttributes += !name !value
    end
    $}$
    ioWithLexiqueList +=  !lexiqueReference !labels !simpleAttributes !compoundAttributes
  }
  
  #·····················································································································
  
  rule <gui_with_option_declaration> ?!@lstringlist ioImportedOptionList {
    $with$
    $option$
    $identifier$ ?let @lstring optionReference
    ioImportedOptionList += !optionReference
  }

  #·····················································································································
  
  rule <gui_attributes> ?!@guiSimpleAttributeListAST ioSimpleGlobalAttributes {
    $identifier$ ?let @lstring mKey
    $:$
    var mValue = @lstring.new {!"" !.here}
    repeat
      $"string"$ ?let @lstring value
      mValue = @lstring.new {!mValue.string + value.string !value.location}
    while
    end
    ioSimpleGlobalAttributes += !mKey !mValue
  }
  
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax galgas4GuiComponentSyntax (galgasScanner4) {

  #·····················································································································

  rule <declaration> ?!@galgasDeclarationAST ioDeclarations
                     ?!@stringset unused ioUserImplicitDeclaredTypeSet {
    $gui$
    $identifier$ ?let @lstring mGUIName
    ${$
    var @lstringlist importedOptionList = {}
    var @guiSimpleAttributeListAST simpleGlobalAttributes = {}
    var @withLexiqueListAST withLexiqueList = {}
    var @projectIndexingDescriptorList projectExtensionList = {}
    repeat
    while
      $project$
      $"string"$ ?let fileExtension
      $indexing$
      $in$
      $"string"$ ?let indexingPathSuffix
      projectExtensionList += !fileExtension !indexingPathSuffix
    while
      <gui_attributes> !?simpleGlobalAttributes
    while
      <gui_with_option_declaration> !?importedOptionList
    while
      <gui_with_lexique_declaration> !?withLexiqueList
    end
  #---
    $}$
    ioDeclarations.mGUIComponentList +=
      !mGUIName
      !importedOptionList
      !simpleGlobalAttributes
      !withLexiqueList
      !projectExtensionList
  }
  
  #·····················································································································
  
  rule <gui_with_lexique_declaration> ?!@withLexiqueListAST ioWithLexiqueList {
    $with$
    $lexique$
    $identifier$ ?let @lstring lexiqueReference
    var @guiLabelListAST labels = {}
    var @guiSimpleAttributeListAST simpleAttributes = {}
    var @guiCompoundAttributeListAST compoundAttributes = {}
    ${$
    repeat
    while
      $label$
      let @uint displayStyle
      select
        displayStyle = 0
      or
        $*$
        displayStyle = 1
      end
      var @terminalLabelListAST terminalList = {}
      repeat
        $\$terminal\$$ ?let  @lstring terminal
        let @uint displayFlags
        select
          displayFlags = 0 # Default
        or
          $-$
          displayFlags = 0xFFFF # Do not diplay
        or
          $($
          $literalInt$ ?let leadingStrip
          if leadingStrip.bigint >= 15 then
            error leadingStrip:"leading strip should be < 15"
          end
          $,$
          $literalInt$ ?let endingStrip
          if endingStrip.bigint >= 15 then
            error endingStrip:"tail strip should be < 15"
          end
          displayFlags = (leadingStrip.bigint << 4) | endingStrip
         $)$
        end
        terminalList += !terminal !displayFlags
      while
        $,$
      end
      labels += !displayStyle !terminalList !@location.here
    while
      $identifier$ ?let @lstring key
      $"string"$ ?let @lstring name
      $:$
      var value = @lstring.new{!"" !@location.here}
      repeat
        $"string"$ ?let @lstring v
        value = @lstring.new {!value.string + v.string !v.location}
      while
      end
      compoundAttributes += !key !name !value
    while
      $identifier$ ?let @lstring name
      $:$
      $"string"$ ?let @lstring value
      simpleAttributes += !name !value
    end
    $}$
    ioWithLexiqueList +=  !lexiqueReference !labels !simpleAttributes !compoundAttributes
  }
  
  #·····················································································································
  
  rule <gui_with_option_declaration> ?!@lstringlist ioImportedOptionList {
    $with$
    $option$
    $identifier$ ?let @lstring optionReference
    ioImportedOptionList += !optionReference
  }

  #·····················································································································
  
  rule <gui_attributes> ?!@guiSimpleAttributeListAST ioSimpleGlobalAttributes {
    $identifier$ ?let @lstring mKey
    $:$
    var mValue = @lstring.new {!"" !.here}
    repeat
      $"string"$ ?let @lstring value
      mValue = @lstring.new {!mValue.string + value.string !value.location}
    while
    end
    ioSimpleGlobalAttributes += !mKey !mValue
  }
  
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTIC ANALYSIS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct @guiAnalysisContext {
  public var @stringlist mImportedOptionComponentList = {}
  public let @guiCommandLineOptionList mBoolOptionList = {}
  public let @guiCommandLineOptionList mUIntOptionList = {}
  public let @guiCommandLineOptionList mStringOptionList = {}
  public var @stringlist mNibAndClassList = {}
  public var @extensionMap mExtensionMap = {}
  public var @importedLexiqueList mWithLexiqueList = {}
  public var @string mBuildRunOption = ""
  public var @projectIndexingDescriptorList mProjectIndexingDescriptorList = {}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @optionComponentMapForGeneration {
  public let @guiAnalysisContext mGuiComponentContext
  insert insertKey error message"redefinition of the '%K' option component: it has been already declared in %L"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc compileAllGuiComponentFromASTGalgas3
  ?let @galgasGUIComponentListAST inGuiComponentListAST
  ?let @semanticContext inSemanticContext
  !@optionComponentMapForGeneration outOptionComponentMapForGeneration
{
  outOptionComponentMapForGeneration = {}
  for guiAST in inGuiComponentListAST do
    guiComponentSemanticAnalysis (
      !guiAST
      !inSemanticContext
      !?outOptionComponentMapForGeneration
    )
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private proc guiComponentSemanticAnalysis ?let @galgasGUIComponentListAST-element inGUIComponentAST
                                          ?let @semanticContext inSemanticContext
                                          ?!@optionComponentMapForGeneration ioOptionComponentMapForGeneration {
  if inGUIComponentAST.mGUIComponentName.string != "cocoa" then
    error inGUIComponentAST.mGUIComponentName : "the gui name should be 'cocoa'"
  end
#--- 
  var @guiAnalysisContext context = .new {}
  context.mProjectIndexingDescriptorList = inGUIComponentAST.mProjectIndexingDescriptorList
#--- Check imported options components
  for (mValue) in inGUIComponentAST.mImportedOptionList do
    [inSemanticContext.mOptionComponentMapForSemanticAnalysis searchKey
      !mValue
      ?5*
    ]
    context.mImportedOptionComponentList += !mValue.string
  end
#--- Nib and main classes
  var runOptionDefined = false
  for (mKey mValue) in inGUIComponentAST.mGlobalSimpleAttributeList do
    if mKey.string == "run" then
      if runOptionDefined then
        error mKey : "option already defined"
      elsif mValue.string == "" then
        error mValue : "run option string should not be empty"
      else
        context.mBuildRunOption = mValue.string
        runOptionDefined = true
      end
    elsif mKey.string == "nibAndMainClass" then
      for component in [mValue.string componentsSeparatedByString !"."] do
        context.mNibAndClassList += !component.mValue
      end
    else
      warning mKey:"the '" + mKey + "' key is invalid and has no effect"
    end
  end
#--- 'with lexique' construct analysis
  for (lexiqueFileName labels simpleAttributes compoundAttributes) in inGUIComponentAST.mWithLexiqueList do (idx)
  #--- Check lexique
    [inSemanticContext.mLexiqueComponentMapForSemanticAnalysis searchKey
      !lexiqueFileName
      ?*
      ?let @terminalMap terminalMap
      ?3*
      ?let lexicalStyleListAST
      ?*
    ]
  #--- Build terminal symbol set
    let terminalSymbolSet = [terminalMap keySet]
  #--- Check labels
    for (* mTerminalList *) in labels do
      for (mTerminal *) in mTerminalList do
        if not [terminalSymbolSet hasKey !mTerminal.string] then
          error mTerminal: "the '$"
            + mTerminal
            + "$' terminal symbol is not declared by the '"
            + lexiqueFileName
            + "' lexique"
        end
      end
    end
  #--- Compound attributes
    var @textMacroList textMacroList = {}
    for (mKey mAttributeName mValue) in compoundAttributes do
      if mKey.string != "textMacro" then
        error mKey: "for a compound attribute, only the 'textMacro' key is valid"
      else
        textMacroList += !mAttributeName.string !mValue.string
      end
    end
  #--- Simple attributes
    var @bool hasAssociatedExtension = false
    var @bool hasTabViewTitle = false
    var @bool hasBlockComment = false
    var @string tabViewTitle = ""
    var @string blockComment = ""
    for (mKey mValue) in simpleAttributes do
      if mKey.string == "fileExtension" then
        [!?context.mExtensionMap insertKey !mValue !lexiqueFileName.string !idx]
        hasAssociatedExtension = true
      elsif mKey.string == "title" then
        if hasTabViewTitle then
          error mKey: "the '" + mKey + "' key should be defined once"
        end
        hasTabViewTitle = true
        tabViewTitle = mValue.string
      elsif mKey.string == "blockComment" then
        if hasBlockComment then
          error mKey: "the '" + mKey + "' key should be defined once"
        end
        hasBlockComment = true
        blockComment = mValue.string
      else
        error mKey: "the '" + mKey + "' key is not valid (valid keys: 'blockComment,', 'fileExtension', 'title')"
      end
    end
  #--- has an extension ?
    if not hasAssociatedExtension then
      error lexiqueFileName:"one or more file extensions should be associated with a lexique (fileExtension : \"...\")"
    end
  #--- has title ?
    if not hasTabViewTitle then
      error lexiqueFileName:"the 'title' key should be defined once (it is not defined)"
    end
  #---
    context.mWithLexiqueList +=
      !lexiqueFileName.string
      !idx
      !blockComment
      !tabViewTitle
      !textMacroList
      !labels
      !lexicalStyleListAST
  end
#--- Generation
  [!?ioOptionComponentMapForGeneration insertKey
    !inGUIComponentAST.mGUIComponentName
    !context
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc generateAllGuiComponentsGalgas3 ?let @optionComponentMapForGeneration inOptionComponentMapForGeneration
                                      ?let @string inOutputDirectory
                                      ?let @bool inQuietOutputByDefault
                                      ?!@stringlist ioApplicationSourceSet
                                      ?!@stringset ioAllProductFileSet {
  for (lkey guiComponentContext) in inOptionComponentMapForGeneration do
    let swiftFileName = "gui-" + lkey + ".swift"
    ioApplicationSourceSet += !swiftFileName
    ioAllProductFileSet += !swiftFileName
    let @string swift_string = [filewrapper guiGenerationTemplates.swift_gui_implementation
      !guiComponentContext
      !inQuietOutputByDefault
    ]
    [@string generateFileWithPattern
      !startPath:inOutputDirectory
      !fileName: swiftFileName
      !lineComment:"//"
      !header:"" # Header
      !defaultUserZone1:"\n\n" # Defaut user zone1
      !generatedZone2: swift_string
      !defaultUserZone2:"\n\n" # Defaut user zone2
      !generatedZone3:"\n\n" # Zone 3
      !makeExecutable:false
    ]
    let objcFileName = "gui-" + lkey + ".m"
    ioApplicationSourceSet += !objcFileName
    ioAllProductFileSet += !objcFileName
    let @string objc_string = [filewrapper guiGenerationTemplates.objc_gui_implementation
      !guiComponentContext
      !inQuietOutputByDefault
    ]
    [@string generateFileWithPattern
      !startPath:inOutputDirectory
      !fileName: objcFileName
      !lineComment:"//"
      !header:"" # Header
      !defaultUserZone1:"\n\n" # Defaut user zone1
      !generatedZone2: objc_string
      !defaultUserZone2:"\n\n" # Defaut user zone2
      !generatedZone3:"\n\n" # Zone 3
      !makeExecutable:false
    ]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#         G E N E R A T I O N    T E M P L A T E S      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

filewrapper guiGenerationTemplates in "+generation-templates/gui_generation" {
}{
}{
  template objc_gui_implementation "gui-implementation.m.galgasTemplate"
    ?@guiAnalysisContext GUI_CONTEXT
    ?@bool QUIET_OUTPUT_BY_DEFAULT

  template swift_gui_implementation "gui-implementation.swift.galgasTemplate"
    ?@guiAnalysisContext GUI_CONTEXT
    ?@bool QUIET_OUTPUT_BY_DEFAULT
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
