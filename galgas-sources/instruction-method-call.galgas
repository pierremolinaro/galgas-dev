#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

shared class @methodCallInstructionAST : @semanticInstructionAST {
  @semanticExpressionAST mReceiverExpression
  @lstring mMethodName
  @actualParameterListAST mActualParameterList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension galgas3InstructionsSyntax {

  #·····················································································································

  rule <semantic_instruction>
    !@semanticInstructionAST outInstruction {
    $[$
    let instructionLocation = @location.here
    <expression> ?let @semanticExpressionAST mReceiverExpression
    $identifier$ ?let mMethodName
    <actual_parameter_list> ?let @actualParameterListAST actualParameterList
    $]$
    outInstruction = @methodCallInstructionAST.new {
      !instructionLocation
      !mReceiverExpression
      !mMethodName
      !actualParameterList
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CONTEXT
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @methodCallInstructionAST enterInstructionInSemanticContext
  ?!@unifiedTypeMap ioTypeMap
{
  [self.mReceiverExpression enterExpressionInSemanticContext !?ioTypeMap]
  for () in self.mActualParameterList do
    [mActualParameter enterParameterInSemanticContext !?ioTypeMap]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @methodCallInstructionAST analyzeSemanticInstruction
  ?let @lstring inUsefulnessCallerEntityName
  ?!@usefulEntitiesGraph ioUsefulEntitiesGraph
  ?let @analysisContext inAnalysisContext
  ?!@semanticInstructionListForGeneration ioInstructionListForGeneration
  ?!@localVarManager ioVariableMap {
#--- Analyze receiver expression
  [self.mReceiverExpression analyzeSemanticExpression
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !@unifiedTypeMap-entry.null
    !inAnalysisContext
    !?ioVariableMap
    ?let @semanticExpressionForGeneration receiverExpression
  ]
#--- Get type method signature
  @formalParameterSignature routineSignature
  @bool hasCompilerArgument
  @methodKind kind
  @string errorMessage
  [[[receiverExpression mResultType] mInstanceMethodMap] searchKey !self.mMethodName ?kind ?routineSignature ?* ?hasCompilerArgument ?* ?errorMessage]
  if errorMessage != "" then
    error self.mMethodName : errorMessage
  end
#--- Check effective parameters
  analyzeRoutineArguments (
    !inUsefulnessCallerEntityName
    !?ioUsefulEntitiesGraph
    !inAnalysisContext
    !self.mMethodName
    !"'" + self.mMethodName + "' instance method header"
    !routineSignature
    !self.mActualParameterList
    !?ioVariableMap
    !?ioInstructionListForGeneration
    ?let @actualParameterListForGeneration actualParameterListForGeneration
  )
#--- Search base type that declares the method
  var baseType = [receiverExpression mResultType]
  var searching = true
  if @uint. errorCount == 0 then
    loop( @uint. max) while searching do
      if [[baseType mSuperType] isNull] then
        searching = false
      else
        if [[[baseType mSuperType] mInstanceMethodMap] hasKey !self.mMethodName.string] then
          baseType = [baseType mSuperType]
        else
          searching = false
        end
      end  
    end
  end
#--- Generate instructions
  ioInstructionListForGeneration += !@methodCallInstructionForGeneration.new {
    !self.mInstructionLocation
    ![receiverExpression mResultType]
    !receiverExpression
    ![self.mMethodName string]
    !kind
    !actualParameterListForGeneration
    !hasCompilerArgument
    !baseType
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! GENERATION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

shared class @methodCallInstructionForGeneration : @semanticInstructionForGeneration {
  @location mInstructionLocation
  @unifiedTypeMap-entry mReceiverType
  @semanticExpressionForGeneration mReceiverExpression
  @string mMethodName
  @methodKind mKind
  @actualParameterListForGeneration mActualParameterList
  @bool mHasCompilerArgument
  @unifiedTypeMap-entry mMethodBaseType
}

#·····················································································································*

override method @methodCallInstructionForGeneration generateInstruction
  ?!@stringset ioInclusionSet
  ?!@uint ioTemporaryVariableIndex
  ?!@stringset ioUnusedVariableCppNameSet
  ?let @bool unused inGenerateSyntaxDirectedTranslationString
  ?!@string ioGeneratedCode {
#--- Receiver expression
  [self.mReceiverExpression generateExpression !?ioGeneratedCode !?ioInclusionSet !?ioTemporaryVariableIndex !?ioUnusedVariableCppNameSet ?let @string receiverCppName]
  [!?ioUnusedVariableCppNameSet removeKey !receiverCppName]
#---
  var parameterCppNameList =@stringlist {}
  var jokerParametersToReleaseList =@stringlist {}
  var inputVariableList =@stringlist {}
  for () in self.mActualParameterList do
    @string parameterCppName
    [mActualParameter generateActualParameter !?ioInclusionSet !?ioTemporaryVariableIndex !?ioGeneratedCode !?jokerParametersToReleaseList !?inputVariableList !?ioUnusedVariableCppNameSet ?parameterCppName]
    parameterCppNameList += !parameterCppName
  end
#--- Generate method call
  switch self.mKind
  case definedAsExtension :
    ioInclusionSet += !"method-" + [[self.mMethodBaseType key] fileNameRepresentation] + "-" + [self.mMethodName fileNameRepresentation]
    if [[self.mReceiverType mTypeKindEnum] isClassType] then
      ioGeneratedCode += "callExtensionMethod_" + [self.mMethodName identifierRepresentation]
                      + " ((const cPtr_" + [[self.mReceiverExpression mResultType] identifierRepresentation] + " *) " + receiverCppName + ".ptr (), "
    else
      ioGeneratedCode += "extensionMethod_" + [self.mMethodName identifierRepresentation] + " (" + receiverCppName + ", "
    end
  case definedAsMember :
    ioGeneratedCode += receiverCppName + ".method_" + [self.mMethodName identifierRepresentation] + " ("
  end
  for () in parameterCppNameList
    do ioGeneratedCode += mValue
    between ioGeneratedCode += ", "
  end
  if [parameterCppNameList length] == 0 then
    if self.mHasCompilerArgument then
      [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
      ioGeneratedCode += compilerCppName ()
      ioGeneratedCode += " COMMA_"
    end
  elsif self.mHasCompilerArgument then
    [!?ioUnusedVariableCppNameSet removeKey !compilerCppName ()]
    ioGeneratedCode += ", " + compilerCppName ()
    ioGeneratedCode += " COMMA_"
  else
    ioGeneratedCode += " COMMA_"
  end
  ioGeneratedCode += [self.mInstructionLocation sourceFile] + ") ;\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
